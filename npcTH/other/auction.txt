//===== rAthena Script =======================================
//= Auction system
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Auction house releated NPCs, and warps
//===== Additional Comments: =================================
//= 1.0 First version, place holders only. [L0ne_W0lf]
//= 1.1 Moved Morocc Auction NPCs to Morocc Ruins. [L0ne_W0lf]
//= 1.2 Warp fixed. Dialog in Ruins/Lighthalzen changed. [Kisuka]
//= 1.3 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//============================================================

// Auction House warpers
//============================================================
moc_ruins,78,173,6	script	Auction Hall Guide#moc	98,{
	callfunc "F_AuctionWarper",1;
}

prontera,218,120,4	script	Auction Hall Guide#prt	117,{
	callfunc "F_AuctionWarper",2;
}

yuno,129,116,0	script	Auction Hall Guide#yuno	90,{
	callfunc "F_AuctionWarper",3;
}

lighthalzen,205,169,6	script	Auction Hall Guide#lhz	833,{
	callfunc "F_AuctionWarper",4;
}

function	script	F_AuctionWarper	{
	mes "[ เจ้าหน้าที่นำทางโรงประมูล ]";
	mes "สวัสดีครับ ท่านต้องการ";
	mes "ที่จะเข้าไปยังภายใน";
	mes "โรงประมูลหรือไม่?";
	next;
	if (select("ใช่, ตกลง:ไม่ล่ะ ขอบคุณ") == 1) {
		.@num = getarg(0);
		if (.@num == 1 || .@num == 4) {
			mes "[ เจ้าหน้าที่นำทางโรงประมูล ]";
			mes "ยอดเยี่ยมเลย! ถ้าอย่างนั้น";
			mes "ขอให้ท่านสนุกและเพลิดเพลิน";
			mes "ไปกับการประมูลนะครับ~";
		} else {
			mes "[ เจ้าหน้าที่นำทางโรงประมูล ]";
			mes "ขอให้สนุกกับการประมูลนะครับ";
		}
		close2;
		switch(.@num) {
		case 1:
			warp "auction_01",179,53; end;
		case 2:
			warp "auction_01",21,43; end;
		case 3:
			warp "auction_02",151,23; end;
		case 4:
			warp "auction_02",43,24; end;
		}
	}
	mes "[ เจ้าหน้าที่นำทางโรงประมูล ]";
	mes "รับทราบครับ";
	mes "แล้วพบกันใหม่นะครับ";
	mes "หากท่านเปลี่ยนใจ";
	mes "อยากจะลองเข้าร่วมประมูล";
	mes "เมื่อไหร่ ก็แวะมาหาได้เสมอนะครับ~";
	close;
}

// Sign posts
//============================================================
-	script	Information Post#dum::AuctionSign	-1,{
	mes "[ ป้ายประชาสัมพันธ์ ]";
	mes "คำแนะนำการวาร์ปไปยังโรงประมูล";
	close;
}

moc_ruins,76,176,6	duplicate(AuctionSign)	Information Post#moc	837
prontera,216,120,4	duplicate(AuctionSign)	Information Post#prt	837
yuno,131,116,0	duplicate(AuctionSign)	Information Post#yuno	837
lighthalzen,207,169,6	duplicate(AuctionSign)	Information Post#lhz	837

// Warps
//============================================================
auction_01,180,49,0	warp	auction_entrance_moc	1,1,moc_ruins,78,171
auction_01,22,37,0	warp	auction_entrance_prt	1,1,prontera,214,120
auction_02,151,17,0	warp	auction_enterance_juno	1,1,yuno,132,119
auction_02,43,17,0	warp	auction_enterance_lhz	1,1,lighthalzen,209,169

// Auction House NPCs
//============================================================
-	script	Auction Broker#dum::AuctionBroker	-1,{
	mes "[ พนักงานประมูล ]";
	mes "ยินดีต้อนรับสู่โรงประมูลครับ";
	mes "ท่านต้องการเลือกชมสินค้าที่มีการนำเสนออยู่หรือไม่?";
	next;
	if (select("ใช่, ขอดูหน่อย:ไม่ล่ะ ขอบคุณ") == 1) {
		mes "[ พนักงานประมูล ]";
		mes "ได้เลยครับ";
		mes "เชิญท่านเลือกชมได้ตามสบาย";
		mes "ลองดูว่ามีสินค้าชิ้นไหน";
		mes "ที่ถูกใจท่านบ้างนะครับ~";
		OpenAuction;
		close;
	}
	mes "[ พนักงานประมูล ]";
	mes "เข้าใจแล้วครับ";
	mes "หากท่านเปลี่ยนใจ";
	mes "ต้องการตรวจสอบรายการประมูล";
	mes "เมื่อไหร่ ก็แวะมาหาข้าได้เสมอนะครับ~";
	close;
}

auction_01,182,68,6	duplicate(AuctionBroker)	Auction Broker#moc1	98
auction_01,182,75,0	duplicate(AuctionBroker)	Auction Broker#moc2	99
auction_01,177,75,2	duplicate(AuctionBroker)	Auction Broker#moc3	98
auction_01,177,68,4	duplicate(AuctionBroker)	Auction Broker#moc4	99
auction_01,21,74,4	duplicate(AuctionBroker)	Auction Broker#prt1	117
auction_01,27,78,4	duplicate(AuctionBroker)	Auction Broker#prt2	116
auction_01,16,78,4	duplicate(AuctionBroker)	Auction Broker#prt3	115
auction_02,158,47,6	duplicate(AuctionBroker)	Auction Broker#yuno1	90
auction_02,145,47,2	duplicate(AuctionBroker)	Auction Broker#yuno2	90
auction_02,151,54,0	duplicate(AuctionBroker)	Auction Broker#yuno3	90
auction_02,152,41,4	duplicate(AuctionBroker)	Auction Broker#yuno4	90
auction_02,57,46,2	duplicate(AuctionBroker)	Auction Broker#lhz1	874
auction_02,31,46,6	duplicate(AuctionBroker)	Auction Broker#lhz2	874
auction_02,43,65,4	duplicate(AuctionBroker)	Auction Broker#lhz3	833
