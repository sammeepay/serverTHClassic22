//===== rAthena Script ======================================= 
//= Card Trader
//===== By: ================================================== 
//= Elias (og2)
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project; SVN r15340+
//===== Description: ========================================= 
//= Card and Points Trader
//===== Additional Comments: ================================= 
//= 1.0 Translated from the Official [Elias]
//= 1.1 Fixed variables and optimized script [Elias]
//= 1.2 Fixed char being stuck with breaks [Elias] (bugreport:5374)
//= 1.3 Optimized: reduced from 123kb to 7kb! [Euphy]
//= 1.4 Variables don't need to load OnInit. [Euphy]
//= 1.4a Misc. [Capuche]
//============================================================ 

prontera,115,90,0	script	Putty	4_F_01,{
	disable_items;
	setarray .@card1[0], 4001,4006,4009,4019,4075,4033,4012,4016,4026,4022,
				4027,4028,4038,4025,4021,4050,4079,4081,4090,4094,
				4101,4104,4110,4114,4119,4108,4095,4231,4280,4008,
				4011,4013,4014,4015,4020,4032,4037,4039,4041,4045,
				4046,4010,4023,4029,4052,4048,4056,4071,4093,4031,
				4036,4034,4042,4055,4061,4087,4096,4116,4122,4170,
				4215,4220,4228,4226,4212,4227,4267,4257,4278,4286,
				4287,4292,4311,4315,4319,4322,4084,4078,4113,4149,
				4153,4196,4240,4247,4256,4057,4066,4067,4112,4150,
				4152,4186,4187,4181,4173,4167,4162,4176,4195,4193,
				4200,4223,4194,4190,4189,4192,4224,4244,4248,4261,
				4260,4259,4274,4275,4313,4299,4304,4294,4076,4127,
				4154,4157,4156,4213,4214,4225,4235;
	setarray .@card2[0], 4293,4297,4288,4283,4295,4307,4308,4309,4132,4326,
				4341,4335,4337,4345,4344,4331,4333,4332,4089,4161,
				4177,4178,4180,4184,4191,4206,4199,4273,4282,4268,
				4289,4321,4316,4343,4339,4369,4377,4385,4383,4382,
				4380,4381,4378,4379,4390,4389,4388,4391,4405,4400,
				4401,4402,4404,4002,4003,4004,4005,4007,4017,4024,
				4030,4035,4040,4043,4044,4049,4051,4053,4058,4060,
				4062,4063,4064,4065,4068,4069,4070,4072,4073,4074,
				4077,4080,4082,4083,4085,4086,4088,4091,4092,4097,
				4098,4099,4100,4102,4103,4106,4107,4109,4111,4115,
				4117,4118,4120,4124,4125,4126,4138,4139,4141,4151,
				4158,4164,4165,4182,4185,4159,4160,4166,4172,4175,
				4188,4201,4202,4204,4205,4208,4209;
	setarray .@card3[0], 4120,4216,4217,4219,4221,4222,4230,4234,4233,4232,
				4237,4238,4242,4243,4245,4246,4249,4252,4255,4258,
				4262,4264,4276,4270,4271,4218,4239,4251,4253,4269,
				4334,4105,4133,4136,4229,4272,4277,4279,4281,4284,
				4285,4290,4296,4298,4301,4310,4314,4317,4325,4327,
				4328,4329,4338,4340,4346,4347,4348,4349,4350,4351,
				4353,4354,4355,4356,4358,4360,4362,4364,4366,4368,
				4370,4371,4373,4375,4387,4406,4129,4155,4291,4392,
				4393,4394,4409,4410,4411,4412,4413,4414,4415,4416,
				4417,4418,4420,4421,4422,4423,4424,4427,4427,4428,
				4429,4431,4432,4433,4434,4435,4436,4437,4438,4439,
				4440,4442,4443,4444,4445,4447,4448,4449,4450,4452,
				4453;
	setarray .@size_card[1],  getarraysize(.@card1), getarraysize(.@card2), getarraysize(.@card3);
	.@points = oversea_event9;
	if (!.@points) {
		mes "[ Putty ]";
		mes "สวัสดีจ๊ะ";
		mes "ไม่รู้ว่าข้าจะช่วยอะไรเจ้าได้บ้างไหมนะ";
		mes "แต่ข้ากำลังพยายามช่วยเหล่านักรบผ่านศึก";
		mes "ด้วยการรับแลกเปลี่ยนการ์ดต่างๆ";
		mes "ที่พวกเขาไม่ได้ใช้ประโยชน์แล้วน่ะ";
		next;
		switch(select("สอบถามข้อมูลเพิ่มเติม","ตอนนี้ข้ายังไม่มีการ์ดเลย")) {
		case 1:
			mes "[ Putty ]";
			mes "ข้าจะให้ 1 แต้ม สำหรับการ์ดทุกๆ 1 ใบที่เจ้านำมาให้";
			mes "แต้มสะสมเหล่านี้สามารถนำมาแลกไอเทมที่ข้ามีอยู่ได้นะ";
			next;
			mes "[ Putty ]";
			mes "ใช้ ^CC0000100 แต้ม^000000 แลก: ^0000CC1 Old Card Album^000000";
			mes "ใช้ ^CC000050 แต้ม^000000 แลก: ^0000CC20 Yggdrasil Berry^000000";
			mes "ใช้ ^CC000020 แต้ม^000000 แลก: ^0000CC10 Blue Potion^000000";
			mes "ใช้ ^CC00001 แต้ม^000000 แลก: ^0000CC4 Honey^000000";
			next;
			break;
		case 2:
			mes "[ Putty ]";
			mes "จดจำข้อเสนอนี้ไว้ให้ดีล่ะ ถ้าเปลี่ยนใจก็แวะมาหาข้าได้นะ!";
			close;
		}
	} else {
		mes "[ Putty ]";
		mes "ยินดีต้อนรับกลับมา!";
		mes "ดูเหมือนเจ้าจะมีแต้มสะสมอยู่บ้างแล้วนะ";
		mes "ตอนนี้เจ้ามีแต้มสะสมอยู่ทั้งหมด ^00cc00" + .@points + "^000000 แต้มจ๊ะ";
		next;
		mes "[ Putty ]";
		mes "เจ้าต้องการจะใช้แต้มแลกของตอนนี้เลย";
		mes "หรือว่าอยากจะเอาการ์ดมาแลกแต้มเพิ่มล่ะ?";
		next;
	}
	switch(select("ข้าต้องการเอาการ์ดมาแลกแต้ม","ข้าต้องการใช้แต้มแลกไอเทม")) {
	case 1:
		mes "[ Putty ]";
		mes "กรุณาบอกชื่อการ์ดที่เจ้าต้องการจะนำมาแลกหน่อยสิ";
		next;
		input .@i$;
		.@input$ = "_"+.@i$;
		freeloop(1);
		for(.@j = 1; .@j<4; .@j++) {
			for(.@i = 0; .@i<.@size_card[.@j]; .@i++)
				if (compare(.@input$,"_"+getitemname(getd(".@card"+.@j+"["+.@i+"]")))) {
					.@card = getd(".@card"+.@j+"["+.@i+"]");
					break;
				}
			sleep2 10; 
			if (.@card) break;
		}
		freeloop(0);
		mes "[ Putty ]";
		if (!.@card) mes "ถ้าเจ้าเปลี่ยนใจอยากแลกการ์ดมอนสเตอร์เมื่อไหร่ ก็กลับมาหาข้าได้เสมอนะ";
		else {
			.@count = countitem(.@card);
			if (!.@count) {
				mes "เจ้าไม่มีการ์ด ^0055FF"+getitemname(.@card)+"^000000 อยู่กับตัวเลยนะ!";
				close;
			}
			mes "เจ้ามีการ์ด ^0055FF"+getitemname(.@card)+"^000000 ทั้งหมด "+.@count+" ใบ";
			mes " ";
			mes "เจ้าต้องการแลกพวกมันเป็นแต้ม (1 แต้มต่อ 1 ใบ) เลยหรือไม่?";
			next;
			switch(select("ตกลง แลกเลย!","ไม่ล่ะ ขอบคุณ")) {
				case 1:
					delitem .@card,.@count;
					oversea_event9 = (.@points+.@count);
					mes "[ Putty ]";
					mes "เรียบร้อยจ๊ะ เจ้าได้รับแต้มเพิ่มมา ^CC0000"+.@count+"^000000 แต้มนะ";
					break;
				case 2:
					mes "[ Putty ]";
					mes "เข้าใจแล้วจ๊ะ บอกข้านะถ้ามีอะไรให้ข้าช่วยอีก";
					break;
			}
		}
		close;
	case 2:
		if (.@points) {
			mes "[ Putty ]";
			mes "นี่คือไอเทมทั้งหมดที่ข้ามีอยู่ในตอนนี้";
			next;
			switch(select("1 Old Card Album - 100 แต้ม","20 Yggdrasil Berry - 50 แต้ม","10 Blue Potion - 20 แต้ม","4 Honey - 1 แต้ม","^777777ไว้วันหลังนะ^000000")) {
				case 1:
					if (.@points < 100) break;
					mes "[ Putty ]";
					mes "วิเศษเลย! ข้าขอให้เจ้าโชคดี";
					mes "กับการเปิดสมุดการ์ดเล่มนี้นะ";
					mes "ข้าสังหรณ์ใจว่าเล่มนี้ต้องมีการ์ดดีๆ แน่นอน!";
					oversea_event9 = .@points - 100;
					getitem 616,1;
					close;
				case 2:
					if (.@points < 50) break;
					mes "[ Putty ]";
					mes "โอ้ เจ้าคงจะชอบการผจญภัยสินะ";
					mes "นี่จ๊ะ... สิ่งนี้ช่วยเจ้าได้แน่นอน!";
					oversea_event9 = .@points - 50;
					getitem 607,20;
					close;
				case 3:
					if (.@points < 20) break;
					mes "[ Putty ]";
					mes "Blue Potions งั้นเหรอ? แน่ใจนะ?";
					mes "ถ้าเจ้าต้องการแบบนั้น... นี่จ๊ะ ได้แล้ว!";
					oversea_event9 = .@points - 20;
					getitem 505,10;
					close;
				case 4:
					if (.@points < 1) break;
					mes "[ Putty ]";
					mes "พวกผึ้งต้องทำงานกันหนักมากเลยนะ";
					mes "กว่าจะได้น้ำผึ้งพวกนี้มา";
					mes "ใช้มันให้คุ้มค่าล่ะ";
					oversea_event9 = .@points - 1;
					getitem 518,4;
					close;
				case 5:
					mes "[ Putty ]";
					mes "ตกลงจ๊ะ แวะมาใหม่ตอนที่มีแต้มมากกว่านี้ได้นะ";
					close;
				}
			mes "[ Putty ]";
			mes "ขอโทษทีนะจ๊ะ ดูเหมือนเจ้าจะมีแต้มไม่พอแลกไอเทมชิ้นนี้";
			close;
		} else {
			mes "[ Putty ]";
			mes "เจ้าไม่มีแต้มสะสมเลย (0 แต้ม) เจ้าต้องมีอย่างน้อย 1 แต้มถึงจะแลกได้นะ";
			close;
		}
	}
}
