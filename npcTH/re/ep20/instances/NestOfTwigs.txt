// ภฮฤฺต๙ภป EUC-KRทฮ ภฏม๖วฯฑโ ภงวั วัฑนพ๎ นฎภๅ.
//===== rAthena Script =======================================
//= Episode 20: Immortals
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- Instance Nest of Twigs from Episode 20 (main quest)
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Main quest - Step 44 (instance NestOfTwigs)
jor_twice,164,244,5	script(CLOAKED)	Lehar#e20pn05::ep20_jor_twice_164_244	4_EP19_LEHAR,{
	if (ep20_main < 42)
		end;
	if (ep20_main == 42) {
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "Lazy กำลังทำอะไรอยู่คนเดียวตรงนั้นน่ะ? มีอะไรอยู่ระหว่างน้ำแข็งงั้นเหรอ?";
		close3;
	}
	// note: (debug) ep20_main == 44 is custom (need check on official)
	// หมายเหตุ: (debug) ep20_main == 44 เป็นค่าที่กำหนดเอง (ต้องตรวจสอบกับเซิร์ฟเวอร์ทางการอีกครั้ง)
	if (ep20_main == 43 || ep20_main == 44) {
		if (is_party_leader() == false) {
			cutin "ep19_lehar01.png",2;
			mes "[Lehar]";
			mes "อ๊ะ นักผจญภัย! ถ้าคุณต้องการจะไป ให้สร้างปาร์ตี้คนเดียวแล้วมาคุยกับฉันอีกครั้งนะ!";
			close3;
		}
		.@md_name$ = "Nest of Twigs";
		
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "" + strcharinfo(0) + "! ฉันกำลังรีบ ตามฉันมาเพื่อตามหา Leon และ Aurelie เร็วเข้า!";
		next;
		switch( select( "พร้อมจะสำรวจ " + .@md_name$ + "", "เข้าสู่ " + .@md_name$ + "", "รอสักครู่..." ) ) {
		case 1:
			cutin "ep19_lehar01.png",2;
			mes "[Lehar]";
			mes "ถ้าคุณพร้อมแล้ว เรามาเริ่มตามไปกันเลยไหม? เฮ้อ ฉันล่ะประหม่าจริงๆ เพราะคาดเดาไม่ได้เลยว่าจะเกิดอะไรขึ้น";
			if (is_party_leader() == true)
				instance_create(.@md_name$);
			close3;
		case 2:
			if (is_party_leader() == false)
				end;
			switch( instance_enter(.@md_name$) ) {
			case IE_OTHER:
				mes "^ff0000เกิดข้อผิดพลาดที่ไม่ทราบสาเหตุ^000000";
				close;
			case IE_NOINSTANCE:
			case IE_NOMEMBER:
				mes "[Lehar]";
				mes "คุณทำเป็นไม่สนใจได้ยังไงกัน?";
				mes "ฉันกำลังรีบมากนะ!";
				next;
				mes "[Lehar]";
				mes "เมื่อคุณพร้อม คุณควรมาคุยกับฉันโดยไม่ชักช้า เข้าใจไหม? ตกลงไหม?";
				close3;
			case IE_OK:
				// warp "1@twig",209,176;
				end;
			}
			end;
		case 3:
			cutin "ep19_lehar01.png",2;
			mes "[Lehar]";
			mes "เดี๋ยวก่อนนะ ฉันลืมอะไรบางอย่างไป รอสักครู่แล้วค่อยมาคุยกับฉันใหม่!";
			close3;
		}
		end;
	}
	if (ep20_main == 45) {
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "ดีจังเลยที่เราสามารถทำบางอย่างได้แล้ว จริงไหม?";
		close3;
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(23106) == 1" );
	end;
}


1@twig,209,176,0	script	#ep20_twig1	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	if (is_party_leader() == false)
		end;
	if ('step == 0) {
		'step = 1;
		enablenpc instance_npcname("ep20_1twig_207_178_A");
		end;
	}
	end;
OnInstanceInit:
	'step = 0;
	'map_twig$ = instance_mapname("1@twig");
	end;
}

1@twig,66,181,3	duplicate(dummy_disabled_npc)	Jormungand Guardian#twig::ep20_1twig_66_181	EP20_JORMUNGUARD1
1@twig,62,182,3	duplicate(dummy_disabled_npc)	Jormungand Guardian#twig::ep20_1twig_62_182	EP20_JORMUNGUARD2
1@twig,67,177,3	duplicate(dummy_disabled_npc)	Jormungand Guardian#twig::ep20_1twig_67_177	EP20_JORMUNGUARD2
1@twig,135,65,3	duplicate(dummy_disabled_npc)	Jormungand Guardian#twig::ep20_1twig_135_65	EP20_JORMUNGUARD2
1@twig,135,61,3	duplicate(dummy_disabled_npc)	Jormungand Guardian#twig::ep20_1twig_135_61	EP20_JORMUNGUARD1
1@twig,135,69,3	duplicate(dummy_disabled_npc)	Jormungand Guardian#twig::ep20_1twig_135_69	EP20_JORMUNGUARD2


1@twig,207,178,5	script(DISABLED)	Lehar#ep20twigtemp::ep20_1twig_207_178_A	EP19_NPC_LEHAR,{
	if (is_party_leader() == false)
		end;
	if ('step != 1)
		end;
	cutin "ep19_lehar05.png",2;
	mes "[Lehar]";
	mes "ว้าว นี่มันเรื่องอะไรกันเนี่ย? นี่เป็นครั้งแรกเลยที่ฉันได้เห็นภูมิประเทศแบบนี้...";
	next;
	cutin "ep19_lehar01.png",2;
	mes "[Lehar]";
	mes "ฉันเห็นรอยเท้าอยู่ตรงนั้น ดูเหมือน Leon และ Aurelie จะไปตามถนนสายนี้ มาพยายามกันให้เต็มที่เถอะ!";
	specialeffect2 EF_POTION8;
	sc_start SC_INCREASEAGI,240000,10;
	next;
	mes "[Lehar]";
	mes "ฉันทำให้เท้าของคุณเบาขึ้นแล้ว รีบไปกันเถอะ!";
	close2;
	cutin "",255;
	if ('step == 1) {
		'step = 2;
		disablenpc();
		donpcevent instance_npcname("ep20_1twig_207_178_B") + "::OnMove_1";	// move 1 (เคลื่อนที่ครั้งที่ 1)
	}
	end;
}


1@twig,207,178,5	script(DISABLED)	Lehar#e20twig::ep20_1twig_207_178_B	EP19_NPC_LEHAR,{
	if (is_party_leader() == false)
		end;
	if ('step < 3)
		end;
	if ('step == 3) {	// spawn 1 (เกิดครั้งที่ 1)
		'step = 4;
		donpcevent instance_npcname("ep20_1twig_207_178_B") + "::OnSpawn_1";
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "เอ๋ นั่นมันตัวอะไรกันน่ะ...? มันยืนอยู่เหมือนพยายามจะขัดขวางพวกเรา...";
		close2;
		npctalk "Lehar: หือ? อะไรนะ?";
		cutin "",255;
		end;
	}
	if ('step == 4) {
		npctalk "Lehar: นักผจญภัย ตอนนี้ไม่ใช่เวลามาคุยกันนะ!";
		end;
	}
	if ('step == 5) {	// move 2 (เคลื่อนที่ครั้งที่ 2)
		'step = 6;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "ฉันคิดว่าพวกมันจะจู่โจมกะทันหันซะอีก! ตกใจหมดเลย ศัตรูอยู่เต็มไปหมด... เรามาเริ่มกันอีกครั้งไหม?";
		specialeffect2 EF_POTION8;
		sc_start SC_INCREASEAGI,240000,10;
		close2;
		if ('step == 6)
			donpcevent instance_npcname("ep20_1twig_207_178_B") + "::OnMove_2";
		cutin "",255;
		end;
	}
	if ('step == 6)	// (npc is moving) (NPC กำลังเคลื่อนที่)
		end;
	if ('step == 7) {	// spawn 2 (เกิดครั้งที่ 2)
		'step = 8;
		donpcevent instance_npcname("ep20_1twig_207_178_B") + "::OnSpawn_2";
		cutin "ep19_lehar04.png",2;
		mes "[Lehar]";
		mes "เอ่อ พวกนั้นคือพวกเดียวกับเมื่อกี้หรือเปล่า? มาจัดการพวกมันอีกครั้งแล้วไปต่อกันเถอะ!";
		close3;
	}
	if ('step == 8) {
		npctalk "Lehar: นักผจญภัย ยังมีพวกที่ต้องสั่งสอนอยู่อีกนะ!";
		end;
	}
	if ('step == 9) {	// move 3 (เคลื่อนที่ครั้งที่ 3)
		'step = 10;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "Leon และ Aurelie ผ่านช่องว่างนี้ไปได้ยังไงกันนะ...?";
		specialeffect2 EF_POTION8;
		sc_start SC_INCREASEAGI,240000,10;
		close2;
		if ('step == 10)
			donpcevent instance_npcname("ep20_1twig_207_178_B") + "::OnMove_3";
		cutin "",255;
		end;
	}
	if ('step == 10)	// (npc is moving) (NPC กำลังเคลื่อนที่)
		end;
	cutin "ep19_lehar01.png",2;
	mes "[Lehar]";
	mes "ฉันเคยคิดเรื่องนี้มาก่อนนะ แต่ฉันไม่คิดว่ามันจะง่ายเลยสำหรับมนุษย์ที่จะเห็นพ้องกับพระเจ้า";
	next;
	cutin "ep19_lehar02.png",2;
	mes "[Lehar]";
	mes "เหะ ฉันพูดอะไรที่มันชัดเจนเกินไปหรือเปล่า? ลืมมันไปเถอะ!";
	close3;

OnMove_1:
	enablenpc();
	enablenpc instance_npcname("ep20_1twig_66_181");
	enablenpc instance_npcname("ep20_1twig_62_182");
	enablenpc instance_npcname("ep20_1twig_67_177");

	npcspeed 150;
	'npc_gid = getnpcid(0);
	'npc_name$ = instance_npcname("ep20_1twig_207_178_B");
	'move[0] = 0;
	donpcevent 'npc_name$ + "::OnMove_1_ByStep";
	end;
OnMove_1_ByStep:
	setarray .@coord[0],
		197,188,
		187,198,
		177,208,
		167,211,
		157,216,	// npctalk
		147,216,
		137,217,
		124,215,
		114,213,
		104,210,
		 94,204,
		 84,194,
		 74,184;
	.@size = getarraysize(.@coord) / 2;

	if ('move[0] == 4)
		npctalk "Lehar: ตรงนี้ดูน่าทึ่งมากเลย!";
	else if ('move[0] == .@size) {
		npctalk "Lehar: หือ มีอะไรแปลกๆ ขวางหน้าอยู่งั้นเหรอ?";
		'step = 3;	// move final (เคลื่อนที่เสร็จสิ้น)
		end;
	}
	.@r = 'move[0] * 2;
	unitwalk 'npc_gid, .@coord[.@r],.@coord[.@r+1], 'npc_name$ + "::OnMove_1_ByStep";
	'move[0]++;
	end;
OnSpawn_1:
	disablenpc instance_npcname("ep20_1twig_66_181");
	disablenpc instance_npcname("ep20_1twig_62_182");
	disablenpc instance_npcname("ep20_1twig_67_177");
	monster 'map_twig$,66,181,"--ja--","EP20_JORMUNGUARD1",1, 'npc_name$ + "::OnDead_1";
	setunitdata $@mobid[0], UMOB_HP, 10;
	monster 'map_twig$,62,182,"--ja--","EP20_JORMUNGUARD2",1, 'npc_name$ + "::OnDead_1";
	setunitdata $@mobid[0], UMOB_HP, 10;
	monster 'map_twig$,67,177,"--ja--","EP20_JORMUNGUARD2",1, 'npc_name$ + "::OnDead_1";
	setunitdata $@mobid[0], UMOB_HP, 10;
	end;
OnDead_1:
	if (mobcount( 'map_twig$, 'npc_name$ + "::OnDead_1" ) > 0)
		end;
	npctalk "Lehar: คุณพยายามได้ดีมาก!";
	'step = 5;
	end;

OnMove_2:
	enablenpc instance_npcname("ep20_1twig_135_65");
	enablenpc instance_npcname("ep20_1twig_135_61");
	enablenpc instance_npcname("ep20_1twig_135_69");
	'move[1] = 0;
	donpcevent 'npc_name$ + "::OnMove_2_ByStep";
	end;
OnMove_2_ByStep:
	setarray .@coord[0],
		64,174,
		57,164,
		56,154,
		56,144,
		56,134,
		60,124,
		61,114,	// npctalk
		68,104,
		78,94,
		88,84,
		98,74,
		108,67,
		118,65,
		128,65;
	.@size = getarraysize(.@coord) / 2;

	if ('move[1] == 6)
		npctalk "Lehar: ทำไม... ทำไมมันยาวแบบนี้ล่ะ?";
	else if ('move[1] == .@size) {
		npctalk "Lehar: ไม่นะ คุณมาอยู่ที่นี่อีกแล้วเหรอ?";
		'step = 7;	// move final (เคลื่อนที่เสร็จสิ้น)
		end;
	}
	.@r = 'move[1] * 2;
	unitwalk 'npc_gid, .@coord[.@r],.@coord[.@r+1], 'npc_name$ + "::OnMove_2_ByStep";
	'move[1]++;
	end;
OnSpawn_2:
	disablenpc instance_npcname("ep20_1twig_135_65");
	disablenpc instance_npcname("ep20_1twig_135_61");
	disablenpc instance_npcname("ep20_1twig_135_69");
	monster 'map_twig$,135,61,"--ja--","EP20_JORMUNGUARD1",1, 'npc_name$ + "::OnDead_2";
	setunitdata $@mobid[0], UMOB_HP, 10;
	monster 'map_twig$,135,69,"--ja--","EP20_JORMUNGUARD1",1, 'npc_name$ + "::OnDead_2";
	setunitdata $@mobid[0], UMOB_HP, 10;
	monster 'map_twig$,135,65,"--ja--","EP20_JORMUNGUARD2",1, 'npc_name$ + "::OnDead_2";
	setunitdata $@mobid[0], UMOB_HP, 10;
	end;
OnDead_2:
	if (mobcount( 'map_twig$, 'npc_name$ + "::OnDead_2" ) > 0)
		end;
	npctalk "Lehar: คุณพยายามได้ดีมาก!";
	'step = 9;
	end;

OnMove_3:
	'move[2] = 0;
	donpcevent 'npc_name$ + "::OnMove_3_ByStep";
	enablenpc instance_npcname("ep20_1twig_117_151");
	enablenpc instance_npcname("ep20_1twig_116_145");
	enablenpc instance_npcname("ep20_1twig_119_149");
	end;
OnMove_3_ByStep:
	setarray .@coord[0],
		138,65,
		148,72,
		158,78,
		166,88,
		173,98,
		178,108,
		179,118,
		176,128,
		168,138,
		158,148,
		148,154,
		138,155,
		128,158;
	.@size = getarraysize(.@coord) / 2;
	
	if ('move[2] == .@size) {
		'step = 11;	// move final (เคลื่อนที่เสร็จสิ้น)
		end;
	}
	.@r = 'move[2] * 2;
	unitwalk 'npc_gid, .@coord[.@r],.@coord[.@r+1], 'npc_name$ + "::OnMove_3_ByStep";
	'move[2]++;
	end;
}

// unknown effect
// 1@twig,121,156,0	script	#ep20_twig2	HIDDEN_WARP_NPC,2,2
// เอฟเฟกต์ที่ไม่ทราบแน่ชัด

1@twig,117,151,3	script(DISABLED)	Aurelie#e20pn06::ep20_1twig_117_151	4_EP19_AURELIE,{
	cutin "ep19_aurelie01.png",2;
	mes "[Aurelie]";
	mes "มันเป็นเรื่องธรรมดาที่เราหาไม่เจอ เพราะเรากางบาเรียแบบนี้ไว้ โครงสร้างของพลังเวทมนตร์นี้มัน...";
	next;
	mes "[Aurelie]";
	mes "(เธอพึมพำ หมกมุ่นอยู่กับความคิดของตัวเอง)";
	close3;
}

1@twig,116,145,7	script(DISABLED)	White cat#e20pn06::ep20_1twig_116_145	4_EP20_NYAR,{
	cutin "ep20_nyar04.png",2;
	mes "[White cat]";
	mes "สุดยอดไปเลย";
	close3;
}

1@twig,119,149,3	script(DISABLED)	Leon#e20pn06::ep20_1twig_119_149	4_EP19_LEON,{
	if ('step < 11)
		end;
	if ('step == 11) {
		if (is_party_leader() == false)
			end;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "อา ในที่สุดก็เจอจนได้ Leon และ Aurelie!";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "อา Lehar ฉันประหลาดใจนะที่ทุกคนหายไปหมด ฉันรออยู่เลยล่ะ";
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "นั่นแหละคือสิ่งที่เรากำลังพูดถึง! เขาจากไปโดยไม่พูดสักคำ! แถมเดินเร็วมากด้วย!";
		next;
		mes "[Lehar]";
		mes "คุณมาที่นี่ได้ยังไงในเมื่อมีวัตถุประหลาดโจมตีคุณอยู่?";
		next;
		cutin "ep19_leon04.png",2;
		mes "[Leon]";
		mes "หืม? เป็นไปไม่ได้... ทุกคนไม่สังเกตเหรอ? มันคือพลังของ Jormungand มันทำร้ายเราไม่ได้หรอก...";
		next;
		cutin "ep19_leon05.png",2;
		mes "[Leon]";
		mes "...อา พับผ่าสิ";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "ขอโทษที ฉันสัมผัสได้ถึงเจตจำนงของ Jormungandr รุนแรงมากจนฉันเข้าใจผิดคิดว่ามันรวมถึงพวกเราทุกคนด้วย";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "ฉันไม่ได้พลาดอะไรไปหรอก ไม่เป็นไร ช่วยอธิบายหน่อยว่าเกิดอะไรขึ้น ได้โปรด!";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "เอาล่ะ เริ่มต้นเลยนะ เราต้องปกป้องสถานที่แห่งนี้และต้นไม้โลกที่นี่ไว้";
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "...ท่าน Leon ความคืบมันหน้าเร็วเกินไปแล้ว! ฉันไม่เข้าใจเลยสักนิด ช่วยอธิบายทีละขั้นตอนได้ไหม?";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "โอ้ งั้นเหรอ... ขอเวลาให้ฉันรวบรวมความคิดสักครู่ได้ไหม? เราจะบอกสิ่งที่เราได้ยินจากที่นี่ให้คุณฟัง";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "มันไม่ใช่เรื่องแปลกเลยที่จะแบ่งปันพลังเวทมนตร์ของพระเจ้าแล้วกลายเป็นผู้พิทักษ์...";
		if ('step == 11)
			'step = 12;
		close3;
	}
	if ('step == 12) {
		if (checkweight(1001217,20) == 0) {	// custom
			mes "การสนทนาไม่สามารถดำเนินต่อได้เนื่องจากคุณมีไอเทมมากเกินไป";
			mes "กรุณาจัดการไอเทมของคุณแล้วลองใหม่อีกครั้ง";
			close;
		}
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "เอาล่ะ มาคุยกันตั้งแต่เริ่มต้น Aurelie คุณช่วยฉันหน่อยได้ไหม?";
		next;
		cutin "ep19_aurelie01.png",2;
		mes "[Aurelie]";
		mes "อย่างที่ฉันเคยบอกไป Leon และฉันคือผู้พิทักษ์แห่ง Isgard แทนที่จะปกป้อง Isgard เรากลับได้รับพรจาก Jormungandr และมีชีวิตอยู่ตลอดกาล นั่นคือเหตุผลที่เราไม่อาจเพิกเฉยต่อพลังเวทมนตร์ของ Jormungandr ได้";
		next;
		mes "[Aurelie]";
		mes "ฉันได้ยินมาว่าพวก Iwin ได้พบร่องรอยของสัตว์ที่สันนิษฐานว่าเป็น Jormungandan จนถึงตอนนั้น... แต่มันไม่เคยปรากฏตัวจริงๆ เลย เมื่อเห็นสิ่งที่อยู่ตรงหน้าเราตอนนี้ สิ่งต่างๆ อาจจะแย่กว่าที่เราคิดไว้ก็ได้";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "เราไม่ใช่กลุ่มเดียวที่สัมผัสได้ถึงเวทมนตร์ของ Jormungand ในดินแดนนี้ พวก Rgan แห่งคริสตจักร Jormungand ก็กำเนิดมาจากเวทมนตร์ของ Jormungand เช่นกัน ดังนั้นพวกเขาควรจะสัมผัสได้เหมือนกัน";
		next;
		mes "[Leon]";
		mes "คุณบอกว่าเจอพวกนั้นแถวนี้เหรอ? ถ้าอย่างนั้น ฉันก็ไม่รู้ว่าพวกเขาสัมผัสได้ถึงสิ่งเดียวกันก่อนเราก้าวหนึ่งและกำลังพยายามหาต้นตออยู่หรือเปล่า เราไม่ควรจะล่าช้าไปกว่านี้";
		next;
		cutin "ep19_aurelie01.png",2;
		mes "[Aurelie]";
		mes "สถานที่นี้คือศูนย์กลางของบาเรียแห่ง Isgard ที่ยึดเหนี่ยวกิ่งก้านของต้นไม้โลกเอาไว้ ตอนนี้มันถูกปกคลุมด้วยโล่ป้องกัน แต่ถ้าคริสตจักร Jormungand สัมผัสได้ถึงบางอย่าง มันก็แค่เรื่องของเวลาก่อนที่พวกเขาจะบุกรุกเข้ามาที่นี่";
		next;
		mes "[Aurelie]";
		mes "หากสถานที่ศักดิ์สิทธิ์นี้ถูกทำลาย บาเรียของ Isgard จะพังทลาย และ Lasgand ที่ถูกจองจำอยู่ในทวีปนี้ จะสามารถไปถึง Midgard ได้";
		next;
		mes "[Aurelie]";
		mes "ดังนั้นเราต้องอยู่ที่นี่ เข้าใจเรื่องนี้ใช่ไหม?";
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "ถ้าคุณบอกว่าให้ปกป้อง... จะปกป้องยังไงให้ชัดเจนล่ะ?";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "มันคงจะดีถ้าสามารถเพิ่มความแข็งแกร่งให้บาเรียนี้ได้ แต่ปัญหาคือจะใช้พลังเวทมนตร์ที่ไหน Aurelie และฉันควรทำในสิ่งที่เราทำได้";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "ถ้าอย่างนั้น.... จนกว่าเราจะพบวิธี ทำไมเราไม่ทำให้มันดูเรียบง่ายล่ะ?";
		next;
		cutin "ep19_lehar02.png",2;
		mes "[Lehar]";
		mes "ทั้งฉันและนักผจญภัยไม่สามารถช่วยเรื่องบาเรียได้โดยตรง แต่อย่างน้อยถ้าป้องกันไม่ให้มีการสืบสวนเกิดขึ้นมันก็น่าจะมีประสิทธิภาพไม่ใช่เหรอ? เราจะซัดพวก Rgan ทุกตัวที่เข้ามาแล้วส่งพวกมันกลับไปซะ!";
		npctalk "" + strcharinfo(0) + " : คุณพูดจริงเหรอ?", "";
		next;
		cutin "ep19_leon01.png",2;
		mes "[Leon]";
		mes "อืม แน่นอน ฉันคิดว่านั่นคือสิ่งที่ดีที่สุดในตอนนี้ ในขณะที่คุณกำลังทำงาน เราจะพยายามหาวิธีที่ดีกว่านี้";
		npctalk "" + strcharinfo(0) + " : จริงเหรอ? เอาแบบนี้เลยเหรอ?", "";
		next;
		cutin "ep19_aurelie01.png",2;
		mes "[Aurelie]";
		mes "คุณควรทำตามที่ Lehar บอกนะ Jormungand ก็ไม่ต้องการให้พวก Rgan ทำลายเกราะป้องกันและเก็บเกี่ยวเวทมนตร์จากพื้นที่นี้เช่นกัน พวกเขาไม่ได้ทำอันตรายต่อดินแดนนี้เลย...";
		next;
		mes "[Aurelie]";
		mes "ถ้าอย่างนั้น เราออกจากที่นี่กันเถอะ คนที่รออยู่ข้างนอกก็ควรจะรู้สถานการณ์นี้ด้วย";
		if ('step == 12 && ep20_main == 43) {
			ep20_main = 44;
			erasequest 23106;
			setquest 23108;
			getitem 1001217,10;	// Ep20_Cat_Whiskers
			getexp2 136611959,0;
			getexp 0,22271670;
			add_reputation_points( REPU_EP20, 70 );
		}
		close2;
		cutin "",255;
		warp "jor_safty1",331,150;
		end;
	}
	// debug
	warp "jor_safty1",331,150;
	end;
}
