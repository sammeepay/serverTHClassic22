//===== rAthena Script =======================================
//= Episode 16.2 - Illusion Enchants
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Illusion series related merchants and enchanters
// Note:
// - Some dialog are missing in Illusion of Luanda exchange NPC.
//===== Changelog: ===========================================
//= 1.0 Initial release of Illusion of Moonlight [crazyarashi]
//= 1.1 Cleanup and improvements [Everade]
//= 1.2 Added Illusion of Vampire enchanter [Capuche]
//= 1.3 Added Illusion of Frozen enchanter [Capuche]
//= 1.4 Added Illusion of Turtle enchanter [Capuche]
//= 1.5 Added Illusion of Luanda enchanter [Capuche]
//= 1.6 Added Illusion of Underwater exchange [Capuche]
//= 1.7 Added Illusion of Twins enchanter [Capuche]
//= 1.8 Added Illusion Merchant [Haydrich]
//= 1.9 Added Illusion of Teddy Bear exchange [Atemo]
//============================================================

//============================================================
//= Illusion of Moonlight
//============================================================
pay_d03_i,160,45,3	script	Gemcutter#ilp20	4_TOWER_17,3,3,{
	mes "[ Gemcutter ]";
	mes "คุณมีธุระอะไรกับฉันงั้นรึ?";
	next;
	switch( select( "คุณกำลังทำอะไรที่นี่?", "อัปเกรดอุปกรณ์" ) ) {
	case 1:
		mes "[ Gemcutter ]";
		mes "ฉันเดินทางมายังหมู่บ้านที่ถูกทำลายแห่งนี้,";
		mes "เพื่อตามหาวัตถุดิบบางอย่าง แต่ดันไปสัมผัสกับแสงประหลาดเข้า แล้วจู่ๆ ก็มาโผล่ที่นี่";
		next;
		mes "[ Gemcutter ]";
		mes "ฉันเลยตัดสินใจว่าจะอยู่ที่นี่ต่ออีกสักพัก คิดว่าถ้าอยู่ใกล้ๆ กับทหารคนนี้ก็น่าจะปลอดภัยดี";
		mes "ฉันมีข้อเสนอให้คุณ ฉันต้องการวัตถุดิบบางอย่างที่หาได้จากในที่แห่งนี้เท่านั้น";
		next;
		mes "[ Gemcutter ]";
		mes "ไปหาพวกมันมาให้ฉัน แล้วฉันจะปรับปรุงอุปกรณ์ให้เป็นการตอบแทน แต่ขอบอกไว้ก่อนนะว่าฉันปรับปรุงได้แค่บางประเภทเท่านั้น";
		next;
		mes "[ Gemcutter ]";
		mes "ถ้าคุณสนใจ เรามาคุยรายละเอียดเกี่ยวกับข้อตกลงของเรากันได้";
		close;
	case 2:
		mes "[ Gemcutter ]";
		mes "ตรวจสอบให้แน่ใจว่า ^0000FFอุปกรณ์ของคุณถูกตีบวกมาอย่างน้อย +9^000000 ก่อนจะนำมาให้ฉัน";
		mes "นั่นคือเงื่อนไขขั้นต่ำสำหรับบริการอัปเกรดของฉัน เพื่อให้อุปกรณ์ของคุณแสดงผลลัพธ์ที่เห็นได้ชัดเจน";
		next;
		mes "[ Gemcutter ]";
		mes "ตรวจสอบให้ดีว่าคุณได้ ^0000FFสวมใส่ไอเทมที่ต้องการจะปรับปรุงไว้แล้ว^000000";
		mes "ไม่อย่างนั้น ฉันจะไม่สามารถประเมินสภาพของมันได้";
		next;
		mes "[ Gemcutter ]";
		mes "อย่างที่คุณน่าจะเดาได้ อุปกรณ์ของคุณจะเปลี่ยนสภาพไปเป็นสิ่งใหม่หลังจากนี้";
		mes "พูดอีกอย่างก็คือ ^0000FFมันจะสูญเสียระดับการตีบวกเดิม, การ์ด และออปชั่นเสริมพลังทั้งหมดไป^000000";
		next;
		mes "[ Gemcutter ]";
		mes "และฉันต้องการ ^0000FFIllusion Stone และวัตถุดิบอื่นๆ^000000 เพื่ออัปเกรดอุปกรณ์ของคุณ";
		mes "เลือกไอเทมที่คุณต้องการ แล้วฉันจะบอกเองว่าต้องใช้อะไรบ้าง";
		close2;
		callshop( "barter_ill_moonlight" );
		end;
	}
	end;

OnTouch:
	if (illusion_moonlight > 7)
		npctalk "ที่นี่มันคือที่ไหนกันแน่?", "", bc_self;
	end;
}


//============================================================
//= Illusion of Vampire
//============================================================
gef_dun01,139,228,3	script	Great Merchant#illgef	4_M_HUMERCHANT,{
	mes "[Great Merchant]";
	mes "เหล่านักผจญภัย คุณมี ^0000cdIllusion Stone^000000 ไหม? หากคุณมี ^0000cdอาวุธ, ชุดเกราะ^000000 หรือ ^0000cdเครื่องประดับที่มีระดับการตีบวกตั้งแต่ +9 ขึ้นไป^000000, ";
	mes "ฉันสามารถแลกเปลี่ยนมันให้เป็นของที่ดีกว่าเดิมได้ โดยใช้ Illusion Stone และวัตถุดิบอื่นๆ เป็นค่าตอบแทน";
	next;
	mes "[Great Merchant]";
	mes "ว่าไงล่ะ คุณต้องการอะไร?";
	next;
	switch( select( "แลกเปลี่ยนอุปกรณ์ Illusion", "Illusion Stone คืออะไร?", "ฉันสามารถเสริมพลังไอเทมที่แลกมาได้ไหม?", "ยกเลิก" ) ) {
	case 1:
		specialeffect EF_HFLIMOON3;
		emotion ET_BEST;
		mes "[Great Merchant]";
		mes "ใช้มันให้คุ้มค่าล่ะ แล้วถ้าเจอ Illusion Stone เพิ่มอีกก็แวะมาหาฉันได้! *หัวเราะในลำคอ*";
		close2;
		callshop( "barter_ill_vampire" );
		end;
	case 2:
		mes "[Great Merchant]";
		mes "คุณถามว่า ^0000cdIllusion Stone^000000 คืออะไรน่ะเหรอ? อืม... ฉันคิดว่าคงไม่มีใครรู้แน่ชัดหรอกว่ามันคืออะไรกันแน่";
		next;
		mes "[Great Merchant]";
		mes "ฉันรู้แค่ว่าพวกมันหายากและพบได้ในสถานที่พิเศษบางแห่งเท่านั้น และฉันก็มาที่นี่เพื่อรวบรวมพวกมันให้กับลูกค้าของฉัน";
		next;
		mes "[Great Merchant]";
		mes "ลูกค้าของฉันต้องการศึกษาว่าหินพวกนี้คืออะไร พวกเขาจ่ายเงินให้ฉันอย่างงามเลยล่ะ";
		next;
		mes "[Great Merchant]";
		mes "ดังนั้นฉันจึงสามารถเสนอ ^0000cdอุปกรณ์ราคาแพงให้นักผจญภัยอย่างพวกคุณเพื่อแลกกับก้อนหินพวกนี้^000000 และยังคงได้กำไรอยู่";
		next;
		mes "[Great Merchant]";
		mes "แค่นำ ^0000cdอุปกรณ์ที่ตีบวกแล้ว, Illusion Stone^000000 และวัตถุดิบต่างๆ ที่หาได้เฉพาะในที่แห่งนี้มา ฉันจะ ^0000cdอัปเกรดอุปกรณ์, อาวุธ, ชุดเกราะ หรือเครื่องประดับ^000000 ให้กับคุณเอง";
		next;
		mes "[Great Merchant]";
		mes "เรื่องนี้มันได้ประโยชน์ทั้งสองฝ่าย บอกฉันได้เลยถ้าคุณสนใจในข้อเสนอของฉัน";
		close;
	case 3:
		mes "[Great Merchant]";
		mes "โอ้ คุณต้องการเสริมพลังให้อุปกรณ์ที่ได้รับไปสินะ รอบคอบดีนี่ ฉันชอบ!";
		next;
		mes "[Great Merchant]";
		mes "เมื่อไม่นานมานี้ มีนักผจญภัยคนหนึ่งที่ผ่านมาทางนี้บอกฉันว่า มีนักเคมีใน Prontera รับเสริมพลังอุปกรณ์ Illusion เพื่อแลกกับ Illusion Stone อยู่";
		next;
		mes "[Great Merchant]";
		mes "ดูเหมือนว่า Illusion Stone จะเป็นหัวข้อที่ยอดนิยมไปทุกที่เลยนะ ใครๆ ก็อยากรู้ว่าพวกมันคืออะไร";
		next;
		mes "[Great Merchant]";
		mes "ถ้าคุณต้องการเสริมพลังให้อุปกรณ์ของคุณล่ะก็ ให้ไปหา <NAVI>Illusion Enchanter<INFO>prontera,90,115,000,0,</INFO></NAVI> แถวๆ ที่ทำการเมือง (Town Office) นะ";
		close;
	case 4:
		mes "[Great Merchant]";
		mes "เจอของที่ถูกใจบ้างไหม? ถ้าต้องการอะไรก็บอกฉันได้ทุกเมื่อนะ";
		close;
	}
	end;
}

//============================================================
//= Illusion of Frozen
//============================================================

// illusion exchange npc - trade items for illusion gears (NPC แลกเปลี่ยน Illusion - แลกไอเทมเป็นอุปกรณ์ Illusion)
ice_dun02,153,18,3	script	Illusion Stone Research	4_M_ALCHE_B,{
	disable_items;
	mes "[Illusion Stone Researcher]";
	mes "อืม... Illusion Stone นี่มันช่างลึกลับจริงๆ...";
	next;
	switch( select( "คุณกำลังทำอะไรที่นี่?", "อัปเกรดอุปกรณ์", "ยกเลิก" ) ) {
	case 1:
		mes "[Illusion Stone Researcher]";
		mes "ฉันมาที่นี่เพื่อตามหา Illusion Stone ที่จำเป็นสำหรับการวิจัยของฉันน่ะ";
		next;
		mes "[Illusion Stone Researcher]";
		mes "ถ้ำแห่งนี้มันลึกลับมาก แต่ฉันไม่แข็งแกร่งพอที่จะสำรวจมันด้วยตัวคนเดียว";
		mes "ถ้าคุณจะเข้าไปข้างในนั้น คุณช่วยหาของบางอย่างมาให้ฉันหน่อยได้ไหม?";
		next;
		mes "[Illusion Stone Researcher]";
		mes "เพื่อเป็นการแลกเปลี่ยน ฉันจะปรับปรุงอุปกรณ์ให้คุณ";
		mes "ว่าไงล่ะ?";
		close;
	case 2:
		mes "[Illusion Stone Researcher]";
		mes "ตรวจสอบให้แน่ใจว่า ^4d4dffอุปกรณ์ของคุณถูกตีบวกมาอย่างน้อย +9^000000 ก่อนจะนำมาให้ฉัน";
		mes "และต้องมั่นใจว่าคุณ ^4d4dffได้สวมใส่ไอเทมที่ต้องการจะปรับปรุงไว้แล้ว^000000";
		next;
		mes "[Illusion Stone Researcher]";
		mes "อย่างที่คุณน่าจะเดาได้ อุปกรณ์ของคุณจะเปลี่ยนสภาพไปเป็นสิ่งใหม่หลังจากนี้";
		mes "พูดอีกอย่างก็คือ ^4d4dffมันจะสูญเสียระดับการตีบวกและระดับการอัปเกรดเดิมไปทั้งหมด^000000";
		next;
		mes "[Illusion Stone Researcher]";
		mes "และฉันต้องการ ^4d4dffIllusion Stone และวัตถุดิบอื่นๆ^000000 เพื่ออัปเกรดอุปกรณ์ของคุณ";
		mes "เลือกไอเทมที่คุณต้องการ แล้วฉันจะบอกเองว่าต้องใช้อะไรบ้าง";
		close2;
		callshop( "barter_ill_frozen" );
		end;
	case 3:
		mes "[Illusion Stone Researcher]";
		mes "มันหาได้ยากจริงๆ...";
		close;
	}
	end;
}


// illusion enhancing - enhance the illusion gears (การเสริมพลัง Illusion - เสริมพลังอุปกรณ์ Illusion)
// note: unknown enhance rate (หมายเหตุ: ไม่ทราบอัตราการเสริมพลังที่แน่นอน)
prontera,90,115,5	script	Illusion Enchanter#0	4_GEFFEN_09,3,3,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "น้ำหนักเกินแล้ว ฉันควรจะไปเอาของออกจากกระเป๋าก่อนดีกว่า";
		close;
	}
	disable_items;
	if (isbegin_quest(12415) == 0) {
		mes "[Illusion Enchanter]";
		mes "โอ้ คุณมีอุปกรณ์ Illusion งั้นเหรอ?";
		next;
		select("เปล่าเลย ฉันไม่รู้ด้วยซ้ำว่าอุปกรณ์ Illusion คืออะไร");
		mes "[Illusion Enchanter]";
		mes "อุ๊ย ขอโทษที ฉันควรจะแนะนำตัวก่อนสินะ";
		next;
		mes "[Illusion Enchanter]";
		mes "ฉันคือนักเคมีผู้เชี่ยวชาญด้านการศึกษา Illusion Stone ซึ่งกำลังเป็นที่โจษจันในหมู่นักผจญภัย";
		next;
		select("Illusion Stone งั้นเหรอ?");
		mes "[Illusion Enchanter]";
		mes "คุณไม่รู้จริงๆ เหรอนี่ย? มันเป็นเทรนด์ที่ฮอตที่สุดในตอนนี้เลยนะ";
		next;
		select("ฉันไม่สนใจพวกตามเทรนด์หรอก");
		mes "[Illusion Enchanter]";
		mes "คุณคงเคยได้ยินเกี่ยวกับสถานที่แปลกๆ ที่ผุดขึ้นมาที่นั่นที่นี่ในช่วงนี้ Illusion Stone สามารถพบได้ในที่เหล่านั้นเท่านั้น,";
		next;
		mes "[Illusion Enchanter]";
		mes "และโครงสร้างอะตอมของมันไม่เหมือนกับแร่ชนิดไหนที่ฉันเคยเห็นมาเลย *บลา บลา* ...ห่วงโซ่เคมีของพวกมัน... *บลา บลา*";
		next;
		select("(แย่แล้ว เขาเริ่มหลุดโลกไปแล้ว!)");
		mes "[Illusion Enchanter]";
		mes "เอาเป็นว่า ฉันสามารถเสริมพลัง (Enchant) ให้กับอุปกรณ์ที่แลกเปลี่ยนหรือซื้อมาด้วย Illusion Stone ได้ ฉันเรียกพวกมันว่าอุปกรณ์ Illusion";
		next;
		select("(เขาไม่มีเซนส์ในการตั้งชื่อเอาเสียเลย)");
		mes "[Illusion Enchanter]";
		mes "อุปกรณ์เหล่านี้อย่างน้อยก็มีการตอบสนองในเชิงบวกต่อ Illusion Stone หรือมีศักยภาพที่จะถูกเสริมพลังได้สำเร็จ";
		next;
		mes "[Illusion Enchanter]";
		mes "จงไปสำรวจโลกแห่งมายา รวบรวม Illusion Stone และนำมาแลกเปลี่ยนเป็นอุปกรณ์ แล้วฉันจะเสริมพลังให้อุปกรณ์เหล่านั้นให้กับคุณเอง";
		next;
		mes "[Illusion Enchanter]";
		mes "เรื่องนี้มันได้ประโยชน์ทั้งสองฝ่าย ฉันต้องการ Illusion Stone สำหรับงานวิจัยของฉัน และคุณก็ต้องการอุปกรณ์ที่ทรงพลังสำหรับการผจญภัย";
		next;
		mes "[Illusion Enchanter]";
		mes "ที่กล่าวมานั้น บริการของฉันไม่ได้ฟรีนะ มันต้องใช้ Illusion Stone จำนวนหนึ่งและเงิน Zeny";
		next;
		mes "[Illusion Enchanter]";
		mes "ถ้าคุณสนใจ อยากจะสมัครสมาชิกบริการของฉันไหมล่ะ? คุณจะได้ส่วนลดด้วยนะ!";
		next;
		if (select( "แน่นอน!", "ไม่สนใจ" ) == 2) {
			mes "[Illusion Enchanter]";
			mes "ตกลง แต่บอกฉันนะถ้าคุณเปลี่ยนใจ";
			close;
		}
		mes "[Illusion Enchanter]";
		mes "เรียบร้อย ฉันจะเพิ่มคุณเข้าไปในรายชื่อสมาชิก หวังว่าครั้งหน้าที่เจอคุณ คุณจะมีอุปกรณ์ Illusion มาด้วยนะ";
		setquest 12415;
		completequest 12415;
		close;
	}
	mes "[Illusion Enchanter]";
	mes "ฉันสามารถเสริมพลังให้อุปกรณ์ Illusion ได้";
	next;
	switch( select( "ค่าบริการเท่าไหร่?", "เพิ่มสถานะให้อุปกรณ์ Illusion", "รีเซ็ตออปชั่นอุปกรณ์ Illusion" ) ) {
	case 1:
		mes "[Illusion Enchanter]";
		mes "ก่อนอื่น ขอบอกไว้ก่อนว่าฉันเสริมพลังได้แค่อุปกรณ์ Illusion บางประเภทเท่านั้น";
		next;
		mes "[Illusion Enchanter]";
		mes "สามารถเสริมพลังได้สูงสุด 2 ช่อง และการเสริมแต่ละช่องต้องใช้ Illusion Stone 5 ก้อน คุณสามารถรีเซ็ตออปชั่นของอุปกรณ์ได้ตลอดเวลา";
		next;
		mes "[Illusion Enchanter]";
		mes "การเสริมพลัง (Enchant) จะไม่มีวันล้มเหลว แต่การรีเซ็ตนั้นบางครั้งอาจล้มเหลวได้ แม้แต่วิทยาศาสตร์สมัยใหม่ก็ยังเปลี่ยนแปลงความจริงข้อนี้ไม่ได้";
		next;
		mes "[Illusion Enchanter]";
		mes "การรีเซ็ตต้องใช้จำนวน Zeny ที่แตกต่างกันไปขึ้นอยู่กับโอกาสสำเร็จที่คุณเลือก เลือกเมนูดูสิถ้าคุณอยากเห็นตัวเลือกการชำระเงินที่มี";
		close;
	case 2:
		.@reset = false;
		break;
	case 3:
		.@reset = true;
		break;
	}
	setarray .@location[0], EQI_HAND_R, EQI_ARMOR, EQI_SHOES, EQI_GARMENT, EQI_ACC_R, EQI_ACC_L, EQI_HEAD_TOP, EQI_HAND_L;
	.@s = select( "อาวุธมือขวา", "ชุดเกราะ", "รองเท้า", "ผ้าคลุม", "เครื่องประดับขวา", "เครื่องประดับซ้าย", "หมวก (ส่วนบน)", "อาวุธมือซ้าย/โล่" ) - 1;
	.@loc = .@location[.@s];
	.@eq_id = getequipid(.@loc);

	switch(.@eq_id) {
	// Illusion of Moonlight
		case 15195:	// Puente_Robe_IL
		case 16063:	// Long_Mace_IL
		case 19209:	// Nurse_Cap_IL
		case 20838:	// Muffler_IL
		case 19210:	// Apple_Of_Archer_IL
		case 22133:	// Shoes_IL
		case 26007:	// Spectral_Spear_IL
		case 26109:	// Staff_Of_Bordeaux_IL
		case 28725:	// Moonlight_Sword_IL
	// Illusion of Vampire
		case 28022:	// Infiltrator_IL
		case 28023:	// Ghoul_Leg_IL
		case 2039:	// Wizardy_Staff_IL
		case 18149:	// Balistar_IL
		case 28612:	// Book_Of_The_Apo_IL
		case 20840:	// Cape_Of_Ancient_Lord_IL
		case 28508:	// Skul_Ring_IL
		case 28509:	// Ring_IL
	// Illusion of Frozen
		case 1846:	// Combo_Battle_Glove_IL
		case 13337:	// Huuma_Flutter_Snow_IL
		case 19223:	// Cap_IL
		case 28922:	// Herald_Of_GOD_IL
		case 20847:	// Clack_Of_Servival_IL
	// Illusion of Teddy bear
		case 28745:	// Illusion_Counter_Dagger
		case 28244:	// Illusion_Gate_Keeper_DD
		case 2051:	// Illusion_Survivor's_Staff
		case 22190:	// Illusion_Boots
		case 19344:	// Illusion_Hot_blooded_Headband
	// Illusion of Turtle
		case 13469:	// Illusion_Immaterial_Sword
		case 1326:	// Illusion_War_Axe
		case 32005:	// Illusion_Pole_Axe
		case 13338:	// Illusion_Wing_Shuriken
		case 16065:	// Illusion_Iron_Driver
		case 19247:	// Illusion_Fancy_Flower
	// Illusion of Twins
		case 450182:	// Sprint_Mail_IL
		case 470066:	// Sprint_Shoes_IL
		case 490120:	// Sprint_Ring_IL
		case 490121:	// Sprint_Glove_IL
		case 550030:	// Thorn_Staff_IL
		case 550031:	// Dea_Staff_IL
		case 530015:	// Gelerdria_IL
		case 500030:	// Excalibur_IL
		case 620010:	// Doom_Slayer_IL
		case 510034:	// Ancient_Dagger_IL
		case 460017:	// Guard_IL
		case 460018:	// Siver_Guard_IL
	// Illusion of Luanda
		case 28626:	// Illusion_Tablet
		case 18174:	// Illusion_Hunter_Bow
		case 19366:	// Illusion_Goibne_Helm
		case 15348:	// Illusion_Goibne_Armor
		case 20923:	// Illusion_Goibne_Spaulders
		case 22192:	// Illusion_Goibne's_Greaves
	// Illusion of Labyrinth
		case 19428:	// Illusion_Morpheus's_Hood
		case 20948:	// Illusion_Morpheus's_Shawl
		case 32238:	// Illusion_Morpheus's_Ring
		case 32239:	// Illusion_Morpheus's_Bracelet
		case 28254:	// Illusion_Butcher
		case 21050:	// Illusion_Tae_Goo_Lyeon
		case 32301:	// Illusion_Gold_Lux
		case 28762:	// Illusion_Bazerald
	// Illusion of underwater
		case 570008:	// Electronic_Guitar_IL
		case 580008:	// Electric_Eel_IL
		case 630006:	// Brionac_IL
		case 610012:	// KatarOfCold_Icicle_IL
		case 600011:	// Death_Guidance_IL
		case 630007:	// Zephyrus_IL
		case 450144:	// Saint_Robe_IL
		case 450145:	// Water_Sprits_Armor_IL
		case 450146:	// Chain_Mail_IL
		case 400053:	// Morrigane's_Helm_IL
		case 480054:	// Morrigane's_Manteau_IL
		case 490069:	// Morrigane's_Belt_IL
		case 490070:	// Morrigane's_Pendant_IL
			break;
		case -1:
			mes "[Illusion Enchanter]";
			mes "คุณต้องสวมใส่ไอเทมในตำแหน่งที่เลือกเพื่อทำการเสริมพลัง";
			close;
		default:
			mes "[Illusion Enchanter]";
			mes "อุปกรณ์ชิ้นนี้ไม่ได้มาจากโลกแห่งมายาอันน่ามหัศจรรย์ ฉันรู้สึกเคืองนะที่คุณกล้าเอามันมาให้ฉันดูเนี่ย";
			close;
	}

	for ( .@i = 0; .@i < MAX_ITEM_RDM_OPT-1; ++.@i ) {
		if (getequiprandomoption(.@loc, .@i, ROA_ID) > 0) {
			mes "^ff0000ตรวจพบ Random Option ในอุปกรณ์ชิ้นนี้ อุปกรณ์ที่มีออปชั่นสุ่มจะไม่สามารถเสริมพลังได้^000000";
			close;
		}
	}

	.@weapon_lvl = getequipweaponlv(.@loc);
	.@loc_enchant = .@loc;	// variable for enhancing function

	switch(.@weapon_lvl) {
	case 0:
		break;
	case 1:
	case 2:
	case 3:
		if (.@loc_enchant == EQI_HAND_L)
			.@loc_enchant = EQI_HAND_R;	// the equipment is a weapon so the enchantments must be for weapons
		break;
	default:	// illusion weapon level 4 can't be enhanced
		mes "[Illusion Enchanter]";
		mes "นี่คืออุปกรณ์ Illusion ที่ดีที่สุดแล้ว ไม่มีเทคโนโลยีใดในยุคสมัยนี้ที่จะสามารถเสริมพลังมันได้อีก แม้แต่นักเสริมพลังที่เก่งที่สุดก็ทำไม่ได้";
		next;
		mes "[Illusion Enchanter]";
		mes "เอ่อ นอกจากว่าเขาจะยอมเสี่ยงเอาสุขภาพจิตและสุขภาพกายของตัวเองเข้าแลกน่ะนะ";
		next;
		mes "[Illusion Enchanter]";
		mes "แต่ใครจะรู้ล่ะ? บางทีสักวันหนึ่งคุณอาจจะได้พบกับใครบางคนที่มีพรสวรรค์พอที่จะจัดการกับอุปกรณ์ล้ำค่าชิ้นนี้ก็ได้";
		close;
	}

	.@refine = getequiprefinerycnt(.@loc);
	.@card[0] = getequipcardid(.@loc,0);
	.@card[1] = getequipcardid(.@loc,1);
	.@card[2] = getequipcardid(.@loc,2);
	.@card[3] = getequipcardid(.@loc,3);

	switch(.@reset) {
	case false:
		.@item_req = 25271;	// IllusionStone
		.@item_req_count = 5;

		if (.@card[2] > 0 && .@card[3] > 0) {
			mes "[Illusion Enchanter]";
			mes "อุปกรณ์ชิ้นนี้ได้รับการเสริมพลังจนถึงขีดจำกัดสูงสุดแล้ว";
			close;
		}
		if (.@weapon_lvl > 0) {
			mes "[Illusion Enchanter]";
			mes "คุณต้องการเพิ่มความสามารถประเภทไหนล่ะ?";
			next;
			.@weapon_enchant_type = select( "ระยะใกล้ (Melee)", "ระยะไกล (Ranged)", "เวทมนตร์ (Magic)" );
		}
		mes "[Illusion Enchanter]";
		if (.@card[3] > 0) {
			.@slot = 2;
			.@string$ = "ที่สอง";
		}
		else {
			.@slot = 3;
			.@string$ = "แรก";
		}
		mes "ฉันจะพยายามเสริมพลังความสามารถช่อง" + .@string$ + " ให้กับไอเทมของคุณ ^ff0000ระดับการตีบวกเดิมและการ์ดของคุณจะไม่ได้รับความเสียหาย และมันจะไม่ถูกทำลายแม้ว่าการพยายามครั้งนี้จะล้มเหลวก็ตาม^000000 คุณต้องการจะไปต่อไหม?";
		next;
		if (select( "เดี๋ยวค่อยกลับมาใหม่", "แน่นอน!" ) == 1) {
			mes "[Illusion Enchanter]";
			mes "ไม่มีปัญหา กลับมาได้ทุกเมื่อถ้าคุณเปลี่ยนใจ";
			close;
		}
		if (countitem(.@item_req) < .@item_req_count) {
			mes "[Illusion Enchanter]";
			mes "ไอเทมนี้ต้องใช้ " + .@item_req_count + " " + getitemname(.@item_req) + " แต่คุณมีไม่พอตามที่ฉันต้องการ";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@loc, .@eq_id) || callfunc("F_IsEquipCardHack", .@loc, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@loc, .@refine))
			close;

		.@card[.@slot] = callsub( S_Enchant, .@loc_enchant, .@slot, .@weapon_enchant_type );
		delitem .@item_req, .@item_req_count;
		delequip .@loc;
		getitem2 .@eq_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
		mes "[Illusion Enchanter]";
		mes "เสร็จเรียบร้อยแล้ว เชิญตรวจสอบดูได้";
		specialeffect2 EF_REPAIRWEAPON;
		close;

	case true:
		if (!F_IsCharm(.@card[2]) || !F_IsCharm(.@card[3])) {
			mes "[Illusion Enchanter]";
			mes "เฉพาะไอเทมที่เสริมพลังครบสองช่องเท่านั้นที่สามารถรีเซ็ตได้ กรุณาเสริมพลังให้ครบสองครั้งก่อน แล้วถ้ายังอยากรีเซ็ตอยู่ค่อยนำมาหาฉันนะ";
			close;
		}
		mes "[Illusion Enchanter]";
		mes "โอกาสในการรีเซ็ตจะแตกต่างกันไป ขึ้นอยู่กับจำนวน Zeny ที่คุณจ่าย คุณต้องการทำอย่างไร?";
		next;
		.@s = select( "เดี๋ยวค่อยกลับมาใหม่", "100,000 Zeny (50%)", "200,000 Zeny (60%)", "300,000 Zeny (70%)", "400,000 Zeny (80%)", "500,000 Zeny (90%)" ) - 1;
		if (.@s == 0) {
			mes "[Illusion Enchanter]";
			mes "ไม่มีปัญหา กลับมาได้ทุกเมื่อถ้าคุณเปลี่ยนใจ";
			close;
		}
		.@cost = 100000 * .@s;
		.@rate = 40 + 10 * .@s;

		if (Zeny < .@cost) {
			mes "[Illusion Enchanter]";
			mes "คุณมีเงินไม่เพียงพอ";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@loc, .@eq_id) || callfunc("F_IsEquipCardHack", .@loc, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@loc, .@refine))
			close;

		Zeny -= .@cost;
		delequip .@loc;
		if (rand(100) < .@rate) {
			getitem2 .@eq_id,1,1,.@refine,0,.@card[0],.@card[1],0,0;
			mes "[Illusion Enchanter]";
			mes "เสร็จเรียบร้อยแล้ว เชิญตรวจสอบดูได้";
			specialeffect2 EF_REPAIRWEAPON;
			close;
		}
		mes "[Illusion Enchanter]";
		mes "อา... ช่างน่าเสียดายจริงๆ เอาเถอะ คนเรามันจะโชคดีไปตลอดได้ยังไงกัน";
		specialeffect2 EF_LORD;
		close;
	}
	end;

S_Enchant:
	.@slot = getarg(1);
	switch( getarg(0)) {	// location
	case EQI_HAND_R:
		switch( getarg(2)) {// weapon enchant type
		case 1:// Melee
			switch( .@slot ) {
			case 2:
			case 3:
				// <item ID>, <rate> (unknown)
				setarray .@item[0],
					4700, 1,	// Strength1
					4701, 1,	// Strength2
					4702, 1,	// Strength3
					4703, 1,	// Strength4
					4720, 1,	// Dexterity1
					4721, 1,	// Dexterity2
					4722, 1,	// Dexterity3
					4723, 1,	// Dexterity4
					4750, 1,	// Luck1
					4751, 1,	// Luck2
					4752, 1,	// Luck3
					4753, 1,	// Luck4
					4808, 1,	// Fighting_Spirit4
					4809, 1,	// Fighting_Spirit3
					4810, 1,	// Fighting_Spirit2
					4811, 1,	// Fighting_Spirit1
					4820, 1,	// Fighting_Spirit5
					29081, 1,	// Expect1Lv
					29082, 1,	// Expect2Lv
					29083, 1,	// Expect3Lv
					29084, 1,	// Expect4Lv
					29085, 1,	// Expect5Lv
					29061, 1,	// Ambition1Lv
					29062, 1,	// Ambition2Lv
					29063, 1,	// Ambition3Lv
					29064, 1,	// Ambition4Lv
					29065, 1,	// Ambition5Lv
					4807, 1,	// Atk_Speed1
					4842, 1;	// Atk_Speed2
				break;
			default:
				return 0;
			}
			break;
		case 2:// Ranged
			switch( .@slot ) {
			case 2:
			case 3:
				// <item ID>, <rate>
				setarray .@item[0],
					4730, 1,	// Agility1
					4731, 1,	// Agility2
					4732, 1,	// Agility3
					4733, 1,	// Agility4
					4720, 1,	// Dexterity1
					4721, 1,	// Dexterity2
					4722, 1,	// Dexterity3
					4723, 1,	// Dexterity4
					4750, 1,	// Luck1
					4751, 1,	// Luck2
					4752, 1,	// Luck3
					4753, 1,	// Luck4
					4807, 1,	// Atk_Speed1
					4842, 1,	// Atk_Speed2
					4832, 1,	// Expert_Archer1
					4833, 1,	// Expert_Archer2
					4834, 1,	// Expert_Archer3
					4835, 1,	// Expert_Archer4
					4836, 1,	// Expert_Archer5
					29091, 1,	// ArchLine1Lv
					29092, 1,	// ArchLine2Lv
					29093, 1,	// ArchLine3Lv
					29094, 1,	// ArchLine4Lv
					29095, 1,	// ArchLine5Lv
					29071, 1,	// Tab1Lv
					29072, 1,	// Tab2Lv
					29073, 1,	// Tab3Lv
					29074, 1,	// Tab4Lv
					29075, 1;	// Tab5Lv
				break;
			default:
				return 0;
			}
			break;
		case 3:// Magic
			switch( .@slot ) {
			case 2:
			case 3:
				// <item ID>, <rate>
				setarray .@item[0],
					4710, 1,	// Inteligence1
					4711, 1,	// Inteligence2
					4712, 1,	// Inteligence3
					4713, 1,	// Inteligence4
					4720, 1,	// Dexterity1
					4721, 1,	// Dexterity2
					4722, 1,	// Dexterity3
					4723, 1,	// Dexterity4
					4750, 1,	// Luck1
					4751, 1,	// Luck2
					4752, 1,	// Luck3
					4753, 1,	// Luck4
					4812, 1,	// Spell4
					4813, 1,	// Spell3
					4814, 1,	// Spell2
					4815, 1,	// Spell1
					4826, 1,	// Spell5
					4805, 1,	// Heal_Amount2
					4850, 1;	// Heal_Amount3
				break;
			default:
				return 0;
			}
			break;
		default:
			return 0;
		}
		break;
	case EQI_ARMOR:
	case EQI_SHOES:
	case EQI_GARMENT:
	case EQI_HAND_L:
	case EQI_HEAD_TOP:
		switch( .@slot ) {
		case 2:
		case 3:
			// <item ID>, <rate>
			setarray .@item[0],
				4700, 700,	// Strength1
				4701, 600,	// Strength2
				4702, 500,	// Strength3
				4703, 200,	// Strength4
				4740, 700,	// Vitality1
				4741, 600,	// Vitality2
				4742, 500,	// Vitality3
				4743, 200,	// Vitality4
				4710, 700,	// Inteligence1
				4711, 600,	// Inteligence2
				4712, 500,	// Inteligence3
				4713, 200,	// Inteligence4
				4750, 700,	// Luck1
				4751, 600,	// Luck2
				4752, 500,	// Luck3
				4753, 200,	// Luck4
				4994, 10,	// Rune of Strength Lv 1
				4995, 10,	// Rune of Strength Lv 2
				4997, 10,	// Rune of Agility Lv 1
				4998, 10,	// Rune of Agility Lv 2
				29009, 10,	// Rune of Vitality Lv 1
				29010, 10,	// Rune of Vitality Lv 2
				29000, 10,	// Rune of Intellect Lv 1
				29001, 10,	// Rune of Intellect Lv 2
				29003, 10,	// Rune of Dexterity Lv 1
				29004, 10,	// Rune of Dexterity Lv 2
				29006, 10,	// Rune of Luck Lv 1
				29007, 10,	// Rune of Luck Lv 2
				4861, 700,	// MHP1
				4862, 600,	// MHP2
				4867, 440,	// MHP3
				4868, 100,	// MHP4
				4992, 10,	// HPAbsorb1_Supplement_Reactor
				4993, 10,	// SPAbsorb1_Supplement_Reactor
				29208, 10,	// SP_Absorption_2
				29210, 10;	// HP_Absorption_23
			break;
		default:
			return 0;
		}
		break;
	case EQI_ACC_L:
	case EQI_ACC_R:
		switch( .@slot ) {
		case 2:
		case 3:
			// <item ID>, <rate>
			setarray .@item[0],
				4700, 576,	// Strength1
				4701, 460,	// Strength2
				4702, 115,	// Strength3
				4703, 11,	// Strength4
				4730, 576,	// Agility1
				4731, 460,	// Agility2
				4732, 115,	// Agility3
				4733, 11,	// Agility4
				4740, 576,	// Vitality1
				4741, 460,	// Vitality2
				4742, 115,	// Vitality3
				4743, 11,	// Vitality4
				4710, 576,	// Inteligence1
				4711, 460,	// Inteligence2
				4712, 115,	// Inteligence3
				4713, 11,	// Inteligence4
				4720, 576,	// Dexterity1
				4721, 460,	// Dexterity2
				4722, 115,	// Dexterity3
				4723, 11,	// Dexterity4
				4750, 576,	// Luck1
				4751, 460,	// Luck2
				4752, 115,	// Luck3
				4753, 11,	// Luck4
				4928, 576,	// SP10
				4870, 460,	// SP25
				4800, 345,	// SP50
				4871, 115,	// SP75
				4801, 11,	// SP100
				4795, 576,	// HP100
				4796, 460,	// HP200
				4797, 345,	// HP300
				4798, 115,	// HP400
				4799, 11;	// HP500
			break;
		default:
			return 0;
		}
		break;
	default:
		return 0;
	}
	.@size = getarraysize(.@item);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		.@total_rate += .@item[.@i+1];
	.@r = rand(.@total_rate);
	for ( .@i = 0; .@i < .@size; .@i += 2 ) {
		.@rate_tmp += .@item[.@i+1];
		if (.@r < .@rate_tmp)
			break;
	}
	return .@item[.@i];

OnTouch:
	if (isbegin_quest(12415) == 0)
		npctalk "Illusion Enchanter: แวะมาคุยกับฉันสิ ถ้าคุณสนใจในอุปกรณ์ Illusion!", "", bc_self;
	end;
}


//============================================================
//= Illusion of Turtle
//============================================================

// Exchange npc
alberta,226,28,3	script	Equipment Researcher	4_TOWER_15,{
	.@illusion_stone_name$ = getitemname(25271);

	mes "[Equipment Researcher]";
	mes "ฉันคือผู้ที่ทำการวิจัยเรื่อง " + .@illusion_stone_name$ + " เพื่อนำมาเสริมพลังให้กับชุดเกราะและอาวุธ";
	mes "ถ้าคุณมีวัตถุดิบที่ฉันต้องการสำหรับงานวิจัย คุณจะยอมส่งพวกมันให้ฉันได้ไหมล่ะ?";
	mes "ถ้าคุณมอบอุปกรณ์และวัตถุดิบบางอย่างให้ฉัน ฉันจะแลกเปลี่ยนพวกมันกับอุปกรณ์ที่เสริมพลังแล้วของฉันเป็นการตอบแทน";
	next;
	switch( select( "คุณกำลังทำอะไรที่นี่?", "แลกเปลี่ยนอุปกรณ์" ) ) {
	case 1:
		mes "[Equipment Researcher]";
		mes "ฉันกำลังศึกษาหาวิธีใช้ " + .@illusion_stone_name$ + " เพื่อเสริมความแข็งแกร่งให้อุปกรณ์น่ะ";
		mes "ฉันเคยพยายามจะไปยังสถานที่ที่พบ " + .@illusion_stone_name$ + " มาด้วยนะ และเกือบจะเอาชีวิตไม่รอดกลับมาแน่ะ..";
		next;
		mes "[Equipment Researcher]";
		mes "ฉันคิดว่าฉันเจอเด็กผู้หญิงคนหนึ่ง...";
		mes "ช่างเถอะ หลังจากนั้นฉันก็หาทางกลับไปที่นั่นไม่ได้อีกเลย";
		mes "ที่นี่ เราจึงใช้วิธีรับวัตถุดิบวิจัยจากเหล่านักผจญภัยที่มี " + .@illusion_stone_name$ + " และฉันจะมอบอุปกรณ์ที่เสริมพลังให้เป็นการตอบแทนแทน";
		close;
	case 2:
		mes "[Equipment Researcher]";
		mes "อุปกรณ์ที่ฉันสามารถแลกเปลี่ยนได้มีอะไรบ้างงั้นเหรอ? รอเดี๋ยวนะ ฉันจะบอกให้ทราบ";
		next;
		mes "[Equipment Researcher]";
		mes "ในส่วนของวัตถุดิบ คุณสามารถนำ ^0000CDอุปกรณ์ที่มีอยู่ซึ่งถูกตีบวกมาเป็นระดับ +9 หรือสูงกว่ามาให้ฉันได้^000000";
		mes "และฉันยังต้องการ ^0000CD" + .@illusion_stone_name$ + " รวมถึงวัตถุดิบอื่นๆ อีกจำนวนหนึ่งด้วย^000000";
		next;
		mes "[Equipment Researcher]";
		mes "แน่นอนว่าเราจะมอบอุปกรณ์ชิ้นใหม่ให้คุณ ดังนั้นอย่าลืมว่า ^0000CDระดับการตีบวกเดิมและออปชั่นเสริมพลังทั้งหมด^000000 จะหายไป";
		mes "เอาล่ะ คุณต้องการอาวุธประเภทไหนล่ะ?";
		close2;
		callshop( "barter_ill_turtle" );
		end;
	}
}


//============================================================
//= Illusion of Luanda
//============================================================

// Exchange npc
com_d02_i,234,266,6	script	Village Soap#Lu	4_M_ORIENT01,{
	mes "[Village Soap]";
	mes "ฉันเองก็ยอมทุ่มสุดตัวเพื่อเหล่านักผจญภัยที่ทำงานหนักเพื่อหมู่บ้านนี้เหมือนกัน ฉันจะช่วยทำให้อุปกรณ์ของคุณแข็งแกร่งขึ้นได้ไหม? มีชิ้นไหนในรายการที่คุณต้องการหรือเปล่า?";
	next;
	if (select( "ดูอุปกรณ์ Illusion", "ยกเลิก" ) == 2) {
		mes "[Village Soap]";
		mes "ไว้ค่อยมาใหม่ทีหลังนะ แค่เตรียมวัตถุดิบมาแล้วฉันจะจัดการสร้างมันให้คุณเอง";
		close;
	}
	mes "[Village Soap]";
	mes "ใครเป็นคนสร้างมันขึ้นมากันนะ? มันอาจจะมีประโยชน์หากคุณพกมันติดตัวไปด้วย";
	close2;
	callshop( "barter_ill_luanda" );
	end;
}


//============================================================
//= Illusion of Underwater
//============================================================

iz_d04_i,134,228,4	script	Horen#Horen	4_M_BIBI,{
	if (checkweight(1201,3) == 0) {
		mes "ไม่สามารถดำเนินบทสนทนาต่อได้เนื่องจากคุณมีไอเทมในตัวมากเกินไป";
		mes "กรุณาจัดการช่องเก็บของของคุณให้เรียบร้อยแล้วลองใหม่อีกครั้ง";
		close;
	}
	mes "[Horen]";
	mes "สวัสดี หากคุณมีวัตถุดิบหายากจากที่แห่งนี้ ฉันสามารถเสริมพลังให้อุปกรณ์ของคุณได้นะ";
	next;
	mes "[Horen]";
	mes "คุณอยากดูรายการอุปกรณ์ที่สามารถอัปเกรดได้ไหม?";
	next;
	if (select( "ใช่", "ไม่" ) == 2) {
		mes "[Horen]";
		mes "ถ้าสนใจก็กลับมาหาฉันใหม่นะ";
		close;
	}
	mes "[Horen]";
	mes "มีหลายประเภทเลยล่ะ ค่อยๆ เลือกดูตามสบาย ถ้าคุณต้องการชิ้นไหน ฉันสามารถเสริมพลังให้ได้ทันที";
	close2;
	callshop( "barter_ill_underwater" );
	end;
}

//============================================================
//= Illusion of Twins
//============================================================
// ant_d02_i,175,186,3	script	From#iltw	MECHANIC,{
ant_d02_i,175,186,3	script	From#iltw	HIDDEN_NPC,{
	if (checkweight(1201,3) == 0) {
		mes "- คุณไม่สามารถดำเนินเควสต์ต่อได้เนื่องจากคุณมีไอเทมมากเกินไป -";
		close;
	}
	mes "[From]";
	mes "ฉันรู้สึกเหมือนตัวเองกำลังจะบ้าตาย";
	mes "มันอาจจะเป็นเพราะอารมณ์ของฉันเอง ฉันเป็นมนุษย์นะ และมันไม่ใช่ว่าคุณตาฝาดไปหรอก";
	mes "ฉันจะช่วยคุณเองถ้าคุณรวบรวมของมาได้เยอะๆ จากที่นี่";
	mes "คุณต้องการจะเสริมพลังให้อุปกรณ์ของคุณไหมล่ะ?";
	close2;
	callshop( "barter_ill_twins" );
	end;

OnInit:
	.@npc_id = getnpcid(0);
	setunitdata .@npc_id, UNPC_CLASS, 4058;
	setunitdata .@npc_id, UNPC_SEX, SEX_MALE;
	setunitdata .@npc_id, UNPC_CLOTHCOLOR, 1;
	setunitdata .@npc_id, UNPC_HAIRSTYLE, 11;
	setunitdata .@npc_id, UNPC_HAIRCOLOR, 6;
	end;
}

//============================================================
//= Illusion Merchant
//============================================================
-	marketshop	market_resonance_stone	-1,100003:2000000:99999,100004:2000000:99999
prontera,88,113,5	script	Illusion Merchant#0829	HIDDEN_NPC,{
	mes "[Illusion Merchant]";
	mes "ฉันกำลังขายคัมภีร์สุ่มออปชั่นอาวุธสองแบบที่ฉันทำขึ้นมาเอง คุณสามารถเลือกวิธีชำระเงินที่ต้องการได้เลย";
	next;
	switch( select( "ซื้อด้วยเงิน Zeny", "ซื้อด้วย Illusion Stone" )) {
		case 1:
			mes "[Illusion Merchant]";
			mes "หวังว่าคุณจะชอบมันนะ";
			close2;
			callshop "market_resonance_stone";
			end;
		case 2:
			mes "[Illusion Merchant]";
			mes "หวังว่าคุณจะชอบมันนะ";
			close2;
			callshop "resonance_stone_barter";
			end;
	}
OnInit:
	.@npc_id = getnpcid(0);
	setunitdata .@npc_id,UNPC_CLASS, JOB_MAGE_HIGH;
	setunitdata .@npc_id,UNPC_SEX,SEX_FEMALE;
	setunitdata .@npc_id,UNPC_HEADTOP,142;
	setunitdata .@npc_id,UNPC_HEADMIDDLE,92;
	setunitdata .@npc_id,UNPC_HAIRSTYLE,2;
	setunitdata .@npc_id,UNPC_HAIRCOLOR,2;

	// Restock
	npcshopupdate "market_resonance_stone",100003,2000000,99999;
	npcshopupdate "market_resonance_stone",100004,2000000,99999;
	end;
}


//============================================================
//= Illusion of Teddy Bear
//============================================================

ein_d02_i,177,158,3	script	Bear Wanting Illusion Stone#ITB	4_NASARIAN,{
	.@item_name$ = getitemname(25271); // IllusionStone
	mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
	mes "ถ้าคุณมอบ " + .@item_name$ + " ให้ฉัน ฉันจะทำสิ่งดีๆ ให้คุณเป็นการตอบแทน!";
	next;
	switch( select( "เหตุผลที่มาอยู่ที่นี่", "สิ่งดีๆ [อุปกรณ์ Illusion]", "เลิกคุย" ) ) {
	case 1:
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "ฉันก็ไม่รู้เหมือนกันว่าทำไม...!";
		mes "หมีเทดดี้ตัวอื่นๆ เขาต่อแถวกัน ฉันก็เลยมาต่อแถวด้วย!";
		mes "โฮวววววว!!!!";
		mes "ฉันไม่ยักรู้เลยว่ามันอยู่ที่นี่!";
		next;
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "หมีเทดดี้ที่มี " + .@item_name$ + "!";
		mes "แข็งแกร่ง!";
		mes "แต่ฉันไม่มี " + .@item_name$ + " เลยสักก้อน!";
		mes "แข็งแกร่ง... น้อยลง...";
		next;
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "ว่าแต่ " + .@item_name$ + " คืออะไรกันนะ?";
		mes "ฉันไม่รู้หรอก แต่ฉันอยากได้มันจัง!";
		close;
	case 2:
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "จะทำให้ของที่คุณมีอยู่กลายเป็นของดี!";
		next;
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "ถ้าอยากได้ของดีๆ ก็เอาอุปกรณ์ที่ตีบวก ^ff0000+9 หรือสูงกว่ามาให้ฉันสิ ของเดิมมันจะหายไปหมดเลยนะ... ฮ่าฮ่า?^000000";
		next;
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "^ff0000" + .@item_name$ + "^000000!!! " + .@item_name$ + " คือสิ่งที่สำคัญที่สุด แต่... วัตถุดิบอื่นๆ ก็จำเป็นเหมือนกันนะ";
		mes "เพราะสำหรับสิ่งดีๆ ทุกอย่าง คุณย่อมต้องใช้ของดีอย่างอื่นมาแลกเสมอ";
		next;
		close2;
		callshop( "barter_ill_teddy" );
		end;
	case 3:
		mes "[Bear ผู้โหยหา " + .@item_name$ + "]";
		mes "โอเค...";
		close;
	}
	end;
}
