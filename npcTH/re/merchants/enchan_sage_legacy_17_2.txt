//===== rAthena Script =======================================
//= Episode 17.2 - Sage's Legacy Enchants
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Sage's Legacy related merchants and enchanters
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//============================================================

ba_in01,87,370,3	script	Lisa#ep172_merchant	20698,{
	if (checkweight(1201,3) == 0) {
		mes "^ff0000ดูเหมือนว่าชนิดหรือน้ำหนักของไอเทมที่คุณมีจะมากเกินไป กรุณาจัดการช่องเก็บของของคุณด้วย^000000";
		close;
	}
	disable_items;
	mes "[Lisa]";
	mes "ยินดีต้อนรับ ยินดีต้อนรับ~";
	next;
	switch( select( "สอบถามข้อมูลอุปกรณ์", "เสริมพลัง Automatic Engine Wing", "เสริมพลัง Automatic Armor", "เสริมพลัง Automatic Leg", "เสริมพลัง Automatic Accessories(R)", "เสริมพลัง Automatic Accessories(L)" ) ) {
	case 1:
		mes "[Lisa]";
		mes "หากคุณนำอุปกรณ์ Automatic และโมดูลเสริมพลังมา เราสามารถออฟชั่นโมดูลที่เหมาะสมลงบนอุปกรณ์ให้ได้ค่ะ";
		next;
		mes "[Lisa]";
		mes "ขั้นตอนนี้ทำตามโมดูลที่ระบุไว้ ดังนั้นอุปกรณ์จะไม่ถูกทำลาย อย่างไรก็ตาม จำนวนการอัปเกรดอาจแตกต่างกันไปขึ้นอยู่กับโมดูลนั้นๆ ค่ะ";
		next;
		mes "[Lisa]";
		mes "ถ้าอย่างนั้น โปรดแวะมาใหม่เมื่อต้องการความช่วยเหลือนะคะ~";
		close;
	case 2:
		.@part = EQI_GARMENT;
		setarray .@equip_required[0],480020,480021;
		break;
	case 3:
		.@part = EQI_ARMOR;
		setarray .@equip_required[0],450127,450128;
		break;
	case 4:
		.@part = EQI_SHOES;
		setarray .@equip_required[0],470022,470023;
		break;
	case 5:
		.@part = EQI_ACC_R;
		setarray .@equip_required[0],490024,490026;
		break;
	case 6:
		.@part = EQI_ACC_L;
		setarray .@equip_required[0],490025,490027;
		break;
	}
	.@equip_id = getequipid(.@part);
	.@equip_name$ = getequipname(.@part);
	.@refine = getequiprefinerycnt(.@part);
	for ( .@i = 0; .@i < 4; ++.@i )
		.@card[.@i] = getequipcardid(.@part,.@i);

	mes "[Lisa]";
	mes "หากคุณนำอุปกรณ์ Automatic และโมดูลเสริมพลังมา เราสามารถออฟชั่นโมดูลที่เหมาะสมลงบนอุปกรณ์ให้ได้ค่ะ";
	next;
	mes "[Lisa]";
	mes strcharinfo(0) + " คะ";
	mes "กรุณาเลือกระดับของโมดูลที่คุณต้องการใช้งานค่ะ";
	next;
	switch( select( "ยกเลิก", "ระดับ Normal", "ระดับ Rare", "ระดับ Unique", "ระดับ Legendary", "ระดับ Epic 1", "ระดับ Epic 2", "ระดับ Epic 3" ) ) {
	case 1:
		mes "[Lisa]";
		mes "จบการสนทนา";
		close;
	case 2:
		setarray .@module_etc[0],1000105,1000106,1000107,1000108,1000109,1000110,1000111,1000112;
		break;
	case 3:
		setarray .@module_etc[0],1000113,1000114,1000115,1000116,1000117,1000118,1000119,1000120,1000121,1000122,1000123,1000124,1000125,1000126,1000127;
		break;
	case 4:
		setarray .@module_etc[0],1000128,1000129,1000130,1000131,1000132,1000133,1000134,1000135,1000136,1000137,1000138,1000139,1000140,1000141,1000142,1000143,1000207,1000208;
		break;
	case 5:
		setarray .@module_etc[0],1000144,1000145,1000146,1000147,1000148,1000149;
		break;
	case 6:
		setarray .@module_etc[0],1000152,1000153,1000154,1000155,1000156,1000157,1000158,1000159,1000160,1000161,1000162,1000163,1000164,1000165,1000166,1000167,1000168;
		break;
	case 7:
		setarray .@module_etc[0],1000169,1000170,1000171,1000172,1000173,1000174,1000175,1000176,1000177,1000178,1000179,1000180,1000181,1000182,1000183,1000184,1000185;
		break;
	case 8:
		setarray .@module_etc[0],1000186,1000187,1000188,1000189,1000190,1000191,1000192,1000193,1000194,1000195,1000196,1000197,1000198,1000199,1000200,1000201,1000202;
		break;
	}
	.@size = getarraysize(.@module_etc);
	.@menu$ = "ยกเลิก:";

	// Build the menu according to all the modules with the given grade in inventory
	for ( .@i = 0; .@i < .@size; ++.@i ) {
		if (countitem(.@module_etc[.@i]) < 1)
			.@menu$ += "^808080(จำนวนโมดูล 0)^000000" + ":";
		else {
			.@item_name$ = getitemname(.@module_etc[.@i]);
			.@item_name$ = replacestr(.@item_name$, "Automatic Modification Module(", "");
			.@item_name$ = replacestr(.@item_name$, ")", "");
			.@menu$ += .@item_name$ + ":";
		}
	}
	.@s = select(.@menu$) - 2;
	if (.@s == -1) {
		mes "[Lisa]";
		mes "จบการสนทนา";
		close;
	}
	.@module_id = .@module_etc[.@s];

	if (countitem(.@module_id) < 1) {
		mes "[Lisa]";
		mes "คุณต้องมีโมดูลในกระเป๋าที่ตรงกับประสิทธิภาพที่ต้องการเพื่อดำเนินการเสริมพลังค่ะ";
		close;
	}
	if (.@equip_required[0] != .@equip_id && .@equip_required[1] != .@equip_id) {
		mes "[Lisa]";
		mes "กรุณาตรวจสอบให้แน่ใจว่าคุณได้สวมใส่อุปกรณ์ Automatic ที่ถูกต้องซึ่งคุณต้องการจะเสริมพลังแล้วค่ะ";
		close;
	}
	if (.@card[1] > 0) {
		mes "[Lisa]";
		mes "อุปกรณ์ Automatic ชิ้นนี้มีความสามารถเต็มขีดจำกัดแล้วค่ะ";
		mes "กรุณาลองกับอุปกรณ์ Automatic ชิ้นอื่นนะคะ";
		close;
	}
	if (.@module_id < 1000105 || .@module_id == 1000150 || .@module_id == 1000151 || (.@module_id >= 1000203 && .@module_id != 1000207 && .@module_id != 1000208)) {
		mes "[Lisa]";
		mes "เกิดข้อผิดพลาดที่ไม่รู้จักขึ้นค่ะ";
		close;
	}

	// Each part has a list of allowed module and a maximum allowed number of the same module
	switch( .@part ) {
	case EQI_GARMENT:
		setarray .@module_etc_data[0],1000105,3,1000106,3,1000125,2,1000126,2,1000127,2,1000135,1,1000142,1,1000208,1;
		break;
	case EQI_ARMOR:
		setarray .@module_etc_data[0],1000105,3,1000106,3,1000122,2,1000123,2,1000124,2,1000128,1,1000129,1,1000130,1,1000131,1,1000132,1,1000133,1,1000207,1,1000140,1;
		.@size = getarraysize(.@module_etc_data);

		// Epic enchants. Max 2 epic enchants of the same enchant allowed
		for (.@i = 1000152; .@i < 1000203; .@i++) {
			setarray .@module_etc_data[.@size], .@i, 2;
			.@size += 2;
		}
		break;
	case EQI_SHOES:
		setarray .@module_etc_data[0],
			1000105,3,1000106,3,1000119,2,1000120,2,1000121,2,1000134,1,1000141,1,
			1000144,1,1000145,1,1000146,1,1000147,1,1000148,1,1000149,1;	// Legendary enchants (must be unique on EQI_SHOES)
		break;
	case EQI_ACC_L:
	case EQI_ACC_R:
		setarray .@module_etc_data[0],1000107,3,1000108,3,1000109,3,1000110,3,1000111,3,1000112,3,1000113,2,1000114,2,1000115,1,1000116,1,1000117,1,1000118,1,1000136,1,1000137,1,1000138,1,1000139,1,1000143,1;
		break;
	}

	.@data_index = inarray(.@module_etc_data, .@module_id);

	if (.@data_index == -1) {
		mes "[Lisa]";
		mes "คุณไม่สามารถออฟชั่นโมดูลนี้ลงในอุปกรณ์ Automatic ชิ้นนี้ได้ค่ะ";
		close;
	}
	// Max number of the selected module allowed
	.@max_module = .@module_etc_data[.@data_index+1];
	
	// Bind the module etc to the specific enchant
	for ( .@i = 1000105; .@i < 1000203; ++.@i ) {
		if (.@i == 1000150 || .@i == 1000151)
			continue;
		.@auto_module[.@i] = 310082 + .@size_auto_module;
		.@size_auto_module++;
	}
	.@auto_module[1000207] = 310178;
	.@auto_module[1000208] = 310179;

	// Check the count of the corresponding enchant in card slots
	.@enchant_id = .@auto_module[.@module_id];
	if (.@enchant_id == 0) {
		mes "[Lisa]";
		mes "เกิดข้อผิดพลาดที่ไม่รู้จักขึ้นค่ะ";
		close;
	}
	if (countinarray(.@card[0], .@enchant_id) >= .@max_module) {
		mes "[Lisa]";
		mes "อุปกรณ์ Automatic ชิ้นนี้ได้รับการเสริมพลังจากโมดูลนี้จนสูงสุดแล้วค่ะ";
		mes "กรุณาลองโมดูลอื่นนะคะ";
		close;
	}
	mes "[Lisa]";
	mes "คุณแน่ใจหรือไม่ว่าต้องการเสริมพลัง ^FF0000" + .@equip_name$ + "^000000 ด้วย ^0000CD" + getitemname(.@module_id) + "^000000? ขั้นตอนนี้ไม่สามารถย้อนกลับได้นะคะ";
	next;
	if (select( "เดี๋ยว... อย่าเพิ่ง", "ฉันแน่ใจ ทำเลย" ) == 1) {
		mes "[Lisa]";
		mes "จบการสนทนา";
		close;
	}
	delitem .@module_id,1;

	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]))
		end;

	for ( .@i = 3; .@i > 0; .@i--) {
		if (.@card[.@i] == 0) {
			.@card[.@i] = .@enchant_id;
			break;
		}
	}
	delequip .@part;
	getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	mes "[Lisa]";
	mes "ว้าววว~ ฉันสัมผัสได้ถึงพลังจากอุปกรณ์ Automatic ของคุณแล้วค่ะ~!";
	close;
}

//= Merchants
-	marketshop	ep172littlemanager00	-1,1000227:172:999999

ba_in01,87,386,3	script	Child Manager ฉฌ#ep172_pet	EP17_2_CHILD_ADMIN1,{
	if (checkweight(1201,3) == 0) {
		mes "- คุณมีไอเทมมากเกินไป -";
		mes "- กรุณาจัดการช่องเก็บของแล้วลองใหม่อีกครั้ง -";
		close;
	}
	mes "[Child Manager ฉฌ]";
	mes "ฉันมีของกระจุกกระจิกเต็มไปหมดเลย!";
	next;
	switch( select( "Little Manager", "Cloud Cotton", "Little Headress", "ยกเลิก" ) ) {
	case 1:
		mes "[Child Manager ฉฌ]";
		mes "ถ้าคุณอยากจะเล่นกับ Little Manager ละก็ มันจะมีค่าใช้จ่าย 30 Barmeal Ticket นะ";
		next;
		if (select( "แลกเปลี่ยน", "หยุด" ) == 2) {
			mes "[Child Manager ฉฌ]";
			mes "ถ้าอย่างนั้น ก็ถึงเวลาที่ฉันต้องไปเล่นแล้ว!";
			close;
		}
		if (countitem(1000103) < 30) {
			mes "[Child Manager ฉฌ]";
			mes "ก็บอกแล้วไง ว่าถ้าอยากเล่นกับ Little Manager คุณต้องมี 30 Barmeal Ticket!";
			close;
		}
		mes "[Child Manager ฉฌ]";
		mes "ขอให้สนุกกับเพื่อนใหม่นะ!";
		delitem 1000103,30;
		getitem 9123,1;
		close;
	case 2:
		callshop "ep172littlemanager00";
		end;
	case 3:
		callshop "barter_Ep_17_2_C_Admin_Acc";
		end;
	case 4:
		mes "[Child Manager ฉฌ]";
		mes "ถ้าอย่างนั้น ก็ถึงเวลาที่ฉันต้องไปเล่นแล้ว!";
		close;
	}
	end;
}

//= Cube Shop
-	marketshop	ep172cubeshop00	-1,100251:10000000:-1,100252:30000000:-1

ba_in01,87,376,3	script	Cube Lane#ep172cube	4_EP17_GUARD_B,{
	mes "[Cube Lane]";
	mes "เรามีคิวบ์เสริมพลังที่จะเปลี่ยนอุปกรณ์ Illusion และ Automatic ระดับ ^0000CD+4^000000 ของคุณให้เป็น ^FF0000+7^000000 ได้ในพริบตา!";
	mes "Varmundt นี่เป็นสุดยอดคนของโลกจริงๆ!";
	close2;
	callshop "ep172cubeshop00";
	end;
}

//= Module Shop
ba_in01,87,373,3	script	Spiera#ep172_soapstone	MD_ASSISTANT,{
	callshop "barter_EP17_2_EP";
	end;
}

//= Equipment Shop
ba_in01,87,383,3	script	Yeoncheong#equipment_exchange	4_EP17_CLEANER_W,{
	if (checkweight(1201,3) == 0) {
		cutin "ep172_omega.bmp",2;
		mes "[Yeoncheong]";
		mes "กระเป๋าเต็มแล้ว กรุณาตรวจสอบน้ำหนักและจำนวนไอเทมที่มีก่อนจะมาคุยใหม่อีกครั้งนะ";
		close3;
	}
	disable_items;
	cutin "ep172_omega",2;
	mes "[Yeoncheong]";
	mes "เรารับแลก Barmeal Ticket สำหรับการอัปเกรดอุปกรณ์ Illusion ของคุณให้กลายเป็นอุปกรณ์ Automatic";
	mes "คุณต้องการจะแลกเปลี่ยนชิ้นไหนดี?";
	next;
	switch( select( "ข้อมูลการแลกเปลี่ยน", "แลกเปลี่ยนอุปกรณ์ Automatic", "แลกเปลี่ยนเครื่องประดับ Automatic", "ออก" ) ) {
	case 1:
		mes "[Yeoncheong]";
		mes "คุณสามารถนำ ^0000CD+9 Illusion Armor, Engine หรือ Leg และ Barmeal Ticket 900 ใบ^000000 มาแลกเป็น ^FF0000Automatic Armor, Engine หรือ Leg^000000 ได้";
		next;
		mes "[Yeoncheong]";
		mes "สำหรับ ^FF0000Automatic Accessories^000000 สามารถแลกได้ด้วย ^0000CDเครื่องประดับ Illusion และ Barmeal Ticket 1500 ใบ^000000";
		next;
		mes "[Yeoncheong]";
		mes "อุปกรณ์ที่คุณต้องการแลกต้องอยู่ในกระเป๋าแล้วค่อยมาคุยกับฉันนะ";
		mes "^FF0000นำอุปกรณ์ที่คุณต้องการแลกมาเพียงชิ้นเดียวเพื่อดำเนินการ^000000";
		close3;
	case 2:
		.@shop$ = "barter_auto_equip_1";
		break;
	case 3:
		.@shop$ = "barter_auto_equip_2";
		break;
	case 4:
		mes "[Yeoncheong]";
		mes "ขอให้เป็นวันที่ดีนะ ท่านแขกผู้มีเกียรติ~";
		close3;
	}
	mes "[Yeoncheong]";
	mes "^FF0000นำอุปกรณ์ที่คุณต้องการแลกมาเพียงชิ้นเดียวเพื่อดำเนินการ^000000";
	mes "หากมีอุปกรณ์หลายชิ้นในกระเป๋า มันจะถูกสุ่มแลกเปลี่ยนหากตรงตามเงื่อนไข";
	close2;
	cutin "",255;
	callshop .@shop$;
	end;
}

//= Device Module Exchange
ba_in01,87,380,3	script	Yecheon#ep172module_trader	4_EP17_CLEANER_W,{
	if (checkweight(1201,3) == 0) {
		cutin "ep172_omega",2;
		mes "[Yecheon]";
		mes "กระเป๋าเต็มแล้ว กรุณาตรวจสอบน้ำหนักและจำนวนไอเทมที่มีก่อนจะมาคุยใหม่อีกครั้งนะ";
		close3;
	}
	disable_items;
	cutin "ep172_omega",2;
	mes "[Yecheon]";
	mes "ฉันสามารถจัดหาโมดูล Automatic, อุปกรณ์ปรับปรุง Automatic สายกายภาพ และอุปกรณ์ปรับปรุง Automatic สายเวทมนตร์ให้ได้ สิ่งเหล่านี้จะช่วยเพิ่มความสามารถให้อุปกรณ์ Automatic ของคุณ";
	next;
	cutin "",255;
	switch( select( "ข้อมูลเพิ่มเติม", "รับโมดูล Automatic", "รับอุปกรณ์ปรับปรุง Automatic สายกายภาพ", "รับอุปกรณ์ปรับปรุง Automatic สายเวทมนตร์", "ยกเลิก" ) ) {
	case 1:
		cutin "ep172_omega",2;
		mes "[Yecheon]";
		mes "คุณสามารถรับ ^FF0000โมดูล Automatic^000000 ได้ในราคา ^0000CDBarmeal Ticket 90ใบ^000000 ฉันจะให้โมดูล Automatic แบบสุ่มหนึ่งชิ้น ยกเว้นโมดูลระดับ Epic";
		next;
		mes "[Yecheon]";
		mes "ฉันยังมี ^FF0000อุปกรณ์ปรับปรุง Automatic สายกายภาพ และอุปกรณ์ปรับปรุง Automatic สายเวทมนตร์^000000 ให้ด้วย คุณสามารถแลกได้ด้วย ^0000CDBarmeal Ticket 45 ใบ^000000 หรือ ^0000CD1,500,000 Zeny^000000";
		next;
		mes "[Yecheon]";
		mes "ถ้าอย่างนั้น ท่านแขกผู้เชี่ยวชาญการต่อสู้ ขอให้พัฒนาความสามารถได้อย่างปลอดภัยนะ";
		close3;
	case 2:
		.@price = 90;
		cutin "ep172_omega",2;
		mes "[Yecheon]";
		mes "ฉันจะแลกเปลี่ยนโมดูล Automatic แบบสุ่ม 1 ชิ้น (ยกเว้นระดับ Epic) สำหรับ " + .@price + " " + mesitemlink(1000103) + " จ๊ะ";
		next;
		cutin "",255;
		if (select( "แลกด้วย Barmeal Ticket", "ไม่แลก" ) == 2) {
			mes "[Yecheon]";
			mes "สิ้นสุดการทำรายการ";
			close3;
		}
		cutin "ep172_omega",2;
		if (countitem(1000103) < .@price) {
			mes "[Yecheon]";
			mes "คุณมี Barmeal Ticket ไม่เพียงพอสำหรับการแลกเปลี่ยน";
			close3;
		}
		mes "[Yecheon]";
		mes "ฉันแลกมันเป็นโมดูล Automatic แบบสุ่มให้แล้วนะ";
		delitem 1000103,.@price;
		getitem rand(1000105,1000149),1;
		close3;
	case 3:
		.@price = 45;
		.@zeny = 1500000;
		cutin "ep172_omega",2;
		mes "[Yecheon]";
		mes "ฉันจะแลกเปลี่ยน ^FF0000อุปกรณ์ปรับปรุง Automatic สายกายภาพ 1 ชิ้น^000000 สำหรับ " + .@price + " " + mesitemlink(1000103) + " หรือ " + .@zeny + " Zeny";
		next;
		cutin "",255;
		switch( select( "แลกด้วย Barmeal Ticket", "แลกด้วย 1,500,000 Zeny", "ไม่แลก" ) ) {
		case 1:
			cutin "ep172_omega",2;
			if (countitem(1000103) < .@price) {
				mes "[Yecheon]";
				mes "คุณมี Barmeal Ticket ไม่เพียงพอสำหรับการแลกเปลี่ยน";
				close3;
			}
			delitem 1000103,.@price;
			break;
		case 2:
			cutin "ep172_omega",2;
			if (Zeny < .@zeny) {
				mes "[Yecheon]";
				mes "คุณมี Zeny ไม่เพียงพอสำหรับการแลกเปลี่ยน";
				close3;
			}
			Zeny -= .@zeny;
			break;
		case 3:
			mes "[Yecheon]";
			mes "สิ้นสุดการทำรายการ";
			close3;
		}
		mes "[Yecheon]";
		mes "ฉันแลกมันเป็นอุปกรณ์ปรับปรุง Automatic สายกายภาพแบบสุ่มให้แล้วนะ";
		.@r = rand(100);
		if (.@r < 50)
			getitem 100164,1;
		else if (.@r < 80)
			getitem 100165,1;
		else
			getitem 100166,1;
		close3;
	case 4:
		.@price = 45;
		.@zeny = 1500000;
		cutin "ep172_omega",2;
		mes "[Yecheon]";
		mes "ฉันจะแลกเปลี่ยน ^FF0000อุปกรณ์ปรับปรุง Automatic สายเวทมนตร์ 1 ชิ้น^000000 สำหรับ " + .@price + " " + mesitemlink(1000103) + " หรือ " + .@zeny + " Zeny";
		next;
		cutin "",255;
		switch( select( "แลกด้วย Barmeal Ticket", "แลกด้วย 1,500,000 Zeny", "ไม่แลก" ) ) {
		case 1:
			cutin "ep172_omega",2;
			if (countitem(1000103) < .@price) {
				mes "[Yecheon]";
				mes "คุณมี Barmeal Ticket ไม่เพียงพอสำหรับการแลกเปลี่ยน";
				close3;
			}
			delitem 1000103,.@price;
			break;
		case 2:
			cutin "ep172_omega",2;
			if (Zeny < .@zeny) {
				mes "[Yecheon]";
				mes "คุณมี Zeny ไม่เพียงพอสำหรับการแลกเปลี่ยน";
				close3;
			}
			Zeny -= .@zeny;
			break;
		case 3:
			mes "[Yecheon]";
			mes "สิ้นสุดการทำรายการ";
			close3;
		}
		mes "[Yecheon]";
		mes "ฉันแลกมันเป็นอุปกรณ์ปรับปรุง Automatic สายเวทมนตร์แบบสุ่มให้แล้วนะ";
		.@r = rand(100);
		if (.@r < 50)
			getitem 100167,1;
		else if (.@r < 80)
			getitem 100168,1;
		else
			getitem 100169,1;
		close3;
	case 5:
		mes "[Yecheon]";
		mes "สิ้นสุดการทำรายการ";
		close3;
	}
	end;
}

ba_in01,87,389,2	script	Butterfly Merchant#ba_in01	4_EP17_MASTER_A,{
	if (ep17_2_main < 8) {
		mes "[Butterfly Merchant]";
		mes "ขออภัยด้วย จำเป็นต้องมีการลงทะเบียนก่อนถึงจะซื้อผีเสื้อได้";
		close;
	}
	mes "[Butterfly Merchant]";
	mes "ยินดีต้อนรับ";
	mes "คุณต้องการปีกเพื่อเคลื่อนที่ไปยัง Barmund Mansion ใช่ไหม?";
	close2;
	callshop "barter_Teleport_Ep17_02";
	end;
}
