//===== rAthena Script =======================================
//= Upgrade Weapon Enchants
//===== By: ==================================================
//= Skorm
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds enchantments to Upgrade weapons.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Standardizing, grammar and bug fixes. [Euphy]
//= 1.2 Moved to Cash Mall [Lemongrass]
//============================================================

// Main NPC :: 201105_luk_enc
//============================================================
itemmall,29,71,3	script	Devil Enchant Master#prq	63,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "เจ้าแบกของเยอะเกินไปแล้ว ไปจัดการสัมภาระแล้วค่อยกลับมาใหม่!";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "น้ำหนักตัวของเจ้าใกล้จะเกินขีดจำกัดแล้ว ไปลดน้ำหนักของลงแล้วกลับมาใหม่!";
		close;
	}
	mes "[Devil Enchant Master]";
	mes "มีอะไร?";
	mes "เจ้ากำลังตามหาข้าอยู่รั้นหรือ?";
	next;
	switch(select("นี่เป็นครั้งแรกที่ข้าได้พบท่าน!:ข้าได้ยินมาว่าท่านคือที่สุด!:ช่วยล้างออฟชั่นให้ข้าหน่อย")) {
	case 1:
		mes "[Devil Enchant Master]";
		mes "ฮ่า ฮ่า ฮ่า~ แน่นอนสิ ข้าไม่ใช่คนประเภทที่จะยอมคุยกับใครสุ่มสี่สุ่มห้าหรอกนะ";
		next;
		mes "[Devil Enchant Master]";
		mes "รวมถึงเจ้าด้วย! ต่อให้เจ้าเอาเงินกองโตมาฟาดตรงหน้า ข้าก็จะไม่ยอม Enchant ให้เจ้าง่ายๆ หรอก!!";
		next;
		mes "[Devil Enchant Master]";
		mes "เจ้าเห็นฉายาของข้าไหม? มีไม่กี่คนหรอกนะที่จะได้ครอบครองชื่อนี้!";
		next;
		mes "[Devil Enchant Master]";
		mes "ข้าจะยอม Enchant ให้เจ้าก็ต่อเมื่อเจ้านำ ^0000ffEnchant Book^000000 มาด้วยเท่านั้น!";
		next;
		mes "[Devil Enchant Master]";
		mes "มิเช่นนั้น ข้าจะไม่ขยับนิ้วทำให้เจ้าแม้แต่นิดเดียว....";
		close;
	case 2:
		if (!countitem(6484)) {
			mes "[Devil Enchant Master]";
			mes "นี่ฟังข้าอยู่หรือเปล่า? ข้าจะทำให้เจ้าก็ต่อเมื่อเจ้ามี Enchant Book มาด้วยเท่านั้น!";
			close;
		}
		set .@select,1;
		break;
	case 3:
		mes "[Devil Enchant Master]";
		if (Zeny < 100000) {
			mes "การล้างออฟชั่นมีค่าธรรมเนียม 100,000 Zeny ดูเหมือนเจ้าจะมีเงินไม่พอนะ...";
			close;
		}
		mes "การล้างออฟชั่นมีค่าใช้จ่าย 100,000 Zeny และข้าจะล้างออฟชั่นทั้งหมดที่อาวุธนั้นมี!";
		next;
		if (select("ขอข้าคิดดูก่อนนะ:ล้างมันเดี๋ยวนี้เลย!") == 1) {
			mes "[Devil Enchant Master]";
			mes "ไปตัดสินใจให้ดีก่อนแล้วค่อยกลับมา!";
			close;
		}
		set .@select,2;
		break;
	}
	set .@part, EQI_HAND_R;

	mes "[Devil Enchant Master]";
	if (!getequipisequiped(.@part)) {
		mes "เจ้าพยายามจะถอดอุปกรณ์ออกระหว่างที่ข้าทำงานงั้นรึ?";
		close;
	}
	setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	// if (!getequipisequiped(.@part)) {
	// 	mes "การถอดอุปกรณ์ออกในระหว่างขั้นตอนการ Enchant นั้นอันตรายมากครับ!";
	// 	close;
	// }
	set .@equip_id, getequipid(.@part);
	.@equip_refine = getequiprefinerycnt(.@part);
	set .@item$, "|1292|1394|1491|1585|2015|13071|13115|16019|18112|21000|";
	if (!compare(.@item$,"|"+.@equip_id+"|")) {
		mes "ข้ายังไม่อยากแตะต้องอุปกรณ์ชิ้นนี้ของเจ้าตอนนี้!";
		close;
	}

	if (.@select == 1) {
		if (!countitem(6484)) {
			mes "นี่ฟังข้าอยู่หรือเปล่า? ข้าจะทำให้เจ้าก็ต่อเมื่อเจ้ามี Enchant Book มาด้วยเท่านั้น!";
			close;
		}
		mes "เจ้าต้องการ Enchant พลังสายไหนลงไปล่ะ?";
		next;
		switch(select("ขอข้าคิดดูก่อน:สายกายภาพ (Physical):สายเวทมนตร์ (Magical)")) {
		case 1:
			mes "[Devil Enchant Master]";
			mes "ไว้เจ้าเปลี่ยนใจเมื่อไหร่ค่อยกลับมาใหม่!";
			close;
		case 2:
			set .@enc_type,1;
			break;
		case 3:
			set .@enc_type,2;
			break;
		}
		mes "[Devil Enchant Master]";
		if (.@equip_card[3]) {
			mes "อุปกรณ์ชิ้นนี้ถูก Enchant ไปแล้ว! เจ้าต้องไปล้างออฟชั่นเดิมออกก่อนถึงจะทำใหม่ได้";
			close;
		}
		mes "กระบวนการนี้อาจล้มเหลวได้ และ ^ff0000มันจะลดระดับการตีบวกของเจ้าลงบางส่วน^000000 แต่การ์ดและอาวุธจะไม่เสียหาย! เจ้ายังยืนยันที่จะทำต่อไหม?";
		next;
		if (select("เอาไว้คราวหน้า!:เริ่มเลย!") == 1) {
			mes "[Devil Enchant Master]";
			mes "ไปตัดสินใจมาให้แน่นอนก่อนเถอะ!";
			close;
		}
		if (.@equip_card[3]) {
			mes "[Devil Enchant Master]";
			mes "ดูเหมือนจะมีปัญหาบางอย่าง ขอข้าตรวจเช็กหน่อย";
			close;
		}

		// [ส่วนการสุ่ม Enchant คงเดิมไว้เพื่อการทำงานของระบบ]
		if (.@enc_type == 1) { // Physical Series
			set .@i, rand(1,1300);
			     if (.@i < 51)    set .@enchant,4734; //Agility5
			else if (.@i < 76)    set .@enchant,4735; //Agility6
			else if (.@i < 88)    set .@enchant,4736; //Agility7
			else if (.@i < 93)    set .@enchant,4737; //Agility8
			else if (.@i < 95)    set .@enchant,4738; //Agility9
			else if (.@i < 96)    set .@enchant,4739; //Agility10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4704; //Strength5
			else if (.@i < 341)   set .@enchant,4705; //Strength6
			else if (.@i < 366)   set .@enchant,4706; //Strength7
			else if (.@i < 378)   set .@enchant,4707; //Strength8
			else if (.@i < 383)   set .@enchant,4708; //Strength9
			else if (.@i < 384)   set .@enchant,4709; //Strength10
			else if (.@i < 434)   set .@enchant,4754; //Luck5
			else if (.@i < 459)   set .@enchant,4755; //Luck6
			else if (.@i < 471)   set .@enchant,4756; //Luck7
			else if (.@i < 476)   set .@enchant,4757; //Luck8
			else if (.@i < 478)   set .@enchant,4758; //Luck9
			else if (.@i < 479)   set .@enchant,4759; //Luck10
			else if (.@i < 679)   set .@enchant,4744; //Vitality5
			else if (.@i < 779)   set .@enchant,4745; //Vitality6
			else if (.@i < 829)   set .@enchant,4746; //Vitality7
			else if (.@i < 854)   set .@enchant,4747; //Vitality8
			else if (.@i < 866)   set .@enchant,4748; //Vitality9
			else if (.@i < 867)   set .@enchant,4749; //Vitality10
			else if (.@i < 967)   set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 992)   set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 1092)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1117)  set .@enchant,4836; //Expert_Archer5
			else if (.@i < 1217)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1242)  set .@enchant,4836; //Expert_Archer5
			else set .@enchant,0;
		} else if (.@enc_type == 2) { // Magical Series
			set .@i, rand(1,1200);
			     if (.@i < 51)    set .@enchant,4714; //Inteligence5
			else if (.@i < 76)    set .@enchant,4715; //Inteligence6
			else if (.@i < 88)    set .@enchant,4716; //Inteligence7
			else if (.@i < 93)    set .@enchant,4717; //Inteligence8
			else if (.@i < 95)    set .@enchant,4718; //Inteligence9
			else if (.@i < 96)    set .@enchant,4719; //Inteligence10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4734; //Agility5
			else if (.@i < 341)   set .@enchant,4735; //Agility6
			else if (.@i < 366)   set .@enchant,4736; //Agility7
			else if (.@i < 378)   set .@enchant,4737; //Agility8
			else if (.@i < 383)   set .@enchant,4738; //Agility9
			else if (.@i < 384)   set .@enchant,4739; //Agility10
			else if (.@i < 484)   set .@enchant,4754; //Luck5
			else if (.@i < 534)   set .@enchant,4755; //Luck6
			else if (.@i < 559)   set .@enchant,4756; //Luck7
			else if (.@i < 571)   set .@enchant,4757; //Luck8
			else if (.@i < 576)   set .@enchant,4758; //Luck9
			else if (.@i < 577)   set .@enchant,4759; //Luck10
			else if (.@i < 777)   set .@enchant,4744; //Vitality5
			else if (.@i < 877)   set .@enchant,4745; //Vitality6
			else if (.@i < 927)   set .@enchant,4746; //Vitality7
			else if (.@i < 952)   set .@enchant,4747; //Vitality8
			else if (.@i < 964)   set .@enchant,4748; //Vitality9
			else if (.@i < 969)   set .@enchant,4749; //Vitality10
			else if (.@i < 1069)  set .@enchant,4812; //Spell4
			else if (.@i < 1094)  set .@enchant,4826; //Spell5
			else if (.@i < 1119)  set .@enchant,4761; //Matk2
			else if (.@i < 1124)  set .@enchant,4806; //Matk3
			else set .@enchant,0;
		} else {
			mes "[Devil Enchant Master]";
			mes "หืม! ไอเทมชิ้นนี้มีปัญหาบางอย่าง รบกวนเจ้าช่วยเช็กมันอีกรอบซิ!";
			close;
		}
		mes "[Devil Enchant Master]";
		if (.@equip_card[3]) {
			mes "ไอเทมชิ้นนี้ได้ถูก Enchant ไปเรียบร้อยแล้ว!";
			close;
		}
		if (!countitem(6484)) {
			mes "เจ้าฟังข้าอยู่ไหม? ข้าจะทำให้เจ้าก็ต่อเมื่อเจ้ามี Enchant Book มาด้วยเท่านั้น!";
			close;
		}
		if (.@enchant == 0) {
			specialeffect EF_SHIELDCHARGE;
			mes "โอ้! ไม่อยากจะเชื่อ!! มันล้มเหลว!! ไว้มาลองใหม่คราวหน้าแล้วกัน!";
			set .@lost_refine, rand(0,.@equip_refine);
			set .@equip_refine, .@equip_refine - .@lost_refine;
		} else {
			specialeffect EF_REPAIRWEAPON;
			mes "ช่องที่ ^9900004^000000 ถูก Enchant สำเร็จแล้ว!";
		}
		delitem 6484,1; //Enchant_Book

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@enchant
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;

		if (.@lost_refine) {
			next;
			mes "[Devil Enchant Master]";
			mes "ความล้มเหลวครั้งนี้ทำให้ระดับการตีบวกลดลง "+.@lost_refine+" เลเวล! อย่าเพิ่งท้อใจไปล่ะ!";
		}
		close;
	} else if (.@select == 2) {
		if (Zeny < 100000) {
			mes "เจ้าต้องพกเงินมาจ่ายค่าธรรมเนียมการล้างออฟชั่นด้วยสิ!!";
			close;
		}
		if (getiteminfo(.@equip_card[3], ITEMINFO_SUBTYPE) != CARD_ENCHANT) {
			mes "ไอเทมชิ้นนี้ยังไม่ได้ถูก Enchant เลยนะ!";
			close;
		}
		if (!getequipisequiped(.@part)) {
			mes "เจ้ากำลังจะถอดอุปกรณ์ออกตอนที่ข้ากำลังทำงานรึ?";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		mes "ข้าได้ล้างพลัง Enchant ออกให้เรียบร้อยแล้ว";
		set Zeny, Zeny - 100000;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] 0
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],0;

		close;
	} else {
		mes "ดูเหมือนเจ้าจะเลือกหัวข้อผิดนะ??";
		close;
	}
}
