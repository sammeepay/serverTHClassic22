//===== rAthena Script =======================================
//= Insurance Exchanger
//===== Description: =========================================
//= Exchange items for New_Insurance item.
//===== Changelogs: ==========================================
//= 1.0 Initial release. [Balfear]
//============================================================

prontera,82,108,5	script	พ่อค้าแห่งดวงใจ#life01	4_M_NFDEADMAN2,5,5,{
	.@old_item = 12209;
	.@new_item = 6413;
	.@old_item_name$ = getitemname(.@old_item);
	.@new_item_name$ = getitemname(.@new_item);

	if (!checkweight(1201,1) || (MaxWeight - Weight) < 500) {
		mes "คุณไม่สามารถดำเนินบทสนทนาต่อได้เนื่องจากมีไอเทมในตัวมากเกินไป";
		mes "กรุณาจัดการไอเทมในกระเป๋าแล้วลองใหม่อีกครั้ง";
		close;
	}
	mes "[ พ่อค้าแห่งดวงใจ ]";
	mes "สวัสดี! ข้าเห็นว่าท่านเป็นนักผจญภัยที่ทุ่มเทหัวใจให้กับการเดินทาง ล่องเรือข้ามทวีป และชื่นชอบความตื่นเต้นในที่อันตรายสินะ!";
	next;
	if(!countitem(.@old_item)){
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "ท่านพอจะมี " + mesitemlink( .@old_item ) + " เก็บไว้ในคลังบ้างไหมล่ะ?";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "ถ้าท่านนำ " + .@old_item_name$ + " มาให้ข้า ข้าจะเปลี่ยนมันเป็น " + mesitemlink( .@new_item ) + " ให้ ท่านคิดว่ายังไง? พอจะนึกออกไหมว่าไอเทมนี้คืออะไร?";
	} else {
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "ท่านเคยคิดอยากจะเปลี่ยน " + mesitemlink( .@old_item ) + " ให้เป็นของที่ดีกว่านี้ไหม? อย่างเช่น... " + mesitemlink( .@new_item ) + " ท่านสนใจไหมล่ะ?";
	}
	next;
	switch(select("มันคืออะไรเหรอ?",(countitem(.@old_item)?"ช่วยแลกให้หน่อย!":""),"ฉันไม่สนใจ")) {
	case 1:
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes mesitemlink( .@old_item ) + " คือไอเทมที่จะช่วยป้องกันไม่ให้ท่านสูญเสียค่าประสบการณ์เมื่อตาย 1 ครั้ง ภายในระยะเวลา 30 นาทีหลังจากใช้งาน";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "แต่หลายคนก็เสียดายเรื่องเงื่อนไขของเวลาใช่ไหมล่ะ? ข้าก็เลยไปศึกษาดูและเตรียม " + mesitemlink( .@new_item ) + " นี้มาให้!";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes mesitemlink( .@new_item ) + " คือไอเทมที่จะป้องกันการเสียค่าประสบการณ์เมื่อตาย 1 ครั้ง เพียงแค่ท่านพกมันไว้ ^EE0000โดยไม่จำกัดระยะเวลาการใช้งาน^000000 แน่นอนว่าหลังจากตายแล้ว "+.@new_item_name$+" จะหายไป";
		next;
		select("ทำไมคุณถึงยอมแลกเปลี่ยนล่ะ?");
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "สงสัยงั้นรึ? ข้าก็เป็นพ่อค้าคนหนึ่งนะ การซื้อขายคือโชคชะตาของข้า ไม่ว่าจะเกี่ยวกับความเป็นหรือความตายของใครก็ตาม ท่านคิดว่าข้าจะยอมพลาดโอกาสทำเงินนี้ไปรึไง? ข้าได้เงิน ส่วนคนเป็นก็ได้ทางรอดจากความเจ็บปวดจากความตาย ไม่ใช่ข้อตกลงที่ดีหรอกรึ?";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "อัตราการแลกเปลี่ยนคือ " + mesitemlink( .@old_item ) + " 5 ชิ้น ต่อ " + mesitemlink( .@new_item ) + " 1 ชิ้น มีคำถามอะไรอีกไหม?";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "หืม~ คิดว่ายังไงล่ะ? เป็นข้อตกลงที่ยุติธรรมใช่ไหม? ท่านอยากจะคอยพะวงเรื่องเวลาไปตลอดรึไง? นี่คือของที่เหล่านักผจญภัยที่รักความท้าทายต้องมีติดตัวไว้นะ";
		close;
		
	case 2:	
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "ใช่เลยๆ ตัดสินใจได้ดี การแลกเปลี่ยนเป็นของที่ดีกว่าน่ะคุ้มค่าที่สุดแล้ว";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "ข้าสามารถเปลี่ยน ^0000cd " + .@old_item_name$ + " 5 ชิ้น เป็น " + .@new_item_name$ + " 1 ชิ้น^000000 ให้ท่านได้";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "อย่างไรก็ตาม ^EE0000[" + .@old_item_name$ + " แบบไม่สามารถซื้อขายได้] จะไม่สามารถนำมาแลกเปลี่ยนได้^000000 ดังนั้นไม่มีประโยชน์ที่จะนำมันมาขอแลกที่นี่นะ";
		next;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "เราจะแลกเปลี่ยนกันเลยไหม?";
		next;
		if (select("แลกเลย!:ฉันยังไม่อยากแลก") == 2)
			break;
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "ท่านต้องการแลกจำนวนเท่าไหร่? ^0000cdท่านสามารถแลกได้สูงสุดครั้งละ 10 ชิ้น (" + .@new_item_name$ + ")^000000";
		next;
		input .@amount;
		if (.@amount == 0) {
			mes "[ พ่อค้าแห่งดวงใจ ]";
			mes "หืม? ไม่สนใจแล้วรึ? นี่เป็นของดีที่เหมาะกับคนอย่างท่านมากเลยนะ แต่ถ้าท่านไม่อยากปกป้องชีวิตตัวเอง ข้าก็คงไม่บังคับ";
			close;
		}
		if (.@amount > 10) {
			mes "[ พ่อค้าแห่งดวงใจ ]";
			mes "อย่างที่บอกไป ^0000cdท่านแลกได้สูงสุดแค่ครั้งละ 10 ชิ้นเท่านั้น^000000 กรุณาตรวจสอบจำนวนอีกครั้ง";
			close;
		}
		if (countitem(.@old_item) < (.@amount*5)) {
			mes "[ พ่อค้าแห่งดวงใจ ]";
			mes "เอ่อ ขอประทานโทษนะ... แต่ดูเหมือนท่านจะมี " +.@old_item_name$+ " ไม่พอนะ? ต้องใช้ " + mesitemlink( .@old_item ) + " 5 ชิ้น ต่อ 1 " + mesitemlink( .@new_item ) + " นะครับ ลองเช็คดูใหม่ซิ";
			close;
		}
		mes "[ พ่อค้าแห่งดวงใจ ]";
		mes "เรียบร้อย! ข้าได้แลกเปลี่ยน ^0000cd" + .@old_item_name$ + "^000000 เป็น ^EE0000" + .@new_item_name$ + "^000000 ให้ท่านแล้ว!! เป็นการแลกเปลี่ยนที่ยอดเยี่ยมไปเลยใช่ไหมล่ะ? คราวหน้าถ้าท่านมี " + .@old_item_name$ + " อีก ก็แวะมาหาข้าได้นะ!";
		delitem .@old_item,.@amount*5;
		getitem .@new_item,.@amount;
		close;
	case 3:
		break;
	}
	mes "[ พ่อค้าแห่งดวงใจ ]";
	mes "นี่คือข้อเสนอที่ยอดเยี่ยมนะ! ท่านนักผจญภัย มันคือของจำเป็นสำหรับคนที่ชอบไปในที่อันตรายแบบท่านเลยล่ะ!";
	next;
	mes "[ พ่อค้าแห่งดวงใจ ]";
	mes "ถ้าท่านเริ่มสนใจเมื่อไหร่ อย่าลืมกลับมาแลกกับข้าล่ะ ตกลงไหม?";
	close;
	
OnTouch:
	if(countitem(12209))
		npctalk "ท่านมีของดีอยู่กับตัวนี่นา สนใจจะเปลี่ยน " + getitemname(12209) + " ให้เป็นของที่เจ๋งกว่านี้ไหมล่ะ?", "", bc_self;
	end;
}
