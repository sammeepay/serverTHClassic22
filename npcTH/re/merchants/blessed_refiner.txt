//===== rAthena Script ======================================= 
//= Blessed Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiners that use Blessed ores to refine equipment.
//= Upon failure, the equipment is not destroyed. The success
//= rate is identical to that for Enriched ores.
//= - "Blacksmith Dister" only refines from +6~12.
//= NOTE: This NPC is currently disabled on official servers.
//===== Changelog: ===========================================
//= 1.0 First version. [Euphy]
//= 1.1 Removed re-roll behavior. [Secret]
//============================================================

// Main NPC :: new_smelting612
//============================================================
-	script	::BlacksmithDister	-1,{
	disable_items;
	mes "[Blacksmith Dister]";
	mes "ในสังคมที่มีการแข่งขันสูงแบบนี้ เราต้องแตกต่างถึงจะอยู่รอด!";
	mes "ข้าจะรับตีบวกอุปกรณ์ที่อยู่ในระดับ +6 ถึง +12 เท่านั้น";
	next;
	mes "[Blacksmith Dister]";
	mes "ข้าต้องใช้ ^ff9999Blessed Oridecon^000000 สำหรับอาวุธ และ ^ff9999Blessed Elunium^000000 สำหรับชุดเกราะ";
	mes "หากตีบวกล้มเหลว ^FF0000อุปกรณ์จะไม่ถูกทำลายและระดับจะไม่ลดลง^000000";
	next;
	mes "[Blacksmith Dister]";
	mes "ว่ายังไงล่ะ? เจ้าพร้อมจะตีบวกหรือยัง?";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[ไม่ได้สวมใส่]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Blacksmith Dister]";
		switch(.@part) {
		case EQI_HEAD_TOP:
			mes "ข้าเป็นช่างตีเหล็กนะ ไม่ใช่ช่างทำผม";
			break;
		case EQI_ARMOR:
			mes "ด้วยค้อนของข้า ข้าจะทำให้เจ้ากลายเป็นดาราที่เจิดจรัสที่สุดเลย";
			break;
		case EQI_HAND_L:
		case EQI_HAND_R:
			mes "การทำมือเทียมไม่ใช่ความเชี่ยวชาญของข้านะ";
			break;
		case EQI_GARMENT:
			mes "ไปเอาไอเทมออกมาสิ ข้าจะได้ตีบวกให้!";
			break;
		case EQI_SHOES:
			mes "กลิ่นแปลกๆ นี่มันมาจากไหนกันเนี่ย?";
			break;
		case EQI_ACC_L:
		case EQI_ACC_R:
			mes "แล้วเครื่องประดับอยู่ไหนล่ะ?";
			break;
		case EQI_HEAD_MID:
			mes "เจ้าต้องการให้ข้าตีบวกชิ้นไหนกันแน่?";
			break;
		case EQI_HEAD_LOW:
			mes "หืม? เจ้าต้องการให้ข้าทำอะไรนะ?";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[Blacksmith Dister]";
		mes "ไอเทมชิ้นนี้ไม่สามารถตีบวกได้";
		close;
	}
	.@equip_id = getequipid(.@part);
	.@itemtype = getiteminfo( .@equip_id, ITEMINFO_TYPE );
	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	if (.@equip_refine < 6 || .@equip_refine > 12) {
		mes "[Blacksmith Dister]";
		mes "อุปกรณ์ชิ้นนี้อยู่ที่ระดับ +"+.@equip_refine+" ข้าจะรับงานเฉพาะระดับ +6 ถึง +12 เท่านั้น!";
		close;
	}
	if( .@itemtype == IT_ARMOR ){
		.@equip_lv = getequiparmorlv( .@part );
		.@type$ = "ชุดเกราะ";

		switch(.@equip_lv) {
			case 1:
				set .@price,20000;
				set .@material,6439; //Unbreakable_Def
				break;
			default:
				// TODO:
				close;
		}
	}else if( .@itemtype == IT_WEAPON ){
		.@equip_lv = getequipweaponlv( .@part );
		.@type$ = "อาวุธ";

		switch( .@equip_lv ){
			case 1:
				set .@price,1000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 2:
				set .@price,2000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 3:
				set .@price,20000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 4:
				set .@price,40000;
				set .@material,6438; //Unbreakable_Weap
				break;
			default:
				// TODO:
				close;
		}
	}else{
		// TODO:
		close;
	}
	set .@ore$,"^ff9999Blessed "+.@type$+" Ore^000000";
	mes "[Blacksmith Dister]";
	mes ""+.@type$+" ชิ้นนี้อยู่ที่ระดับ +"+.@equip_refine+" การจะตีบวกเพิ่ม ข้าต้องการ "+.@ore$+" และค่าธรรมเนียมอีก "+callfunc("F_InsertComma",.@price)+" Zeny";
	mes "เจ้าต้องการจะดำเนินการต่อหรือไม่?";
	next;
	if(select("ตกลง:ไม่ล่ะ") == 2) {
		mes "[Blacksmith Dister]";
		mes "ข้ากำลังยุ่ง อย่ามาล้อเล่นกับข้านะ!";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[Blacksmith Dister]";
		mes ""+.@type$+" ชิ้นนี้ผ่านการตีบวกมาหลายครั้งแล้ว ถึงแม้อุปกรณ์จะไม่หายไปหากล้มเหลว แต่ "+.@ore$+" จะสูญหายไปทันทีนะ!";
		next;
		mes "[Blacksmith Dister]";
		mes "และแน่นอนว่าค่าธรรมเนียมก็ไม่คืนให้ด้วย! ยังอยากจะไปต่อไหม?";
		next;
		if(select("ไปต่อ:หยุดแค่นี้ก่อน") == 2) {
			mes "[Blacksmith Dister]";
			mes "ดีแล้วล่ะ... ข้าเองก็คงเสียใจถ้ามันล้มเหลวเหมือนกัน";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[Blacksmith Dister]";
		mes "วัตถุดิบไม่เพียงพอ";
		mes "สำหรับการตีบวก "+.@type$+" เลเวล "+.@equip_lv+" ข้าต้องการ "+.@ore$+" และเงิน "+callfunc("F_InsertComma",.@price)+" Zeny";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	mes "[Blacksmith Dister]";
	mes "เคร้ง! เคร้ง! เคร้ง!";
	if (getequippercentrefinery(.@part, true) > rand(100)) {
		specialeffect EF_BLESSING;
		successrefitem .@part;
		next;
		mes "[Blacksmith Dister]";
		mes "ฮ่าๆๆๆๆๆๆๆ!!!";
		next;
		mes "[Blacksmith Dister]";
		mes "การตีบวกประสบความสำเร็จอย่างงดงาม~ ฮ่าๆ!";
		close;
	}
	specialeffect EF_CURSEATTACK;
	specialeffect2 EF_SUI_EXPLOSION;
	next;
	emotion (!rand(5))?ET_ANGER:ET_HUK;
	mes "[Blacksmith Dister]";
	mes "โอ้... พระช่วย!!!!";
	next;
	mes "[Blacksmith Dister]";
	mes "โถ่เอ๊ย! ถึงแม้จะมีพลังของ "+.@ore$+" ช่วยรักษาอุปกรณ์เอาไว้ก็เถอะ...";
	next;
	mes "[Blacksmith Dister]";
	mes "แต่พอตีบวกล้มเหลวแบบนี้ อารมณ์ข้าบูดชะมัดเลย!!";
	close;
}
prt_in,52,56,3	duplicate(BlacksmithDister)	Blacksmith Dister#prt	826
payon,148,172,3	duplicate(BlacksmithDister)	Blacksmith Dister#pay	826
alberta_in,20,56,3	duplicate(BlacksmithDister)	Blacksmith Dister#alb	826
yuno_in01,175,18,3	duplicate(BlacksmithDister)	Blacksmith Dister#yuno	826
ein_in01,26,82,3	duplicate(BlacksmithDister)	Blacksmith Dister#ein	826
lhz_in02,280,15,3	duplicate(BlacksmithDister)	Blacksmith Dister#lhz	826
