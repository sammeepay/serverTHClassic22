//===== rAthena Script =======================================
//= Merchant WoE TE
//===== Description: =========================================
//= [Official Conversion]
//= Merchants and Rental merchant template for WoE TE
//	(duplicates in re/woe_te/<castle file>).
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

te_prt_gld,114,153,4	shop	TE Potion Merchant#prt	4_M_TELEPORTER,11557:-1,11558:-1
te_alde_gld,215,184,4	duplicate(TE Potion Merchant#prt)	TE Potion Merchant#alde	4_M_TELEPORTER

// Rental Items manager
//=======================================
-	script	rental_woe_TE	-1,{
	mes "[ ผู้จัดการไอเทมเช่า ]";
	if (jobcanentermap("te_prtcas01") == 1) {
		.@list01$ = "[โล่] [เสื้อเกราะ] [ผ้าคลุม]";
		.@list02$ = "[หมวก] [เครื่องประดับ] [รองเท้า]";
		.@list03$ = "[ดาบ] [หอก] [ขวาน] [กาตาร์]";
		.@list04$ = "[กระบอง] [คทา] [สนับมือ] [หนังสือ]";
		.@list05$ = "[ธนู] [ปืน] [เครื่องดนตรี] [แส้]";

		mes "^4d4dffเรามีอุปกรณ์สวมใส่สำหรับการต่อสู้มากมายที่ท่านสามารถนำไปใช้ในพื้นที่ Gloria และ Kafragarten ตามกฎของ Siege Battle TE ครับ^000000";
		next;
		mes "[ ผู้จัดการไอเทมเช่า ]";
		mes "^4d4dffเราเปิดให้เช่าฟรีเป็นเวลา 1 วันครับ^000000";
		mes "อย่างไรก็ตาม ไอเทมเช่าเหล่านี้ ^4d4dffจะไม่มีผลใดๆ เมื่ออยู่นอกพื้นที่ Siege Battle TE นะครับ^000000";
		next;
		mes "[ ผู้จัดการไอเทมเช่า ]";
		mes "^4d4dffท่านต้องมีเลเวล 40 ขึ้นไปจึงจะสามารถเช่าไอเทมได้^000000";
		mes "กรุณาเลือกประเภทอุปกรณ์ที่ท่านต้องการ";
		next;
		while(1) {
			switch( select( "ดูรายการไอเทมเช่า", .@list01$, .@list02$, .@list03$, .@list04$, .@list05$, "ยกเลิก" ) ) {
			case 1:
				mes "[ ผู้จัดการไอเทมเช่า ]";
				mes "นี่คือรายการไอเทมเช่าและรายละเอียดต่างๆ ครับ";
				close2;
				readbook 11059,1;// WoE_TE_Rental_List
				end;
			case 2:
				callsub( S_Rent, "โล่, เสื้อเกราะ และผ้าคลุม",
					 2178,"TE Siege Buckler",		// TE_Woe_Buckler
					 2179,"TE Siege Shield",		// TE_Woe_Shield
					 2180,"TE Siege Magic Guard",	// TE_Woe_Magic_Guard
					15062,"TE Siege Long Coat",		// TE_Woe_Coat
					15063,"TE Siege Mail",			// TE_Woe_Chain_Mail
					15064,"TE Siege Magic Coat",	// TE_Woe_Mage_Coat
					20702,"TE Siege Muffler",		// TE_Woe_Muffler
					20703,"TE Siege Cape",			// TE_Woe_Manteau
					20704,"TE Siege Magic Cape" );	// TE_Woe_Magic_Manteau
				break;
			case 3:
				callsub( S_Rent, "หมวก, เครื่องประดับ และรองเท้า",
					2496,"TE Siege Shoes",			// TE_Woe_Shoes
					2497,"TE Siege Boots",			// TE_Woe_Boots
					2498,"TE Siege Magic Sandals",	// TE_Woe_Magic_Sandal
					2944,"TE Guardian Ring",		// TE_Ring_Of_Protection
					2945,"TE Ring of Wrath",		// TE_Ring_Of_Rage
					2946,"TE Ring of Rebellion",	// TE_Ring_Of_Defiance
					18732,"TE Siege Cap",			// TE_Woe_Cap
					18733,"TE Siege Bone Helm",		// TE_Woe_Bone_Helm
					18734,"TE Siege Magic Eyes" );	// TE_Woe_Magic_Eyes
				break;
			case 4:
				callsub( S_Rent, "ดาบ, หอก, ขวาน และกาตาร์",
					1299,"TE Siege Katar",				// TE_Woe_Katar
					1399,"TE Siege Two-Handed Axe",		// TE_Woe_Two_Hand_Saxe
					1495,"TE Siege Lance",				// TE_Woe_Lance
					13317,"TE Siege Magic Shuriken",	// TE_Woe_Huuma
					21006,"TE Siege Two-Handed Sword",	// TE_Woe_Two_Hand_Sword
					1319,"TE Siege Axe",				// TE_Woe_Axe
					1437,"TE Siege Pike",				// TE_Woe_Pike
					13083,"TE Siege Knife",				// TE_Woe_Knife
					13439,"TE Siege Sword" );			// TE_Woe_Sword
				break;
			case 5:
				callsub( S_Rent, "คทา, กระบอง, หนังสือ และสนับมือ",
					2019,"TE Siege Two-Handed Staff",// TE_Woe_Two_Hand_Staff
					1591,"TE Siege Book",			// TE_Woe_Book
					1667,"TE Siege Staff",			// TE_Woe_Staff
					1834,"TE Siege Fist",			// TE_Woe_Fist
					16025,"TE Siege Mace" );		// TE_Woe_Mace
				break;
			case 6:
				callsub( S_Rent, "ธนู, ปืน, เครื่องดนตรี และแส้",
					18118,"TE Siege Bow",			// TE_Woe_Bow
					13117,"TE Siege Pistol",		// TE_Woe_Pistol
					13184,"TE Siege Rifle",			// TE_Woe_Rifle
					13185,"TE Siege Gatling Gun",	// TE_Woe_Gatling
					13186,"TE Siege Shotgun",		// TE_Woe_Shotgun
					13187,"TE Siege Grenade Gun",	// TE_Woe_Grenade
					1932,"TE Siege Guitar",			// TE_Woe_Guitar
					1987,"TE Siege Rope" );			// TE_Woe_Rope
				break;
			case 7:
				mes "[ ผู้จัดการไอเทมเช่า ]";
				mes "แล้วแวะมาใหม่นะครับ";
				close;
			}
		}
	}
	mes "เรามีอุปกรณ์สวมใส่สำหรับการต่อสู้มากมายที่ท่านสามารถนำไปใช้ในพื้นที่ Gloria และ Kafragarten ตามกฎของ Siege Battle TE ครับ";
	next;
	mes "[ ผู้จัดการไอเทมเช่า ]";
	mes "ขออภัยด้วยครับ อาชีพของท่านไม่ได้รับอนุญาตให้เข้าร่วมใน Siege Battle TE";
	mes "ข้าเกรงว่าข้าคงไม่สามารถมอบไอเทมใดๆ ให้ท่านได้";
	close;

S_Rent:
	mes "[ ผู้จัดการไอเทมเช่า ]";
	if (checkweight(1304,3) == 0) {
		mes "สัมภาระของท่านหนักเกินกว่าจะรับอุปกรณ์เช่าชิ้นใหม่ได้";
		mes "กรุณาจัดการพื้นที่ว่างในกระเป๋าแล้วลองใหม่อีกครั้งครับ";
		close;
	}
	mes "เรามี " + getarg(0) + " เตรียมไว้ให้ท่านแล้ว";
	mes "กรุณาเลือกไอเทมที่ต้องการ";
	next;
	for ( .@i = 1; .@i < getargcount(); .@i += 2 ) {
		if (rentalcountitem(getarg(.@i)) > isequippedcnt(getarg(.@i))) // ไม่นับไอเทมที่สวมใส่อยู่
			.@menu$ = .@menu$ + "^4d4dff" + getarg(.@i+1) + " - เช่าไปแล้ว^000000:";
		else
			.@menu$ = .@menu$ + getarg(.@i+1) + ":";
	}
	.@total_item = (getargcount() -1) /2;
	.@s = select( .@menu$ + ":ดูรายการอื่น" ) -1;
	if (.@s <= .@total_item) {
		.@index = .@s *2 +1;
		mes "[ ผู้จัดการไอเทมเช่า ]";
		if (rentalcountitem(getarg(.@index)) > isequippedcnt(getarg(.@index)))
			mes "ท่านมี " + getarg(.@index+1) + " อยู่กับตัวแล้วครับ";
		else {
			mes "นี่คือ " + getarg(.@index+1) + " ของท่าน";
			mes "^4d4dffไอเทมนี้จะมีผลใช้งานได้ในอีก 24 ชั่วโมงข้างหน้า^000000 และจะหมดอายุลงหลังจากนั้นครับ";
			rentitem getarg(.@index),86400;
		}
		mes "ต้องการรับอะไรเพิ่มเติมอีกไหมครับ?";
		next;
	}
	return;
}
