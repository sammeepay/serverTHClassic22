//===== rAthena Script =======================================
//= Merchants from new dungeons for Level 175
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- Merchants related to new dungeons.
//===== Changelog: ===========================================
//= 1.0 Initial release of Einbech Dungeon 3 merchants. [Capuche]
//============================================================

einbech,149,257,4	script	Site manager Elmen#extdq0	4_TOWER_16,{
	if (checkweight(25814,100) == 0 || checkweight(1201,1) == 0 || (MaxWeight - Weight) < 10000) {
		mes "- คุณไม่สามารถดำเนินการต่อได้เนื่องจากคุณมีไอเทมมากเกินไป -";
		close;
	}
	mes "[Elmen]";
	mes "สวัสดี นักผจญภัย";
	mes "หากคุณมาที่นี่เพื่อซื้ออุปกรณ์ขุด Dynite หรือต้องการหลอมแร่ให้เป็น Dynite คุณมาถูกที่แล้วล่ะ";
	next;
	switch( select( "การหลอม Dynite", "ร้านค้า Dynite", "แลกเปลี่ยนอุปกรณ์ Dynite" ) ) {
	case 1:
		npctalk "Elmen: คุณสามารถหลอม Dynite ได้สูงสุดครั้งละ 100 ก้อน เครื่องจักรนี้มันตัวนิดเดียว", "", bc_self;
		mes "[Elmen]";
		mes "" + mesitemlink(25814) + " แร่ที่หลอมแล้ว";
		mes "" + mesitemlink(25809) + " ^777777(ปัจจุบันมี " + countitem(25809) + ")^000000";
		mes "" + mesitemlink(25810) + " ^777777(ปัจจุบันมี " + countitem(25810) + ")^000000";
		mes "" + mesitemlink(25811) + " ^777777(ปัจจุบันมี " + countitem(25811) + ")^000000";
		mes "" + mesitemlink(25812) + " ^777777(ปัจจุบันมี " + countitem(25812) + ")^000000";
		mes "" + mesitemlink(25813) + " ^777777(ปัจจุบันมี " + countitem(25813) + ")^000000";
		mes "คุณต้องใช้แร่ทั้ง 5 ชนิดอย่างละหนึ่งก้อน";
		next;
		input .@val;
		if (.@val > 100) {
			mes "[Elmen]";
			mes "ขออภัยด้วย นักผจญภัย";
			mes "การหลอม Dynite สามารถทำได้สูงสุดครั้งละ 100 ก้อนเท่านั้น เครื่องจักรมันเล็กน่ะ เลยช่วยไม่ได้จริงๆ โปรดเข้าใจด้วยนะ";
			close;
		}
		if (.@val == 0) {
			mes "[Elmen]";
			mes "ยกเลิกงั้นเหรอ?";
			mes "ถ้าคุณต้องการหลอม Dynite เมื่อไหร่ ก็มาหาได้ตลอดนะ ไม่มีใครทำได้นอกจากฉันอีกแล้วล่ะ";
			close;
		}
		if (countitem(25809) < .@val || countitem(25810) < .@val || countitem(25811) < .@val || countitem(25812) < .@val || countitem(25813) < .@val) {
			mes "[Elmen]";
			mes "นี่! แบบนี้มันทำให้ลำบากกันทั้งคู่เลยนะ วัตถุดิบที่ต้องใช้หลอมน่ะหมดหรือเปล่า? วัตถุดิบน่ะ!";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		progressbar_npc "FFFF00",1;
		delitem 25809,.@val;	// Ein_REDIUM
		delitem 25810,.@val;	// Ein_RINDIUM
		delitem 25811,.@val;	// Ein_ODIUM
		delitem 25812,.@val;	// Ein_PURDIUM
		delitem 25813,.@val;	// Ein_WHIDIUM
		getitem 25814,.@val;	// Ein_DYNITE
		mes "[Elmen]";
		mes "หลอม Dynite จำนวน " + .@val + " ก้อนเรียบร้อย วันนี้มันสมบูรณ์แบบมาก!";
		close;
	case 2:
		mes "[Elmen]";
		mes "มีเครื่องประดับและใบอนุญาตดัดแปลงมากมาย ตรวจดูสิ่งที่คุณต้องการให้ดีก่อนตัดสินใจซื้อล่ะ";
		close2;
		callshop "barter_ein_dun03";
		end;
	case 3:
		mes "[Elmen]";
		mes "ฉันสามารถแลกเปลี่ยนอุปกรณ์ด้วย Dynite ได้ แต่มันถูกบรรจุอยู่ในกล่องที่ปิดสนิทน่ะ";
		mes "นำ ^4d4dffDynites^000000 มาให้ฉัน 50 ก้อน แล้วฉันจะเปิด ^4d4dffweapon box^000000 ให้คุณเอง";
		next;
		mes "[Elmen]";
		mes "ฉันก็ไม่รู้หรอกนะว่าจะมีอะไรออกมา แต่คุณภาพมันดีแน่นอน คุณอยากจะเปิดมันไหมล่ะ?";
		next;
		if (select( "หลับตาแล้วแลกเปลี่ยน", "ไม่แลกเปลี่ยน" ) == 2) {
			mes "[Elmen]";
			mes "เห้ น่าเสียดายนะเนี่ย มันเป็นข้อเสนอที่ดีแท้ๆ";
			close;
		}
		if (countitem(25814) < 50) {
			mes "[Elmen]";
			mes "คุณมี Dynite ไม่พอเพื่อนยาก";
			mes "ฉันก็ไม่รู้หรอกนะว่าจะมีอะไรออกมา แต่มันยากนะที่จะไปที่อื่นแล้วหาอุปกรณ์แบบนี้ในราคานี้ได้";
			close;
		}
		delitem 25814,50;
		// unknown text (ข้อความที่ไม่ทราบแน่ชัด)
		getgroupitem(IG_EIN_1HITEMBOX);
		end;
	}
}
