//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
prontera,184,177,6	script	Refine Master	851,{
	disable_items;
	setarray .@cert_weapon[0],
		6864,	// Guarantee_Weapon_19Up
		6875,	// Guarantee_Weapon_18Up
		6874,	// Guarantee_Weapon_17Up
		6873,	// Guarantee_Weapon_16Up
		6872,	// Guarantee_Weapon_15Up
		6871,	// Guarantee_Weapon_14Up
		6870,	// Guarantee_Weapon_13Up
		6584,	// Guarantee_Weapon_12Up
		6238,	// Guarantee_Weapon_11Up
		6228,	// Guarantee_Weapon_9Up
		6229,	// Guarantee_Weapon_8Up
		6230,	// Guarantee_Weapon_7Up
		6231,	// Guarantee_Weapon_6Up
		6456;	// Guarantee_Weapon_5Up
	setarray .@cert_armor[0],
		6865,	// Guarantee_Armor_19Up
		6881,	// Guarantee_Armor_18Up
		6880,	// Guarantee_Armor_17Up
		6879,	// Guarantee_Armor_16Up
		6878,	// Guarantee_Armor_15Up
		6877,	// Guarantee_Armor_14Up
		6876,	// Guarantee_Armor_13Up
		6585,	// Guarantee_Armor_12Up
		6239,	// Guarantee_Armor_11Up
		6232,	// Guarantee_Armor_9Up
		6233,	// Guarantee_Armor_8Up
		6234,	// Guarantee_Armor_7Up
		6235,	// Guarantee_Armor_6Up
		6457;	// Guarantee_Armor_5Up
	setarray .@cert_level[0],19,18,17,16,15,14,13,12,11,9,8,7,6,5;
	.@size_cert = getarraysize(.@cert_weapon);

	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@cert_weapon[.@i]) > 0 || countitem(.@cert_armor[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}

	if (.@check == 0) {
		mes "[Refine Master]";
		mes "สวัสดี!";
		mes "มีอะไรให้ช่วยไหม?";
		mes "ข้าคือผู้เชี่ยวชาญด้าน";
		mes "การตีบวกไอเทมโดยเฉพาะ";
		mes "แต่ตอนนี้ข้าไม่ได้เปิดรับงานทั่วไปแล้วล่ะ";
		next;
		switch(select("ขอตัวลาล่ะ:หืม... ชักจะสงสัยแล้วสิ")) {
		case 1:
			mes "[Refine Master]";
			mes "โชคดีนะ นักผจญภัย";
			close;
		case 2:
			mes "[Refine Master]";
			mes "จริงๆ แล้ว บางครั้งข้าก็รับตีบวกให้เฉพาะนักผจญภัยที่มี ^006400Refine Ticket^000000 (ใบตีบวก) มาด้วยเท่านั้นน่ะ...";
			mes "บ๊ายบาย~!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[Refine Master]";
	mes "ยินดีต้อนรับ!";
	mes "ข้าสามารถตีบวกไอเทมให้เจ้าได้ ^006400ตามระดับเลเวลของใบตีบวก^000000 ที่เจ้ามีเลยนะ";
	mes "ไม่ต้องกังวลไป! งานของข้าไม่มีคำว่าพลาด ไอเทมไม่มีวันแตกแน่นอน";
	next;
	if(select("เดี๋ยวข้ากลับมาใหม่:ตีบวกไอเทมด้วย Ticket") == 1) {
		mes "[Refine Master]";
		mes "ตกลง";
		mes "แล้วแวะมาใหม่นะ";
		close;
	}
	mes "[Refine Master]";
	mes "เจ้าต้องการจะตีบวกอุปกรณ์ชิ้นไหนล่ะ?";
	next;
	setarray .@position$[1],"หมวกส่วนบน","เสื้อเกราะ","มือซ้าย","มือขวา","ผ้าคลุม","รองเท้า","เครื่องประดับ 1","เครื่องประดับ 2","หมวกส่วนกลาง","หมวกส่วนล่าง";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for ( .@i = 1; .@i <= 10; ++.@i )
		.@menu$ += (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : .@position$[.@i] + "- [ว่าง]") + ":";

	.@part = .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Refine Master]";
		mes "เจ้าต้องสวมใส่อุปกรณ์ที่ต้องการจะตีบวกก่อนนะ";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[Refine Master]";
		mes "โอ้ ข้าขอโทษด้วย";
		mes "ไอเทมชิ้นนี้ไม่สามารถตีบวกได้";
		close;
	}
	.@refineitemid = getequipid(.@part);
	.@refinerycnt = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );
	if( .@itemtype == IT_WEAPON ){
		switch( getequipweaponlv( .@part ) ){
			case 1: case 2: case 3: case 4:
				copyarray .@tickets[0], .@cert_weapon[0], .@size_cert;
				.@type$ = "อาวุธ (Weapon)";
				break;
			default:
				close;
		}
	}else if( .@itemtype == IT_ARMOR ){
		switch( getequiparmorlv( .@part ) ){
			case 1:
				copyarray .@tickets[0], .@cert_armor[0], .@size_cert;
				.@type$ = "ชุดป้องกัน (Armor)";
				break;
			default:
				close;
		}
	}else{
		close;
	}
	.@check = 0;
	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@tickets[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}
	if (.@check == 0) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "ถ้าเจ้าต้องการจะตีบวก ^006400"+.@type$+"^000000 ชิ้นนี้ล่ะก็ รบกวนไปหา ^006400ใบตีบวก (Refine Ticket)^000000 ที่ตรงประเภทมาด้วยนะ";
		mes "แล้วเจอกันใหม่!";
		close;
	}
	mes "[Refine Master]";
	mes "โปรดเลือก ^006400"+.@type$+" Refine Ticket^000000 ที่เจ้าต้องการใช้งาน";
	next;
	.@menu$ = "";
	for ( .@i = 0; .@i < .@size_cert; ++.@i )
		.@menu$ += getitemname(.@tickets[.@i]) + ":";
	.@select = select(.@menu$)-1;
	.@ticket_lv = .@cert_level[.@select];
	.@ticket_id = .@tickets[.@select];

	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[Refine Master]";
		mes "ไม่มี "+getitemname(.@ticket_id)+" ในตัวเจ้านี่นา เจ้าเก็บไว้ในคลังหรือเปล่า?";
		mes "ลองตรวจสอบดูอีกรอบนะ";
		mes "แล้วเจอกันใหม่!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Refine Master]";
		mes "^8B4513ไอเทมชิ้นนี้มีระดับการตีบวกสูงกว่าหรือเท่ากับใบการันตีแล้ว^000000";
		mes "โปรดนำไอเทมที่มีระดับต่ำกว่าเลเวลของใบตีบวกมาใช้แทนนะ";
		close;
	}
	mes "[Refine Master]";
	mes "ข้ากำลังจะตีบวก ^006400"+getequipname(.@part)+"^8B4513 เป็นระดับ +"+.@ticket_lv+"^000000 โดยใช้ ^006400"+getitemname(.@ticket_id)+"^000000 นะ";
	mes "จะให้ข้าเริ่มเลยไหม?";
	next;
	if(select("ไม่ล่ะ:ตกลง เริ่มเลย") == 1) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "โอ้ เจ้าเปลี่ยนใจซะแล้ว";
		mes "ตกลง";
		mes "ไว้เจ้ากลับมาใหม่เมื่อพร้อมนะ";
		close;
	}
	mes "[Refine Master]";
	mes "เยี่ยม";
	mes "ตามที่เจ้าปรารถนา!";
	mes "ข้ามีวิธีตีบวกที่เป็นเอกลักษณ์เฉพาะตัว...";
	mes ".......คา บูมมม!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id) < 1) {
		next;
		mes "เกิดข้อผิดพลาด!";
		mes "โปรดแจ้งปัญหานี้แก่ทีมงาน";
		close;
	}
	delitem .@ticket_id,1;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
		callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
		mes "[Refine Master]";
		emotion ET_FRET;
		mes "เดี๋ยวสิ...";
		mes "คิดว่าข้ากินหญ้าหรือไง?!";
		mes "เจ้าสลับไอเทมตอนข้าเผลอเรอะ! ออกไปจากที่นี่เลยนะ!";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[Refine Master]";
	mes "เรียบร้อยแล้ว นี่ไงล่ะ~";
	mes "ยินดีด้วยนะท่าน ^0000FF"+strcharinfo(0)+"^000000!";
	mes "ยินดีกับ "+.@type$+" ที่ส่องสว่างของเจ้าด้วย";
	mes "มันดูยอดเยี่ยมมาก!";
	mes "ลาก่อน~!";
	close;
}
