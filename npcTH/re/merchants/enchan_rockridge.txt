//===== rAthena Script =======================================
//= Rock Ridge npcs
//===== Description: =========================================
//= [Official Conversion]
//= Enchant npcs on Rockridge.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// enchant npc
har_in01,17,74,7	script	Contraband Processor#pa	4_DR_SOLDIER,{
	if (checkweight(1201,1) == 0) {
		mes "ฉันถือไอเทมหลายชนิดเกินไป ฉันควรไปเอาของออกจากกระเป๋าก่อน";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "น้ำหนักตัวฉันมากเกินไป ฉันควรไปเอาของออกจากกระเป๋าก่อน";
		close;
	}
	disable_items;
	mes "[Contraband Processor]";
	mes "คุณเคยลองซื้อไอเทมผิดกฎหมายด้วย Rock Ridge Coin หรือยัง? ฉันสามารถทำให้ไอเทมเหล่านั้นของคุณดีขึ้นได้นะ";
	next;
	.@reset = select( "ทำให้ดีขึ้น? ยังไงเหรอ?", "ออฟชั่นไอเทมผิดกฎหมาย", "รีเซ็ตระดับออฟชั่นไอเทมผิดกฎหมาย" ) - 2;
	if (.@reset == -1) {
		mes "[Contraband Processor]";
		mes "อ๊ะ เราเคยเจอกันมาก่อนหรือเปล่า? ฉันเจอผู้คนมากมายในแต่ละวันจนจำหน้าได้ไม่หมดหรอก ให้ฉันอธิบายให้คุณฟังอีกรอบแล้วกัน";
		next;
		mes "[Contraband Processor]";
		mes "ฉันสามารถออฟชั่นอุปกรณ์ที่คุณซื้อด้วย Rock Ridge Coin ได้ โดยออฟได้สูงสุด 3 ช่องสำหรับชุดเกราะและผ้าคลุม และสูงสุด 2 ช่องสำหรับเครื่องประดับ";
		next;
		mes "[Contraband Processor]";
		mes "ฉันคิดค่าธรรมเนียม 2 Rock Ridge Coin ต่อช่อง ทั้งการออฟชั่นและการรีเซ็ต";
		next;
		mes "[Contraband Processor]";
		mes "อ้อ และจำไว้ว่าไอเทมผิดกฎหมายพวกนี้พังง่ายมาก โดยเฉพาะเวลาที่คุณรีเซ็ตมัน";
		close;
	}
	switch( select( "ยกเลิก", "ชุดเกราะ (Armor)", "ผ้าคลุม (Garment)", "เครื่องประดับ (ช่องขวา)" ) ) {
	case 1:
		mes "[Contraband Processor]";
		mes "คุณนี่ตลกจัง";
		close;
	case 2:
		.@part = EQI_ARMOR;
		break;
	case 3:
		.@part = EQI_GARMENT;
		break;
	case 4:
		.@part = EQI_ACC_R;
		break;
	}
	.@equip_id = getequipid(.@part);
	switch( .@equip_id ) {
	case 15176:	// Vigilante_Suit
	case 15177:	// Elemental_Robe
	case 15178:	// Golden_Ninja_Suit
	case 15179:	// Mine_Worker's_Vest
	case 15180:	// Hippie Clothes

	case 20820:	// Elemental_Cape
	case 20821:	// Golden_Scarf
	case 20822:	// Mine_Worker's_Backpack

	case 28441:	// Vigilante_Badge
	case 28442:	// Hippie_Feather
		break;
	case -1:
		mes "[Contraband Processor]";
		mes "คุณจะมาเสียเวลาฉันทำไม? ไปสวมใส่อุปกรณ์ของคุณก่อนสิ";
		close;
	default:
		mes "[Contraband Processor]";
		mes "ฉันไม่รับจัดการไอเทมแบบนี้";
		close;
	}
	.@refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	copyarray .@tmp_card[0], .@card[0], 4;
	if ((.@card[1] && getiteminfo(.@card[1], ITEMINFO_SUBTYPE) == CARD_NORMAL) || (.@card[2] && getiteminfo(.@card[2], ITEMINFO_SUBTYPE) == CARD_NORMAL) || (.@card[3] && getiteminfo(.@card[3], ITEMINFO_SUBTYPE) == CARD_NORMAL)) {// armor enchant system custom check
		mes "[Contraband Processor]";
		mes "มีบางอย่างผิดปกติเกิดขึ้น";
		close;
	}
	switch( .@reset ) {
	case TRUE:
		if (.@card[3] == 0) {
			mes "[Contraband Processor]";
			mes "มันยังไม่ได้ออฟชั่นเลย คุณจะให้ฉันรีเซ็ตอะไรล่ะ?";
			close;
		}
		mes "[Contraband Processor]";
		mes "โอ้ คุณต้องการจะรีเซ็ตมันจริงๆ เหรอ? อย่างที่ฉันบอกไปก่อนหน้านี้ พวก Vigilante Badge ผิดกฎหมายน่ะพังง่ายมากนะ";
		if (select( "เดี๋ยวฉันกลับมาใหม่", "ตกลง ทำเลย" ) == 1) {
			mes "[Contraband Processor]";
			mes "ตกลง กลับมาถ้าคุณเปลี่ยนใจนะ";
			close;
		}
		if (.@part == EQI_ACC_R)
			.@chance_of_breaking_on_reset = 45;
		else
			.@chance_of_breaking_on_reset = 30;
		.@card[1] = .@card[2] = .@card[3] = 0;
		break;
	case FALSE:
		switch( .@part ) {
		case EQI_ARMOR:
		case EQI_GARMENT:
			if (.@card[1] > 0) {
				mes "[Contraband Processor]";
				mes "ชิ้นนี้ออฟชั่นจนถึงขีดจำกัดสูงสุดแล้ว บางทีคุณควรจะรีเซ็ตมันก่อน";
				close;
			}
			if (.@card[3] == 0)
				.@enchant_slot = 3;
			else if (.@card[2] == 0)
				.@enchant_slot = 2;
			else
				.@enchant_slot = 1;
			if (.@refine >= 9) {
				.@r = rand(1,215);
				if (.@r < 16) .@card[.@enchant_slot] = getiteminfo("Agility2", ITEMINFO_ID);
				else if (.@r < 31) .@card[.@enchant_slot] = getiteminfo("Dexterity2", ITEMINFO_ID);
				else if (.@r < 46) .@card[.@enchant_slot] = getiteminfo("Strength2", ITEMINFO_ID);
				else if (.@r < 61) .@card[.@enchant_slot] = getiteminfo("Inteligence2", ITEMINFO_ID);
				else if (.@r < 76) .@card[.@enchant_slot] = getiteminfo("Def20", ITEMINFO_ID);
				else if (.@r < 91) .@card[.@enchant_slot] = getiteminfo("Mdef5", ITEMINFO_ID);
				else if (.@r < 106) .@card[.@enchant_slot] = getiteminfo("Critical3", ITEMINFO_ID);
				else if (.@r < 121) .@card[.@enchant_slot] = getiteminfo("Evasion6", ITEMINFO_ID);
				else if (.@r < 135) .@card[.@enchant_slot] = getiteminfo("Expert_Archer1", ITEMINFO_ID);
				else if (.@r < 149) .@card[.@enchant_slot] = getiteminfo("Fatal1", ITEMINFO_ID);
				else if (.@r < 163) .@card[.@enchant_slot] = getiteminfo("Spell1", ITEMINFO_ID);
				else if (.@r < 173) .@card[.@enchant_slot] = getiteminfo("Expert_Archer2", ITEMINFO_ID);
				else if (.@r < 183) .@card[.@enchant_slot] = getiteminfo("Fatal2", ITEMINFO_ID);
				else if (.@r < 193) .@card[.@enchant_slot] = getiteminfo("Spell2", ITEMINFO_ID);
				else if (.@r < 197) .@card[.@enchant_slot] = getiteminfo("Expert_Archer3", ITEMINFO_ID);
				else if (.@r < 201) .@card[.@enchant_slot] = getiteminfo("Fatal3", ITEMINFO_ID);
				else if (.@r < 205) .@card[.@enchant_slot] = getiteminfo("Spell3", ITEMINFO_ID);
				else if (.@r < 209) .@card[.@enchant_slot] = getiteminfo("Attack_Delay_1", ITEMINFO_ID);
				else if (.@r < 210) .@card[.@enchant_slot] = getiteminfo("Expert_Archer4", ITEMINFO_ID);
				else if (.@r < 211) .@card[.@enchant_slot] = getiteminfo("Fatal4", ITEMINFO_ID);
				else if (.@r < 212) .@card[.@enchant_slot] = getiteminfo("Spell4", ITEMINFO_ID);
				else .@card[.@enchant_slot] = getiteminfo("Attack_Delay_2", ITEMINFO_ID);
			}
			else {
				.@r = rand(1,206);
				if (.@r < 14) .@card[.@enchant_slot] = getiteminfo("Agility1", ITEMINFO_ID);
				else if (.@r < 27) .@card[.@enchant_slot] = getiteminfo("Dexterity1", ITEMINFO_ID);
				else if (.@r < 40) .@card[.@enchant_slot] = getiteminfo("Strength1", ITEMINFO_ID);
				else if (.@r < 53) .@card[.@enchant_slot] = getiteminfo("Inteligence1", ITEMINFO_ID);
				else if (.@r < 66) .@card[.@enchant_slot] = getiteminfo("Def15", ITEMINFO_ID);
				else if (.@r < 79) .@card[.@enchant_slot] = getiteminfo("Mdef2", ITEMINFO_ID);
				else if (.@r < 92) .@card[.@enchant_slot] = getiteminfo("Critical1", ITEMINFO_ID);
				else if (.@r < 105) .@card[.@enchant_slot] = getiteminfo("Evasion3", ITEMINFO_ID);
				else if (.@r < 114) .@card[.@enchant_slot] = getiteminfo("Agility2", ITEMINFO_ID);
				else if (.@r < 123) .@card[.@enchant_slot] = getiteminfo("Dexterity2", ITEMINFO_ID);
				else if (.@r < 132) .@card[.@enchant_slot] = getiteminfo("Strength2", ITEMINFO_ID);
				else if (.@r < 141) .@card[.@enchant_slot] = getiteminfo("Inteligence2", ITEMINFO_ID);
				else if (.@r < 150) .@card[.@enchant_slot] = getiteminfo("Def20", ITEMINFO_ID);
				else if (.@r < 159) .@card[.@enchant_slot] = getiteminfo("Mdef5", ITEMINFO_ID);
				else if (.@r < 168) .@card[.@enchant_slot] = getiteminfo("Critical3", ITEMINFO_ID);
				else if (.@r < 177) .@card[.@enchant_slot] = getiteminfo("Evasion6", ITEMINFO_ID);
				else if (.@r < 184) .@card[.@enchant_slot] = getiteminfo("Expert_Archer1", ITEMINFO_ID);
				else if (.@r < 191) .@card[.@enchant_slot] = getiteminfo("Fatal1", ITEMINFO_ID);
				else if (.@r < 198) .@card[.@enchant_slot] = getiteminfo("Spell1", ITEMINFO_ID);
				else if (.@r < 201) .@card[.@enchant_slot] = getiteminfo("Expert_Archer2", ITEMINFO_ID);
				else if (.@r < 204) .@card[.@enchant_slot] = getiteminfo("Fatal2", ITEMINFO_ID);
				else .@card[.@enchant_slot] = getiteminfo("Spell2", ITEMINFO_ID);
			}
			break;
		case EQI_ACC_R:
			if (.@card[2] > 0) {
				mes "[Contraband Processor]";
				mes "ชิ้นนี้ออฟชั่นจนถึงขีดจำกัดสูงสุดแล้ว บางทีคุณควรจะรีเซ็ตมันก่อน";
				close;
			}
			if (.@card[3] == 0)
				.@enchant_slot = 3;
			else
				.@enchant_slot = 2;
			.@r = rand(1,236);
			if (.@r < 16) .@card[.@enchant_slot] = getiteminfo("Agility1", ITEMINFO_ID);
			else if (.@r < 31) .@card[.@enchant_slot] = getiteminfo("Dexterity1", ITEMINFO_ID);
			else if (.@r < 46) .@card[.@enchant_slot] = getiteminfo("Strength1", ITEMINFO_ID);
			else if (.@r < 61) .@card[.@enchant_slot] = getiteminfo("Inteligence1", ITEMINFO_ID);
			else if (.@r < 76) .@card[.@enchant_slot] = getiteminfo("Vitality1", ITEMINFO_ID);
			else if (.@r < 91) .@card[.@enchant_slot] = getiteminfo("Luck1", ITEMINFO_ID);
			else if (.@r < 106) .@card[.@enchant_slot] = getiteminfo("HP100", ITEMINFO_ID);
			else if (.@r < 121) .@card[.@enchant_slot] = getiteminfo("SP50", ITEMINFO_ID);
			else if (.@r < 131) .@card[.@enchant_slot] = getiteminfo("Agility2", ITEMINFO_ID);
			else if (.@r < 141) .@card[.@enchant_slot] = getiteminfo("Dexterity2", ITEMINFO_ID);
			else if (.@r < 151) .@card[.@enchant_slot] = getiteminfo("Strength2", ITEMINFO_ID);
			else if (.@r < 161) .@card[.@enchant_slot] = getiteminfo("Inteligence2", ITEMINFO_ID);
			else if (.@r < 171) .@card[.@enchant_slot] = getiteminfo("Vitality2", ITEMINFO_ID);
			else if (.@r < 181) .@card[.@enchant_slot] = getiteminfo("Luck2", ITEMINFO_ID);
			else if (.@r < 191) .@card[.@enchant_slot] = getiteminfo("HP200", ITEMINFO_ID);
			else if (.@r < 201) .@card[.@enchant_slot] = getiteminfo("SP100", ITEMINFO_ID);
			else if (.@r < 208) .@card[.@enchant_slot] = getiteminfo("Expert_Archer1", ITEMINFO_ID);
			else if (.@r < 215) .@card[.@enchant_slot] = getiteminfo("Spell1", ITEMINFO_ID);
			else if (.@r < 222) .@card[.@enchant_slot] = getiteminfo("Fighting_Spirit1", ITEMINFO_ID);
			else if (.@r < 229) .@card[.@enchant_slot] = getiteminfo("Attack_Delay_1", ITEMINFO_ID);
			else if (.@r < 236) .@card[.@enchant_slot] = getiteminfo("Fatal1", ITEMINFO_ID);
			else .@card[.@enchant_slot] = getiteminfo("Atk_Speed1", ITEMINFO_ID);
			break;
		default:
			mes "[Contraband Processor]";
			mes "คุณแอบเปลี่ยนอุปกรณ์ตอนคุยหรือเปล่าเนี่ย?";
			close;
		}
		break;
	}
	if (countitem(25250) < 2) {
		mes "[Contraband Processor]";
		mes "ฉันคิดค่าบริการ 2 Rock Ridge Coin สำหรับงานนี้ ไปนำมันมาให้ฉัน";
		close;
	}
	delitem 25250,2;// Rock_Ridge_Coin

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@tmp_card[0], .@tmp_card[1], .@tmp_card[2], .@tmp_card[3]))
		close;

	delequip .@part;
	if (rand(100) < .@chance_of_breaking_on_reset) {
		specialeffect2 EF_LORD;
		mes "[Contraband Processor]";
		mes "อา... ผลออกมาไม่ค่อยดีเท่าไหร่นะ";
		mes "ก็นะ ถือว่าเป็นโชคร้ายของฉันแล้วกัน";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	mes "[Contraband Processor]";
	mes "เสร็จแล้ว ลองเช็คดูสิ";
	close;
}


har_in01,24,69,3	script	Affable Lady#rockridge0	1_F_MERCHANT_02,{
	if (checkweight(1301,3) == 0) {
		mes "[Affable Lady]";
		mes "คุณคงจะแข็งแรงมากเลยนะเนี่ยที่แบกของเยอะขนาดนี้ไว้กับตัว";
		next;
		mes "[Affable Lady]";
		mes "ถ้าคุณอยากจะลดน้ำหนักกระเป๋า มีพนักงาน Kafra อยู่แถวๆ ทางไปสำนักงานนายอำเภอนะ";
		close;
	}
	mes "[Affable Lady]";
	mes "คุณเป็นนักผจญภัยสินะ ฉันชอบคนที่มีความมุ่งมั่นแบบคุณจริงๆ";
	mes "แล้ว... ที่คุณมาที่นี่ เป็นเพราะ ^0000CDสิ่งนั้น^000000 หรือเปล่า?";
	next;
	if (select( "เพราะอะไรเหรอ?", "ใช่แล้ว" ) == 1) {
		mes "[Affable Lady]";
		mes "หึๆ อย่ามาทำเป็นไขสือหน่อยเลย ฉันหมายถึงอุปกรณ์พิเศษที่คุณหาซื้อได้ใน Rock Ridge ไงล่ะ";
		next;
		mes "[Affable Lady]";
		mes "คนขายชุดเกราะจะเอาสินค้าใหม่เข้ามาทุกวันศุกร์ และมันก็ถูกขายจนเกลี้ยงเกือบจะทันทีที่มาถึงทุกครั้งเลยด้วย";
		close;
	}
	mes "[Affable Lady]";
	mes "*หัวเราะเบาๆ* คุณต้องการชิ้นไหนล่ะ: สำหรับ ^0000CDไหล่^000000, ^0000CDคอ หรือ หน้าอก^000000 ดีจ๊ะ?";
	next;
	switch( select( "เอ่อ อะไรก็ได้ครับ/ค่ะ", "สำหรับไหล่", "สำหรับคอหรือหน้าอก" ) ) {
	case 1:
		mes "[Affable Lady]";
		mes "*หัวเราะเบาๆ* แน่นอนจ้ะ คนหนุ่มสาวใส่อะไรก็ดูดีไปหมดนั่นแหละ";
		mes "แต่ถ้าอยากได้อะไรที่พิเศษจริงๆ ลองไปดูที่ร้านขายชุดเกราะสิจ๊ะ";
		close;
	case 2:
		mes "[Affable Lady]";
		mes "เลือกชิ้นที่คุณชอบมาสิ แล้วฉันจะบอกรายละเอียดให้";
		mes "อุปกรณ์ทุกชิ้นราคา ^0000CD3,000,000 Zeny^000000 เท่ากันหมดไม่ว่าจะประเภทไหน";
		next;
		.@cost = 3000000;
		setarray .@item_id[0],20820,20821,20822;
		setarray .@item_name$[0], "Elemental Cape", "Golden Scarf", "Mine Worker Backpack";
		.@s = select( "Elemental Cape", "Golden Scarf", "Mine Worker Backpack", "ฉันไม่รู้ว่าคุณกำลังพูดถึงเรื่องอะไร" ) - 1;
		break;
	case 3:
		mes "[Affable Lady]";
		mes "เลือกชิ้นที่คุณชอบมาสิ แล้วฉันจะบอกรายละเอียดให้";
		mes "อุปกรณ์ทุกชิ้นราคา ^0000CD1,500,000 Zeny^000000 เท่ากันหมดไม่ว่าจะประเภทไหน";
		next;
		.@cost = 1500000;
		setarray .@item_id[0],28441,28442;
		setarray .@item_name$[0], "Vigilante Badge", "Hippie Feather";
		.@s = select( "Vigilante Badge", "Hippie Feather", "ฉันไม่รู้ว่าคุณกำลังพูดถึงเรื่องอะไร" ) - 1;
		break;
	}
	if (.@item_id[.@s] == 0) {
		mes "[Affable Lady]";
		mes "อุ๊ย ฉันเดาว่าคุณคงจะไร้เดียงสาเกินกว่าจะเข้าใจสิ่งที่ฉันเพิ่งพูดไปนะ";
		mes "คุณควรลืมมันไปเสียเพื่อความปลอดภัยของตัวคุณเอง";
		close;
	}
	mes "[Affable Lady]";
	mes "อ๊ะ คุณสนใจ";
	mes "[" + mesitemlink( .@item_id[.@s], false, .@item_name$[.@s] ) + "] สินะ";
	mes "คลิกที่ชื่อเพื่อดูรายละเอียดเพิ่มเติมได้เลย";
	mes "อย่างที่บอกไป ราคาคือ ^0000CD"+callfunc("F_InsertComma",.@cost)+" Zeny^000000 คุณต้องการซื้อไหมจ๊ะ?";
	next;
	if (select( "ใช่", "ไม่" ) == 2) {
		mes "[Affable Lady]";
		mes "เห้อ นี่ฉันเสียเวลาอธิบายให้คนที่ไม่อยากซื้อฟังงั้นเหรอเนี่ย";
		mes "อุปกรณ์แต่ละชิ้นมีเรื่องราวของมันนะ และมันอาจจะถือโกรธเอาได้ถ้าคุณไม่พามันไปด้วย";
		next;
		mes "[Affable Lady]";
		mes "ฉันแนะนำว่าอย่าไปบอกเรื่องนี้กับใครอื่นล่ะ";
		mes "และสัญญากับฉันนะว่าจะกลับมา ฉันจะเก็บอุปกรณ์ไว้ให้คุณเอง";
		close;
	}
	if (Zeny < .@cost) {
		mes "[Affable Lady]";
		mes "โอ๊ะโอ คุณมีเงินไม่พอซื้อชิ้นนี้จ้ะ";
		mes "ทำไมไม่ลองกลับมาใหม่ทีหลังล่ะ? ฉันจะอยู่ที่นี่เสมอแหละ";
		close;
	}
	mes "[Affable Lady]";
	mes "ได้เลยจ้ะ นี่จ๊ะ " + .@item_name$[.@s] + " ของคุณ";
	Zeny = Zeny - .@cost;
	getitem .@item_id[.@s],1;
	next;
	mes "[Affable Lady]";
	mes "ผู้คนมักจะยอมให้ภาพลักษณ์ที่เห็นหลอกตาเอาได้ คนส่วนใหญ่ไม่คิดหรอกว่าหญิงแก่อย่างฉันจะขายของพิเศษแบบนี้";
	mes "กลับมาอีกนะถ้าต้องการอุปกรณ์เพิ่ม ฉันจะอยู่ที่นี่เสมอ *หัวเราะเบาๆ*";
	close;
}

har_in01,34,81,5	script	Howard#rr	4_M_TATIO,{
	if (checkweight(1301,1) == 0) {
		mes "[Howard]";
		mes "เอ่อ คุณกำลังแบกของเยอะอยู่นะนั่น";
		mes "ทำไมไม่ไป";
		mes "ทำให้กระเป๋าเบาลงก่อนล่ะ?";
		close;
	}
	mes "[Howard]";
	mes "สวัสดีครับ วันนี้มีอะไรให้ผมช่วยไหม?";
	next;
	if (countitem(25250) < 1) {
		mes "[Howard]";
		mes "หืม คุณไม่มีเหรียญนี่นา";
		mes "ทำไมไม่ไปสะสมมาให้เรียบร้อยก่อนล่ะ?";
		close;
	}
	mes "[Howard]";
	mes "โอ้ คุณมีเหรียญแล้วนี่";
	mes "ขอบคุณที่ช่วยเหลือ Rock Ridge นะครับ";
	mes "ลองเดินดูรอบๆ ก่อนว่ามีอะไรที่คุณต้องการไหม";
	next;
	mes "[Howard]";
	mes "คุณอยากได้อะไรดีครับ?";
	next;
	setarray .@data$[0],
		 24, 15176, "Vigilante Suit",	// <cost>, <item ID>, <item name>
		 24, 15177, "Elemental Robe",
		 24, 15178, "Golden Ninja Suit",
		 24, 15179, "Mine Worker Vest",
		 24, 15180, "Hippie Clothes",
		150, 20834, "Drifter's Cape",
		150, 22131, "Spurred Boots",
		180, 16060, "Liquor Bottle",
		300, 26107, "Elder Staff",
		100, 31140, "Costume Black Cowboy Hat",
		100, 31141, "Costume Cactus Flower Corsage";

	.@size = getarraysize(.@data$);
	for ( .@i = 0; .@i < .@size; .@i += 3 )
		.@menu$ += .@data$[.@i+2] + ":";
	.@s = ( select(.@menu$) - 1 ) * 3;

	mes "[Howard]";
	mes mesitemlink( atoi( .@data$[.@s+1] ), false, .@data$[.@s+2] ) + " สินะครับ เข้าใจแล้ว";
	mes "นั่นจะต้องใช้ " + .@data$[.@s] + " Rock Ridge Coin ครับ";
	mes "แน่ใจใช่ไหมครับว่าต้องการไอเทมชิ้นนี้?";
	next;
	if (select( "ใช่", "ไม่" ) == 2) {
		mes "[Howard]";
		mes "ถ้าคุณตัดสินใจได้แล้ว ค่อยมาคุยกับผมอีกทีนะครับ";
		close;
	}
	if (countitem(25250) < atoi(.@data$[.@s])) {
		mes "[Howard]";
		mes "คุณมีเหรียญไม่พอครับ";
		mes "กรุณานำมาเพิ่มด้วยนะครับ";
		close;
	}
	mes "[Howard]";
	mes "นี่ครับ ขอให้สนุกนะ!";
	delitem 25250, atoi(.@data$[.@s]);// Rock Ridge Coin
	getitem atoi(.@data$[.@s+1]),1;
	close;
}

-	shop	shotguns_rockridge	-1,13154:-1,13155:-1,13192:-1,13193:-1,13194:-1
-	shop	revolvers_rockridge	-1,13102:-1,13120:-1,13122:-1
-	shop	bullets_rockridge	-1,13200:-1,13221:-1,13222:-1,13228:-1,13229:-1,13230:-1,13231:-1,13232:-1

har_in01,19,86,4	script	Weapon Dealer#rockridge01	1_F_02,{
	if (checkweight(1301,3) == 0) {
		mes "[Weapon Dealer]";
		mes "คุณแบกของหนักเกินไปที่จะซื้ออาวุธนะ";
		mes "กรุณาไปทำให้กระเป๋าเบาลงก่อนจ๊ะ";
		close;
	}
	mes "[Weapon Dealer]";
	mes "ฉันมีปืนลูกซอง, ปืนรีวอลเวอร์ และกระสุนขายจ้ะ";
	mes "บอกมาสิว่าอยากได้ไอเทมประเภทไหน แล้วฉันจะเปิดรายการให้ดู";
	next;
	switch( select( "ปืนลูกซอง (Shotguns)", "ปืนรีวอลเวอร์ (Revolvers)", "กระสุน (Bullets)", "ขายไอเทม", "ยกเลิก" ) ) {
	case 1:
		mes "[Weapon Dealer]";
		mes "อยากดูปืนลูกซองเหรอ? เข้าใจแล้วจ้ะ";
		mes "เดี๋ยวฉันจะโชว์รุ่นดีๆ ให้ดูนะ";
		close2;
		callshop "shotguns_rockridge", 1;
		end;
	case 2:
		mes "[Weapon Dealer]";
		mes "อยากดูปืนรีวอลเวอร์เหรอ? เข้าใจแล้วจ้ะ";
		mes "ฉันเพิ่งได้รุ่นดีๆ เข้ามาไม่กี่กระบอกเอง";
		close2;
		callshop "revolvers_rockridge", 1;
		end;
	case 3:
		mes "[Weapon Dealer]";
		mes "ต้องการกระสุนสินะ เข้าใจแล้วจ้ะ";
		mes "กระสุนทั้งหมดของฉันน่ะคุณภาพเยี่ยมที่สุดเลยล่ะ";
		close2;
		callshop "bullets_rockridge", 1;
		end;
	case 4:
		mes "[Weapon Dealer]";
		mes "แน่นอนจ้ะ ถ้าคุณมีของที่ฉันพอจะรับซื้อได้";
		mes "ไหนดูสิว่าคุณมีอะไรมาบ้าง";
		close2;
		callshop "bullets_rockridge", 2;
		end;
	case 5:
		mes "[Weapon Dealer]";
		mes "ไว้มาใหม่นะจ๊ะ";
		close;
	}
}

-	marketshop	armor_rockridge_1	-1,2247:500000:10,2248:500000:10,5018:700000:10
-	marketshop	armor_rockridge_2	-1,5067:700000:10,5071:700000:10,5075:700000:10
-	marketshop	armor_rockridge_3	-1,2267:100000:10,2268:500000:10,2241:5000:10
-	marketshop	armor_rockridge_4	-1,5067:700000:10,5071:700000:10,5075:700000:10,31144:1500000:3

har_in01,16,86,4	script	Armor Dealer#rockridge02	1_M_04,{
	if (checkweight(1301,3) == 0) {
		mes "[Armor Dealer]";
		mes "คุณแบกของหนักเกินไปที่จะซื้ออะไรได้";
		mes "กรุณาไปทำให้กระเป๋าเบาลงก่อนครับ";
		close;
	}
	.@day = gettime(DT_DAYOFWEEK);
	if (.@day == SUNDAY) {
		mes "[Armor Dealer]";
		mes "วันนี้ร้านผมปิดครับ";
		mes "สัปดาห์ละครั้ง ผมจะปิดร้านแล้วใช้เวลาทั้งวันขอบคุณในสิ่งที่มีอยู่";
		next;
		mes "[Armor Dealer]";
		mes "...และมันก็เป็นวันเดียวกับที่ไม่มีของมาส่งด้วยล่ะนะ";
		close;
	}
	if (.@day == MONDAY)
		.@string$ = "Romantic Gents, Western Graces, และ Feather Bonnet";
	else if (.@day == TUESDAY)
		.@string$ = "Sombreros, Indian Headbands, และ Cowboy Hats";
	else if (.@day == WEDNESDAY)
		.@string$ = "Cigarettes, Pipes, และ Grampa Beards";
	else if (.@day == THURSDAY)
		.@string$ = "Romantic Gents, Western Graces, และ Feather Bonnet";
	else if (.@day == FRIDAY)
		.@string$ = "Sombreros, Indian Headbands, Cowboy Hats, และ Indian Feather Headbands ซึ่งเป็นคอสตูมหายากอย่างสุดท้ายที่มีจำนวนจำกัดในแต่ละสัปดาห์";
	else
		.@string$ = "Cigarettes, Pipes, และ Grampa Beards";
	mes "[Armor Dealer]";
	mes "สวัสดีครับ ยินดีต้อนรับสู่ร้านชุดเกราะ Rock Ridge";
	mes "วันนี้มีไอเทมประเภท " +.@string$ + " เข้าสต็อกครับ";
	next;
	mes "[Armor Dealer]";
	mes "ชุดเกราะมันกินพื้นที่มากกว่าอาวุธ ดังนั้นผมเลยรับของมาส่งได้ในจำนวนจำกัดในแต่ละสัปดาห์";
	mes "ผมจะแสดงรายการไอเทมที่มีอยู่ให้ดูครับ ^0000CDถ้ามันว่างเปล่า แสดงว่าของถูกขายไปหมดแล้วครับ^000000";
	next;
	mes "[Armor Dealer]";
	mes "และด้วยเหตุผลเดียวกันนั้น ผมเลยไม่สามารถให้ ^0000CDส่วนลดพิเศษ^000000 ได้";
	mes "แต่ ^0000CDสินค้าของผมทุกชิ้นน่ะหาจากที่อื่นได้ยากนะ^000000 ลองเลือกดูหน่อยเป็นไงครับ?";
	close2;
	if (.@day == MONDAY || .@day == THURSDAY)
		callshop "armor_rockridge_1", 1;
	else if (.@day == TUESDAY)
		callshop "armor_rockridge_2", 1;
	else if (.@day == WEDNESDAY || .@day == SATURDAY)
		callshop "armor_rockridge_3", 1;
	else if (.@day == FRIDAY)
		callshop "armor_rockridge_4", 1;
	end;
	
OnSun0000:	// custom restock, no official info
	npcshopupdate "armor_rockridge_1",2247,0,10;
	npcshopupdate "armor_rockridge_1",2248,0,10;
	npcshopupdate "armor_rockridge_1",5018,0,10;
	npcshopupdate "armor_rockridge_2",5067,0,10;
	npcshopupdate "armor_rockridge_2",5071,0,10;
	npcshopupdate "armor_rockridge_2",5075,0,10;
	npcshopupdate "armor_rockridge_3",2267,0,10;
	npcshopupdate "armor_rockridge_3",2268,0,10;
	npcshopupdate "armor_rockridge_3",2241,0,10;
	npcshopupdate "armor_rockridge_4",5067,0,10;
	npcshopupdate "armor_rockridge_4",5071,0,10;
	npcshopupdate "armor_rockridge_4",5075,0,10;
	npcshopupdate "armor_rockridge_4",31144,0,3;
	end;
}

har_in01,34,84,3	shop	Tool Dealer#rockridge00	1_F_03,611:-1,645:-1,656:-1,601:-1,602:-1,1065:-1,610:-1
