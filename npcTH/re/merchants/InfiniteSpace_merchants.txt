//===== rAthena Script =======================================
//= Infinite Space
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Infinite Space related merchants and enchanter
//===== Changelogs: ==========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Removed unecessary use of functions [Everade]
//= 1.2 Added warp scroll merchant [Everade]
//============================================================

// Food Merchant
-	shop	inf_ration	-1,512:-1,513:-1,515:-1,516:-1

cmd_fild07,63,268,1	script	Emergency Food Merchant#pa0829_01	4_M_BIBI,{
	mes "[Emergency Food Merchant]";
	mes "ฉันมีของเยอะแยะเลยที่นี่ แน่นอนว่ามันไม่ได้ให้ฟรีๆ หรอกนะ";
	close2;
	callshop "inf_ration",1;
	end;
}

// Warp Scroll Seller
cmd_fild07,375,167,1	script	Ruins Black Trader#pa0829_01	4_F_JOB_HUNTER,{
	mes "[Ruins Black Trader]";
	mes "เฮ้ มันไม่ลำบากแย่เหรอที่ต้องเดินเข้าเดินออกที่นี่ทุกครั้งน่ะ? แค่ ^0000ff20,000^000000 Zeny คุณก็จะได้สกรอลล์ที่จะพาคุณไปยังหน้าทางเข้าโบราณสถานได้โดยตรงเลยนะ คุณคิดว่ายังไงล่ะ?";
Purchase:
	next;
	switch( select( "ขอซื้ออันหนึ่ง", "ฉันว่าไม่ดีกว่า" )) {
		case 1:
			if (!checkweight(22980,1) || (MaxWeight - Weight) < 1000) {
				mes "คุณไม่สามารถสนทนาต่อได้เนื่องจากคุณมีจำนวนไอเทมมากเกินไป";
				mes "กรุณาจัดระเบียบไอเทมของคุณและลองใหม่อีกครั้ง";
				close;
			}
			else if (Zeny < 20000) {
				mes "[Ruins Black Trader]";
				mes "ดูเหมือนคุณจะไม่มีเงินเหลือแล้วนะ สกรอลล์ราคา 20,000 Zeny";
				close;
			}
			Zeny -= 20000;
			getitem 22980,1;
			mes "[Ruins Black Trader]";
			mes "เป็นการซื้อขายที่ดีมาก ต้องการอีกไหม?";
			goto Purchase;
		case 2:
			mes "[Ruins Black Trader]";
			mes "กลับมาได้ทุกเมื่อนะ";
			close;
	}
}

// Equipment Shop
cmd_fild07,57,275,5	script	Artifact Appraiser#pa0829_01	1_F_02,{
	if (!checkweight(1201,1) || (MaxWeight - Weight) < 1000) {
		mes "คุณไม่สามารถสนทนาต่อได้เนื่องจากคุณมีจำนวนไอเทมมากเกินไป";
		mes "กรุณาจัดระเบียบไอเทมของคุณและลองใหม่อีกครั้ง";
		close;
	}
	.@stone_id = 6905;
	mes "[ Artifact Appraiser ]";
	mes "เลือกประเภทของอุปกรณ์ที่คุณต้องการซื้อ คุณสามารถซื้อได้มากเท่าที่ต้องการ ตราบใดที่คุณยังมี " + getitemname(.@stone_id) + " อยู่";
	next;
	switch (select("ยกเลิก:อาวุธ:ชุดเกราะ")) {
		case 1:
			mes "[ Artifact Appraiser ]";
			mes "โปรดกลับมาใหม่เมื่อคุณต้องการนะ~";
			close;

		case 2:
			setarray .@equip_id,1994,1938,13323,13126,28703,2024,16038,21014,28105,18128;
			.@price = 50;
			break;

		case 3:
			setarray .@equip_id,15141,22075,20779,19033;
			.@price = 50;
			break;
	}
	.@menu$ = "ยกเลิก:";
	for (.@i = 0; .@i < getarraysize(.@equip_id); .@i++)
		.@menu$ += getitemname(.@equip_id[.@i]) + ":";
	.@s = select(.@menu$) - 1;
	switch (.@s) {
		case 0:
			mes "[ Artifact Appraiser ]";
			mes "โปรดกลับมาใหม่เมื่อคุณต้องการนะ~";
			close;

		default:
			.@s--;
			mes "[ Artifact Appraiser ]";
			mes "คุณต้องใช้ ^0000FF" + .@price + "^000000 " + getitemname(.@stone_id) + " เพื่อซื้อ " + getitemname(.@equip_id[.@s]) + "~";
			next;
			if (select("ยกเลิก:ซื้อสินค้า") == 1) {
				mes "[ Artifact Appraiser ]";
				mes "โปรดกลับมาใหม่เมื่อคุณต้องการนะ~";
				close;
			}
			if (countitem(.@stone_id) < .@price) {
				mes "[ Artifact Appraiser ]";
				mes "คุณมี " + getitemname(.@stone_id) + " ไม่เพียงพอที่จะซื้อไอเทมนี้";
				close;
			}
			mes "[ Artifact Appraiser ]";
			mes "ขอบคุณที่ไว้ใจ โปรดกลับมาใหม่ในครั้งหน้า";
			delitem .@stone_id,.@price;
			getitem .@equip_id[.@s],1;
			close;
	}
}

// Equipment Enchanter
cmd_fild07,60,275,3	script	Artifact Enhancer#pa0829_01	4_F_JOB_BLACKSMITH,{
	if (!checkweight(1201,1) || (MaxWeight - Weight) < 1000) {
		mes "คุณไม่สามารถสนทนาต่อได้เนื่องจากคุณมีจำนวนไอเทมมากเกินไป";
		mes "กรุณาจัดระเบียบไอเทมของคุณและลองใหม่อีกครั้ง";
		close;
	}
	disable_items;
	.@stone_id = 6905;
	function equip_check;
	mes "[ Artifact Enhancer ]";
	mes "ถ้าคุณกำลังมองหาวิธีเสริมความแข็งแกร่งให้อุปกรณ์ที่คุณได้รับจาก Infinite Space คุณมาถูกที่แล้วล่ะ";
	mes "คุณมี " + getitemname(.@stone_id) + " บ้างไหม?";
	next;
	switch (select("วิธี Enchant อุปกรณ์ทำยังไง?:Enchant อุปกรณ์:รีเซ็ต Enchant ของอุปกรณ์")) {
		case 1:
			mes "[ Artifact Enhancer ]";
			mes "คุณจะสามารถหา " + getitemname(.@stone_id) + " ได้ หากคุณออกสำรวจพื้นที่ใต้ประภาคาร Paros";
			next;
			mes "[ Artifact Enhancer ]";
			mes "ด้วยวัตถุดิบนั้น ฉันสามารถเสริมพลังให้อุปกรณ์ที่คุณได้รับจาก Infinite Space ได้";
			next;
			mes "[ Artifact Enhancer ]";
			mes "ฉันสามารถ Enchant ได้เฉพาะช่องที่ 3 และช่องที่ 4 เท่านั้น";
			next;
			mes "[ Artifact Enhancer ]";
			mes "ไม่มีโอกาสที่อุปกรณ์จะถูกทำลายในระหว่างขั้นตอนการ Enchant แต่มีความเป็นไปได้สูงที่มันจะถูกทำลายเมื่อทำการรีเซ็ต Enchant";
			next;
			mes "[ Artifact Enhancer ]";
			mes "ถ้าอย่างนั้นก็มาหาฉันเมื่อคุณต้องการจะ Enchant อุปกรณ์นะ";
			break;

		case 2:
			.@stone_id = 6905;
			.@fee = 20;
			mes "[ Artifact Enhancer ]";
			mes "โปรดเลือกอุปกรณ์ที่คุณต้องการจะ Enchant";
			next;
			switch (select("ยกเลิก:อาวุธ:ชุดเกราะ:รองเท้า:ผ้าคลุม:หมวก")) {
				case 1:
					mes "[ Artifact Enhancer ]";
					mes "ถ้าอย่างนั้นก็มาหาฉันเมื่อคุณต้องการจะ Enchant อุปกรณ์นะ";
					close;

				case 2:
					.@part = EQI_HAND_R;
					break;

				case 3:
					.@part = EQI_ARMOR;
					break;

				case 4:
					.@part = EQI_SHOES;
					break;

				case 5:
					.@part = EQI_GARMENT;
					break;

				case 6:
					.@part = EQI_HEAD_TOP;
					break;
			}
			.@equip_id = getequipid(.@part);
			.@refine = getequiprefinerycnt(.@part);
			equip_check(.@equip_id);
			for (.@i = 0;.@i < 4;.@i++) {
				.@card[.@i] = getequipcardid(.@part,.@i);
				.@check[.@i] = getequipcardid(.@part,.@i);
			}
			if (.@card[2] > 0) {
				mes "[ Artifact Enhancer ]";
				mes "ฉันสามารถ Enchant ได้สูงสุดถึงช่องที่สามเท่านั้น อุปกรณ์ของคุณไม่สามารถ Enchant เพิ่มเติมได้แล้ว";
				close;
			}
			switch (.@part) {
				case EQI_HAND_R:
					setarray .@enchant$,
					"4700,4701:4710,4711:4720,4721",
					"4811,4810,4809,4808,4820,4821,4822,4823:4815,4814,4813,4812,4826,4827,4828,4829:4832,4833,4834,4835,4836,4837,4838,4839";
					break;

				case EQI_ARMOR:
				case EQI_SHOES:
					setarray .@enchant$,
					"4700,4701,4702,4703:4710,4711,4712,4713:4720,4721,4722,4723",
					"4795,4796,4797:4870,4871,4800:4870,4871,4800";
					break;

				case EQI_GARMENT:
				case EQI_HEAD_TOP:
					setarray .@enchant$,
					"4700,4701,4702,4703:4710,4711,4712,4713:4720,4721,4722,4723",
					"4861,4862,4867,4868,4900:4861,4862,4867,4868,4900:4861,4862,4867,4868,4900";
					break;
			}
			.@slot = (.@card[3] > 0 ? 2 : 3);
			.@index = (.@slot == 3 ? 0 : 1);
			mes "[ Artifact Enhancer ]";
			mes "คุณสามารถเลือกประเภทการ Enchant ได้ 3 รูปแบบ และค่าธรรมเนียมคือ " + .@fee + " " + getitemname(.@stone_id);
			mes "ฉันจะทำให้แน่ใจว่ามันจะถูก Enchant อย่างไม่มีปัญหาแน่นอน";
			next;
			.@type = select("ออก:สายกายภาพ:สายเวทมนตร์:สายโจมตีไกล") - 2;
			mes "[ Artifact Enhancer ]";
			if (.@slot == 3)
				mes "ตกลง มาเริ่มการ Enchant ครั้งแรกกัน";
			else
				mes "ตกลง มาเริ่มการ Enchant ครั้งที่สองกัน";
			next;
			if (select("เดี๋ยวฉันกลับมาใหม่:ดำเนินการต่อ") == 1) {
				mes "[ Artifact Enhancer ]";
				mes "ถ้าคุณเปลี่ยนใจ ก็กลับมาหาฉันได้นะ";
				close;
			}
			if (countitem(.@stone_id) < .@fee) {
				mes "[ Artifact Enhancer ]";
				mes "หืม.. จะว่าไป ดูเหมือนคุณจะไม่ได้ฟังที่ฉันพูดนะ คุณมี " + getitemname(.@stone_id) + " ไม่พอน่ะ";
				close;
			}
			explode(.@T$,.@enchant$[.@index],":");
			explode(.@TT$,.@T$[.@type],",");
			.@enchant = atoi(.@TT$[rand(getarraysize(.@TT$))]);
			.@card[.@slot] = .@enchant;
			delitem .@stone_id,.@fee;
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@check[0], .@check[1], .@check[2], .@check[3]))
				close;
			delequip .@part;
			getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
			specialeffect2 EF_REPAIRWEAPON;
			mes "[ Artifact Enhancer ]";
			mes "หืม.. ทำออกมาได้ดีเลย ตรวจสอบอุปกรณ์ของคุณดูสิ";
			break;
		
		case 3:
			.@stone_id = 6905;
			.@fee = 30;
			.@break_chance = 30;
			mes "[ Artifact Enhancer ]";
			mes "โปรดเลือกอุปกรณ์ที่คุณต้องการจะรีเซ็ต Enchant";
			next;
			switch (select("ยกเลิก:อาวุธ:ชุดเกราะ:รองเท้า:ผ้าคลุม:หมวก")) {
				case 1:
					mes "[ Artifact Enhancer ]";
					mes "ถ้าอย่างนั้นก็มาหาฉันเมื่อคุณต้องการจะ Enchant อุปกรณ์นะ";
					close;

				case 2:
					.@part = EQI_HAND_R;
					break;

				case 3:
					.@part = EQI_ARMOR;
					break;

				case 4:
					.@part = EQI_SHOES;
					break;

				case 5:
					.@part = EQI_GARMENT;
					break;

				case 6:
					.@part = EQI_HEAD_TOP;
					break;
			}
			.@equip_id = getequipid(.@part);
			.@refine = getequiprefinerycnt(.@part);
			equip_check(.@equip_id);
			for (.@i = 0; .@i < 4; .@i++) {
				.@card[.@i] = getequipcardid(.@part,.@i);
				.@check[.@i] = .@card[.@i];
			}
			if (.@card[3] == 0) {
				mes "[ Artifact Enhancer ]";
				mes "อุปกรณ์ของคุณยังไม่มี Enchant ใดๆ ติดอยู่เลย";
				close;
			}
			mes "[ Artifact Enhancer ]";
			mes "มีความเป็นไปได้ที่อุปกรณ์ Infinite Space ของคุณจะถูกทำลายในกระบวนการรีเซ็ต คุณยังยินดีที่จะดำเนินการต่อไหม?";
			next;
			if (select("ออก:ดำเนินการต่อ") == 1) {
				mes "[ Artifact Enhancer ]";
				mes "ถ้าคุณเปลี่ยนใจ ก็กลับมาหาฉันได้นะ";
				close;
			}
			if (countitem(.@stone_id) < .@fee) {
				mes "[ Artifact Enhancer ]";
				mes "หืม.. จะว่าไป ดูเหมือนคุณจะไม่ได้ฟังที่ฉันพูดนะ คุณมี " + getitemname(.@stone_id) + " ไม่พอน่ะ";
				close;
			}
			delitem .@stone_id,.@fee;
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@check[0], .@check[1], .@check[2], .@check[3]))
				close;
			delequip .@part;
			if (rand(1,100) > .@chance) {
				getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],0,0;
				specialeffect2 EF_REPAIRWEAPON;
				mes "[ Artifact Enhancer ]";
				mes "หืม.. ทำออกมาได้ดีเลย ตรวจสอบอุปกรณ์ของคุณดูสิ";
			} else {
				specialeffect2 EF_REFINEFAIL;
				mes "[ Artifact Enhancer ]";
				mes "ก็นะ ฉันเตือนคุณแล้ว ดวงคุณไม่ดีเลยว่าไหม?";
			}
			break;
	}
	close;

	function	equip_check	{
		setarray .@equip_id,1994,1938,13323,13126,28703,2024,16038,21014,28105,18128,15141,22075,20779,19033;
		if (!getarg(0)) {
			mes "[ Artifact Enhancer ]";
			mes "คุณถอดอุปกรณ์ออกหรือเปล่า?";
			close;
		}
		if (inarray(.@equip_id,getarg(0)) == -1) {
			mes "[ Artifact Enhancer ]";
			mes "อุปกรณ์นี้ไม่เหมาะสำหรับการ Enchant อย่าลืมล่ะว่ามีเพียงอุปกรณ์ชุด Infinite เท่านั้นที่สามารถ Enchant ได้";
			close;
		}
		return;
	}
}
