//===== rAthena Script =======================================
//= Training Zone
//===== Description: =========================================
//= NPCs related to the training zone.
//===== Changelog: ===========================================
//= 1.0 Initial Release [Atemo]
//============================================================

// Storage
tra_fild01,204,110,2	script	Training Ground Warehouse Soldier#fild01_1	8W_SOLDIER,{
	mes "[Training Ground Warehouse Soldier]";
	mes "คุณต้องการใช้คลังฝากของไหม? ต้องใช้ 1000 Zeny เพื่อใช้งานคลังฝากของในสนามฝึกหัด โปรดเลือกคลังฝากของที่คุณต้องการใช้งาน";
	next;
	// switch( select( "?? Storage 1", "?? Storage 2", "?? Storage 3", "?? Guild Storage", "?? Cancel" ) ) {
	switch( select( "?? Storage 1", "", "", "?? Guild Storage", "?? Cancel" ) ) {
	case 1:
		.@storage_num = 1;
		break;
	case 2:
		.@storage_num = 2;
		break;
	case 3:
		.@storage_num = 3;
		break;
	case 4:
		// copied from npc/re/merchants/guild_warehouse.txt
		.@guildid = getcharid( 2 );

		if( .@guildid == 0 ){
			mes "[Training Ground Warehouse Soldier]";
			mes "คลังฝากของกิลด์ให้บริการเฉพาะสมาชิกกิลด์เท่านั้น";
			close;
		}

		if( getgdskilllv( .@guildid, "GD_GUILD_STORAGE" ) == 0 || !guild_has_permission( GUILD_PERM_STORAGE ) ){
			mes "[Training Ground Warehouse Soldier]";
			mes "ดูเหมือนว่ากิลด์ของคุณจะยังไม่สามารถใช้งานคลังกิลด์ได้ หรือคุณอาจจะไม่มีสิทธิ์ในการเข้าถึงคลังฝากของ";
			mes "โปรดกลับมาอีกครั้งหลังจากตรวจสอบสกิลกิลด์และสิทธิ์ในการเข้าถึงคลังฝากของแล้ว";
			close;
		}

		if( Zeny < 1000 ){
			mes "[Training Ground Warehouse Soldier]";
			mes "ฉันเกรงว่าคุณจะมีเงินไม่เพียงพอสำหรับชำระค่าธรรมเนียม";
			mes "ค่าธรรมเนียมคือ 1000 Zeny";
			close;
		}

		mes "[Training Ground Warehouse Soldier]";
		mes "ถ้าอย่างนั้นฉันจะเปิดคลังฝากของกิลด์ให้ ขอให้มีความสุขกับช่วงเวลานี้นะ!";
		close2;

		if( Zeny < 1000 ){
			// Cheat prevention
			end;
		}

		if( guildopenstorage() == GSTORAGE_OPEN ){
			Zeny -= 1000;
			end;
		}else{
			mes "[Training Ground Warehouse Soldier]";
			mes "ฉันเกรงว่าตอนนี้กำลังมีคนอื่นใช้งานคลังฝากของอยู่";
			mes "โปรดลองใหม่อีกครั้งในภายหลัง";
			close;
		}
	case 5:
		end;
	}
	// TODO (custom text)
	if (!callfunc("F_CanOpenStorage")) {
		mes "[Training Ground Warehouse Soldier]";
		mes "ฉันขอโทษด้วย แต่คุณ";
		mes "จำเป็นต้องมีสกิลพื้นฐาน";
		mes "(Basic Skill) เลเวล 6";
		mes "เพื่อใช้งานบริการคลังฝากของ";
		close2;
		cutin "",255;
		end;
	}

	if (Zeny < 1000) {
		mes "[Training Ground Warehouse Soldier]";
		mes "^4d4dffต้องใช้ 1000 Zeny เพื่อใช้งานคลังฝากของในสนามฝึกหัด^000000";
		close;
	}
	Zeny -= 1000;
	openstorage; // TODO: open correct storage 1-3
	end;
}
tra_fild01,15,110,6	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild01_2	8W_SOLDIER
tra_fild01,113,32,0	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild01_3	8W_SOLDIER
tra_fild01,111,100,4	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild01_4	8W_SOLDIER

tra_fild02,204,110,2	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild02_1	8W_SOLDIER
tra_fild02,15,110,6	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild02_2	8W_SOLDIER
tra_fild02,113,32,0	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild02_3	8W_SOLDIER
tra_fild02,111,100,4	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild02_4	8W_SOLDIER

tra_fild03,204,110,2	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild03_1	8W_SOLDIER
tra_fild03,15,110,6	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild03_2	8W_SOLDIER
tra_fild03,113,32,0	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild03_3	8W_SOLDIER
tra_fild03,111,100,4	duplicate(Training Ground Warehouse Soldier#fild01_1)	Training Ground Warehouse Soldier#fild03_4	8W_SOLDIER


// Misc NPCs

// Cleaner
tra_fild01,142,113,6	script	Cleaning Robot#fild01	EP17_2_OMEGA_CLEANER,{
OnStart:
	.@num = atoi( charat(strnpcinfo(2), 5) );

	if (.is_moving[.@num])
		end;
	.is_moving[.@num] = true;
	stopnpctimer;

	setarray .@emotions[0], ET_SWEAT, ET_PROFUSELY_SWEAT, ET_HUNGRY, ET_OHNO;
	.@r = rand( getarraysize(.@emotions) );
	emotion .@emotions[.@r];

	setarray .@speed[0], 300, 400, 500, 600;
	.@r = rand( getarraysize(.@speed) );
	npcspeed .@speed[.@r];

	getmapxy .@map$,.@x,.@y, BL_NPC;
	switch( .@num ) {
	case 1:
		if (.@x <= 126)
			.sens[.@num] = 1;
		else if (.@x >= 198)
			.sens[.@num] = -1;
		.@x += 8 * .sens[.@num];
		break;
	case 2:
		if (.@x <= 21)
			.sens[.@num] = 1;
		else if (.@x >= 85)
			.sens[.@num] = -1;
		.@x += 8 * .sens[.@num];
		break;
	case 3:
		if (.@y <= 33)
			.sens[.@num] = 1;
		else if (.@y >= 89)
			.sens[.@num] = -1;
		.@y += 8 * .sens[.@num];
		break;
	}
	unitwalk getnpcid(0),.@x,.@y, strnpcinfo(0) + "::OnMoveEnd";
	end;
OnMoveEnd:
	.@num = atoi( charat(strnpcinfo(2), 5) );
	.is_moving[.@num] = false;
	initnpctimer;
	end;
OnTimer30000:
	donpcevent strnpcinfo(0) + "::OnStart";
	end;

OnInit:
	.@num = atoi( charat(strnpcinfo(2), 5) );
	.sens[.@num] = 1;
	initnpctimer;
	end;
}
tra_fild02,53,113,2	duplicate(Cleaning Robot#fild01)	Cleaning Robot#fild02	EP17_2_OMEGA_CLEANER
tra_fild03,110,57,4	duplicate(Cleaning Robot#fild01)	Cleaning Robot#fild03	EP17_2_OMEGA_CLEANER


// Chitchat
tra_fild01,122,75,6	script	Neighborhood Grandmother#npc02	4_F_JPNOBA,{
	mes "[Neighborhood Grandmother]";
	mes "โฮะโฮะโฮะ- การได้เห็นพวกคนหนุ่มสาวเหงื่อไหลไคลย้อยและตั้งใจทำงานหนักแบบนี้ มันทำให้ฉันรู้สึกกระปรี้กระเปร่าขึ้นมาเลยล่ะ";
	next;
	mes "[Neighborhood Grandmother]";
	mes "โอ้ ว่าแต่ คุณรู้เรื่องนั้นหรือเปล่า?";
	next;
	mes "[Neighborhood Grandmother]";
	mes "หุ่นจำลองในสนามฝึกที่ทนทานต่อการโจมตีทางกายภาพจะมีไอคอนรูปดาบอยู่เหนือหัว และหุ่นจำลองที่ทนทานต่อการโจมตีด้วยเวทมนตร์จะมีไอคอนรูปไม้เท้าอยู่เหนือหัวของพวกมัน";
	next;
	mes "[Neighborhood Grandmother]";
	mes "ถ้าคุณรู้เรื่องนี้ คุณก็จะสามารถโจมตีพวกมันได้แรงขึ้นใช่ไหมล่ะ?";
	close;
}

tra_fild01,34,102,3	script	Cat#npc03	4_CAT_REST,{
	npctalk "เมี้ยว-", "", bc_self;
	end;
}

tra_fild01,34,120,3	script	Fress#npc05	4_M_SITDOWN,{
	mes "[Fress]";
	mes "ถ้าคุณต้องการไป Prontera คุณสามารถออกไปได้ทางยามที่ประจำอยู่ตามจุดต่างๆ ทั่วพื้นที่ หรือผ่านวาร์ปพอร์ทัลที่ตำแหน่ง 3 และ 9 นาฬิกา พูดจริงๆ นะ";
	close;
}

tra_fild01,128,142,7	script	Nine Oranges#npc04	4_F_ROGUE,{
	mes "[Nine Oranges]";
	mes "จำไว้นะ! เมื่อคุณเลือกชื่อแล้ว มันยากที่จะเปลี่ยน ดังนั้นควรเลือกให้ดี";
	next;
	mes "[Nine Oranges]";
	mes "ว่าแต่ หุ่นจำลอง Undead อยู่ที่ไหนล่ะ?";
	next;
	mes "[Nine Oranges]";
	mes "เพื่อนของฉันบอกว่ามันมีไอคอนรูปกระดูกอยู่เหนือหัว...? คุณต้องไปดูมันให้ได้ก่อนนะ";
	close;
}

tra_fild01,97,64,3	script	Village#npc01	4_M_CHN8GUEK,{
	mes "[Village ]";
	mes "หวัดดี! อากาศดีจริงๆ เลยว่าไหม? ฉันแวะมาดูสนามฝึกหัดแห่งใหม่น่ะ";
	next;
	mes "[Village ]";
	mes "สนามฝึกเก่าก็ไม่ได้แย่นะ แต่จริงๆ แล้วพวกเขาก็แค่เช่าพื้นที่ที่เหล่านักผจญภัยมือใหม่ใช้กันมานาน มันก็เลยมีความไม่สะดวกอยู่บ้าง";
	next;
	mes "[Village ]";
	mes "แต่ตอนนี้พวกเขากำลังสร้างสนามฝึกใหม่ที่กว้างขวางและดูดีขนาดนี้ ดูเหมือนว่าช่วงนี้การเงินของพวกเขาจะมั่นคงทีเดียวนะ!";
	close;
}

tra_fild02,101,90,5	script	Jar Cat#npc09	4_CAT_SAILOR5,{
	mes "[Jar Cat]";
	mes "นี่เป็นครั้งแรกของคุณในสนามฝึกหรือเปล่า? เดี๋ยวฉันจะอธิบายให้ฟังเอง ในฐานะรุ่นพี่น่ะ";
	next;
	mes "[Jar Cat]";
	mes "สนามฝึกมีสามแห่งด้วยกัน คือ สนามฝึกพื้นฐาน, สนามฝึก 1/10 และสนามฝึก 1/100 ส่วนความแตกต่างของแต่ละสนามนั้น ฉันจะปล่อยให้คุณค้นหาด้วยตัวเองแล้วกันนะ";
	next;
	mes "[Jar Cat]";
	mes "อ้อ จริงด้วย! หากต้องการย้ายไปยังสนามฝึกอื่น คุณสามารถใช้พอร์ทัลวาร์ปที่ตำแหน่ง 3 นาฬิกา และ 9 นาฬิกาได้ จำไว้ล่ะ!";
	close;
}

tra_fild02,166,46,3	script	Little One#npc06	4_M_KID2,{
	mes "[Little One]";
	mes "พ่อบอกหนูว่า! หุ่นจำลองที่มีไอคอนรูปไม้เท้าอยู่บนหัวน่ะทนทานต่อเวทมนตร์นะ!";
	next;
	mes "[Little One]";
	mes "ดังนั้น... ถ้าคุณสามารถเอาชนะหุ่นพวกนั้นด้วยเวทมนตร์ได้ คุณก็แข็งแกร่งจริงๆ สินะ?!";
	close;
}

tra_fild02,129,93,6	script	Kekekekereurur#npc10	4_GEFFEN_02,{
	mes "[Kekekekereurur]";
	mes "เขาว่ากันว่าการฝึกกับหุ่นจำลองที่มีไอคอนรูปแมวอยู่ข้างบนจะช่วยให้คุณสู้กับ Doram ใน PvP ได้นะ";
	next;
	mes "[Kekekekereurur]";
	mes "ฉันไม่ได้มาฝึกที่นี่เพราะโดนพวก Doram ที่เดินผ่านไปมาอัดมาหรอกนะ จำไว้ล่ะ!";
	close;
}

tra_fild03,98,46,7	script	Lumberjack#npc13	4_M_NOV_HUNT,{
	mes "[Lumberjack]";
	mes "วู้บ- ฉ่า- วู้บ- ฉ่า-";
	close;
}

tra_fild03,53,140,5	script	Dong#npc15	4_M_CACTUSMAN1,{
	mes "[Dong]";
	mes "อ๊าาาา- หยุดก่อน! หยุด! ฉันไม่ใช่มอนสเตอร์ อย่าโจมตีฉันนะ";
	next;
	mes "[Dong]";
	mes "เฮ้อ- ขอบคุณนะ บางคนมักจะเข้าใจผิดว่าฉันเป็นมอนสเตอร์ Mukka แต่จริงๆ แล้วฉันเป็นกระบองเพชรจากสถานที่ที่เรียกว่า RockRidge น่ะ";
	next;
	mes "[Dong]";
	mes "ถ้าคุณได้ไปเยี่ยมเยียน RockRidge คุณจะได้พบกับเหล่ากระบองเพชรที่มีชีวิตอย่างพวกเรา แวะไปเที่ยวบ้างนะ ฮ่าฮ่าฮ่า";
	close;
}

tra_fild03,92,108,7	script	Cat#npc12	4_CAT_REST,{
	npctalk "โกร๋ง... โกร๋ง...", "", bc_self;
	end;
}

tra_fild03,180,102,4	script	Coco#npc14	4_COCO,{
	emotion ET_QUESTION;
	mes "[Coco]";
	mes "?";
	close;
}
