//===== rAthena Script =======================================
//= Dynamic NPC: Kachua's Secret Box
//===== Description: =========================================
//- [Official conversion]
//= Kachua's Secret Box is a Gachapon NPC.
//= It lets the player exchange a Kachua's Secret Key for a random item.
//= It also gives a Kachua's Mileage Coupon for each pull.
//===== Changelogs: ==========================================
//= 1.0 First version. [secretdataz]
//============================================================

sec_in02,126,178,3	script	Kachua's Secret Box#bm	4_TREASURE_BOX,{
	.@key$ = "K_Secret_Key";
	.@keyname$ = getitemname( .@key$ );
	.@box$ = "Main_Lucky_Box";
	.@boxname$ = getitemname( .@box$ );
	mes "กล่องลับที่ไม่เคยมีใครล่วงรู้ว่ามีอะไรซ่อนอยู่ข้างใน...";
	mes "^4d4dffเจ้าสามารถเปิดกล่องนี้ได้โดยการใช้ " + mesitemlink( .@key$ ) + "^000000";
	next;
	switch(select("^4d4dffเปิดกล่อง 1 ครั้ง (ใช้ 1 " + .@keyname$ + ")^000000","^4d4dffเปิดกล่อง 10 ครั้ง (ใช้ 10 " + .@keyname$ + ")^000000")) {
		case 1:
			mes "^FF0000[ข้อควรระวัง]^000000";
			mes "^FF0000เจ้าจะได้รับไอเทมแบบสุ่มหนึ่งชิ้นเพื่อแลกกับ 1 " + .@keyname$ + "^000000";
			mes "^FF0000ไอเทมที่สุ่มได้ไปแล้วจะไม่สามารถยกเลิก หรือแลกคืนกลับเป็น " + .@keyname$ + " ได้อีก^000000";
			next;
			if(select("ดำเนินการต่อ","ยกเลิกการสนทนา") == 2) {
				mes "เจ้าตัดสินใจที่จะไม่เปิดกล่องในตอนนี้";
				close;
			} else if (countitem(.@key$) < 1) {
				mes "เจ้ามี " + mesitemlink( .@key$ ) + " ไม่เพียงพอ";
				close;
			} else {
				if (checkweight(1201,1) == 0 || ((MaxWeight - Weight) * 100 / MaxWeight) < 10) {
					mes "^4d4dffกรุณาตรวจสอบให้แน่ใจว่าเจ้ามีพื้นที่ว่างในกระเป๋าเพียงพอ^000000";
					close;
				}
				delitem(.@key$, 1);
				consumeitem(.@box$);
				mes "เปิด " + .@boxname$ + " เรียบร้อยแล้ว!";
				mes "เจ้าโชคดีไหมล่ะ?";
				specialeffect2 EF_VALLENTINE;
				close;
			}
		case 2:
			mes "^FF0000[ข้อควรระวัง]^000000";
			mes "^FF0000เจ้าจะได้รับไอเทมแบบสุ่ม 10 ชิ้นเพื่อแลกกับ 10 " + .@keyname$ + "^000000";
			mes "^FF0000ไอเทมที่สุ่มได้ไปแล้วจะไม่สามารถยกเลิก หรือแลกคืนกลับเป็น " + .@keyname$ + " ได้อีก^000000";
			next;
			if(select("ดำเนินการต่อ","ยกเลิกการสนทนา") == 2) {
				mes "เจ้าตัดสินใจที่จะไม่เปิดกล่องในตอนนี้";
				close;
			} else if (countitem(.@key$) < 10) {
				mes "เจ้ามี " + mesitemlink( .@key$ ) + " ไม่เพียงพอ";
				close;
			} else {
				for (.@i = 1; .@i <= 10; ++.@i) {
					progressbar "4d4dff",2;
					if (checkweight(1201,1) == 0 || ((MaxWeight - Weight) * 100 / MaxWeight) < 10) {
						mes "^4d4dffกรุณาตรวจสอบให้แน่ใจว่าเจ้ามีพื้นที่ว่างในกระเป๋าเพียงพอ^000000";
						close;
					}
					if (countitem(.@key$) < 1) { 
						close;
					}
					delitem(.@key$, 1);
					consumeitem(.@box$);
					dispbottom "เปิด " + .@boxname$ + " ไปแล้ว " + .@i + " ครั้ง... กำลังดำเนินการเปิดชิ้นต่อไป",0xFFFFFF;
					specialeffect2 EF_VALLENTINE;
				}
				mes "เปิด " + .@boxname$ + " ทั้งหมด 10 ครั้งเรียบร้อยแล้ว!";
				mes "ดวงของเจ้าเป็นอย่างไรบ้าง?";
				close;
			}
	}
}
