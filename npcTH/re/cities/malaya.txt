//===== rAthena Script ======================================= 
//= Port Malaya
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Port Malaya Town Script
//===== Additional Comments: ================================= 
//= 0.1 Alberta sailors only. [Euphy]
//= 0.2 Adapted from Masao's conversion.
//= 1.0 Added remaining NPCs.
//= 1.1 Added Jeepney script. [DeadlySilence]
//============================================================ 

// Port Malaya Transportation
//============================================================
// Old coordinates: alberta (237,71)
alberta,196,202,3	script	ลูกเรือ Optamara#alberta	100,{
	mes "[ลูกเรือ Optamara]";
	mes "เฮ้ นายตรงนั้นน่ะ!";
	mes "ค่าโดยสารไป Port Malaya ราคา 10,000 Zeny สนใจไหมล่ะ?";
	next;
	switch(select("เกี่ยวกับ Port Malaya:ไป Port Malaya:ยกเลิก")) {
	case 1:
		mes "[ลูกเรือ Optamara]";
		mes "Port Malaya เป็นหมู่บ้านเล็กๆ ที่ตั้งอยู่ทางทิศตะวันตกเฉียงใต้ของ Alberta";
		next;
		mes "[ลูกเรือ Optamara]";
		mes "ก่อนหน้านี้ข้าเข้าไปที่นั่นไม่ได้เพราะพวกเขามีปัญหานิดหน่อย แต่ดูเหมือนตอนนี้ทุกอย่างจะคลี่คลายแล้วนะ!";
		next;
		mes "[ลูกเรือ Optamara]";
		mes "ข้าก็ไม่รู้รายละเอียดแน่ชัดหรอกนะ แต่หมู่บ้านนั่นให้ความรู้สึกเหมือนสวนสาธารณะที่แสนสบายและอบอุ่นเลยล่ะ";
		next;
		mes "[ลูกเรือ Optamara]";
		mes "เกือบไปแล้ว... เกือบจะใจอ่อนจนไม่ยอมกลับ Alberta ที่แสนคุ้นเคยซะแล้ว";
		next;
		mes "[ลูกเรือ Optamara]";
		mes "ข้าเชื่อว่าเจ้าเองก็จะรู้สึกแบบเดียวกันทันทีที่ไปถึง Port Malaya";
		close;
	case 2:
		if (Zeny < 10000) {
			mes "[ลูกเรือ Optamara]";
			mes "โอ้พระเจ้า!!";
			mes "เจ้ารู้ไหมว่ามันไกลแค่ไหนน่ะ แน่นอนว่ามันไม่ได้ไปกันฟรีๆ นะ รบกวนกลับมาพร้อมเงิน 10,000 Zeny ด้วยล่ะ ขออภัยจริงๆ!";
			close;
		}
		mes "[ลูกเรือ Optamara]";
		mes "เยี่ยม!";
		mes "มุ่งหน้าสู่ Port Malaya ได้เลย!!";
		set Zeny, Zeny-10000;
		close2;
		warp "malaya",271,55;
		end;
	case 3:
		mes "[ลูกเรือ Optamara]";
		mes "เหล่านักผจญภัยสมัยนี้ดูท่าทางยุ่งกันจริงนะเนี่ย ไว้ถ้าเจ้าว่างเมื่อไหร่ก็แวะมาได้ เราจะไปเที่ยว Port Malaya ด้วยกัน";
		close;
	}
}

malaya,276,55,4	script	ลูกเรือ Optamara#malaya	100,{
	if (malaya_hi < 10) {
		mes "[ลูกเรือ Optamara]";
		mes "ข้าว่าคนในหมู่บ้านยังดูวุ่นๆ กันอยู่นะ เยี่ยมชมเสร็จหรือยังล่ะ? เรามีเรือมุ่งหน้ากลับไป Alberta เสมอนะ";
		next;
	} else if (malaya_hi < 20) {
		mes "[ลูกเรือ Optamara]";
		mes "ดูเหมือนจะมีนักผจญภัยนิรนามคนหนึ่งช่วยคลายความกังวลให้ชาวเมืองได้นะ เย้! เจ้าอยากจะกลับไปที่ Alberta หรือยัง?";
		next;
	} else {
		mes "[ลูกเรือ Optamara]";
		mes "สนใจจะกลับไป Alberta พร้อมกับข้าไหม?";
		next;
	}
	switch(select("กลับ Alberta:ยังไม่กลับ")) {
	case 1:
		mes "[ลูกเรือ Optamara]";
		mes "เยี่ยมเลย! ออกเดินทางกลับ Alberta กันเถอะ!!";
		close2;
		warp "alberta",192,217;
		end;
	case 2:
		mes "[ลูกเรือ Optamara]";
		mes "ไม่เป็นไร ถ้าเจ้าอยากกลับ Alberta เมื่อไหร่ก็แวะมาหาข้าได้เสมอ";
		close;
	}
}

// Generic Port Malaya NPCs
//============================================================
ma_in01,30,94,4	script	เจ้าของโรงเตี๊ยม#ma	583,{
	if (malaya_hi < 10) {
		mes "[เจ้าของโรงเตี๊ยม]";
		mes "ตายจริง! แวะมาเที่ยวเหรอจ๊ะ?";
		mes "มาช่างผิดจังหวะจริงๆ เลย ตอนนี้หมู่บ้านกำลังวุ่นวายอยู่ ข้าเลยต้องปิดร้านน่ะ ขอโทษด้วยนะ";
		close;
	} else if (malaya_hi < 20) {
		mes "[เจ้าของโรงเตี๊ยม]";
		mes "เจ้าคือนักผจญภัยที่ชาวบ้านพูดถึงกันใช่ไหม? ข้ากลับมาเปิดโรงเตี๊ยมได้อีกครั้งก็เพราะเจ้านะ ขอบคุณมากจริงๆ";
		next;
	} else {
		mes "[เจ้าของโรงเตี๊ยม]";
		mes "ยินดีต้อนรับจ้ะ";
		mes "นี่คือโรงเตี๊ยมที่ดีที่สุดใน Port Malaya ชื่อว่า 'Cabin in the City' จ้ะ";
		next;
	}
	mes "[เจ้าของโรงเตี๊ยม]";
	mes "อยากจะพักผ่อนสักหน่อยไหม? ค่าที่พักราคา 5,000 Zeny จ้ะ";
	next;
	switch(select("บันทึกจุดเกิด:เข้าพัก - 5,000z:ยกเลิก")) {
	case 1:
		mes "[เจ้าของโรงเตี๊ยม]";
		mes "บันทึกข้อมูลเรียบร้อยแล้วจ้ะ แล้วพบกันใหม่นะ";
		savepoint "ma_in01",43,98,1,1;
		close;
	case 2:
		if (Zeny < 5000) {
			mes "[เจ้าของโรงเตี๊ยม]";
			mes "ขอโทษทีจ้ะ ข้าว่าเงินของเจ้าดูจะขาดไปนิดหน่อยนะ";
			close;
		}
		mes "[เจ้าของโรงเตี๊ยม]";
		mes "หวังว่าเจ้าจะมีความสุขกับการพักผ่อนที่นี่นะ";
		close2;
		set Zeny, Zeny-5000;
		percentheal 100,100;
		warp "ma_in01",43,98;
		end;
	case 3:
		close;
	}
}

-	script	Unidentified Creature#i	-1,{
	end;
OnInit:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"01","02","03","04"; break;
		case 5: setarray .@npc$,"05","06","07","08"; break;
		case 9: setarray .@npc$,"09","10","11"; break;
		default: end;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	end;
OnEnable:
	enablenpc();
	end;
OnDisable:
	disablenpc();
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"02","03","04"; break;
		case 5: setarray .@npc$,"06","07","08"; break;
		case 9: setarray .@npc$,"10","11"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch_:
	if (getcharid(1)) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "สิ่งมีชีวิตปริศนาเหลือบมองมาที่คุณ มันหน้าแดงก่ำแล้วหายวับไป";
			close;
		}
		if (rand(1,5)%2) {
			switch(atoi(strnpcinfo(2))) {
				case 1: set $ma_name04$,strcharinfo(0); break;
				case 5: set $ma_name05$,strcharinfo(0); break;
				case 9: set $ma_name06$,strcharinfo(0); break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "สิ่งมีชีวิตปริศนาเหลือบมองมาที่คุณเพียงแวบเดียวแล้วก็หายตัวไป";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "สิ่งมีชีวิตปริศนาจ้องมองมาที่คุณครู่หนึ่งก่อนจะวิ่งหนีไป";
	close;
}
ma_fild01,74,367,6	duplicate(Unidentified Creature#i)	Unidentified Creature#01	572,2,2
ma_fild02,282,41,4	duplicate(Unidentified Creature#i)	Unidentified Creature#05	572,2,2
ma_scene01,195,92,4	duplicate(Unidentified Creature#i)	Unidentified Creature#09	572,2,2

-	script	Unidentified Creature#j	-1,{
	end;
OnEnable:
	enablenpc();
	end;
OnDisable:
	disablenpc();
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 2: setarray .@npc$,"01","03","04"; break;
		case 3: setarray .@npc$,"01","02","04"; break;
		case 4: setarray .@npc$,"01","02","03"; break;
		case 6: setarray .@npc$,"05","07","08"; break;
		case 7: setarray .@npc$,"05","06","08"; break;
		case 8: setarray .@npc$,"05","06","07"; break;
		case 10: setarray .@npc$,"09","11"; break;
		case 11: setarray .@npc$,"09","10"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch_:
	set .@i, atoi(strnpcinfo(2));
	if (.@i%4 == 2) {
		if (is_party_leader() == true)
			set .@pass,1;
	} else if (.@i%4 == 3) {
		if (checkquest(4229,PLAYTIME) > -1 && checkquest(9223,PLAYTIME) > -1 && checkquest(12278,PLAYTIME) > -1)
			set .@pass,1;
	} else if (.@i%4 == 0) {
		if (rand(1,100)%10 == 7)
			set .@pass,1;
	}
	if (.@pass) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "สิ่งมีชีวิตปริศนาเหลือบมองมาที่คุณ มันหน้าแดงก่ำแล้วหายวับไป";
			close;
		}
		if (rand(1,5)%2) {
			switch(.@i) {
			case 2:
			case 3:
			case 4:
				set $ma_name04$,strcharinfo(0);
				break;
			case 6:
			case 7:
			case 8:
				set $ma_name05$,strcharinfo(0);
				break;
			case 10:
			case 11:
				set $ma_name06$,strcharinfo(0);
				break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "สิ่งมีชีวิตปริศนาเหลือบมองมาที่คุณเพียงแวบเดียวแล้วก็หายตัวไป";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "สิ่งมีชีวิตปริศนาจ้องมองมาที่คุณครู่หนึ่งก่อนจะวิ่งหนีไป";
	close;
}
ma_fild01,109,116,4	duplicate(Unidentified Creature#j)	Unidentified Creature#02	572,2,2
ma_fild01,280,150,6	duplicate(Unidentified Creature#j)	Unidentified Creature#03	572,2,2
ma_fild01,309,221,6	duplicate(Unidentified Creature#j)	Unidentified Creature#04	572,2,2
ma_fild02,246,324,4	duplicate(Unidentified Creature#j)	Unidentified Creature#06	572,2,2
ma_fild02,71,296,6	duplicate(Unidentified Creature#j)	Unidentified Creature#07	572,2,2
ma_fild02,32,263,4	duplicate(Unidentified Creature#j)	Unidentified Creature#08	572,2,2
ma_scene01,158,139,4	duplicate(Unidentified Creature#j)	Unidentified Creature#10	572,2,2
ma_scene01,167,112,6	duplicate(Unidentified Creature#j)	Unidentified Creature#11	572,2,2

malaya,227,311,4	script	คุณยาย#ma01	575,{
	if (malaya_hi < 10) {
		mes "[คุณยาย]";
		mes "ไม่ใช่คนที่นี่งั้นรึ? ดูแลตัวเองดีๆ นะพ่อหนุ่ม";
		next;
		mes "[คุณยาย]";
		mes "ยายล่ะเป็นห่วงลูกสะใภ้ที่กำลังท้องอยู่จริงๆ หวังว่าจะไม่มีเรื่องร้ายๆ เกิดขึ้นนะ";
		close;
	} else if (malaya_hi < 20) {
		mes "[คุณยาย]";
		mes "ยายได้ยินมาว่ามีคนนอกมาช่วยคนในหมู่บ้านเราไว้";
		next;
		mes "[คุณยาย]";
		mes "ถ้าอย่างนั้น ลูกสะใภ้ของยายก็คงจะปลอดภัยแล้วล่ะ";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[คุณยาย]";
		mes "ได้ยินมาว่ามีคนชื่อ "+getd("$ma_name0"+rand(1,6)+"$")+" คอยตามติด Meoneonuncle ไปทุกที่เลยล่ะ";
		next;
		mes "[คุณยาย]";
		mes "ยายล่ะขอเอาใจช่วยในความรักที่ผิดจารีตของพวกเขาจริงๆ";
		close;
	}
	mes "[คุณยาย]";
	mes "ยายกังวลเรื่องลูกสะใภ้ที่กำลังท้องเหลือเกิน หวังว่า Meoneonuncle จะไม่มาวุ่นวายกับนางนะ";
	next;
	select("Meoneonuncle คือใครเหรอครับ?");
	mes "[คุณยาย]";
	mes "ไม่ใช่คนที่นี่สินะ? งั้นยายจะบอกให้ว่าทำไมแม้แต่นักเดินทางอย่างเจ้าถึงต้องระวังตัว";
	next;
	mes "[คุณยาย]";
	mes "Meoneonuncle เคยเป็นผู้หญิงที่อาศัยอยู่ใน Port Malaya เมื่อนานมาแล้ว นางแต่งงานกับชายที่ดีและมีลูกด้วยกันหนึ่งคน";
	next;
	mes "[คุณยาย]";
	mes "แต่ความสุขมักไม่ยั่งยืนใช่ไหมล่ะ? นางแท้งลูกเพราะอุบัติเหตุครั้งหนึ่ง...";
	next;
	mes "[คุณยาย]";
	mes "นางโศกเศร้ากับการสูญเสียลูกอยู่หลายวัน จนสุดท้ายนางก็ตรอมใจตาย";
	next;
	select("เป็นเรื่องที่เศร้ามากเลยครับ");
	mes "[คุณยาย]";
	mes "จากนั้นวิญญาณของ Meoneonuncle ก็เริ่มปรากฏตัว และคอยพรากเอาเด็กในครรภ์ไปจากหญิงที่กำลังตั้งท้องในหมู่บ้าน";
	next;
	mes "[คุณยาย]";
	mes "ถ้าเจ้าเคยเดินไปรอบๆ Port Malaya ตอนกลางคืน...";
	next;
	mes "[คุณยาย]";
	mes "แล้วได้ยินเสียง 'ติก-ติก' หรือ 'วัก-วัก' ล่ะก็ จงระวังตัวไว้ให้ดี เพราะนั่นคือเสียงกระพือปีกของ Meoneonuncle ยังไงล่ะ";
	close;
}

malaya,189,263,4	script	ชายหนุ่มนักตีกลอง#ma02	578,{
	if (malaya_hi < 10) {
		emotion ET_HUK;
		mes "[ชายหนุ่มนักตีกลอง]";
		mes "อึก... มันเหมือนกับตอนนั้นเลย...";
		next;
		emotion ET_HUK;
		mes "[ชายหนุ่มนักตีกลอง]";
		mes "ตอนที่ดวงจันทร์ถูกกลืนกิน... อ๊ากก!";
		close;
	} else if (malaya_hi < 20) {
		mes "[ชายหนุ่มนักตีกลอง]";
		mes "ข้าคิดว่าหมู่บ้านนี้รอดมาได้เพราะนักผจญภัยนิรนามคนนั้นแท้ๆ";
		next;
		emotion ET_OHNO;
		mes "[ชายหนุ่มนักตีกลอง]";
		mes "ฟู่... ข้านึกว่า Bakonawa จะโผล่ออกมาอีกรอบซะแล้ว";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[ชายหนุ่มนักตีกลอง]";
		mes "ว่ากันว่า "+getd("$ma_name0"+rand(1,6)+"$")+" เป็นคนหยุด Bakonawa ไม่ให้กลืนกินดวงจันทร์ล่ะ สุดยอดไปเลยใช่ไหม? ข้าล่ะโล่งอกจริงๆ";
		close;
	}
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "Bakonawa หรือที่รู้จักกันในชื่อ 'อสูรกายผู้กลืนกินดวงจันทร์' แห่ง Port Malaya";
	next;
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "แต่ข้ารู้มาว่า Bakonawa ก็สามารถ 'กลืนกินผู้คน' ได้เหมือนกันนะ";
	next;
	select("เฮือก! ถ้าอย่างนั้นมันก็อันตรายมากเลยสิ?");
	emotion ET_KIK;
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "ฮ่าๆๆ มันมีวิธีหยุดไม่ให้ Bakonawa กลืนกินคนอยู่";
	next;
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "ใช่แล้ว แม้แต่อสูรกายที่ร้ายกาจที่สุดซึ่งเคยกลืนดวงจันทร์ไปแล้วถึง 6 ดวง ก็ยังมีจุดอ่อนนะ";
	next;
	select("โห จุดอ่อนคืออะไรเหรอ?");
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "มันไวต่อเสียงน่ะ! ดังนั้นเวลาที่มันกำลังจะกลืนกินดวงจันทร์!!";
	next;
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "นั่นแหละโอกาสของเจ้า!! จะเป็นเสียงอะไรก็ได้!! ไม่ว่าจะเป็นหม้อ, ฉาบ, กลอง หรือกระทะ แค่ทำเสียงดังเข้าไว้!!";
	next;
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "แล้ว Bakonawa จะตกใจจนคายดวงจันทร์ออกมาแล้วหนีไปเอง!";
	next;
	mes "[ชายหนุ่มนักตีกลอง]";
	mes "พกกลองติดตัวไว้สิ เจ้าจะได้ใช้มันแน่ๆ";
	close;
}

malaya,270,59,4	script	ยามเฝ้าท่าเรือ#ma03	570,{
	if (malaya_hi < 10) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[ยามเฝ้าท่าเรือ]";
		mes "ช่วงนี้หมู่บ้านวุ่นวายจังแฮะ ข้าจะมาอู้งานแบบนี้จะดีเหรอเนี่ย?";
		close;
	} else if (malaya_hi < 20) {
		emotion ET_STARE_ABOUT;
		mes "[ยามเฝ้าท่าเรือ]";
		mes "ตอนนี้หมู่บ้านเริ่มกลับมาสงบแล้ว แต่เจ้าก็ยังควรระวังตัวเวลาเดินเล่นตอนกลางคืนนะ";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[ยามเฝ้าท่าเรือ]";
		mes "เขาว่ากันว่า "+getd("$ma_name0"+rand(1,6)+"$")+" หนีรอดมาจากเงื้อมมือของ Buwaya ได้หลังจากถูกจับไปล่ะ คนคนนี้ต้องแข็งแกร่งมากแน่ๆ";
		close;
	}
	emotion ET_HUK;
	mes "[ยามเฝ้าท่าเรือ]";
	mes "อะไรนะ!! ท่าเรือปลอดภัยงั้นเหรอ!!";
	next;
	mes "[ยามเฝ้าท่าเรือ]";
	mes "หือ? อ้าว ไม่ใช่กัปตันหรอกเรอะ? ตกใจหมดเลยโธ่! อ๊ะ จริงด้วย! ข้าได้รับเอกสารแจ้งเตือนสำหรับนักเดินทางมาล่ะ";
	next;
	select("เอกสารอะไรเหรอ?");
	mes "[ยามเฝ้าท่าเรือ]";
	mes "ในนี้บอกว่านักเดินทางควรระวังพวกมอนสเตอร์เวลาเดินอยู่ในหมู่บ้านตอนกลางคืน";
	next;
	mes "[ยามเฝ้าท่าเรือ]";
	mes "ปกติพวก Buwaya จะไม่ค่อยเข้ามาในหมู่บ้านหรอก แต่ก็มีเคสที่พวกมันแอบลักพาตัวคนใส่กล่องไม้ที่พวกมันแบกอยู่ตอนกลางคืนเหมือนกันนะ";
	next;
	mes "[ยามเฝ้าท่าเรือ]";
	mes "ฮะ?! แล้วในนี้ยังบอกอีกว่า มอนสเตอร์ที่ชื่อ Wokwok มักจะแวะเวียนมาที่หมู่บ้านบ่อยเป็นพิเศษตอนกลางคืน ควรหลีกเลี่ยงพวกมันไว้ล่ะ";
	next;
	switch(select("เข้าใจแล้วครับ:มีวิธีป้องกันพวกมันไหม?")) {
	case 1:
		mes "[ยามเฝ้าท่าเรือ]";
		mes "ระวังตัวด้วยล่ะตอนกลางคืนน่ะ!";
		close;
	case 2:
		mes "[ยามเฝ้าท่าเรือ]";
		mes "เขาว่ากันว่าถ้าเจ้าถูกจับใส่กล่องไม้ของ Buwaya ให้พยายามโจมตีไปที่จุดอ่อนข้างในกล่องนั่น";
		next;
		mes "[ยามเฝ้าท่าเรือ]";
		mes "และถ้าเจ้าได้ยินเสียงกระพือปีกของ Wokwok มาจากไกลๆ ให้รีบวิ่งเข้าอาคารที่ใกล้ที่สุดทันที";
		next;
		emotion ET_SCRATCH;
		mes "[ยามเฝ้าท่าเรือ]";
		mes "แต่ถ้าเสียงกระพือปีกมันเบามาก หรือถ้าไม่ได้ยินอะไรเลย... ก็เตรียมตัวสู้แล้วก็สวดมนต์ขอให้โชคดีเถอะนะ";
		close;
	}
}

malaya,88,252,4	script	เด็กหญิงตัวน้อย#ma04	576,{
	if (malaya_hi < 10) {
		mes "[เด็กหญิงตัวน้อย]";
		mes "หนูพยายามทำใจดีสู้เสืออยู่ค่ะ เพราะหนูต้องไปพบภูตในป่าให้ได้";
		close;
	} else if (malaya_hi < 20) {
		mes "[เด็กหญิงตัวน้อย]";
		mes "อิอิ ท่านพ่อบอกว่าถ้าหมู่บ้านสงบสุขเมื่อไหร่ หนูค่อยไปหาภูตในป่าก็ได้ค่ะ";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		mes "[เด็กหญิงตัวน้อย]";
		mes "เมื่อกี้พี่ "+.@name$+" แวะมาเล่านิทานตลกๆ ให้หนูฟังด้วยล่ะ";
		next;
		mes "[เด็กหญิงตัวน้อย]";
		mes "'ยายมีขายหอย ยายมอยขายหมี'";
		mes "อิอิ ตลกใช่ไหมคะ? พี่ "+.@name$+" ดูเป็นคนสนุกสนานจังเลย";
		close;
	}
	mes "[เด็กหญิงตัวน้อย]";
	mes "ท่านพ่อบอกว่า ถ้าหนูเป็นเพื่อนกับภูตแห่งพงไพร หนูจะไม่เป็นโรคผิวหนังอีกเลยค่ะ";
	next;
	select("ภูตที่ว่านั่นคืออะไรเหรอครับ?");
	mes "[เด็กหญิงตัวน้อย]";
	mes "Encan'to ไงคะ! พวกภูต Encan'to อาศัยอยู่ในต้นไม้ใหญ่หรือโขดหินในป่า มีทั้งภูตเด็กผู้ชายและเด็กผู้หญิง...";
	next;
	mes "[เด็กหญิงตัวน้อย]";
	mes "แต่ภูตเด็กผู้ชายสวยกว่าภูตเด็กผู้หญิงอีกนะคะ ทำไมถึงเป็นแบบนั้นกันนะ?";
	close;
}

malaya,219,92,6	script	เด็กน้อย#ma05	577,{
	if (malaya_hi < 10) {
		mes "[เด็กน้อย]";
		mes "แม่บอกว่าห้ามออกไปเล่นข้างนอกเพราะมันอันตราย ทำไมเหรอครับ?";
		close;
	} else if (malaya_hi < 20) {
		mes "[เด็กน้อย]";
		mes "แม่บอกว่าออกไปเล่นได้แล้ว แต่ให้เล่นแค่ใน Port Malaya เท่านั้นนะ";
		next;
		mes "[เด็กน้อย]";
		mes "อิอิ แต่ผมก็ไม่เคยคิดจะออกไปนอก Port Malaya อยู่แล้วล่ะ";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[เด็กน้อย]";
		mes "ได้ยินมาว่ามีคนแอบไปขโมยหมวกของ Jejeling ที่ Baryo Mahiwaga มาด้วยล่ะ";
		next;
		mes "[เด็กน้อย]";
		mes "ทำไมถึงต้องไปขโมยหมวกของมอนสเตอร์ด้วยนะ? พี่ "+getd("$ma_name0"+rand(1,6)+"$")+" ต้องอยากได้มันมากแน่ๆ เลย";
		next;
		mes "[เด็กน้อย]";
		mes "โอ๊ะ เดี๋ยวก่อน! เรื่องนี้ต้องเป็นความลับนะ ชู่ว! ช่วยทำเป็นไม่ได้ยินที่ผมพูดเมื่อกี้ทีนะครับ";
		close;
	}
	mes "[เด็กน้อย]";
	mes "ผมว่าต้องมีคนคอยขโมยหมวกในหมู่บ้านไปแน่ๆ เลย";
	next;
	mes "[เด็กน้อย]";
	mes "หมวกที่แม่ผมเพิ่งซักไปเมื่อวานหายวับไปเลย หมวกของเพื่อนบ้านก็หายไปเมื่อไม่กี่วันก่อนเหมือนกัน";
	next;
	mes "[เด็กน้อย]";
	mes "อืม... พี่คิดว่าเป็นฝีมือของพวก Jejeling หรือเปล่าครับ?";
	next;
	mes "[เด็กน้อย]";
	mes "ผมไปหาเพื่อนที่ Baryo Mahiwaga มา เขาบอกว่าเห็น Jejeling ใส่หมวกที่หายไปด้วยล่ะ...";
	close;
}

malaya,363,283,4	script	ชาวเมือง#ma06	582,{
	if (malaya_hi < 10) {
		mes "[ชาวเมือง]";
		mes "อืม... ถึงเวลาที่ต้องระวังคำสาปของแม่มดแล้วหรือยังนะ?";
		next;
		mes "[ชาวเมือง]";
		mes "ระวังเข็มของ Mongkukurum ไว้ให้ดีเถอะ นักเดินทาง";
		close;
	} else if (malaya_hi < 20) {
		mes "[ชาวเมือง]";
		mes "ยินดีต้อนรับสู่ Port Malaya นะท่านนักเดินทาง..";
		next;
		mes "[ชาวเมือง]";
		mes "ช่วงนี้หมู่บ้านยังดูวุ่นวายอยู่ ถ้าเจอใครที่มีตาสีแดงล่ะก็ พยายามอย่าไปจ้องตาพวกเขาล่ะ";
		close;
	}
	set .@name$, getd("$ma_name0"+rand(1,6)+"$");
	switch(rand(1,6)) {
	case 1:
		mes "[ชาวเมือง]";
		mes "ระวังตัวเวลาเดินในหมู่บ้านตอนกลางคืนด้วยล่ะ เพราะ "+.@name$+" ก็เพิ่งถูก Wokwok เล่นงานไป";
		close;
	case 2:
		mes "[ชาวเมือง]";
		mes "ว่ากันว่า "+.@name$+" สามารถฝึก Tikbalang ให้เชื่องได้สำเร็จ คนคนนี้ใจกล้าจริงๆ อยากรู้จังว่าจะมีโอกาสได้เห็น Tikbalang ตัวนั้นไหมนะ?";
		close;
	case 3:
		mes "[ชาวเมือง]";
		mes "เจ้าเคยเห็นพวก Bongisungisu ไหม? ข้าได้ยินว่า "+.@name$+" กำลังออกล่าพวกมันอยู่น่ะ";
		close;
	case 4:
		mes "[ชาวเมือง]";
		mes "ถ้าเจ้าคิดจะออกไปนอกหมู่บ้าน ระวังพวก Tiyanak ไว้ด้วยล่ะ (ในสคริปต์เขียนผิดเป็น Tiucknuc) ได้ยินว่า "+.@name$+" ถูกมันหลอกจนต้องหามส่งโรงพยาบาลเลยนะ";
		close;
	default:
		break;
	}
	mes "[ชาวเมือง]";
	mes "ช่วงนี้หมู่บ้านปั่นป่วนจริงๆ ดูเหมือนพวก Mongkukurum จะกลับมาที่นี่อีกแล้ว";
	next;
	select("Mongkukurum คืออะไรเหรอครับ?");
	mes "[ชาวเมือง]";
	mes "มันคือมอนสเตอร์ที่คนแถวนี้เรียกว่าแม่มด รูปร่างเหมือนคนและสวมใส่เสื้อผ้าเปี๊ยบเลยล่ะ";
	next;
	mes "[ชาวเมือง]";
	mes "Mongkukurum จะสร้างตุ๊กตาขึ้นมาจากคนที่มันเห็น แล้วสาปแช่งคนคนนั้นด้วยการใช้เข็มแทงตุ๊กตา";
	next;
	select("พอจะมีวิธีแยกแยะเจ้าตัวนี้ไหม?");
	mes "[ชาวเมือง]";
	mes "มีอยู่ทางเดียว... พวก Mongkukurum ทุกตัวจะมีดวงตาสีแดงก่ำ";
	next;
	mes "[ชาวเมือง]";
	mes "เจ้าสามารถแยกพวกมันได้ด้วยดวงตา แต่! มีสิ่งหนึ่งที่เจ้าต้องจำไว้ให้แม่น";
	next;
	mes "[ชาวเมือง]";
	mes "เจ้าจะถูก Mongkukurum ครอบงำทันที ถ้าเจ้าจ้องมองไปที่ตาของมันนานเกินไป";
	next;
	mes "[ชาวเมือง]";
	mes "ห้ามจ้องตาพวกมันนานเด็ดขาด!";
	close;
}

malaya,41,127,6	script	คุณปู่#ma07	574,{
	if (malaya_hi < 10) {
		mes "[คุณปู่]";
		mes "พวกคนนอกไม่ค่อยเป็นที่ต้อนรับเท่าไหร่หรอกนะ ในช่วงที่หมู่บ้านวุ่นวายแบบนี้";
		close;
	} else if (malaya_hi < 20) {
		mes "[คุณปู่]";
		mes "เจ้าโชคร้ายหน่อยนะที่มาเที่ยวหมู่บ้านในช่วงเวลาที่ไม่ค่อยมีใครอยากต้อนรับแขกแบบนี้";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[คุณปู่]";
		mes "เขาว่ากันว่าเจ้า "+getd("$ma_name0"+rand(1,6)+"$")+" เอา Jellopy มาโยนทิ้งขว้างในหมู่บ้านนี้";
		next;
		mes "[คุณปู่]";
		mes "เหอะๆ... สงสัยจะเป็นพวกที่ไม่เห็นค่าของแม้แต่ Jellopy สินะ";
		close;
	}
	mes "[คุณปู่]";
	mes "เจ้าต้องคอยดูให้ดีนะว่ามี Bangungot อาศัยอยู่ในต้นไม้ที่เจ้ากำลังจะตัดหรือเปล่า";
	next;
	mes "[คุณปู่]";
	mes "เพราะเรื่องประหลาดจะเกิดขึ้น ถ้าเจ้าเอาไม้จากต้นที่มี Bangungot สถิตอยู่มาทำเป็นเสาเอกของบ้าน";
	next;
	select("จะเกิดอะไรขึ้นเหรอครับ?");
	mes "[คุณปู่]";
	mes "สำหรับ Bangungot แล้ว การตัดต้นไม้มันหมายถึงการทำลายบ้านของมัน มันจึงโกรธแค้นมาก";
	next;
	mes "[คุณปู่]";
	mes "เวลาเจ้าพยายามจะนอนใกล้ๆ เสาต้นนั้น ทีแรกมันจะคอยแกล้งให้เจ้าตื่นนอนไม่ได้";
	next;
	mes "[คุณปู่]";
	mes "แต่เมื่อเวลาผ่านไปและความแค้นของมันเพิ่มขึ้น มันจะขึ้นมานั่งทับบนตัวเจ้าจนเจ้าหายใจไม่ออกและตายไปในที่สุด";
	next;
	select("น่ากลัวจังเลยครับ...");
	mes "[คุณปู่]";
	mes "ฮ่าๆๆ เสาบ้านปกติมันไม่มี Bangungot หรอกน่า อย่าไปเก็บมาคิดจนนอนไม่หลับล่ะ";
	close;
}

malaya,63,185,4	script	หญิงสาว#ma08	583,{
	if (malaya_hi < 10) {
		mes "[หญิงสาว]";
		mes "ข้าไม่มีอะไรจะคุยกับเจ้าหรอกนะ";
		close;
	} else if (malaya_hi < 20) {
		mes "[หญิงสาว]";
		mes "ข้าเป็นห่วงพวกเด็กๆ เหลือเกิน หวังว่าพวกเขาจะไม่ขวัญเสียกับเรื่องที่เกิดขึ้นในหมู่บ้านนะ";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		set .@rand, rand(1,10);
		if (.@rand < 5) {
			mes "[หญิงสาว]";
			mes "ข้าชอบฟังเรื่องตลกนะ ไม่นานมานี้ "+.@name$+" แวะมาเล่าเรื่องขำๆ ให้ข้าฟังด้วยล่ะ";
			next;
		}
		switch(.@rand) {
		case 1:
			mes "[หญิงสาว]";
			mes "'คนดีผีคุ้ม... แต่ถ้าคนดื้อมักจะโดนผีเคาะหัวนะ'";
			next;
			break;
		case 2:
			mes "[หญิงสาว]";
			mes "'ทำไมผีถึงชอบมาตอนดึกๆ... ก็เพราะผีมาหา 'นอย' (เที่ยงคืน) ไงล่ะ'";
			next;
			break;
		case 3:
			mes "[หญิงสาว]";
			mes "'ทำไมไม้บรรทัดถึงสั้น... ก็เพราะมันไม่ได้ 'ยาว' (Yao) เหมือนคนในหมู่บ้านเรายังไงล่ะ'";
			next;
			break;
		case 4:
			mes "[หญิงสาว]";
			mes "ที่ร้านขายของชำ "+.@name$+" พูดว่า 'ผมจะซื้อเข็มกับด้ายครับ! ส่วนคุณช่วยซื้อกองฟางมาให้ผมหาเข็มหน่อยนะ'";
			next;
			mes "[หญิงสาว]";
			mes "พอเขาพูดจบ เขาก็โดนเตะออกจากร้านทันทีเลยล่ะ";
			next;
			break;
		case 5:
			mes "[หญิงสาว]";
			mes "พนักงาน Kafra ที่เป็นเพื่อนกับข้า เล่าเรื่องลูกค้าคนหนึ่งให้ฟังด้วย";
			next;
			mes "[หญิงสาว]";
			mes "เขาบอกว่า 'Kafra จ๊ะ คนฉลาดมักไม่แต่งงาน และถ้าพวกเขาแต่งงาน พวกเขาก็จะไม่ใช่คนฉลาดอีกต่อไป'";
			next;
			mes "[หญิงสาว]";
			mes "คิกๆ... ข้าชอบเรื่องตลกนะ แต่มันออกจะเข้าใจยากไปนิดสำหรับข้า";
			close;
		default:
			mes "[หญิงสาว]";
			mes "ข้าชอบเรื่องตลกจริงๆ นะ อ้อ! แล้ว "+.@name$+" ยังบอกอีกว่า ความรักก็เหมือนภาพถ่ายล่ะ เจ้าว่าไหม?";
			next;
			mes "[Woman]";
			mes "เพราะมันต้องผ่าน 'ห้องมืด' (Darkness) ก่อนที่จะ 'ล้าง' (Develop) ออกมาให้เห็นได้ยังไงล่ะ";
			next;
		}
		mes "[หญิงสาว]";
		mes "คิกๆ ตลกใช่ไหมล่ะ?";
		close;
	}
	mes "[หญิงสาว]";
	mes "ถ้าเจ้าได้ยินเสียงทารกร้องไห้มาจากในป่านอกหมู่บ้าน ห้ามเข้าไปใกล้เด็ดขาดเลยนะ";
	next;
	select("ทำไมเหรอครับ?");
	mes "[หญิงสาว]";
	mes "มีมอนสเตอร์ที่ชื่อ Tiyanak (ในสคริปต์คือ Tiucknuc) มันจะป้วนเปี้ยนอยู่นอกหมู่บ้าน แล้วแปลงร่างเป็นทารกเพื่อร้องไห้เรียกร้องความสนใจ";
	next;
	mes "[หญิงสาว]";
	mes "แต่เมื่อนักเดินทางผู้ใจบุญอุ้มทารกคนนั้นขึ้นมา มันจะคืนร่างเดิมแล้วเข้าจู่โจมทันที";
	next;
	select("มอนสเตอร์ตัวนี้เจ้าเล่ห์จังเลยครับ");
	mes "[หญิงสาว]";
	mes "ใช่ มันนิสัยเสียมากที่หลอกใช้ความเมตตาของคนเดินทาง แต่ว่า...";
	next;
	mes "[หญิงสาว]";
	mes "มีข่าวลือว่ามอนสเตอร์ตัวนี้เกิดจากวิญญาณของเด็กที่ไม่ยอมไปเกิดน่ะ ฟังแล้วก็น่าเศร้าเหมือนกันนะ";
	close;
}

// Jeepneys
//============================================================
function	script	F_Malaya_Jeepney	{
	.@mapName$        = getarg(0);
	.@passengers      = getarg(1);

	// ดึงข้อความข้อมูลรถเพิ่มเติมจากอาร์กิวเมนต์
	for (.@i = 5; .@i < getargcount(); .@i++) {
		setd(".@msgJeepneyInfo$[" + (.@i - 5) + "]", getarg(.@i));
	}

	if (malaya_hi < 10) {
		mes "[คนขับรถจี๊ปนีย์]";
		mes getarg(2);
		close;
	} else if ((malaya_hi >= 10) && (malaya_hi < 20)) {
		mes "[คนขับรถจี๊ปนีย์]";
		mes getarg(3);
		close;
	} else {
		mes "[คนขับรถจี๊ปนีย์]";
		mes getarg(4);
		next;
	}

	switch(select("ขึ้นรถ [จำนวนผู้โดยสาร " + getmapusers(.@mapName$) + "/" + .@passengers + "]:จี๊ปนีย์คืออะไร?:มีรถคันอื่นอีกไหม?:อ่า... ใช่...")) {
	case 1:
		if (getmapusers(.@mapName$) >= .@passengers) {
			mes "[คนขับรถจี๊ปนีย์]";
			mes "เกรงว่ารถคันนี้จะเต็มแล้วล่ะ";
			mes "เสียใจด้วยนะ ลองไปดูรถคันอื่นดีไหม?";
			close;
		} else {
			mes "[คนขับรถจี๊ปนีย์]";
			mes "ขอให้เป็นวันที่ดีนะ";
			close2;
			warp .@mapName$,29,24;
			end;
		}
	case 2:
		// วนลูปแสดงข้อมูลเฉพาะของ NPC ตัวนั้นๆ
		for (.@i = 0; .@i < getarraysize(.@msgJeepneyInfo$); .@i++) {
			mes "[คนขับรถจี๊ปนีย์]";
			mes getd(".@msgJeepneyInfo$[" + .@i + "]");

			if (.@i < (getarraysize(.@msgJeepneyInfo$) - 1)) {
				next;
			}
		}
		close;
	case 3:
		mes "[คนขับรถจี๊ปนีย์]";
		mes "โอ้! แน่นอนว่ามีรถคันอื่นๆ คอยให้บริการอยู่ด้วย";
		next;
		mes "[คนขับรถจี๊ปนีย์]";
		mes "ใน Port Malaya จะมีรถจี๊ปนีย์ขนาด 12, 30 และ 60 ที่นั่ง โดยแต่ละขนาดจะมีรถวิ่งอยู่ไซส์ละ 3 คัน";
		next;
		mes "[คนขับรถจี๊ปนีย์]";
		mes "คุณอยากทราบตำแหน่งของรถคันอื่นๆ ไหมล่ะ?";
		next;
		switch (select("ฉันอยากรู้ที่จอดรถขนาด 12 ที่นั่ง:ฉันอยากรู้ที่จอดรถขนาด 30 ที่นั่ง:ฉันอยากรู้ที่จอดรถขนาด 60 ที่นั่ง")) {
		case 1:
			viewpoint 1, 237, 240, 1, 0xF7E009;
			viewpoint 1, 67 , 44 , 2, 0xF7E009;
			viewpoint 1, 282, 129, 3, 0xF7E009;
			set .@zif_in, 12;
			break;
		case 2:
			viewpoint 1, 134, 250, 4, 0xF7E009;
			viewpoint 1, 341, 153, 5, 0xF7E009;
			viewpoint 1, 293, 290, 6, 0xF7E009;
			set .@zif_in, 30;
			break;
		case 3:
			viewpoint 1, 242, 221, 7, 0xF7E009;
			viewpoint 1, 62 , 245, 8, 0xF7E009;
			viewpoint 1, 257, 58 , 9, 0xF7E009;
			set .@zif_in, 60;
			break;
		}
		mes "[คนขับรถจี๊ปนีย์]";
		mes "ตำแหน่งของรถขนาด " + .@zif_in + " ที่นั่ง ถูกทำเครื่องหมายไว้บนแผนที่ของคุณแล้ว";
		next;
		break;
	}
	mes "[คนขับรถจี๊ปนีย์]";
	mes "ขอให้เดินทางโดยสวัสดิภาพ";
	close;
}

malaya,237,240,4	script	คนขับรถจี๊ปนีย์#01	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif01",
		12,
		"อูยยยย.... ข้าละกลัวเหลือเกิน... กลัวมาก... เกิดอะไรขึ้นกับที่นี่กันแน่?",
		"ที่นี่ยังวุ่นวายอยู่เลย... ข้าจะยังขับรถจี๊ปนีย์ที่นี่ต่อได้ไหมนะ...",
		"ฮ่า ฮ่า ฮ่า ยินดีต้อนรับ ข้าชื่อ ^1561EAVol^000000 ผู้ให้บริการรถจี๊ปนีย์ขนาด 12 ที่นั่งครับ",
		"เพิ่งเคยมา Port Malaya ครั้งแรกหรือเปล่า?",
		"คนขับรถจี๊ปนีย์อย่างพวกเรามีหน้าที่โปรโมตรถด้วย งั้นข้าขออธิบายหน่อยนะ",
		"ตอนแรกจี๊ปนีย์ถูกสร้างมาเพื่อขนส่งสินค้าให้รวดเร็ว แต่ตอนนี้กลายเป็นรถสาธารณะไปแล้วล่ะ",
		"รสนิยมคนเราไม่เหมือนกัน เจ้าเลยจะได้เห็นรถที่ตกแต่งแบบเท่ๆ หรือประณีตแตกต่างกันไป",
		"แน่นอนว่าเจ้าไม่ต้องขับเอง แค่ขึ้นมานั่งเฉยๆ ก็พอ เชิญได้เลยนะไม่ต้องเกรงใจ"
	);
}

malaya,67,44,4	script	คนขับรถจี๊ปนีย์#02	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif02",
		12,
		"อะ... อะไรกัน... เกิดอะไรขึ้นในโรงพยาบาลนั่นน่ะ...",
		"ข้าว่าเสียงครางโหยหวนจากโรงพยาบาลนั่นเริ่มเบาลงแล้วนะ...",
		"ข้าชื่อ ^1561EAChui^000000 ขับรถจี๊ปนีย์ 12 ที่นั่งครับ หวัดดี~",
		"เพิ่งเคยมา Port Malaya ครั้งแรกหรือเปล่า?",
		"คนขับรถจี๊ปนีย์อย่างพวกเรามีหน้าที่โปรโมตรถด้วย งั้นข้าขออธิบายหน่อยนะ",
		"ตอนแรกจี๊ปนีย์ถูกสร้างมาเพื่อขนส่งสินค้าให้รวดเร็ว แต่ตอนนี้กลายเป็นรถสาธารณะไปแล้วล่ะ",
		"รถคันนี้ข้าปรับแต่งเองกับมือเลยนะจะบอกให้...",
		"เพื่อความปลอดภัยเราเลยต้องจำกัดจำนวนผู้โดยสาร แต่มันปลอดภัยหายห่วงแน่นอน..."
	);
}

malaya,282,129,4	script	คนขับรถจี๊ปนีย์#03	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif03",
		12,
		"คนนอกไม่ควรมาคุยเล่นแถวนี้นะ... บางทีหัวหน้า Mumbaki อาจจะมีทางแก้เรื่องนี้ก็ได้",
		"เจ้าคือนักผจญภัยที่เพิ่งไปพบ Mumbaki มางั้นรึ? ไม่สิ... งั้นเจ้าไม่ควรอยู่ตรงนี้... ใช่แล้ว ผีหลอกกก... อ๊ากกก...",
		"ข้าชื่อ ^1561EATop^000000 ขับหนึ่งในรถจี๊ปนีย์ 12 ที่นั่งที่มีอยู่ 3 คันในเมืองนี้ครับ",
		"เพิ่งเคยมาที่นี่ครั้งแรกเหรอ?",
		"คนขับรถจี๊ปนีย์อย่างพวกเรามีหน้าที่โปรโมตรถด้วย งั้นข้าขออธิบายหน่อยนะ",
		"ตอนแรกจี๊ปนีย์ถูกสร้างมาเพื่อขนส่งสินค้าให้รวดเร็ว แต่ตอนนี้กลายเป็นรถสาธารณะไปแล้วล่ะ",
		"หน้าตารถจะเปลี่ยนไปตามสไตล์ของคนขับแต่ละคนนะ...",
		"และตอนนี้มันก็ได้กลายเป็นเอกลักษณ์ของ Port Malaya ไปแล้วล่ะ"
	);
}

malaya,134,250,6	script	คนขับรถจี๊ปนีย์#04	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif04",
		30,
		"ไป... ไปให้พ้นเลยนะ...",
		"อ๊ากก... ตกใจหมดเลย... ไปซะ... ชิ้วๆ...",
		"ข้าชื่อ ^1561EABrav^000000 คนขับรถ 30 ที่นั่งยินดีรับใช้ครับ ข้าขับปลอดภัยสุดๆ เพราะข้าเป็นคนขวัญอ่อนน่ะ",
		"เพิ่งเคยมา Port Malaya ครั้งแรกหรือเปล่า?",
		"ตอนแรกจี๊ปนีย์ถูกสร้างมาเพื่อขนส่งสินค้าให้รวดเร็ว แต่ตอนนี้กลายเป็นรถสาธารณะไปแล้วล่ะ",
		"รถที่ถูกดัดแปลงเหล่านี้จะถูกตกแต่งโดยเจ้าของรถเอง",
		"ถ้าเจ้ามา Port Malaya เจ้าต้องลองนั่งจี๊ปนีย์ดูสักครั้งนะ",
		"ฮ่า ฮ่า ฮ่า แน่นอนว่าเจ้าขับเองไม่ได้หรอก"
	);
}

malaya,341,153,4	script	คนขับรถจี๊ปนีย์#05	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif05",
		30,
		"ผะ... ผีในเมือง... เฮ้... เจ้ามีผีติดหลังมาด้วยหรือเปล่าน่ะ?",
		"ข้าว่าช่วงนี้ผีเริ่มน้อยลงแล้วนะ... เมืองดูเงียบสงบขึ้นนิดหน่อย...",
		"หวัดดี ข้าชื่อ ^1561EALivil^000000 คนขับรถ 30 ที่นั่ง ความปลอดภัยคือสิ่งที่ข้าให้ความสำคัญที่สุด",
		"เพิ่งมาที่นี่ครั้งแรกเหรอ?",
		"ข้าจะให้คำแนะนำสั้นๆ เป็นการบริการพิเศษแล้วกัน",
		"ตอนแรกจี๊ปนีย์ถูกสร้างมาเพื่อขนส่งสินค้าให้รวดเร็ว แต่ตอนนี้กลายเป็นรถสาธารณะไปแล้วล่ะ",
		"เพื่อความปลอดภัย รถจี๊ปนีย์เลยต้องมีการจำกัดจำนวนผู้โดยสารน่ะ",
		"บางคันเคยบรรทุกถึง 180 คนเลยนะ แต่ใน Port Malaya ทุกวันนี้ไม่มีขนาดนั้นหรอก"
	);
}

malaya,293,290,6	script	คนขับรถจี๊ปนีย์#06	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif06",
		30,
		"ทะ... ทหารยาม... ทหารยามมัวทำอะไรกันอยู่เนี่ย...",
		"ในที่สุดพวกทหารยามก็ทำอะไรสักทีสินะ? ฟู่... เกือบไปแล้ว...",
		"ข้าชื่อ ^1561EAGad^000000 คนขับรถจี๊ปนีย์ 30 ที่นั่งที่ไว้ใจได้ที่สุด",
		"ดูท่าเจ้าจะเพิ่งเคยมาเมืองนี้สินะ",
		"การแนะนำนักเดินทางก็เป็นงานของข้าเหมือนกัน!! ฟังข้านะ",
		"จี๊ปนีย์เคยเป็นรถขนส่งสินค้า แต่ตอนนี้ถูกตกแต่งและดัดแปลงด้วยเทคโนโลยีใหม่ของ Port Malaya เชียวนะ!!",
		"คันนี้ข้าทำเองกับมือเลยนะ ฮ่า ฮ่า ฮ่า... เท่ใช่ไหมล่ะ? ฮ่า ฮ่า ฮ่า",
		"ถึงลูกค้าจะเป็นพระเจ้า แต่ข้าก็ให้เจ้าขับเองไม่ได้หรอกนะ... ฮ่า ฮ่า ฮ่า"
	);
}

malaya,242,221,4	script	คนขับรถจี๊ปนีย์#07	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif07",
		60,
		"แย่แล้ว... ข้าว่าทั้งหมู่บ้านต้องโดนผีสิงแน่ๆ... บางที... ข้าควรไปหาหัวหน้า Mumbaki สักหน่อย...",
		"ผี... ข้ามั่นใจว่าหัวหน้า Mumbaki ต้องรู้วิธีกำจัดผีพวกนี้แน่ๆ...",
		"ข้าชื่อ ^1561EAHott^000000 ยินดีให้บริการ ข้าคือคนขับรถ 60 ที่นั่งที่ปลอดภัยที่สุด",
		"จี๊ปนีย์คือระบบขนส่งสาธารณะของ Port Malaya ครับ",
		"เพราะฉะนั้นมันเลยมีอยู่ทุกที่ แถมฟรีด้วย! ข้ารู้ว่ารถแต่ละขนาดจำกัดคนไม่เท่ากัน แต่นั่นไม่ใช่ปัญหาหรอก เพราะรถเรามีเยอะแยะไปหมด"
	);
}

malaya,62,245,6	script	คนขับรถจี๊ปนีย์#08	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif08",
		60,
		"ชิ้ว~ ไปซะถ้าเจ้าเป็นผี แต่ถ้าเป็นคนก็ไปไกลๆ เหมือนกันนะ~ ชิ้ว~",
		"เฮ้... เจ้า... เป็นคนใช่ไหม? ใช่ไหมนะ? หือ? ช่วยบอกทีว่าเป็นคนเถอะ... ได้โปรด...",
		"หวัดดี ข้าชื่อ ^1561EAMuyan^000000 คนขับรถ 60 ที่นั่งคันนี้ ยินดีที่ได้รู้จักนะ",
		"จี๊ปนีย์คือรถสาธารณะ ดังนั้นความปลอดภัยต้องมาก่อนเสมอ",
		"รถของข้าได้รับใบรับรอง 'จี๊ปนีย์ 60 ที่นั่ง' ระดับสูงสุดของ Port Malaya เชียวนะ",
		"นั่นหมายความว่า!! มันปลอดภัยแบบสุดๆ!! ไม่ต้องกังวลเรื่องการเดินทางเลยล่ะ"
	);
}

malaya,257,58,6	script	คนขับรถจี๊ปนีย์#09	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif09",
		60,
		"...เจ้าเป็นคนนอกนี่... อย่าไปแตะต้องอะไรมั่วซั่ว และระวังตัวตอนอยู่ในเมืองด้วยล่ะ...",
		"ในเมืองยังอันตรายอยู่นิดหน่อยนะ ทางที่ดีอย่าเพิ่งไปจับอะไรจนกว่าจะแน่ใจว่าปลอดภัย",
		"สวัสดี ข้าชื่อ ^1561EAGramma^000000 คนขับรถจี๊ปนีย์ 60 ที่นั่งแห่ง Port Malaya ครับ",
		"เพิ่งเคยมา Port Malaya ครั้งแรกหรือเปล่า?",
		"คนขับรถจี๊ปนีย์อย่างพวกเรามีหน้าที่โปรโมตรถด้วย งั้นข้าขออธิบายหน่อยนะ",
		"ตอนแรกจี๊ปนีย์ถูกสร้างมาเพื่อขนส่งสินค้าให้รวดเร็ว แต่ตอนนี้กลายเป็นรถสาธารณะไปแล้วล่ะ",
		"รสนิยมคนเราไม่เหมือนกัน เจ้าเลยจะได้เห็นรถที่ตกแต่งแบบเท่ๆ หรือประณีตแตกต่างกันไป",
		"แน่นอนว่าเจ้าไม่ต้องขับเอง แค่ขึ้นมานั่งเฉยๆ ก็พอ เชิญได้เลยนะไม่ต้องเกรงใจ"
	);
}
