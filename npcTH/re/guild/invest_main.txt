//===== rAthena Script =======================================
//= Hall of Abyss - Core Scripts
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Expansion for War of Emperium FE.
//= Invest in each realm to unlock new maps:
//= - Guild Dungeon F2, total 20,000,000z.
//= - Hall of Abyss, total 70,000,000z.
//===== Additional Comments: =================================
//= 1.0 First version. Missing official times. [Euphy]
//= 1.1 Added GM management function. [Euphy]
//============================================================

// Global Variables
//============================================================
// Note: an asterisk (*) represents any realm (prt, pay, gef, alde).
/*
$2011_agit_invest
-- 0: Inactive.
-- 1: Investment in progress.
-- 2: Investment results calculating.
-- 4: Investment calculations complete (only when at least one $*_invest_result is 3).
-- 11-21: Tie, revote required.

$agit_result_notice
-- 0: Inactive.
-- 1-12: Interim statements (displaying $@vfund_*x) available.

$agit_result_timer
-- 0: Inactive.
-- 0-59: Time elapsed during investment (in hours).

$agit_revoting_timer
-- 0: Inactive.
-- 0-29: Time elapsed during revote (in minutes).
-- 30: Revote closed.
-- 31: Revote results calculating.

$*_invest_result
-- 0: No data.
-- 1: Realm not connected.
-- 2: Guild Dungeon F2 open.
-- 3: Abyss Gate (Hall of Abyss and Gulid Dungeon F2) open.

$fund_*[01-05]
-- Investment units per guild castle.
$fund_*_extra
-- Investment units per realm.

$@vfund_*[01-05]
$@vfund_*_extra
-- Investment units, used in interim statements only.
*/

// Investment Functions & Controller
//============================================================
// Note: The times in this section are almost entirely custom.

-	script	#invest_timer	-1,{
OnClock0000:	// Open investments on Wednesday (1 hour after WoE)
	if (gettime(DT_DAYOFWEEK) == WEDNESDAY && !agitcheck()) {
		set $2011_agit_invest,1;
		donpcevent "#fund_master::OnInvest_start";
	}
	end;
OnClock1200:	// Close investments on Friday (60 hours after investments open)
	if (gettime(DT_DAYOFWEEK) == FRIDAY && !agitcheck()) {
		set $2011_agit_invest,2;
		donpcevent "#fund_master::OnInvest_stop";
	}
	end;
OnClock1235:	// Open dungeons on Friday (at least 31 minutes after investments close)
	if (gettime(DT_DAYOFWEEK) == FRIDAY && !agitcheck())
		donpcevent "#fund_master::OnResult";
	end;
OnClock2000:	// Close dungeons on Tuesday (1 hour before WoE)
	if (gettime(DT_DAYOFWEEK) == TUESDAY)
		donpcevent "#fund_master::OnReset";
	end;
}

function	script	F_Invest_Status	{
	set .@day, gettime(DT_DAYOFWEEK);
	set .@hour, gettime(DT_HOUR);
	set .@minute, gettime(DT_MINUTE);

	// Inactive.
	if (agitcheck())
		return 0;

	// Open for investments.
	if (.@day >= WEDNESDAY && (.@day < FRIDAY || (.@day == FRIDAY && .@hour <= 12)))
		return 1;

	// Investments closed, calculating results.
	if (.@day == FRIDAY && .@hour == 12 && .@minute < 35)
		return 2;

	// Calculations complete, dungeons open.
	if ((.@day == FRIDAY && (.@hour > 12 || (.@hour == 12 && .@minute >= 35))) || .@day > FRIDAY ||
	    .@day < TUESDAY || (.@day == TUESDAY && .@hour < 20))
		return 3;

	// Dungeons closed.
	return 0;
}

//callfunc "F_Invest_Abyss","<variable>","<NPC name>"{,"<addon message>"};
function	script	F_Invest_Abyss	{
	.@npc_name$ = getarg(1);
	mes "การลงทุนมีอยู่ 2 วิธี คือลงทุนด้วย Zeny หรือใช้ใบรับรองการลงทุน (Investment Certificate) ที่ออกโดยสมาคมพ่อค้า Cat Paw";
	mes "คุณต้องการเลือกลงทุนด้วยวิธีไหนดีครับ?";
	next;
	switch(select("ลงทุนด้วย Zeny:ลงทุนด้วยใบรับรองการลงทุน (Investment Certificates):ยกเลิก")) {
	case 1:
		mes .@npc_name$;
		mes "ตกลงว่าคุณตัดสินใจที่จะลงทุนด้วย Zeny นะครับ";
		mes "หน่วยการลงทุนขั้นต่ำคือ ^4d4dff10,000 Zeny^000000";
		mes "คุณสามารถลงทุนได้สูงสุด 50 หน่วย ซึ่งเท่ากับ ^4d4dff500,000 Zeny^000000 ครับ";
		next;
		switch(select("1 หน่วย 10,000 Zeny:10 หน่วย 100,000 Zeny:50 หน่วย 500,000 Zeny:ระบุจำนวนอื่น:ยกเลิก")) {
		case 1:
			set .@units,1;
			break;
		case 2:
			set .@units,10;
			break;
		case 3:
			set .@units,50;
			break;
		case 4:
			mes .@npc_name$;
			mes "โปรดระบุจำนวนหน่วยที่คุณต้องการลงทุนครับ";
			mes "โปรดเลือกระหว่าง 1 ถึง 200";
			mes "กด 0 เพื่อยกเลิก";
			next;
			input .@units;
			if (.@units < 0 || .@units > 200) {
				mes .@npc_name$;
				mes "คุณระบุจำนวนเกินขีดจำกัดต่อการลงทุนหนึ่งครั้งครับ";
				mes "โปรดลองใหม่อีกครั้ง";
				close;
			} else if (.@units == 0) {
				mes .@npc_name$;
				mes "ยกเลิก";
				close;
			} else
				break;
		case 5:
			mes .@npc_name$;
			mes "ยกเลิก";
			close;
		}
		set .@zeny, .@units * 10000;
		if (Zeny < .@zeny) {
			mes .@npc_name$;
			mes "คุณมีเงินไม่เพียงพอครับ";
			close;
		}
		mes .@npc_name$;
		mes "คุณได้ลงทุนไปแล้วจำนวน "+callfunc("F_InsertPlural",.@units,"หน่วย")+" เป็นเงิน "+callfunc("F_InsertComma",.@zeny)+" Zeny"+getarg(2,"")+" ครับ";
		mes "อาจจะไม่มากนักแต่นี่คือของขวัญเล็กๆ น้อยๆ คุณสามารถนำไปแลกเปลี่ยนเป็นไอเทมได้ที่เกาะ Malangdo นะครับ";
		set Zeny, Zeny-.@zeny;
		setd getarg(0), getd(getarg(0)) + .@units;
		getitem 6488,.@units; //Thanks_Invest_Ticket
		close;
	case 2:
		mes .@npc_name$;
		mes "โอ้ คุณนำใบรับรองการลงทุน (Investment Certificates) มาด้วยสินะ";
		mes "คุณต้องการจะส่งใบรับรองจำนวนกี่ใบดีครับ?";
		next;
		switch(select("1 ใบรับรองการลงทุน:5 ใบรับรองการลงทุน:ระบุจำนวนอื่น:ยกเลิก")) {
		case 1:
			set .@tickets,1;
			break;
		case 2:
			set .@tickets,5;
			break;
		case 3:
			mes .@npc_name$;
			mes "โปรดระบุจำนวนใบรับรองการลงทุนที่คุณต้องการจะส่งครับ";
			mes "โปรดเลือกระหว่าง 1 ถึง 20";
			mes "กด 0 เพื่อยกเลิก";
			next;
			input .@tickets;
			if (.@tickets < 0 || .@tickets > 20) {
				mes .@npc_name$;
				mes "คุณระบุจำนวนเกินขีดจำกัดต่อการลงทุนหนึ่งครั้งครับ";
				mes "โปรดลองใหม่อีกครั้ง";
				close;
			} else if (.@tickets == 0) {
				mes .@npc_name$;
				mes "ยกเลิก";
				close;
			} else
				break;
		case 4:
			mes .@npc_name$;
			mes "ยกเลิก";
			close;
		}
		if (countitem(6489) < .@tickets) {
			mes .@npc_name$;
			mes "ดูเหมือนว่าคุณจะมีใบรับรองการลงทุนน้อยกว่าจำนวนที่ต้องการจะส่งนะครับ";
			close;
		}
		set .@units, .@tickets * 10;
		mes .@npc_name$;
		mes "ใบรับรองการลงทุนจำนวน "+.@tickets+" ใบ มีมูลค่าเท่ากับการลงทุนจำนวน "+.@units+" หน่วย ("+((.@units < 100)?.@units:insertchar(""+.@units,",",1))+"0,000z)"+getarg(2,"")+" ครับ";
		mes "อาจจะไม่มากนักแต่นี่คือของขวัญเล็กๆ น้อยๆ คุณสามารถนำไปแลกเปลี่ยนเป็นไอเทมได้ที่เกาะ Malangdo นะครับ";
		delitem 6489,.@tickets; //Cats_Invest_Certif
		setd getarg(0), getd(getarg(0)) + .@units;
		getitem 6488,.@units; //Thanks_Invest_Ticket
		close;
	case 3:
		mes .@npc_name$;
		mes "ยกเลิก";
		close;
	}
}

// Script Core :: agit_fund
//============================================================
prt_gld,1,1,0	script	#fund_master	844,{
	end;
OnInit:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 1) {
		set $2011_agit_invest,1;
		end;
	}
	if ((.@invest_status == 2 && $2011_agit_invest == 1) ||
	    (.@invest_status == 3 && ($2011_agit_invest == 1 || $2011_agit_invest == 2))) {
		donpcevent "#fund_master::OnCalculate";
		end;
	}
	end;
OnCalculate_Election:
	set .@force_vote,1;
OnCalculate:
	setarray .@cas$[0],"prt","pay","gef","alde";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		// All funds per realm are summed.
		set .@funds[.@i], getd("$fund_"+.@cas$[.@i]+"01")+getd("$fund_"+.@cas$[.@i]+"02")+getd("$fund_"+.@cas$[.@i]+"03")+getd("$fund_"+.@cas$[.@i]+"04")+getd("$fund_"+.@cas$[.@i]+"05")+getd("$fund_"+.@cas$[.@i]+"_extra");

		// 70,000,000z opens Hall of Abyss and F2.
		if (.@funds[.@i] >= 7000) {
			setd "$"+.@cas$[.@i]+"_invest_result",3;
			set .@total_7000,1;
		}
		// 20,000,000z opens F2 and a chance for Hall of Abyss.
		else if (.@funds[.@i] >= 2000) {
			setd "$"+.@cas$[.@i]+"_invest_result",2;
			set .@total_2000,1;
		}
		// Anything below that opens nothing.
		else
			setd "$"+.@cas$[.@i]+"_invest_result",1;
	}
	// No further actions if 20,000,000z isn't reached.
	if (!.@total_2000)
		end;
	// No further actions if any realm hits 70,000,000z.
	if (.@total_7000) {
		set $2011_agit_invest,4;
		end;
	}
	// Otherwise, highest investment above 20,000,000z opens Hall of Abyss.
	set .@max, .@funds[0];
	for(set .@i,1; .@i<4; set .@i,.@i+1) {
		if (.@funds[.@i] > .@max) {
			set .@max, .@funds[.@i];
			set .@ele, .@i;
			deletearray .@rand$[0], getarraysize(.@rand$);
		} else if (.@funds[.@i] == .@max && .@funds[.@i] >= 2000) {
			if (!getarraysize(.@rand$))
				set .@rand$[0], .@cas$[.@ele];
			set .@rand$[getarraysize(.@rand$)], .@cas$[.@i];
		}
	}
	if (.@max < 2000)
		end;
	if (getarraysize(.@rand$)) {

		// Tie: declare a random winner.
		if (!.@force_vote) {
			set .@i, rand(getarraysize(.@rand$));
			setd "$"+.@rand$[.@i]+"_invest_result",3;
			set $2011_agit_invest,4;
			end;
		}

		// Tie: initiate a 30-minute election.
		set .@str$, implode(.@rand$,"|");
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (compare(.@str$,.@cas$[.@i]))
				set .@election, .@election | (1<<.@i);
		}
		switch(.@election) {
			case 3:  set $2011_agit_invest,11; break; //Prontera|Payon
			case 9:  set $2011_agit_invest,12; break; //Prontera|Al De Baran
			case 5:  set $2011_agit_invest,13; break; //Prontera|Geffen
			case 10: set $2011_agit_invest,14; break; //Al De Baran|Payon
			case 6:  set $2011_agit_invest,15; break; //Geffen|Payon
			case 12: set $2011_agit_invest,16; break; //Al De Baran|Geffen
			case 11: set $2011_agit_invest,17; break; //Prontera|Payon|Al De Baran
			case 7:  set $2011_agit_invest,18; break; //Prontera|Payon|Geffen
			case 13: set $2011_agit_invest,19; break; //Prontera|Al De Baran|Geffen
			case 14: set $2011_agit_invest,20; break; //Al De Baran|Payon|Geffen
			case 15: set $2011_agit_invest,21; break; //Prontera|Payon|Al De Baran|Geffen
		}
		if ($2011_agit_invest >= 11)
			donpcevent "Revoting Manager#Invest1::OnStart";
		end;
	} else {
		setd "$"+.@cas$[.@ele]+"_invest_result",3;
		set $2011_agit_invest,4;
		end;
	}
	end;
OnInvest_start:
	announce "นับจากนี้เป็นต้นไป เราจะเริ่มเปิดรับการลงทุนเพื่อเชื่อมต่อ 'Abyss Gate' ในพื้นที่ WOE ได้แก่: Valkyrie Realm, Greenwood Lake, Luina และ Britoniah",bc_all,0xFF0000; //FW_NORMAL 10
	set $fund_prt01,0;
	set $fund_prt02,0;
	set $fund_prt03,0;
	set $fund_prt04,0;
	set $fund_prt05,0;
	set $fund_pay01,0;
	set $fund_pay02,0;
	set $fund_pay03,0;
	set $fund_pay04,0;
	set $fund_pay05,0;
	set $fund_gef01,0;
	set $fund_gef02,0;
	set $fund_gef03,0;
	set $fund_gef04,0;
	set $fund_gef05,0;
	set $fund_alde01,0;
	set $fund_alde02,0;
	set $fund_alde03,0;
	set $fund_alde04,0;
	set $fund_alde05,0;
	donpcevent "Investment_total#fund00::OnEnable";
	end;
OnInvest_stop:
	announce "พวกเราได้สิ้นสุดการลงทุนเพื่อเชื่อมต่อ 'Abyss Gate' เรียบร้อยแล้ว",bc_all,0xFF0000; //FW_NORMAL 10
	donpcevent "Investment_total#fund00::OnReset";
	donpcevent "#fund_master::OnCalculate_Election";
	end;
OnResult:
	announce "ขณะนี้ได้ผลสรุปการลงทุนสำหรับ 'Abyss Gate' แล้ว คุณสามารถตรวจสอบได้ที่ป้ายสถานะการลงทุนในแต่ละพื้นที่ WOE",bc_all,0xFF0000; //FW_NORMAL 10
	if ($2011_agit_invest == 4) {
		set $fund_prt01,0;
		set $fund_prt02,0;
		set $fund_prt03,0;
		set $fund_prt04,0;
		set $fund_prt05,0;
		set $fund_prt_extra,0;
		set $fund_pay01,0;
		set $fund_pay02,0;
		set $fund_pay03,0;
		set $fund_pay04,0;
		set $fund_pay05,0;
		set $fund_pay_extra,0;
		set $fund_gef01,0;
		set $fund_gef02,0;
		set $fund_gef03,0;
		set $fund_gef04,0;
		set $fund_gef05,0;
		set $fund_gef_extra,0;
		set $fund_alde01,0;
		set $fund_alde02,0;
		set $fund_alde03,0;
		set $fund_alde04,0;
		set $fund_alde05,0;
		set $fund_alde_extra,0;
	} else if ($2011_agit_invest == 2)
		donpcevent "#fund_master::OnCalculate";
	end;
OnReset:
	set $2011_agit_invest,0;
	donpcevent "Investment_total#fund00::OnReset";
	end;
}

prt_gld,2,2,0	script	Investment_total#fund00	844,{
	callfunc "F_GM_NPC";
	mes "ระบบทำงานไม่ถูกต้องอย่างนั้นหรือ?";
	next;
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "หากคุณคิดว่าตัวจับเวลาการลงทุนหยุดทำงาน โปรดปรับแต่งตัวจับเวลาใหม่";
		next;
		switch(select("เปิดใช้งานตัวจับเวลา:ยกเลิก")) {
		case 1:
			donpcevent "Investment_total#fund00::OnTimerReset";
			mes "คุณได้เปิดใช้งานตัวจับเวลาแล้ว";
			close;
		case 2:
			mes "ยกเลิก";
			close;
		}
	} else {
		mes "ข้าไม่จำเป็นต้องปรับเปลี่ยนอะไรในตอนนี้";
		close;
	}

OnInit:
	if (callfunc("F_Invest_Status") == 1) {
		initnpctimer;
		donpcevent "Investment_total#fund00::OnVarInit";
	} else {
		set $agit_result_timer,0;
		set $agit_result_notice,0;
	}
	end;
OnVarInit:
	setarray .@cas$[0],"prt","pay","gef","alde";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		setd "$@vfund_"+.@cas$[.@i]+"01", getd("$fund_"+.@cas$[.@i]+"01");
		setd "$@vfund_"+.@cas$[.@i]+"02", getd("$fund_"+.@cas$[.@i]+"02");
		setd "$@vfund_"+.@cas$[.@i]+"03", getd("$fund_"+.@cas$[.@i]+"03");
		setd "$@vfund_"+.@cas$[.@i]+"04", getd("$fund_"+.@cas$[.@i]+"04");
		setd "$@vfund_"+.@cas$[.@i]+"05", getd("$fund_"+.@cas$[.@i]+"05");
		setd "$@vfund_"+.@cas$[.@i]+"_extra", getd("$fund_"+.@cas$[.@i]+"_extra");
	}
	end;
OnEnable:
	set $agit_result_timer,0;
	set $agit_result_notice,0;
	initnpctimer;
	end;
OnReset:
	set $agit_result_timer,0;
	set $agit_result_notice,0;
	stopnpctimer;
	end;
OnTimerReset:
	initnpctimer;
	end;
OnTimer3600000:
	if (callfunc("F_Invest_Status") == 1) {
		if ($agit_result_timer < 59) {
			set $agit_result_timer, $agit_result_timer+1;

			// แสดงประกาศทุกๆ 5 นาที จนถึง 55 และที่นาทีที่ 58
			if ($agit_result_timer % 10 == 5 || $agit_result_timer % 10 == 0 || $agit_result_timer == 58) {
				set $agit_result_notice, $agit_result_notice+1;
				donpcevent "Investment_total#fund00::OnVarInit";
				announce "ฉบับที่ "+$agit_result_notice+" งบแสดงฐานะระหว่างกาลพร้อมให้ตรวจสอบแล้ว คุณสามารถตรวจสอบได้ที่ป้ายสถานะการลงทุนในแต่ละพื้นที่ WOE",bc_all,0xFF0000; //FW_NORMAL 10
			}

			donpcevent "Investment_total#fund00::OnTimerReset";
		} else
			stopnpctimer;
		end;
	} else {
		donpcevent "Investment_total#fund00::OnReset";
		end;
	}
}

prt_gld,164,98,3	script	Revoting Manager#Invest1	421,{
	mes "[Revoting Manager]";
	mes "เราไม่เปิดรับการลงคะแนนเพิ่มเติมแล้ว";
	mes "โปรดรอการประกาศผลอย่างเป็นทางการ";
	close;
OnInit:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 2 && $2011_agit_invest >= 11 && $agit_revoting_timer < 30) {
		donpcevent "Revoting Manager#Invest2::OnEnable";
		initnpctimer;
	} else if (.@invest_status == 3 && $2011_agit_invest >= 11)
		donpcevent "Revoting Manager#Invest1::OnCalculate";
	donpcevent "Revoting Manager#Invest1::OnDisable";
	end;
OnCalculate:
	switch($2011_agit_invest) {
		case 11: setarray.@cas$[0],"prt","pay"; break;
		case 12: setarray.@cas$[0],"prt","alde"; break;
		case 13: setarray.@cas$[0],"prt","gef"; break;
		case 14: setarray.@cas$[0],"alde","pay"; break;
		case 15: setarray.@cas$[0],"gef","pay"; break;
		case 16: setarray.@cas$[0],"alde","gef"; break;
		case 17: setarray.@cas$[0],"prt","pay","alde"; break;
		case 18: setarray.@cas$[0],"prt","pay","gef"; break;
		case 19: setarray.@cas$[0],"prt","alde","gef"; break;
		case 20: setarray.@cas$[0],"alde","pay","gef"; break;
		case 21: setarray.@cas$[0],"prt","pay","alde","gef"; break;
		default: end;
	}
	set .@max, getd("$fund_"+.@cas$[0]+"_extra");
	for(set .@i,1; .@i<getarraysize(.@cas$); set .@i,.@i+1) {
		set .@funds, getd("$fund_"+.@cas$[.@i]+"_extra");
		if (.@funds > .@max) {
			set .@max, .@funds;
			set .@ele, .@i;
			deletearray .@rand$[0], getarraysize(.@rand$);
		} else if (.@funds == .@max) {
			if (!getarraysize(.@rand$))
				set .@rand$[0], .@cas$[.@ele];
			set .@rand$[getarraysize(.@rand$)], .@cas$[.@i];
		}
	}
	if (getarraysize(.@rand$)) {
		set .@i, rand(getarraysize(.@rand$));
		setd "$"+.@rand$[.@i]+"_invest_result",3;
	} else
		setd "$"+.@cas$[.@ele]+"_invest_result",3;
	set $2011_agit_invest,4;
	set $agit_revoting_timer,0;
	end;
OnEnable:
	enablenpc "Revoting Manager#Invest1";
	end;
OnDisable:
	disablenpc "Revoting Manager#Invest1";
	end;
OnStart:
	initnpctimer;
	donpcevent "Revoting Manager#Invest2::OnNotice_on";
	end;
OnEnd:
	stopnpctimer;
	disablenpc "Revoting Manager#Invest1";
	end;
OnTimerReset:
	initnpctimer;
	end;
OnTimer60000:
	set .@revote, $agit_revoting_timer;
	if (.@revote < 30) {
		set $agit_revoting_timer, $agit_revoting_timer+1;
		donpcevent "Revoting Manager#Invest1::OnTimerReset";
	} else if (.@revote == 30) {
		announce "Cat Paw Merchant Guild: ขณะนี้เราจะทำการปิดการลงคะแนนเพิ่มเติมแล้ว เราจะประกาศผลการเลือกตั้งในเร็วๆ นี้",bc_all,0xFF0000; //FW_NORMAL 10
		donpcevent "Revoting Manager#Invest2::OnDisable";
		donpcevent "Revoting Manager#Invest1::OnEnable";
		set $agit_revoting_timer, $agit_revoting_timer+1;
		donpcevent "Revoting Manager#Invest1::OnTimerReset";
	} else if (.@revote == 31) {
		donpcevent "Revoting Manager#Invest1::OnCalculate";
		donpcevent "Revoting Manager#Invest1::OnDisable";
		stopnpctimer;
		end;
	} else {
		donpcevent "Revoting Manager#Invest1::OnDisable";
		stopnpctimer;
	}
	end;
}

prt_gld,164,98,3	script	Revoting Manager#Invest2	421,{
	set .@invest, $2011_agit_invest;
	if (.@invest < 11) {
		mes "[Revoting Manager]";
		mes "ไม่! ตอนนี้ไม่ใช่เวลาที่จะทำการลงคะแนนใหม่";
		close2;
		disablenpc "Revoting Manager#Invest2";
		end;
	}
	set .@playtime, checkquest(7349,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[Revoting Manager]";
		mes "คุณได้เข้าร่วมการลงคะแนนไปแล้ว";
		mes "โปรดทำตัวตามสบายและรอฟังผลสรุป";
		close;
	} else if (.@playtime == 2)
		erasequest 7349;
	mes "[Revoting Manager]";
	mes "คุณต้องการจะเข้าร่วมการลงคะแนนเพื่อเลือกพื้นที่ที่จะเชื่อมต่อกับ 'Abyss Gate' หรือไม่?";
	next;
	if(select("เข้าร่วมการลงคะแนน:ข้ามไปก่อน") == 2) {
		mes "[Revoting Manager]";
		mes "คุณได้ยกเลิกการลงคะแนน";
		close;
	}
	mes "[Revoting Manager]";
	mes "คุณสามารถลงคะแนนได้เพียงครั้งเดียว และไม่จำเป็นต้องเสียค่าลงทุนใดๆ สำหรับการเลือกครั้งนี้";
	mes "โปรดเลือกหนึ่งในพื้นที่ WOE ต่อไปนี้";
	next;

	// สร้างเมนู
	explode(.@candidates$, callsub(OnGetCandidates, .@invest),"|");
	for(set .@i,0; .@i<getarraysize(.@candidates$); set .@i,.@i+1) {
		if (.@candidates$[.@i] == "alde")
			set .@menu$[.@i],"Luina";
		else if (.@candidates$[.@i] == "gef")
			set .@menu$[.@i],"Britoniah";
		else if (.@candidates$[.@i] == "pay")
			set .@menu$[.@i],"Greenwood Lake";
		else if (.@candidates$[.@i] == "prt")
			set .@menu$[.@i],"Valkyrie Realm";
	}
	set .@vote, select(implode(.@menu$,":"))-1;

	mes "[Revoting Manager]";
	mes "คุณได้ลงคะแนนให้พื้นที่ "+.@menu$[.@vote];
	mes "ขอบคุณสำหรับการลงคะแนน";
	setquest 7349;
	setd "$fund_"+.@candidates$[.@vote]+"_extra", getd("$fund_"+.@candidates$[.@vote]+"_extra") + 1;
	close;
OnGetCandidates:
	switch(getarg(0)) {
		case 11: return "prt|pay";
		case 12: return "prt|alde";
		case 13: return "prt|gef";
		case 14: return "alde|pay";
		case 15: return "gef|pay";
		case 16: return "alde|gef";
		case 17: return "prt|pay|alde";
		case 18: return "prt|pay|gef";
		case 19: return "prt|alde|gef";
		case 20: return "alde|pay|gef";
		case 21: return "prt|pay|alde|gef";
	}
	return "";
OnInit:
	disablenpc "Revoting Manager#Invest2";
	end;
OnEnable:
	enablenpc "Revoting Manager#Invest2";
	end;
OnDisable:
	disablenpc "Revoting Manager#Invest2";
	stopnpctimer;
	end;
OnNotice_on:
	initnpctimer;
	end;
OnTimer5000:
	announce "Cat Paw Merchant Guild: เนื่องจากไม่มีพื้นที่ WOE ใดที่มีระดับการลงทุนถึงเกณฑ์สูงสุดในการเชื่อมต่อ 'Abyss Gate'...",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer8000:
	announce "Cat Paw Merchant Guild: เรากำลังจะเชื่อมต่อ 'Abyss Gate' ไปยังพื้นที่ WOE ที่ได้รับเงินลงทุนมากที่สุด",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer11000:
	announce "Cat Paw Merchant Guild: อย่างไรก็ตาม มีบางพื้นที่ที่มียอดเงินลงทุนเท่ากันพอดี ดังนั้นเราจึงต้องตัดสินด้วยการลงคะแนน",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer14000:
	announce "Cat Paw Merchant Guild: จะเปิดให้ลงคะแนนเพิ่มเติมเป็นเวลา 30 นาที โดยคุณสามารถลงคะแนนได้ที่ Valkyrie Realm",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer20000:
	explode(.@candidates$, callsub(OnGetCandidates, $2011_agit_invest),"|");
	for(set .@i,0; .@i<getarraysize(.@candidates$); set .@i,.@i+1) {
		if (.@candidates$[.@i] == "alde")
			set .@choices$[.@i],"Luina";
		else if (.@candidates$[.@i] == "gef")
			set .@choices$[.@i],"Britoniah";
		else if (.@candidates$[.@i] == "pay")
			set .@choices$[.@i],"Greenwood Lake";
		else if (.@candidates$[.@i] == "prt")
			set .@choices$[.@i],"Valkyrie Realm";
	}
	announce "Cat Paw Merchant Guild: ผู้ท้าชิงสำหรับการลงคะแนนเพิ่มเติมคือ... "+implode(.@choices$," vs. ")+".",bc_all,0xFF0000; //FW_NORMAL 10
	set $fund_prt_extra,0;
	set $fund_pay_extra,0;
	set $fund_gef_extra,0;
	set $fund_alde_extra,0;
	set $agit_revoting_timer,0;
	donpcevent "Revoting Manager#Invest2::OnEnable";
	stopnpctimer;
	end;
}

/*
prtg_cas01,2,2,0	script	Abrai	55,{
	mes "I should quit my steward job.";
	close;
OnAgitInvest:
	switch(callfunc("F_Invest_Status")) {
	case 0:
		set $2011_agit_invest,0;
		donpcevent "#fund_master::OnReset";
		break;
	case 1:
		set $2011_agit_invest,1;
		donpcevent "#fund_master::OnInvest_start";
		break;
	case 2:
		set $2011_agit_invest,2;
		donpcevent "#fund_master::OnInvest_stop";
		break;
	case 3:
		donpcevent "#fund_master::OnResult";
		break;
	}
	end;
}
*/

// Realms - Merchant Guilds, Gate Managers, Status Boards
//============================================================
-	script	Cat Paw Merchant Guild	-1,{

	if (compare(strnpcinfo(4),"alde")) {
		set .@npc$,"alde";
		set .@name$,"Megrez";
		set .@realm$,"Luina";
		set .@intro$,"ผู้จัดการสาขา Luina ใน Al De Baran";
	} else if (compare(strnpcinfo(4),"gef")) {
		set .@npc$,"gef";
		set .@name$,"Meraq";
		set .@realm$,"Britoniah";
		set .@intro$,"ผู้จัดการสาขา Britoniah";
	} else if (compare(strnpcinfo(4),"pay")) {
		set .@npc$,"pay";
		set .@name$,"Phecda";
		set .@realm$,"Greenwood Lake";
		set .@intro$,"ผู้จัดการสาขา Payon Greenwood Lake";
	} else if (compare(strnpcinfo(4),"prt")) {
		set .@npc$,"prt";
		set .@name$,"Dubae";
		set .@realm$,"Valkyrie Realm";
		set .@intro$,"ผู้จัดการสาขา Prontera Valkyrie Realm";
	}
	set .@n$,"["+.@name$+"]";

	mes .@n$;
	mes "ข้าคือ "+.@name$+" เป็น"+.@intro$+" ของสมาคมพ่อค้า Cat Paw Merchant Guild";
	mes "ข้าให้คำปรึกษาเกี่ยวกับการลงทุนเพื่อพัฒนา Abyss Gate";
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select("Abyss Gate คืออะไร?:เกี่ยวกับการลงทุนพัฒนาประตู?:ข้าต้องการลงทุนในพื้นที่นี้:ยกเลิก")) {
	case 1:
		mes .@n$;
		mes "พวกเราเรียกมันว่า Abyss Gate เพื่อความสะดวก แต่จริงๆ แล้วมันคือ ^4d4dffรอยแยกสู่มิตอื่น^000000";
		next;
		mes .@n$;
		mes "รอยแยกเหล่านี้ถูกพบมากขึ้นตามที่ต่างๆ หลังจากที่ Morocc ได้สร้าง Crack of Dimension ขึ้นมา";
		mes "ข้าส่วนตัวคิดว่า Abyss Gate ที่พบเมื่อเร็วๆ นี้เป็นส่วนหนึ่งของอดีตหรืออาจจะมาจากอนาคตเลยก็ได้";
		next;
		mes .@n$;
		mes "ว่ากันว่า Abyss Gate เชื่อมต่อกับมิติอื่นที่อยู่นอกเหนือโลกของเราอย่างรุนแรง";
		mes "อาจเป็นเพราะมีสิ่งที่คอยติดตามพลังของ Emperium อยู่";
		next;
		mes .@n$;
		mes "จนถึงตอนนี้พบ Abyss Gate ทั้งหมด 4 แห่ง ทั้งหมดเชื่อมต่อกับภูมิภาค Agit ที่เต็มไปด้วยพลังของ Emperium";
		next;
		mes .@n$;
		mes "เราตั้งชื่อแต่ละแห่งตามมิติที่อยู่เหนือรอยแยกนั้น";
		mes "^4d4dffWay of the Warrior, Hill of the Dead, Winds of the Ancient, และ Hero's Tears^000000";
		mes "และรอยแยกทั้งสี่นี้ถูกเรียกว่า ^4d4dffAbyss Gate^000000";
		next;
		mes .@n$;
		mes "รอยแยกอาจนำไปสู่อดีต อนาคต หรือแม้แต่โลกที่ไม่รู้จัก";
		mes "พลังที่ Abyss Gate ถือครองนั้นทั้งน่ากลัวแต่ก็หอมหวาน";
		next;
		mes .@n$;
		mes "เจ้าสนใจที่จะสำรวจ Abyss Gate ด้วยงั้นรึ?";
		mes "ฮ่าๆ เจ้าต้องผ่านหลายขั้นตอนก่อนที่จะเริ่มสำรวจได้นะ";
		next;
		mes .@n$;
		mes "หากเจ้าสนใจ";
		mes "พอจะมีเวลามานั่งคุยเรื่องการลงทุนเพื่อคงสภาพของ Abyss Gate ไหมล่ะ?";
		close;
	case 2:
		mes .@n$;
		mes "เจ้าสงสัยเกี่ยวกับการลงทุนพัฒนาประตูงั้นรึ?";
		mes "เจ้าอยากรู้เรื่องอะไรล่ะ?";
		next;
		switch(select("เกี่ยวกับตัวประตู?:เกี่ยวกับการลงทุน?:ลงทุนอย่างไร?:สิทธิประโยชน์จากการลงทุน?")) {
		case 1:
			mes .@n$;
			mes "ประตูก็คือชื่อของมันนั่นแหละ มันคือบานประตู";
			mes "เป็นทางผ่านมิติที่เชื่อมต่อจากที่นี่ไปยังที่นั่น";
			next;
			mes .@n$;
			mes "Abyss Gate ถูกสร้างขึ้นในฐานะเส้นทางที่ไม่เสถียรอย่างมากจากนอกรอยแยกมิติ";
			mes "ดังนั้นการเดินทางผ่านประตูจึงจำเป็นต้องมีการเชื่อมต่อมิติแบบประดิษฐ์";
			next;
			mes .@n$;
			mes "มนุษย์ยังห่างไกลจากเทคโนโลยีขั้นสูงที่จำเป็นต้องใช้";
			mes "แต่สมาคมพ่อค้า Cat Paw ของเรามีเทคโนโลยีขั้นสูงในการใช้ Crack of Dimension";
			next;
			mes .@n$;
			mes "เราจึงได้ยื่นข้อเสนอ";
			mes "ด้วยสัญญาว่าจะสนับสนุนสมาคมพ่อค้า Cat Paw และจ่ายค่าใช้จ่ายมหาศาลเพื่อคงการเชื่อมต่อกับประตูไว้";
			mes "เราจะสร้างการเชื่อมต่อกับ Crack of Dimension ให้";
			next;
			mes .@n$;
			mes "แน่นอนว่าการทำงานกับ Crack of Dimension ไม่ใช่เรื่องง่ายและมีความเสี่ยง";
			mes "มีกรณีที่มีผู้คนสูญหายไประหว่างการเดินทางข้ามมิติอยู่บ้าง";
			next;
			mes .@n$;
			mes "ระยะเวลาการคงสภาพที่ดีที่สุดคือประมาณ 4 วัน";
			mes "เจ้าลงทุนในพวกเรา และเราจะสร้างประตูที่เชื่อมต่อกับรอยแยกมิติให้";
			next;
			mes .@n$;
			mes "ฟังดูเป็นข้อเสนอแบบ Win-Win ที่สมบูรณ์แบบเลยใช่ไหมล่ะ?";
			close;
		case 2:
			mes .@n$;
			mes "การฝ่ารอยแยกมิติที่ไม่เสถียรและสร้างทางผ่านนั้นอันตรายมาก";
			mes "ไม่ต้องพูดถึงทรัพยากรมหาศาลที่ต้องใช้เลย";
			next;
			mes .@n$;
			mes "หากเจ้าช่วยดูแลเรื่องค่าใช้จ่ายและค่าเสี่ยงภัยโดยการลงทุนกับสมาคมพ่อค้า Cat Paw";
			mes "เราจะสร้างและคงสภาพประตูไว้ให้โดยไม่คำนึงถึงความเสี่ยง";
			next;
			mes .@n$;
			mes "แต่แน่นอนว่าเงินที่ต้องใช้สำหรับค่าประตูนั้นสูงมาก";
			mes "มันจะเป็นเรื่องง่ายขึ้นถ้าเจ้าสามารถหาผู้ร่วมลงทุนมาช่วยแชร์ยอดเงินได้";
			next;
			mes .@n$;
			mes "มีค่าธรรมเนียมพื้นฐานในการเริ่มเชื่อมต่อประตู";
			mes "ผลลัพธ์ขึ้นอยู่กับจำนวนเงินลงทุนที่รวบรวมได้";
			next;
			mes .@n$;
			mes "Abyss Gate มีอยู่ในแต่ละภูมิภาค Agit";
			mes "เจ้าจินตนาการออกไหมว่าสมาคมของเราต้องใช้เงินและความเสียสละเท่าไหร่ในการพัฒนาทุกพื้นที่?";
			next;
			mes .@n$;
			mes "นี่คือเหตุผลที่มีการแข่งขันการลงทุนระหว่างภูมิภาค";
			mes "หากรวบรวมค่าบำรุงรักษาประตูได้ไม่เพียงพอ ภูมิภาคที่มีการลงทุนมากที่สุดย่อมได้รับผลประโยชน์ไป";
			next;
			mes .@n$;
			mes "หาก ^4d4dffAbyss Gate ถูกเชื่อมต่อสำเร็จ ดันเจี้ยนของพื้นที่ฐานทัพ";
			mes "จะเปิดให้ผู้อื่นเข้าได้แทนที่จะจำกัดเฉพาะกิลด์เจ้าของปราสาท^000000 ซึ่งเป็นสิ่งที่น่าดึงดูดใจมากสำหรับการลงทุน";
			next;
			mes .@n$;
			mes "แต่ก็นะ นั่นคือเฉพาะตอนที่ประตูถูกเชื่อมต่อแล้วเท่านั้น";
			close;
		case 3:
			mes .@n$;
			mes "มันง่ายมาก";
			mes "แค่ไปพบผู้จัดการสาขาของ ^4d4dffภูมิภาคที่เจ้าต้องการลงทุนในช่วงเวลาการลงทุน^000000 แล้วทำการลงทุนได้เลย";
			next;
			mes .@n$;
			mes "ปกติเราจะรับ ^4d4dffสกุลเงินทั่วไปที่นี่ นั่นคือ Zeny^000000";
			mes "แต่เนื่องจากเราพูดถึงการสนับสนุนสมาคมพ่อค้า Cat Paw เจ้าสามารถลงทุนด้วยไอเทมที่เป็นที่ต้องการใน Malangdo ได้ด้วย";
			next;
			mes .@n$;
			mes "ให้นำไอเทมที่จำเป็นไปมอบให้ Namis ใน Malangdo ผู้รับผิดชอบงานทั่วไปของสมาคมเรา";
			mes "แล้วนำใบรับรองการลงทุนกลับมาเพื่อเพิ่มในยอดการลงทุน";
			next;
			mes .@n$;
			mes "เจ้าต้องไปลงทุน ณ ภูมิภาคที่เจ้าต้องการ เพื่อให้ยอดการลงทุนของเจ้าถูกนับในพื้นที่นั้น";
			close;
		case 4:
			mes .@n$;
			mes "โดยพื้นฐานแล้ว Abyss Gate คือการทำสัญญาและเชื่อมต่อกับปราสาทที่เป็นเจ้าของ Agit";
			mes "ดังนั้น เราจะมอบทุกสิ่งที่เรามีให้ทันทีที่ค่าใช้จ่ายในการเชื่อมต่อและคงสภาพประตูนั้นเพียงพอ";
			next;
			mes .@n$;
			mes "ในส่วนของค่าใช้จ่ายขั้นต่ำและขั้นสูงสุดสำหรับงานทั้งหมด";
			mes "หากเงินลงทุนที่รวบรวมได้ ^4d4dffเกินค่าใช้จ่ายขั้นต่ำ^000000 เราสามารถเชื่อมต่อเจ้าไปยัง ^4d4dffชั้นลึกที่สุดของกิลด์ดันเจี้ยนที่ไม่รู้จัก^000000 ได้";
			next;
			mes .@n$;
			mes "หากเงินลงทุนเกินค่าใช้จ่ายขั้นสูงสุด ภูมิภาคนั้นจะได้รับการเชื่อมต่อกับ";
			mes "^4d4dffAbyss Gate^000000 โดยไม่ต้องคำนึงถึงอันดับการลงทุน ซึ่งรวมถึงชั้นลึกที่สุดของกิลด์ดันเจี้ยนด้วย";
			next;
			mes .@n$;
			mes "^ff0000หากไม่มีภูมิภาค Agit ใดที่ลงทุนถึงขั้นสูงสุด ภูมิภาคที่มี";
			mes "ส่วนร่วมในการลงทุนสูงสุดจะได้รับการเชื่อมต่อ Abyss Gate โดยใช้เงินลงทุนที่รวบรวมจากทุกภูมิภาค^000000";
			next;
			mes .@n$;
			mes "เมื่อ Abyss Gate ถูกเชื่อมต่อแล้ว ชั้นลึกที่สุด (2F) ของกิลด์ดันเจี้ยน";
			mes "จะเปิดให้นักผจญภัยทุกคนที่เข้าร่วมการลงทุนในภูมิภาคนั้นเข้าได้";
			next;
			mes .@n$;
			mes "การเข้าใช้ดันเจี้ยนจะถูกนับรวมสำหรับการลงทุนครั้งต่อไปของภูมิภาค Agit";
			mes "ดังนั้นการมีความสัมพันธ์ที่ดีกับกิลด์ที่ครอบครองปราสาท Agit จะเป็นผลประโยชน์เสริม";
			next;
			mes .@n$;
			mes "อย่างไรก็ตาม การคงสภาพการเชื่อมต่อกับ Abyss Gate นั้นไม่ใช่เรื่องง่าย ต้องมีการลงทุนและเชื่อมต่อใหม่ทุกสัปดาห์";
			mes "ดังนั้นโปรดให้การสนับสนุนและลงทุนอย่างต่อเนื่องหากเจ้าต้องการพลังแห่งสิ่งที่ไม่รู้จัก";
			close;
		}
	case 3:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
			mes "- ขณะนี้เจ้าถือไอเทม -";
			mes "- มากเกินไป -";
			mes "- โปรดลองใหม่อีกครั้งหลังจาก -";
			mes "- ลดน้ำหนักตัวลงแล้ว -";
			close;
		}
		if (countitem(6488) >= 4800) {
			mes .@n$;
			mes "เจ้ามี Thank You Ticket มากเกินไปที่จะรับเพิ่มได้";
			mes "ไม่สามารถมอบตั๋วให้ได้มากกว่านี้ โปรดใช้ตั๋วที่มีแล้วค่อยกลับมาใหม่";
			mes "ตั๋วสามารถนำไปแลกเปลี่ยนเป็นไอเทมต่างๆ ได้ที่ Malangdo";
			close;
		}
		if (callfunc("F_Invest_Status") != 1) {
			mes .@n$;
			mes "ตอนนี้เรายังไม่เปิดรับการลงทุน";
			mes "โปรดกลับมาใหม่ในช่วงระยะเวลาการลงทุน";
			close;
		}
		set .@guild, getcharid(2);
		for(set .@i,1; .@i<=5; set .@i,.@i+1) {
			set .@castle_name$[.@i], getcastlename(.@npc$+"g_cas0"+.@i);
			set .@owner_id[.@i], getcastledata(.@npc$+"g_cas0"+.@i,CD_GUILD_ID);
			set .@owner_name$[.@i], getguildname(.@owner_id[.@i]);
			if (.@guild == .@owner_id[.@i]) {
				set .@menu$, .@menu$+.@castle_name$[.@i]+" ["+.@owner_name$[.@i]+"] Guild:";
				set .@castles_owned, .@castles_owned | (1<<.@i);
			} else
				set .@menu$, .@menu$+"^aaaaaaไม่ใช่กิลด์ที่ยึดครอง "+.@castle_name$[.@i]+"^000000:";
		}
		mes .@n$;
		mes "เจ้าต้องการลงทุนในภูมิภาค ^4d4dff"+.@realm$+"^000000 หรือไม่?";
		mes "หากมีปราสาทที่ถูกยึดครองอยู่แล้ว เจ้าจะลงทุนในนามของปราสาทและกิลด์ที่ยึดครองพื้นที่นั้น";
		next;
		if (.@castles_owned) {
			set .@i, select(.@menu$+"ยกเลิก");
			if (.@i == 6) {
				mes .@n$;
				mes "ยกเลิก";
				close;
			}
			if (.@guild != .@owner_id[.@i]) {
				mes .@n$;
				mes "กิลด์ที่ยึดครอง "+.@castle_name$[.@i]+" คือ "+.@owner_name$[.@i];
				mes "ดูเหมือนจะไม่ใช่กิลด์ของเจ้านะ";
				close;
			}
			mes .@n$;
			mes "ลงทุนในนามกิลด์ "+.@owner_name$[.@i]+" ของปราสาท "+.@castle_name$[.@i];
			callfunc "F_Invest_Abyss","$fund_"+.@npc$+"0"+.@i,.@n$;
			end;
		} else {
			mes .@n$;
			callfunc "F_Invest_Abyss","$fund_"+.@npc$+"_extra",.@n$," สำหรับ "+.@realm$;
			end;
		}
	case 4:
		mes .@n$;
		mes "อย่างนั้นรึ?";
		mes "โปรดกลับมาเมื่อไหร่ก็ตามที่เจ้าสนใจจะลงทุนพัฒนา Abyss Gate นะ";
		close;
	}
}

-	script	Gate Manager#realm	-1,{

	if (compare(strnpcinfo(4),"alde")) {
		set .@npc$,"alde";
		set .@name$,"Alkor";
		set .@gate$,"Hero's Tears";
		set .@realm$,"Luina";
	} else if (compare(strnpcinfo(4),"gef")) {
		set .@npc$,"gef";
		set .@name$,"Mizar";
		set .@gate$,"Hill of the Dead";
		set .@realm$,"Britoniah";
	} else if (compare(strnpcinfo(4),"pay")) {
		set .@npc$,"pay";
		set .@name$,"Alkaid";
		set .@gate$,"Winds of the Ancient";
		set .@realm$,"Greenwood Lake";
	} else if (compare(strnpcinfo(4),"prt")) {
		set .@npc$,"prt";
		set .@name$,"Arios";
		set .@gate$,"Way of the Warrior";
		set .@realm$,"Valkyrie Realm";
	}
	set .@n$,"["+.@name$+"]";

	if (callfunc("F_Invest_Status") != 3) {
		mes .@n$;
		mes "ขณะนี้ยังไม่มีประตูเปิดไปยังดันเจี้ยน";
		close;
	}
	set .@status, getd("$"+.@npc$+"_invest_result");
	if (.@status == 3) {
		mes .@n$;
		mes "เราเชื่อมต่อ Abyss Gate ["+.@gate$+"] สำเร็จแล้วด้วยความช่วยเหลือจากการลงทุนของพวกเจ้า";
		mes "Agit ดันเจี้ยนจะเปิดให้ทุกคนเข้าได้โดยเสียค่าธรรมเนียมเพียงเล็กน้อยในภูมิภาค "+.@realm$;
		next;
		mes .@n$;
		mes "โปรดมาใช้บริการบ่อยๆ เพราะค่าธรรมเนียมแรกเข้าจะถูกเก็บไว้เป็นยอดลงทุนของภูมิภาค";
		mes "เจ้าต้องการเข้าสู่ Agit ดันเจี้ยนหรือไม่?";
		next;
		set .@i, select(.@realm$+" Dungeon ชั้น 1 - 10000z:"+.@realm$+" Dungeon ชั้น 2 - 10000z:ยกเลิก");
		if (.@i == 3) {
			mes .@n$;
			mes "ยกเลิก";
			close;
		}
		if (Zeny < 10000) {
			mes .@n$;
			mes "ข้ากำลังเรียกเก็บค่าธรรมเนียมแรกเข้าเพียงเล็กน้อย";
			mes "เงินสมทบของเจ้าจะถูกเก็บไว้สำหรับการลงทุนครั้งต่อไป";
			mes "ค่าธรรมเนียมแรกเข้าคือ 10000z";
			close;
		}
		mes .@n$;
		mes "ข้ากำลังส่งเจ้าไปยัง "+.@realm$+" Agit Dungeon ชั้น "+.@i;
		set Zeny, Zeny-10000;
		setd "$fund_"+.@npc$+"_extra", getd("$fund_"+.@npc$+"_extra") + 1;
		close2;
		if (compare(strnpcinfo(2),"alde"))
			warp "gld_dun02"+((.@i == 2)?"_2":""),32,122;
		else if (compare(strnpcinfo(2),"gef"))
			warp "gld_dun04"+((.@i == 2)?"_2":""),39,258;
		else if (compare(strnpcinfo(2),"pay"))
			warp "gld_dun01"+((.@i == 2)?"_2":""),186,165;
		else if (compare(strnpcinfo(2),"prt"))
			warp "gld_dun03"+((.@i == 2)?"_2":""),164,268;
		end;
	} else if (.@status == 2) {
		mes .@n$;
		mes "ขณะนี้ "+.@realm$+" Agit Dungeon ชั้น 2 พร้อมใช้งานแล้ว แต่เนื่องจากไม่ได้เชื่อมต่อกับ Abyss Gate";
		mes "Agit ดันเจี้ยนนี้จึงเข้าได้เฉพาะสมาชิกกิลด์เจ้าของปราสาทเท่านั้น";
		close;
	} else {
		mes .@n$;
		mes "ภูมิภาค "+.@realm$+" ไม่ได้รับเลือกให้เชื่อมต่อกับประตูในครั้งนี้";
		mes "โปรดรอระยะเวลาการลงทุนครั้งถัดไป";
		close;
	}
	end;
}

-	script	Investment Status Board	-1,{
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 1) {
		if (compare(strnpcinfo(4),"alde")) {
			set .@npc$,"alde";
			set .@realm$,"Luina";
		} else if (compare(strnpcinfo(4),"gef")) {
			set .@npc$,"gef";
			set .@realm$,"Britoniah";
		} else if (compare(strnpcinfo(4),"pay")) {
			set .@npc$,"pay";
			set .@realm$,"Greenwood Lake";
		} else if (compare(strnpcinfo(4),"prt")) {
			set .@npc$,"prt";
			set .@realm$,"Valkyrie Realm";
		}
		set .@notice, $agit_result_notice;
		if (.@notice < 1 || .@notice > 12) {
			mes "ขณะนี้ยังไม่มีข้อมูลสรุปยอดการลงทุน";
			close;
		}
		mes "ประกาศครั้งที่ "+callfunc("F_GetNumSuffix",.@notice)+" (หน่วยละ 10,000z)";
		for(set .@i,1; .@i<=5; set .@i,.@i+1) {
			set .@map$, .@npc$+"g_cas0"+.@i;
			set .@fund[.@i], getd("$@vfund_"+.@npc$+"0"+.@i);
			mes getcastlename(.@map$)+" กิลด์ "+getguildname(getcastledata(.@map$,CD_GUILD_ID))+": "+.@fund[.@i];
		}
		set .@fund[0], getd("$@vfund_"+.@npc$+"_extra");
		mes .@realm$+" (ทั่วไป): "+.@fund[0];
		mes "ยอดรวมการลงทุนทั้งหมดของ "+.@realm$+": "+(.@fund[0]+.@fund[1]+.@fund[2]+.@fund[3]+.@fund[4]+.@fund[5]);
		close;
	} else if (.@invest_status == 2) {
		mes "!- ประกาศ -! ";
		mes "ไม่สามารถดูยอดรวมได้เนื่องจากกำลังคำนวณจำนวนเงินลงทุนทั้งหมด";
		mes "โปรดรอการประกาศผลอย่างเป็นทางการ";
		close;
	} else if (.@invest_status == 3) {
		setarray .@invest_result[0], $prt_invest_result,$pay_invest_result,$gef_invest_result,$alde_invest_result;
		setarray .@invest_region$[0],  "Valkyrie Realm",  "Greenwood Lake",       "Britoniah",            "Luina";
		mes "!- ผลการลงทุนของภูมิภาค Agit ทั้งหมด -!";
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@invest_result[.@i] == 1)
				mes "^aaaaaa["+.@invest_region$[.@i]+"] ต่ำกว่าเกณฑ์ขั้นต่ำ^000000";
			else if (.@invest_result[.@i] == 2) {
				mes "["+.@invest_region$[.@i]+"] บรรลุเกณฑ์ขั้นต่ำแล้ว";
				mes "-> กิลด์ดันเจี้ยนชั้นลึกที่สุด (2F) พร้อมใช้งาน";
			} else if (.@invest_result[.@i] == 3) {
				mes "^4d4dff["+.@invest_region$[.@i]+"] บรรลุเกณฑ์ขั้นสูงสุดแล้ว^000000";
				mes "-> Abyss Gate พร้อมใช้งาน";
			} else
				mes "ไม่มีข้อมูลสำหรับ ["+.@invest_region$[.@i]+"]";
		}
		close;
	} else {
		mes "!- ประกาศ -!";
		mes "จะไม่มีการประกาศข้อมูลการลงทุนในขณะนี้";
		close;
	}
}

// Inside Castles - Gate Managers (ภายในปราสาท - ผู้จัดการประตู)
//============================================================
-	script	Gate Manager#castle	-1,{

	set .@id, atoi(charat(strnpcinfo(2),getstrlen(strnpcinfo(2))-1));
	if (compare(strnpcinfo(2),"alde")) {
		set .@npc$,"alde";
		set .@realm$,"Luina";
	} else if (compare(strnpcinfo(2),"gef")) {
		set .@npc$,"gef";
		set .@realm$,"Britoniah";
	} else if (compare(strnpcinfo(2),"pay")) {
		set .@npc$,"pay";
		set .@realm$,"Greenwood Lake";
	} else if (compare(strnpcinfo(2),"prt")) {
		set .@npc$,"prt";
		set .@realm$,"Valkyrie Realm";
	}

	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (getcharid(2) != .@GID) {
		mes "[Gate Manager]";
		mes "ดูเหมือนเจ้าจะไม่ใช่สมาชิกของกิลด์ที่เป็นเจ้าของปราสาทหลังนี้";
		mes "หากต้องการความช่วยเหลือ โปรดไปพบสมาชิกสมาคมของเราได้ที่ลานกว้างของ "+.@realm$;
		close;
	}
	mes "[Gate Manager]";
	mes "สวัสดี ข้าคือผู้จัดการประตูจากแผนกวาร์ปของสมาคมพ่อค้า Cat Paw";
	mes "วันนี้มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select("ลงทุนเพื่อเชื่อมต่อประตู:เข้าสู่ดันเจี้ยนชั้นลึกที่สุด:เข้าสู่ Abyss Gate:ยกเลิก")) {
	case 1:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
			mes "[Gate Manager]";
			mes "เจ้ามีไอเทมมากเกินไปที่จะรับ Thank You Ticket ซึ่งเป็นของขวัญเล็กๆ น้อยๆ สำหรับการลงทุนของเจ้า";
			mes "โปรดกลับมาใหม่หลังจากทำให้ช่องเก็บของว่างลงแล้ว";
			close;
		}
		if (countitem(6488) >= 4800) {
			mes "[Gate Manager]";
			mes "เจ้ามี Thank You Ticket มากเกินไปที่จะรับเพิ่มได้";
			mes "ไม่สามารถมอบตั๋วให้ได้มากกว่านี้ โปรดใช้ตั๋วที่มีแล้วค่อยกลับมาใหม่";
			mes "ตั๋วสามารถนำไปแลกเปลี่ยนเป็นไอเทมต่างๆ ได้ที่ Malangdo";
			close;
		}
		if (callfunc("F_Invest_Status") != 1) {
			mes "[Gate Manager]";
			mes "ตอนนี้เรายังไม่เปิดรับการลงทุน";
			mes "โปรดกลับมาใหม่ในช่วงระยะเวลาการลงทุน";
			close;
		}
		if (getcharid(2) != .@GID) {
			mes "[Gate Manager]";
			mes "ดูเหมือนเจ้าจะไม่ใช่สมาชิกของกิลด์ที่เป็นเจ้าของปราสาทหลังนี้";
			mes "โปรดไปลงทุนแบบปกติได้ที่ลานกว้างของ "+.@realm$;
			close;
		}
		mes "[Gate Manager]";
		mes "ดำเนินการลงทุนภายใต้ชื่อกิลด์ของเจ้า";
		callfunc "F_Invest_Abyss","$fund_"+.@npc$+"0"+.@id,"[Gate Manager]";
		end;
	case 2:
		callsub L_Enter,1,.@realm$,.@npc$;
		mes "[Gate Manager]";
		mes "เจ้าสนใจที่จะสำรวจชั้นลึกที่สุด (2F) ของ "+.@realm$+" กิลด์ดันเจี้ยนสินะ";
		mes "ให้ข้าส่งเจ้าไปเถอะ ขอให้โชคดี";
		close2;
		if (compare(strnpcinfo(2),"alde")) {
			switch(.@id) {
				case 1: warp "gld_dun02_2",32,122; end;
				case 2: warp "gld_dun02_2",79,30; end;
				case 3: warp "gld_dun02_2",165,38; end;
				case 4: warp "gld_dun02_2",160,148; end;
				case 5: warp "gld_dun02_2",103,169; end;
			}
		} else if (compare(strnpcinfo(2),"gef")) {
			switch(.@id) {
				case 1: warp "gld_dun04_2",39,258; end;
				case 2: warp "gld_dun04_2",125,270; end;
				case 3: warp "gld_dun04_2",268,251; end;
				case 4: warp "gld_dun04_2",268,108; end;
				case 5: warp "gld_dun04_2",230,35; end;
			}
		} else if (compare(strnpcinfo(2),"pay")) {
			switch(.@id) {
				case 1: warp "gld_dun01_2",186,165; end;
				case 2: warp "gld_dun01_2",54,165; end;
				case 3: warp "gld_dun01_2",54,39; end;
				case 4: warp "gld_dun01_2",186,39; end;
				case 5: warp "gld_dun01_2",223,202; end;
			}
		} else if (compare(strnpcinfo(2),"prt")) {
			switch(.@id) {
				case 1: warp "gld_dun03_2",28,251; end;
				case 2: warp "gld_dun03_2",164,268; end;
				case 3: warp "gld_dun03_2",164,179; end;
				case 4: warp "gld_dun03_2",268,203; end;
				case 5: warp "gld_dun03_2",199,28; end;
			}
		}
		end;
	case 3:
		callsub L_Enter,2,.@realm$,.@npc$;
		mes "[Gate Manager]";
		mes "ข้าจะนำทางเจ้าไปยัง Abyss Gap ที่เชื่อมต่อกับ "+.@realm$+", Abyss Gate 'Way of the Warrior'";
		close2;
		if (compare(strnpcinfo(2),"alde")) {
			switch(.@id) {
				case 1: warp "gld2_ald",175,41; end;
				case 2: warp "gld2_ald",77,64; end;
				case 3: warp "gld2_ald",46,127; end;
				case 4: warp "gld2_ald",104,246; end;
				case 5: warp "gld2_ald",241,156; end;
			}
		} else if (compare(strnpcinfo(2),"gef")) {
			switch(.@id) {
				case 1: warp "gld2_gef",28,199; end;
				case 2: warp "gld2_gef",217,46; end;
				case 3: warp "gld2_gef",171,195; end;
				case 4: warp "gld2_gef",30,67; end;
				case 5: warp "gld2_gef",115,40; end;
			}
		} else if (compare(strnpcinfo(2),"pay")) {
			switch(.@id) {
				case 1: warp "gld2_pay",33,112; end;
				case 2: warp "gld2_pay",119,27; end;
				case 3: warp "gld2_pay",205,111; end;
				case 4: warp "gld2_pay",175,221; end;
				case 5: warp "gld2_pay",61,221; end;
			}
		} else if (compare(strnpcinfo(2),"prt")) {
			switch(.@id) {
				case 1: warp "gld2_prt",10,27; end;
				case 2: warp "gld2_prt",14,247; end;
				case 3: warp "gld2_prt",259,236; end;
				case 4: warp "gld2_prt",226,25; end;
				case 5: warp "gld2_prt",97,11; end;
			}
		}
		end;
	case 4:
		mes "[Gate Manager]";
		mes "ยกเลิก";
		close;
	}
	end;

L_Enter:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 3) {
		set .@status, getd("$"+getarg(2)+"_invest_result");
		if ((getarg(0) == 1 && .@status == 2) || .@status == 3)
			return;
		else {
			mes "[Gate Manager]";
			mes "ภูมิภาค "+getarg(1)+" ไม่ได้รับเลือกให้เชื่อมต่อกับประตูในครั้งนี้";
			mes "โปรดรอระยะเวลาการลงทุนครั้งถัดไป";
			close;
		}
	} else if (.@invest_status < 3) {
		mes "[Gate Manager]";
		mes "ขณะนี้ยังไม่มีประตูเปิดไปยังดันเจี้ยน";
		mes "ประตูจะถูกเชื่อมต่อทันทีที่ทราบผลสรุปการลงทุน";
		mes "โปรดรอสักครู่";
		close;
	} else {
		mes "[Gate Manager]";
		mes "ประตูกำลังจะหายไปในไม่ช้า";
		mes "ไม่สามารถเข้าสู่ดันเจี้ยนได้อีกต่อไป";
		mes "โปรดลองใหม่อีกครั้งในคราวหน้า";
		close;
	}
}

// Item Investments :: tooja_namis
//============================================================
malangdo,218,126,4	script	Cat Paw Merchants Notice	857,{
	mes "<< ประกาศจากกลุ่มพ่อค้า Cat Paw >>";
	mes "'^C379CEลงทุนด้วยไอเทมที่ไม่ได้ใช้เพื่อรับรางวัล!^000000'";
	next;
	switch(select("อ่านประกาศ:ตรวจสอบกิจกรรมที่ 1:ตรวจสอบกิจกรรมที่ 2")) {
	case 1:
		mes "- ประกาศ -";
		mes "สวัสดีลูกค้าทุกท่านของกลุ่มพ่อค้า Cat Paw! ด้วยความรักจากพวกท่าน ทำให้กลุ่มพ่อค้า Cat Paw ของเราเติบโตขึ้นในทุกๆ วัน ผู้บริหารและพนักงานทุกคนขอสัญญาว่าจะทำหน้าที่ให้ดีที่สุดเพื่อขยายขอบเขตและการพัฒนาของเราสืบต่อไป";
		next;
		mes "- ประกาศกิจกรรม! -";
		mes " กิจกรรมชื่อว่า '^C379CEลงทุนด้วยไอเทมที่ไม่ได้ใช้เพื่อรับรางวัล!^000000' กำลังดำเนินการอยู่ ซึ่งท่านสามารถนำไอเทมที่ไม่ได้ใช้ในคลังเก็บของมาแลกเปลี่ยนเป็น Investment Certificate ของกลุ่มพ่อค้า Cat Paw ได้";
		next;
		mes "รายการไอเทมที่สามารถแลกเปลี่ยนได้จะถูกประกาศในแผ่นแยกต่างหาก ขอบคุณสำหรับเวลาของท่าน";
		mes "- จากพนักงานทุกคนของกลุ่มพ่อค้า Cat Paw -";
		next;
		mes "มีคำเตือนเล็กๆ อยู่ที่ด้านล่าง";
		mes " ";
		mes "^FF0000กิจกรรมนี้อาจมีการเปลี่ยนแปลงหรือยกเลิกได้ตามสถานการณ์ของกลุ่มพ่อค้า^000000";
		close;
	case 2:
		mes "'^C379CEลงทุนด้วยไอเทมที่ไม่ได้ใช้เพื่อรับรางวัล!^000000'";
		mes "นี่คือรายละเอียดของกิจกรรมที่ 1";
		next;
		setarray .@event1$[0],
			"Wings of the Insect Queen","Crown of the Fly King","I hear Dragon Scales are tough 01","The Meaning of the Mother's Nightmare",
			"To refine ore","Hekekek Orcs, Orcs!!","Study of Yin-Yang","Secrets of the Pyramid King","Tick-tock the time goes",
			"Planting trees on Mellow Island","Ancient language research","Weapons made of sharp teeth","Toy boss doll 01",
			"A tiger is just a cat after all","The shattered spear and shield?","Toy boss doll 02","Best in summer! Cold scales",
			"Piece of Thanatos' armor","I hear Dragon Scales are tough 02","The identity of the creepy eye","If only I could see!";
		set .@menu$, "ออก:"+implode(.@event1$[0],":");
		while(1) {
			set .@i, select(.@menu$)-1;
			switch(.@i) {
				case 0: close;
				case 1: set .@str$,"Queen's Wing 2 ชิ้น"; break;
				case 2: set .@str$,"Broken Crown 2 ชิ้น"; break;
				case 3: set .@str$,"Fire Dragon Scale 2 ชิ้น"; break;
				case 4: set .@str$,"Mother's Nightmare 3 ชิ้น"; break;
				case 5: set .@str$,"Gemstone 5 ชิ้น"; break;
				case 6: set .@str$,"Heroic Emblem 5 ชิ้น"; break;
				case 7: set .@str$,"Taeguk Plate 3 ชิ้น"; break;
				case 8: set .@str$,"Broken Pharaoh Symbol 3 ชิ้น และ Tutankhamen's Mask 3 ชิ้น"; break;
				case 9: set .@str$,"Pocket Watch 2 ชิ้น"; break;
				case 10: set .@str$,"Young Twig 3 ชิ้น"; break;
				case 11: set .@str$,"Rossata Piece 5 ชิ้น"; break;
				case 12: set .@str$,"Fang of Garm 5 ชิ้น"; break;
				case 13: set .@str$,"Baphomet Doll 3 ชิ้น"; break;
				case 14: set .@str$,"Tiger's Footskin 1 ชิ้น และ Tiger Skin 5 ชิ้น"; break;
				case 15: set .@str$,"Broken Shield Piece 3 ชิ้น และ Shining Spear Blade 3 ชิ้น"; break;
				case 16: set .@str$,"Osiris Doll 3 ชิ้น"; break;
				case 17: set .@str$,"Ice Scale 1 ชิ้น"; break;
				case 18: set .@str$,"Skeletal Armor Piece 1 ชิ้น"; break;
				case 19: set .@str$,"Darkred Scale Piece 1 ชิ้น"; break;
				case 20: set .@str$,"Will of Red Darkness 2 ชิ้น"; break;
				case 21: set .@str$,"Foolishness of the Blind 2 ชิ้น"; break;
			}
			mes "["+.@event1$[.@i-1]+"]";
			mes .@str$+" เพื่อแลกกับ Cat Paw Merchants Investment Certificate 1 ใบ";
			next;
		}
	case 3:
		mes "โปรดรอพบกับกิจกรรม '^C379CEลงทุนด้วยไอเทมที่ไม่ได้ใช้เพื่อรับรางวัล!^000000' กิจกรรมที่ 2!! เร็วๆ นี้!";
		close;
	}
}

malangdo,218,123,1	script	Namis#invest	545,4,4,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- ขณะนี้เจ้าถือไอเทม -";
		mes "- มากเกินไป -";
		mes "- โปรดลองใหม่อีกครั้งหลังจาก -";
		mes "- ลดน้ำหนักตัวลงแล้ว -";
		close;
	}
	emotion ET_HUK;
	emotion ET_FRET;
	emotion ET_ANGER;
	mes "[Namis]";
	mes "เพอร์-เพอร์-เพอร์รร-เพอร์รรร-เพอร์~ เมี้ยว!!";
	mes "อะ-- อะไรกัน? เสียมารยาทจริง เจ้าทำข้าตกใจนะ";
	next;
	switch(select("เจ้าต้องการอะไร? ข้าจะลงทุน!:เป้าหมายของกิจกรรมนี้คืออะไร?:เจ้าแอบหลับอยู่รึเปล่า?")) {
	case 1:
		break;
	case 2:
		mes "[Namis]";
		mes "งั้นรึ เหยื่อรายใหม่อีกคนที่มาทำสัญญากับกลุ่มพ่อค้า Cat Paw สินะ?";
		next;
		select("หือ? อะไรนะ! เจ้าหาว่าข้าเป็นเหยื่ออย่างนั้นรึ?");
		mes "[Namis]";
		mes "ข้าจะพูดแค่ครั้งเดียว เพราะฉะนั้นฟังให้ดี!";
		mes "พวกเรากลุ่มพ่อค้า Cat Paw ได้วางแผนกิจกรรมสำหรับคนงานที่ซื่อสัตย์อย่างเจ้าเอาไว้";
		next;
		mes "[Namis]";
		mes "จงวิ่งไปที่คลังของเจ้า แล้วนำพวกของเก่าคร่ำครึที่เน่าเสียมาให้เรา แล้วเราจะแลกมันด้วย 'Cat Paw Merchants Investment Certificate' ที่มีมูลค่ามหาศาลให้";
		next;
		mes "[Namis]";
		mes "มันจะเป็นการฉลาดมากถ้าเจ้าทำเช่นนั้น เพราะกลุ่มพ่อค้า Cat Paw กำลังทำกิจกรรมนี้แบบยอมขาดทุนเลยนะ!";
		next;
		mes "[Namis]";
		mes "รายละเอียดอยู่บนป้ายประกาศนั่น";
		close;
	case 3:
		mes "[Namis]";
		mes "ฮึก- ซู๊ด-";
		mes "จำไว้เจ้ามนุษย์! ไม่มีแมวตัวไหนทำงานหนักเท่าข้าอีกแล้ว";
		close;
	}
	mes "[Namis]";
	mes "เจ้าคงมาที่นี่เพื่อเข้าร่วมกิจกรรม '^C379CEลงทุนด้วยไอเทมที่ไม่ได้ใช้เพื่อรับรางวัล!^000000' ของกลุ่มพ่อค้า Cat Paw สินะ";
	next;
	mes "[Namis]";
	mes "แล้วเจ้าอยากเข้าร่วมในส่วนของกิจกรรมไหนล่ะ?";
	next;
	switch(select("ไม่สนใจ:ไอเทมกิจกรรมที่ 1:ไอเทมกิจกรรมที่ 2")) {
	case 1:
		mes "[Namis]";
		mes "อย่ามาแหย่แมวที่กำลังยุ่งอยู่นะ!";
		close;
	case 2:
		mes "[Namis]";
		mes "งั้นเจ้าจะทำกิจกรรมที่ 1";
		mes "เลือกจากรายการที่แสดงอยู่นี่เลย";
		next;
		setarray .@event1$[0],
			"Wings of the Insect Queen","Crown of the Fly King","I hear Dragon Scales are tough 01","The Meaning of the Mother's Nightmare",
			"To refine ore","Hekekek Orcs, Orcs!!","Study of Yin-Yang","Secrets of the Pyramid King","Tick-tock the time goes",
			"Planting trees on Mellow Island","Ancient language research","Weapons made of sharp teeth","Toy boss doll 01",
			"A tiger is just a cat after all","The shattered spear and shield?","Toy boss doll 02","Best in summer! Cold scales",
			"Piece of Thanatos' armor","I hear Dragon Scales are tough 02","The identity of the creepy eye","If only I could see!";
		set .@i, select(implode(.@event1$[0],":"))-1;
		switch(.@i+1) {
			case 1: callsub L_Check,"Queen's Wing 2 ชิ้น",6326,2,"Wings of the Insect Queen? ชื่อน่าสมเพชจัง ถ้าเป็นข้านะ จะตั้งชื่อให้ดูเท่กว่านี้อีก!",.@event1$[.@i];
			case 2: callsub L_Check,"Broken Crown 2 ชิ้น",7754,2,"ให้ตายสิ! พวกแมวที่คิดกิจกรรมนี้ต้องเล่นสนุกกันอยู่แน่ๆ คิดชื่อที่ดีกว่านี้ไม่ได้รึไง?",.@event1$[.@i];
			case 3: callsub L_Check,"Fire Dragon Scale 2 ชิ้น",7451,2,"เกล็ดมังกร? อืม... ข้าว่ามันก็แข็งแรงดีนะ แต่พวกเขาจะเอาไปใช้ทำอะไรกันล่ะ?",.@event1$[.@i];
			case 4: callsub L_Check,"Mother's Nightmare 3 ชิ้น",7020,3,"ถึงข้าจะไม่มีสิทธิ์เข้าไปยุ่งก็เถอะ แต่ข้าสงสัยจังว่าพวกเขาเอาไอเทมที่เก็บไปทำอะไรกัน?",.@event1$[.@i];
			case 5: callsub L_Check,"Gemstone 5 ชิ้น",7300,5,"ค่อยมีประโยชน์ขึ้นมาหน่อย แร่นี้คงจะกลายเป็นเครื่องประดับที่สวยงามได้",.@event1$[.@i];
			case 6: callsub L_Check,"Heroic Emblem 5 ชิ้น",968,5,"ดี ดีมาก นี่แหละชื่อแบบที่ข้าต้องการ!",.@event1$[.@i];
			case 7: callsub L_Check,"Taeguk Plate 3 ชิ้น",7169,3,"รับของแบบนี้เข้ามา บางทีพวกเขาอาจจะกำลังวางแผนทำอะไรที่อันตรายอยู่ก็ได้นะ",.@event1$[.@i];
			case 8: callsub L_Check,"Broken Pharaoh Symbol 3 ชิ้น และ Tutankhamen's Mask 3 ชิ้น",7113,3,"ข้าได้ยินว่าพีระมิดอยู่ที่ Morocc แต่ข้าไม่เคยไปเลย มนุษย์บางคนเรียกมันว่า Morco ล่ะ",.@event1$[.@i],7114,3;
			case 9: callsub L_Check,"Pocket Watch 2 ชิ้น",7513,2,"พวกพ่อค้าขี้เกียจพวกนั้น จะเอาพวกนาฬิกาพกที่ตัวเองไม่ใช้ไปทำไมกันนะ?",.@event1$[.@i];
			case 10: callsub L_Check,"Young Twig 3 ชิ้น",7018,3,"เกาะ Mellow มีสภาพอากาศเหมือนกับที่ที่พวกเราชาวแมวเคยอยู่ แต่บางครั้งต้นไม้ก็ตายเพราะมันเป็นเกาะน่ะ",.@event1$[.@i];
			case 11: callsub L_Check,"Rossata Piece 5 ชิ้น",7211,5,"ข้าเดาว่าพวกเขารวบรวมของพวกนี้เพราะพวกมนุษย์มีมันอยู่ แต่การวิจัยภาษาโบราณมันมีประโยชน์อะไรกัน?",.@event1$[.@i];
			case 12: callsub L_Check,"Fang of Garm 5 ชิ้น",7036,5,"มนุษย์ที่อ่อนแอต้องการอาวุธที่คมกริบ แต่พวกแมวอย่างเรามีกรงเล็บที่สวยงามและแหลมคมอยู่แล้ว แล้วจะรวบรวมของพวกนี้ไปทำไมกัน!!",.@event1$[.@i];
			case 13: callsub L_Check,"Baphomet Doll 3 ชิ้น",750,3,"ตุ๊กตา Baphomet นี่เหมาะเอาไว้เล่นแก้เบื่อดีนะ",.@event1$[.@i];
			case 14: callsub L_Check,"Tiger's Footskin 1 ชิ้น และ Tiger Skin 5 ชิ้น",1030,1,"เจ้ารู้ไหมว่าเสือก็คือแมวเหมือนกัน? เพราะฉะนั้น แมวเก่งกว่าเสืออยู่แล้ว",.@event1$[.@i],1029,5;
			case 15: callsub L_Check,"Broken Shield Piece 3 ชิ้น และ Shining Spear Blade 3 ชิ้น",7108,3,"ข้าไม่รู้เลยว่าพวกเขาต้องการของพวกนี้ไปทำไม",.@event1$[.@i],7109,3;
			case 16: callsub L_Check,"Osiris Doll 3 ชิ้น",751,3,"น่าเสียดายจัง~ ข้าไม่เคยได้เล่นตุ๊กตา Osiris เลย",.@event1$[.@i];
			case 17: callsub L_Check,"Ice Scale 1 ชิ้น",7562,1,"เกาะ Mellow เป็นสถานที่ที่วิเศษมากสำหรับการอยู่อาศัย แต่บนเรือน่ะร้อนเกินไป",.@event1$[.@i];
			case 18: callsub L_Check,"Skeletal Armor Piece 1 ชิ้น",7450,1,"อึ๋ยยย... ทำไมพวกเขาถึงรวบรวมของที่น่าขนลุกแบบนี้ล่ะ?",.@event1$[.@i];
			case 19: callsub L_Check,"Darkred Scale Piece 1 ชิ้น",6091,1,"เกล็ดมังกร? อืม... ข้าว่ามันก็แข็งแรงดีนะ แต่พวกเขาจะเอาไปใช้ทำอะไรกันล่ะ?",.@event1$[.@i];
			case 20: callsub L_Check,"Will of Red Darkness 2 ชิ้น",7566,2,"น่าสนใจ! มันจะร้องไห้หรือทำอะไรไหมนะถ้าข้าจิ้มไปที่ตาของมัน?",.@event1$[.@i];
			case 21: callsub L_Check,"Foolishness of the Blind 2 ชิ้น",7021,2,"ชายตาบอดคนนี้ช่างโง่เขลานักที่ไม่รู้ว่าการรวมแมว 300 ตัวจะรักษาอาการตาบอดได้...",.@event1$[.@i];
		}
	case 3:
		mes "[Namis]";
		mes "เข้าร่วมกิจกรรมที่ 2 งั้นรึ?";
		mes "มันยังไม่เริ่มเลย รอไปก่อนสิ!";
		close;
	}
	end;
L_Check:
	set .@args, getargcount();
	if (.@args > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete (ไม่ครบ)
		else
			set .@items,2; //complete (ครบ)
	}
	if (countitem(getarg(1)) < getarg(2) || .@items == 1) {
		mes "[Namis]";
		mes "เจ้าไม่มีไอเทมงั้นรึ?";
		mes "ใช้ "+getarg(0)+" เพื่อแลกกับ Cat Paw Merchants Investment Certificate 1 ใบนะ";
		next;
		mes "[Namis]";
		mes "อย่ามากวนข้าเลย ไปอ่านรายละเอียดบนกระดานประกาศตรงนั้นไป!";
		close;
	}
	mes "[Namis]";
	mes "'^8E5601กิจกรรม "+getarg(4)+"^000000'";
	mes "ดูเหมือนจะเป็นไอเทมที่ถูกต้องนะ";
	mes getarg(3);
	mes "โอ้! เกือบลืมไป เจ้าจะลงทุนเลยไหม?";
	next;
	if(select("ลงทุน:ไม่ลงทุน") == 2) {
		mes "[Namis]";
		mes "อย่ามาแหย่แมวที่กำลังยุ่งอยู่นะ!";
		close;
	}
	delitem getarg(1),getarg(2);
	if (.@items == 2)
		delitem getarg(5),getarg(6);
	getitem 6489,1; //Cats_Invest_Certif
	mes "[Namis]";
	mes "นี่คือใบรับรองของเจ้า จงกตัญญูต่อกลุ่มพ่อค้าสำหรับกิจกรรมเหล่านี้ด้วยล่ะ!";
	close;
OnTouch:
	if (rand(2))
		emotion ET_SLEEPY;
	else
		specialeffect EF_SLEEPATTACK;
	end;
}

// Investment Rewards :: tooja_japanki (รางวัลการลงทุน)
//============================================================
malangdo,215,119,4	script	Thanks Ticket Machine	564,{
	if (MaxWeight - Weight < 4500 || checkweight(1201,1) == 0) {
		mes "- ขณะนี้เจ้าถือไอเทม -";
		mes "- มากเกินไป -";
		mes "- โปรดลองใหม่อีกครั้งหลังจาก -";
		mes "- ลดน้ำหนักตัวลงแล้ว -";
		close;
	}
	mes "[ประกาศจากตู้ขายของอัตโนมัติ]";
	mes "^6815EA1. กรุณาอย่าเตะเครื่อง @_@";
	mes "2. คุณสามารถใส่ได้เฉพาะ Invest Thanks Ticket เท่านั้น";
	mes "3. เกี่ยวกับ Jelly Box หากคุณเปิดมัน คุณจะได้รับไอเทมแบบสุ่ม";
	mes "4. ขอให้สนุกนะ~^000000";
	mes "- คุณแมว เจ้าของเครื่อง -";
	next;
	switch(select("เสร็จสิ้น:ซื้อสินค้า:Red Paw Jelly Bag (5 Tickets):Black Paw Jelly Bag (50 Tickets)")) {
	case 1:
		close;
	case 2:
		mes "ตู้ขายของอัตโนมัติกำลังขายไอเทมตามรายการต่อไปนี้";
		next;
		switch(select("เสร็จสิ้น:1 Siege Arrow Quiver A (2 Tickets):1 Siege Arrow Quiver S (2 Tickets):30 White Potion (12 Tickets):30 White Slim Potion (14 Tickets):10 Dark Water (20 Tickets):20 Siege_Violet_Potion (30 Tickets):10 Coldproof Potion (30 Tickets):10 Thunderproof Potion (30 Tickets):10 Earthproof Potion (30 Tickets):10 Fireproof Potion (30 Tickets):10 Elemental Converter[Fire] (30 Tickets):10 Elemental Converter[Water] (30 Tickets):10 Elemental Converter[Wind] (30 Tickets):10 Elemental Converter[Earth] (30 Tickets)")) {
			case 1: close;
			case 2: callsub L_Purchase,2,12678,1; //Siege_Arrow_Quiver_A
			case 3: callsub L_Purchase,2,12677,1; //Siege_Arrow_Quiver_S
			case 4: callsub L_Purchase,12,504,30; //White_Potion
			case 5: callsub L_Purchase,14,547,30; //White_Slim_Potion
			case 6: callsub L_Purchase,20,12020,10; //Water_Of_Darkness
			case 7: callsub L_Purchase,30,11547,20; //Woe_Violet_Potion
			case 8: callsub L_Purchase,30,12119,10; //Resist_Water
			case 9: callsub L_Purchase,30,12121,10; //Resist_Wind
			case 10: callsub L_Purchase,30,12120,10; //Resist_Earth
			case 11: callsub L_Purchase,30,12118,10; //Resist_Fire
			case 12: callsub L_Purchase,30,12114,10,"Elemental Converter[Fire]"; //Elemental_Fire
			case 13: callsub L_Purchase,30,12115,10,"Elemental Converter[Water]"; //Elemental_Water
			case 14: callsub L_Purchase,30,12117,10,"Elemental Converter[Wind]"; //Elemental_Wind
			case 15: callsub L_Purchase,30,12116,10,"Elemental Converter[Earth]"; //Elemental_Earth
		}
		end;
	case 3:
		mes "เลือกปุ่ม Red Paw Jelly Bag จำเป็นต้องใส่ ^FF0000Invest Thanks Ticket 5 ใบ^000000 และไอเทมแบบสุ่มจะปรากฏออกมา";
		next;
		if (countitem(6488) < 5) {
			mes "คุณมี ^FF0000Invest Thanks Ticket^000000 ไม่เพียงพอที่จะดำเนินการต่อ";
			close;
		}
		delitem 6488,5; //Thanks_Invest_Ticket
		set .@i, rand(1,10000);
		     if (.@i <= 100) callsub L_Bag,12531,1; //White_Potion_Box
		else if (.@i <= 200) callsub L_Bag,12549,1; //White_Slim_Pot_Box2
		else if (.@i <= 250) callsub L_Bag,11549,10; //Woe_Blue_Potion
		else if (.@i <= 660) callsub L_Bag,617,1; //Old_Violet_Box
		else if (.@i <= 710) callsub L_Bag,12532,1; //Royal_Jelly_Box2
		else if (.@i <= 760) callsub L_Bag,12533,1; //Blue_Herb_Box2
		else if (.@i <= 810) callsub L_Bag,505,5; //Blue_Potion
		else if (.@i <= 1110) callsub L_Bag,644,1; //Gift_Box
		else if (.@i <= 1120) callsub L_Bag,607,1; //Yggdrasilberry
		else if (.@i <= 1130) callsub L_Bag,608,2; //Seed_Of_Yggdrasil
		else if (.@i <= 1140) callsub L_Bag,12292,5; //Unripe_Fruit
		else if (.@i <= 1150) callsub L_Bag,12293,5; //Dried_Yggdrasilberry
		else if (.@i <= 1250) callsub L_Bag,12679,1; //Sg_White_Potion_Box
		else if (.@i <= 1471) callsub L_Bag,603,1; //Old_Blue_Box
		else if (.@i <= 1571) callsub L_Bag,12550,1,"Poison Bottle Box(30)"; //Poison_Bottle_Box2
		else if (.@i <= 1671) callsub L_Bag,12020,10; //Water_Of_Darkness
		else if (.@i <= 1871) callsub L_Bag,12030,5; //Box_Of_Grudge
		else if (.@i <= 1871) callsub L_Bag,12030,5; //Box_Of_Grudge
		else if (.@i <= 2071) callsub L_Bag,12031,5; //Sleepy_Box
		else if (.@i <= 2371) callsub L_Bag,12033,2; //Box_Of_Sunlight
		else if (.@i <= 2571) callsub L_Bag,12114,5,"Elemental Converter[Fire]"; //Elemental_Fire
		else if (.@i <= 2771) callsub L_Bag,12115,5,"Elemental Converter[Water]"; //Elemental_Water
		else if (.@i <= 2971) callsub L_Bag,12117,5,"Elemental Converter[Wind]"; //Elemental_Wind
		else if (.@i <= 3171) callsub L_Bag,12116,5,"Elemental Converter[Earth]"; //Elemental_Earth
		else if (.@i <= 3271) callsub L_Bag,12680,1; //Sg_Blue_Potion_Box
		else if (.@i <= 3471) callsub L_Bag,12118,5; //Resist_Fire
		else if (.@i <= 3671) callsub L_Bag,12119,5; //Resist_Water
		else if (.@i <= 3871) callsub L_Bag,12121,5; //Resist_Wind
		else if (.@i <= 4071) callsub L_Bag,12120,5; //Resist_Earth
		else if (.@i <= 4271) callsub L_Bag,929,20; //Immortal_Heart
		else if (.@i <= 4471) callsub L_Bag,1059,20; //Transparent_Cloth
		else if (.@i <= 4671) callsub L_Bag,905,20; //Stem
		else if (.@i <= 4871) callsub L_Bag,606,10; //Aloebera
		else if (.@i <= 5071) callsub L_Bag,609,10; //Amulet
		else if (.@i <= 5271) callsub L_Bag,710,2; //Illusion_Flower
		else if (.@i <= 5471) callsub L_Bag,576,20; //Prickly_Fruit
		else if (.@i <= 5671) callsub L_Bag,621,10; //Bitter_Herb
		else if (.@i <= 5871) callsub L_Bag,709,10; //Izidor
		else if (.@i <= 6071) callsub L_Bag,605,10; //Anodyne
		else if (.@i <= 6171) callsub L_Bag,11548,10; //Woe_White_Potion
		else if (.@i <= 6371) callsub L_Bag,706,10; //Four_Leaf_Clover
		else if (.@i <= 6571) callsub L_Bag,521,10; //Leaflet_Of_Aloe
		else if (.@i <= 6771) callsub L_Bag,707,10; //Singing_Plant
		else if (.@i <= 6971) callsub L_Bag,610,10; //Leaf_Of_Yggdrasil
		else if (.@i <= 7021) callsub L_Bag,12676,1; //Sg_Violet_Potion_Box
		else if (.@i <= 7171) callsub L_Bag,6249,10; //Savage_Meat
		else if (.@i <= 7321) callsub L_Bag,6252,10; //Wolf_Blood
		else if (.@i <= 7471) callsub L_Bag,6254,10; //Beef_Head_Meat
		else if (.@i <= 7621) callsub L_Bag,6253,10; //Cold_Ice
		else if (.@i <= 7771) callsub L_Bag,6256,10; //Ice_Fragment
		else if (.@i <= 7921) callsub L_Bag,6257,10; //Ice_Crystal
		else if (.@i <= 8071) callsub L_Bag,6260,10; //Petti_Tail
		else if (.@i <= 8309) callsub L_Bag,7932,20; //Poison_Herb_Nerium
		else if (.@i <= 8547) callsub L_Bag,7933,20; //Poison_Herb_Rantana
		else if (.@i <= 8785) callsub L_Bag,7934,20; //Poison_Herb_Makulata
		else if (.@i <= 9023) callsub L_Bag,7935,20; //Poison_Herb_Seratum
		else if (.@i <= 9261) callsub L_Bag,7936,20; //Poison_Herb_Scopolia
		else if (.@i <= 9499) callsub L_Bag,7937,20; //Poison_Herb_Amoena
		else if (.@i <= 9699) callsub L_Bag,6217,20; //Mandragora_Flowerpot
		else callsub L_Bag,12016,10; //Speed_Up_Potion
		end;
	case 4:
		mes "เลือกปุ่ม Black Paw Jelly Bag จำเป็นต้องใส่ ^FF0000Invest Thanks Ticket 50 ใบ^000000 และไอเทมแบบสุ่มจะปรากฏออกมา";
		next;
		if (countitem(6488) < 50) {
			mes "คุณมี ^FF0000Invest Thanks Ticket^000000 ไม่เพียงพอที่จะดำเนินการต่อ";
			close;
		}
		delitem 6488,50; //Thanks_Invest_Ticket
		set .@i, rand(1,10000);
		     if (.@i <= 100) callsub L_Bag,12531,1; //White_Potion_Box
		else if (.@i <= 200) callsub L_Bag,12549,1; //White_Slim_Pot_Box2
		else if (.@i <= 250) callsub L_Bag,11549,20; //Woe_Blue_Potion
		else if (.@i <= 450) callsub L_Bag,617,1; //Old_Violet_Box
		else if (.@i <= 500) callsub L_Bag,12532,1; //Royal_Jelly_Box2
		else if (.@i <= 550) callsub L_Bag,12533,1; //Blue_Herb_Box2
		else if (.@i <= 600) callsub L_Bag,505,10; //Blue_Potion
		else if (.@i <= 800) callsub L_Bag,644,1; //Gift_Box
		else if (.@i <= 810) callsub L_Bag,607,2; //Yggdrasilberry
		else if (.@i <= 820) callsub L_Bag,608,3; //Seed_Of_Yggdrasil
		else if (.@i <= 830) callsub L_Bag,12292,10; //Unripe_Fruit
		else if (.@i <= 840) callsub L_Bag,12293,10; //Dried_Yggdrasilberry
		else if (.@i <= 940) callsub L_Bag,12679,1; //Sg_White_Potion_Box
		else if (.@i <= 1140) callsub L_Bag,603,1; //Old_Blue_Box
		else if (.@i <= 1240) callsub L_Bag,12550,1,"Poison Bottle Box(30)"; //Poison_Bottle_Box2
		else if (.@i <= 1340) callsub L_Bag,12020,20; //Water_Of_Darkness
		else if (.@i <= 1440) callsub L_Bag,12030,10; //Box_Of_Grudge
		else if (.@i <= 1540) callsub L_Bag,12031,10; //Sleepy_Box
		else if (.@i <= 1690) callsub L_Bag,12033,4; //Box_Of_Sunlight
		else if (.@i <= 1790) callsub L_Bag,12114,10,"Elemental Converter[Fire]"; //Elemental_Fire
		else if (.@i <= 1890) callsub L_Bag,12115,10,"Elemental Converter[Water]"; //Elemental_Water
		else if (.@i <= 1990) callsub L_Bag,12117,10,"Elemental Converter[Wind]"; //Elemental_Wind
		else if (.@i <= 2090) callsub L_Bag,12116,10,"Elemental Converter[Earth]"; //Elemental_Earth
		else if (.@i <= 2190) callsub L_Bag,12680,1; //Sg_Blue_Potion_Box
		else if (.@i <= 2290) callsub L_Bag,12118,10; //Resist_Fire
		else if (.@i <= 2390) callsub L_Bag,12119,10; //Resist_Water
		else if (.@i <= 2490) callsub L_Bag,12121,10; //Resist_Wind
		else if (.@i <= 2590) callsub L_Bag,12120,10; //Resist_Earth
		else if (.@i <= 2690) callsub L_Bag,929,40; //Immortal_Heart
		else if (.@i <= 2790) callsub L_Bag,1059,40; //Transparent_Cloth
		else if (.@i <= 2890) callsub L_Bag,905,40; //Stem
		else if (.@i <= 2990) callsub L_Bag,606,20; //Aloebera
		else if (.@i <= 3090) callsub L_Bag,609,20; //Amulet
		else if (.@i <= 3190) callsub L_Bag,710,4; //Illusion_Flower
		else if (.@i <= 3290) callsub L_Bag,576,40; //Prickly_Fruit
		else if (.@i <= 3390) callsub L_Bag,621,20; //Bitter_Herb
		else if (.@i <= 3490) callsub L_Bag,709,20; //Izidor
		else if (.@i <= 3590) callsub L_Bag,605,20; //Anodyne
		else if (.@i <= 3690) callsub L_Bag,11548,20; //Woe_White_Potion
		else if (.@i <= 3790) callsub L_Bag,706,20; //Four_Leaf_Clover
		else if (.@i <= 3890) callsub L_Bag,521,20; //Leaflet_Of_Aloe
		else if (.@i <= 3990) callsub L_Bag,707,20; //Singing_Plant
		else if (.@i <= 4090) callsub L_Bag,610,20; //Leaf_Of_Yggdrasil
		else if (.@i <= 4140) callsub L_Bag,12676,1; //Sg_Violet_Potion_Box
		else if (.@i <= 4240) callsub L_Bag,6249,20; //Savage_Meat
		else if (.@i <= 4340) callsub L_Bag,6252,20; //Wolf_Blood
		else if (.@i <= 4440) callsub L_Bag,6254,20; //Beef_Head_Meat
		else if (.@i <= 4540) callsub L_Bag,6253,20; //Cold_Ice
		else if (.@i <= 4640) callsub L_Bag,6256,20; //Ice_Fragment
		else if (.@i <= 4740) callsub L_Bag,6257,20; //Ice_Crystal
		else if (.@i <= 4840) callsub L_Bag,6260,20; //Petti_Tail
		else if (.@i <= 4940) callsub L_Bag,7932,40; //Poison_Herb_Nerium
		else if (.@i <= 5040) callsub L_Bag,7933,40; //Poison_Herb_Rantana
		else if (.@i <= 5140) callsub L_Bag,7934,40; //Poison_Herb_Makulata
		else if (.@i <= 5240) callsub L_Bag,7935,40; //Poison_Herb_Seratum
		else if (.@i <= 5340) callsub L_Bag,7936,40; //Poison_Herb_Scopolia
		else if (.@i <= 5440) callsub L_Bag,7937,40; //Poison_Herb_Amoena
		else if (.@i <= 5540) callsub L_Bag,6217,40; //Mandragora_Flowerpot
		else if (.@i <= 6000) callsub L_Bag,12016,20; //Speed_Up_Potion
		else if (.@i <= 6500) callsub L_Bag,2483,1; //Siege_Greave
		else if (.@i <= 7000) callsub L_Bag,2484,1; //Siege_Boots
		else if (.@i <= 7500) callsub L_Bag,2485,1; //Siege_Shoes
		else if (.@i <= 8000) callsub L_Bag,2586,1; //Siege_Manteau
		else if (.@i <= 8500) callsub L_Bag,2587,1; //Siege_Muffler
		else if (.@i <= 9000) callsub L_Bag,15046,1; //Siege_Plate
		else if (.@i <= 9500) callsub L_Bag,15047,1; //Siege_Suits
		else callsub L_Bag,15048,1; //Siege_Robe
		end;
	}
	end;

//callsub L_Purchase,<ticket cost>,<reward ID>,<reward amount>{,<reward name>}; (ฟังก์ชันการซื้อสินค้า)
L_Purchase:
	if (countitem(6488) < getarg(0)) {
		mes "คุณมี ^FF0000Invest Thanks Ticket^000000 ไม่เพียงพอที่จะดำเนินการต่อ";
		close;
	}
	delitem 6488,getarg(0); //Thanks_Invest_Ticket
	getitem getarg(1),getarg(2);
	mes "- เรียบร้อย! -";
	next;
	mes "คุณได้รับไอเทม ^0000FF"+((getargcount() > 3)?getarg(3,""):getitemname(getarg(1)))+"^000000 แล้ว";
	close;

//callsub L_Bag,<reward ID>,<reward amount>{,<reward name>}; (ฟังก์ชันการสุ่มถุงรางวัล)
L_Bag:
	getitem getarg(0),getarg(1);
	mes "- เรียบร้อย! -";
	next;
	mes "คุณได้รับไอเทม ^0000FF"+((getargcount() > 2)?getarg(2,""):getitemname(getarg(0)))+"^000000 แล้ว";
	close;
}
