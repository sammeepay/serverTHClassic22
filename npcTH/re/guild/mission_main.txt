//===== rAthena Script =======================================
//= Guild Mission - Core Script
//===== Description: =========================================
//= Template for guild mission added in episode 14.3
//===== Changelogs: ==========================================
//= [Official Conversion]
//= 1.0 First version [Secretdataz]
//= 1.1 Fixed potential exploit from original Aegis script. [Secretdataz]
//============================================================

-	script	gld_mission#core	-1,{
function brief;
function mob_brief;
function calcexp;
function calcjobexp;

	if (!checkweight(1201,3)){
		mes "เจ้ามีไอเทมมากเกินไปที่จะดำเนินการเควสต่อ";
		mes "กรุณาลดน้ำหนักตัวลงแล้วลองใหม่อีกครั้ง";
		close;
	}

	.@zone$ = strnpcinfo(2);
	if(.@zone$ == "gqaldeg"){
		.@zoneid = 3;
		.@edition = 1;
	} else if(.@zone$ == "gqaru"){
		.@zoneid = 4;
		.@edition = 2;
	} else if(.@zone$ == "gqgefg"){
		.@zoneid = 2;
		.@edition = 1;
	} else if(.@zone$ == "gqpayg"){
		.@zoneid = 1;
		.@edition = 1;
	} else if(.@zone$ == "gqprtg"){
		.@zoneid = 0;
		.@edition = 1;
	} else if(.@zone$ == "gqsch"){
		.@zoneid = 5;
		.@edition = 2;
	} else if(.@zone$ == "gqtealde"){
		.@zoneid = 6;
		.@edition = 3;
	} else if(.@zone$ == "gqteprt"){
		.@zoneid = 7;
		.@edition = 3;
	} else {
		// Custom error message. (ข้อความแจ้งเตือนข้อผิดพลาด)
		debugmes "Guild mission script called from an unknown agit region [" + .@zone$ + "].";
		end;
	}
	.@npcname$ = .npcname$[.@zoneid];

	mes .@npcname$;
	mes "เรามีภารกิจและเควสรายวันที่เจ้าสามารถทำให้สำเร็จได้ในพื้นที่ของ " + .region$[.@zoneid] + "";
	mes "มีทั้งเควสรายวันแบบง่ายๆ ไปจนถึงภารกิจที่เจ้าต้องทำให้สำเร็จภายในช่วงเวลา Siege (กิลด์วอร์) เท่านั้น ลองเก็บไปคิดดูนะ~";
	next;
	switch(select("พิชิตดินแดน " + .region$[.@zoneid], "เควสรายวัน Guild Dungeon", "เกี่ยวกับเควส", "ข้าไม่ต้องการมัน")){
		case 1: // Conquering. (การพิชิต)
			.@gid = getcharid(2);
			if(.@gid && is_guild_leader() == true){
				.@time_check1 = checkquest(.conquer_delay_questid[.@zoneid], PLAYTIME);
				if(.@time_check1 == 0){
					mes .@npcname$;
					mes "เพิ่งจะผ่านไปไม่ถึงชั่วโมงนับจากครั้งล่าสุดที่เราตรวจสอบเลยนะ!";
					mes "กลับมาใหม่เมื่อเวลาพักสิ้นสุดลง";
					next;
					mes .@npcname$;
					mes "หากเจ้ามีโชคเข้าข้าง เจ้าจะได้รับผลลัพธ์ที่ดีแน่นอน";
					close;
				} else if(.@time_check1 == 2){
					erasequest .conquer_delay_questid[.@zoneid];
					mes .@npcname$;
					mes "ข้าได้รับการยืนยันแล้วว่าเวลารอสำหรับภารกิจพิชิตดินแดนได้สิ้นสุดลงแล้ว";
					mes "ข้าจะลบบันทึกที่ไม่จำเป็นออกให้เจ้าเอง";
					close;
				} else {
					.@siege_check = (.@edition == 1 ? agitcheck() : .@edition == 2 ? agitcheck2() : agitcheck3());
					.@time = atoi(gettimestr("%H%M",4));
					.@after_time = ((.weekday[.@zoneid] == gettime(4)) && ((.@time > .start_time[.@zoneid]) && (.@time < .end_time[.@zoneid])));

					if(.@siege_check && .@after_time){
						mes .@npcname$;
						mes "ตาม ^4d4dffจำนวนปราสาทที่กิลด์ของเจ้าพิชิตได้ในขณะนี้^000000 ข้าจะมอบรางวัลให้แก่เจ้า";
						mes "เจ้าต้องการตรวจสอบตอนนี้เลยไหม? เจ้าก็รู้นี่ว่า ^4d4dffจำนวนนั้นสามารถเปลี่ยนแปลงได้ตลอดเวลาแม้ในขณะที่เรากำลังคุยกันอยู่^000000 จริงไหม?";
						next;
						if(select("ข้าจะกลับมาทีหลัง","ตรวจสอบตอนนี้เลย!") == 1){
							mes .@npcname$;
							mes "ถูกต้องแล้ว ชีวิตคือเรื่องของจังหวะเวลา";
							mes "จงกลับมาในตอนที่เจ้าเจิดจรัสที่สุดเถิด";
							close;
						} else {
							.@total = 0;
							for(.@i = 0; .@i < 5; ++.@i){
								.@total += (.@gid == getcastledata(.castle$[.@i+.@zoneid*5],CD_GUILD_ID));
							}

							if(!.@total){
								mes .@npcname$;
								mes "โชคร้ายหน่อยนะ ดูเหมือนจะไม่มีปราสาทที่กิลด์ของเจ้าพิชิตได้เลย";
								mes "พยายามเข้าล่ะ!";
								close;
							} else {
								mes .@npcname$;
								if(.@total == 5)
									mes "เหลือเชื่อจริงๆ";
								else
									mes "ขณะนี้ จำนวนปราสาททั้งหมดที่กิลด์ของเจ้าพิชิตได้";
								for(.@i = 0; .@i < 5; ++.@i){
									mes getcastlename(.castle$[.@i+.@zoneid*5]) + (.@gid == getcastledata(.castle$[.@i+.@zoneid*5],CD_GUILD_ID) ? ": ^4d4dffครอบครองแล้ว^000000" : "");
								}
								if(.@total == 5)
									mes "กิลด์ของเจ้าพิชิตดินแดน " + .region$[.@zoneid] + " ได้สำเร็จ!";
								else
									mes "คือ " + .@total + " แห่ง!";
								next;
								mes .@npcname$;
								mes "ข้าจะมอบรางวัลให้ตามนั้น หวังว่าเจ้าจะใช้มันให้เกิดประโยชน์นะ";
								mes "เจ้าจะมีเวลารอ 1 ชั่วโมงจนกว่าจะส่งรายงานครั้งถัดไปได้ จำไว้ให้ดีแล้วมาลองใหม่ล่ะ";
								setquest .conquer_delay_questid[.@zoneid];
								getitem 6489,.@total;
								getitem 6615,.@total;
								getitem 12888,.@total;
								close;
							}
						}
					} else {
						mes .@npcname$;
						mes "ข้าขอโทษด้วย แต่เจ้าจะทำภารกิจนี้สำเร็จได้ในช่วงเวลา Siege เท่านั้น ข้าเกรงว่าตอนนี้ข้าคงช่วยอะไรไม่ได้";
						close;
					}
				}
			} else {
				mes .@npcname$;
				mes "ข้าขออภัยด้วย แต่ภารกิจพิชิตดินแดนนี้มีไว้สำหรับหัวหน้ากิลด์เท่านั้น";
				mes "สมาชิกกิลด์ควรไปลองทำเควสรายวันใน Guild Dungeon แทนนะ";
				close;
			}
			break;
		case 2: // Daily Quests (เควสรายวัน)
			mes .@npcname$;
			mes "นี่คือภารกิจที่เจ้าสามารถทำให้สำเร็จได้ใน Guild Dungeon ของพื้นที่ " + .region$[.@zoneid] + "";
			mes "เจ้าสามารถยกเลิกเควสได้ถ้าต้องการ ไม่ต้องลังเลที่จะท้าทายตัวเองล่ะ";
			next;
			.@cancel_index = (.@edition == 1 ? 3 : 2);
			for(.@i = 0; .@i < 3; ++.@i){
				if(.daily_hunting_questid[.@zoneid*3 + .@i])
					.@menu$ = .@menu$ + .region$[.@zoneid] + " ภารกิจที่ " + (.@i+1) + (isbegin_quest(.daily_hunting_questid[.@zoneid*3 + .@i]) ? " ^4d4dffตรวจสอบผลลัพธ์^000000" : "") + ":";
			}
			.@sel = select(.@menu$) - 1;
			.@index = .@zoneid*3 + .@sel;
			if(.@sel == .@cancel_index){
				mes .@npcname$;
				mes "เจ้าไม่จำเป็นต้องรีบร้อน เพราะภารกิจเหล่านี้อยู่ใน Guild Dungeon";
				mes "แวะมาลองทำเมื่อไหร่ก็ได้ที่เจ้าต้องการ";
				close;
			}
			if(BaseLevel < .level_req[.@index]){
				mes .@npcname$;
				mes "หืม น่าเสียดายที่ภารกิจนี้จำกัดไว้สำหรับผู้ที่มี ^4d4dffเลเวล " + .level_req[.@index] + "^000000 ขึ้นไปเท่านั้น";
				mes "เลเวลของเจ้ายังไม่สูงพอ";
				close;
			}
			.@hunting = checkquest(.daily_hunting_questid[.@index], HUNTING);
			.@playtime = checkquest(.daily_delay_questid[.@index], PLAYTIME);
			if(.@hunting > -1){
				if(.@hunting == 2){
					mes .@npcname$;
					mes "โอ้ เจ้าทำได้ดีมาก";
					mes "มันอาจจะไม่มากนัก แต่หวังว่าจะเป็นประโยชน์ต่อเจ้านะ";
					setquest .daily_delay_questid[.@index];
					erasequest .daily_hunting_questid[.@index];
					getexp calcexp(.@zoneid,.@index),calcjobexp(.@edition);
					getitem 6615,1;
				} else {
					mes .@npcname$;
					mes "หืม? ดูเหมือนว่าเจ้าจะยังทำภารกิจไม่สำเร็จนะ..";
					mes "มีปัญหาอะไรรึเปล่า?";
					next;
					if(select("ไม่มี","ขอยอมแพ้เควสนี้") == 1){
						mes .@npcname$;
						mes "ข้าไม่แน่ใจว่าเจ้าจะใช้ Guild Dungeon ได้นานแค่ไหน แต่ก็ขอให้โชคดีนะ";
						close;
					} else {
						mes .@npcname$;
						mes "เอาเถอะ ไม่มีใครบังคับเจ้าอยู่แล้ว";
						mes "ทำอะไรที่เจ้าอยากทำเถอะ";
						mes "งั้นข้าจะยกเลิกภารกิจนี้ให้";
						erasequest .daily_hunting_questid[.@index];
						close;
					}
				}
			} else {
				if(.@playtime == 0 || .@playtime == 1){
					mes .@npcname$;
					mes "กฎถูกกำหนดไว้ว่าเจ้าสามารถทำภารกิจสำเร็จได้เพียงหนึ่งอย่างต่อวันเท่านั้น";
					mes "ข้าอยากให้เจ้ากลับมาใหม่เมื่อเวลารอสิ้นสุดลง";
					close;
				} else if(.@playtime == 2){
					erasequest .daily_delay_questid[.@index];
					mes .@npcname$;
					mes "ข้าได้รับการยืนยันแล้วว่าเวลารอสำหรับภารกิจนี้สิ้นสุดลงแล้ว";
					mes "ข้าจะลบบันทึกที่ใช้ในการยืนยันออกให้";
					mes "ทีนี้ ก็ลองทำเควสให้สำเร็จดูสิ!";
					close;
				} else {
					mes .@npcname$;
					mes "นี่คือภารกิจลำดับที่ " + .ordinal$[.@sel] + " ใน " + .region$[.@zoneid] + "";
					brief(.daily_hunting_questid[.@index]);
					next;
					mes .@npcname$;
					if(mob_brief(.daily_hunting_questid[.@index]))
						mes "ข้าจะมอบรางวัลให้ตามนั้นเมื่อเจ้าทำภารกิจสำเร็จ";
					else
						mes "ข้าจะมอบค่าประสบการณ์ให้เป็นการตอบแทน";
					next;
					if(select("ยอมรับ","ปฏิเสธ") == 1){
						mes .@npcname$;
						mes "ทางเข้า Guild Dungeon นั้นค่อนข้างจำกัด แต่ก็ไม่ใช่ว่าเจ้าจะหาทางไปไม่ได้หรอกนะ";
						mes "ข้าจะสวดภาวนาให้เจ้าประสบความสำเร็จในการต่อสู้กับมอนสเตอร์เหล่านั้น";
						setquest .daily_hunting_questid[.@index];
						close;
					} else {
						mes .@npcname$;
						mes "เอาเถอะ ทำตามที่เจ้าต้องการเถอะ";
						close;
					}
				}
			}
			break;
		case 3: // About quests (เกี่ยวกับเควส)
			mes .@npcname$;
			mes "มันไม่ยากเลยสักนิด";
			mes "ข้าจะมอบรางวัล ^4d4dffโดยขึ้นอยู่กับจำนวนปราสาทที่เจ้าพิชิตได้^000000";
			mes "^4d4dffประเภทและคุณภาพของรางวัลจะขึ้นอยู่กับจำนวนปราสาทที่พิชิตได้ ณ วินาทีที่ข้าตรวจสอบ^000000";
			next;
			mes .@npcname$;
			mes "และแน่นอน ข้าจะตรวจสอบเฉพาะในช่วงเวลา Siege เท่านั้น";
			mes "^4d4dffหลังจากได้รับรางวัลแล้วจะมีเวลารอ 1 ชั่วโมง ดังนั้นเจ้าควรคำนวณจังหวะเวลาให้ดีด้วยนะ^000000";
			next;
			mes .@npcname$;
			mes "หากเจ้าเป็นหัวหน้ากิลด์ที่กำลังเข้าร่วมในการต่อสู้ช่วงกิลด์วอร์ เจ้าควรจะมาลองดูสักครั้งนะ";
			next;
			mes .@npcname$;
			mes "เควสรายวัน Guild Dungeon ประกอบด้วยเควสการต่อสู้ที่เจ้าสามารถทำให้สำเร็จได้ใน Guild Dungeon";
			mes "มันง่ายมาก";
			close;
		case 4: // I don't need it. (ข้าไม่ต้องการมัน)
			mes .@npcname$;
			mes "เจ้าช่างเป็นคนที่มีความมั่นใจจริงๆ~!";
			close;
	}
	end;

OnInit:
	setarray .castle$[0],
		"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
		"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
		"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
		"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",
		"arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",
		"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05",
		"te_aldecas1","te_aldecas2","te_aldecas3","te_aldecas4","te_aldecas5",
		"te_prtcas01","te_prtcas02","te_prtcas03","te_prtcas04","te_prtcas05";
	setarray .npcname$[0],
		"[Altir]","[Alshine]","[Denev]","[Tarazed]","[Mirah]","[Almark]","[Becrux]","[Acrux]";
	setarray .region$[0],
		"Valkyrie Realm","Greenwood Lake","Britoria","Luina","Valfreyja","Nidhoggur","Kafragarten","Gloria";
	setarray .weekday[0], // Day of the week. 0 = Sunday, 6 = Saturday. (วันในสัปดาห์ 0=วันอาทิตย์, 6=วันเสาร์)
		0,0,0,0,0,0,6,6;
	setarray .start_time[0],
		2200,2200,2200,2200,2200,2200,2200,2200;
	setarray .end_time[0],
		2230,2230,2230,2230,2230,2230,2230,2230;
	setarray .conquer_delay_questid[0], // Quest ID for cooldowns (เควสไอดีสำหรับคูลดาวน์)
		7517,7520,7519,7518,7522,7521,7524,7523;
	setarray .level_req[0], // Level requirement for quests. There are 2-3 quests per region (เลเวลที่ต้องการสำหรับเควส มี 2-3 เควสต่อพื้นที่)
		100,120,120,	//prt
		100,120,120,	//pay
		100,120,120,	//gef
		100,120,120,	//alde
		90,90,0,		//aru
		90,90,0,		//sch
		70,70,0,		//tealde
		70,70,0;		//teprt
	setarray .max_level[0], // Exp increase cap for quests. 0 for unlimited/not exist (จำกัดเลเวลสูงสุดสำหรับการเพิ่ม Exp. 0 คือไม่จำกัดหรือไม่ระบุ)
		130,140,0,	//prt
		130,140,0,	//pay
		130,140,0,	//gef
		130,140,0,	//alde
		120,120,0,	//aru
		120,120,0,	//sch
		99,99,0,	//tealde
		99,99,0;	//teprt
	setarray .daily_hunting_questid[0], //Quest ID for daily quests. (เควสไอดีสำหรับเควสรายวัน)
		7525,7526,7527,	//prt
		7534,7535,7536,	//pay
		7531,7532,7533,	//gef
		7528,7529,7530,	//alde
		7539,7540,0,	//aru
		7537,7538,0,	//sch
		7561,7562,0,	//tealde
		7541,7542,0;	//teprt
	setarray .daily_delay_questid[0],	//Quest ID for mob hunting check. (เควสไอดีสำหรับตรวจสอบการล่ามอนสเตอร์)
		7543,7544,7545,	//prt
		7552,7553,7554,	//pay
		7549,7550,7551,	//gef
		7546,7547,7548,	//alde
		7557,7558,0,	//aru
		7555,7556,0,	//sch
		7563,7564,0,	//tealde
		7559,7560,0;	//teprt
	setarray .ordinal$[0],"แรก","ที่สอง","ที่สาม";
	end;

	function brief {
		switch(getarg(0)){
			case 7527:
			case 7530:
			case 7533:
			case 7536:
				mes "มันคือการจัดการกับสิ่งมีชีวิตที่ทรงพลังซึ่งปรากฏกายใน Corridor of the Abyss";
				break;
			default:
				mes "มันคือการกำจัดเหล่ามอนสเตอร์ที่ปรากฏกายใน Guild Dungeon";
				break;
		}
		return;
	}

	function mob_brief {
		switch(getarg(0)){
			case 7525:
				mes "เจ้าควรจะล่า Caterpillar และ Creamy Fear อย่างละ 50 ตัวหรือมากกว่านั้น..";
				break;
			case 7526:
				mes "กำจัด Abyss Kobold ทั้ง 3 ชนิด อย่างละ 30 ตัวหรือมากกว่านั้น";
				break;
			case 7527:
				mes "^4d4dffเจ้าต้องกำจัด 'Pyuriel' ผู้เกรี้ยวกราด และนักรบ 'Lora'^000000";
				return 1;
			case 7528:
				mes "^4d4dffจงล่า Giant Hornet และ Ancient Worm อย่างละ 50 ตัวขึ้นไป^000000";
				break;
			case 7529:
				mes "^4d4dffจงล่า Killer Mantis 40 ตัวขึ้นไป และ Angra Mantis 50 ตัวขึ้นไป^000000";
				break;
			case 7530:
				mes "พวกมันคือ ^4d4dff'Elvira' และ 'Gioia' ซึ่งเป็นจักรกลประหลาดที่เกิดจากน้ำตาของเหล่าฮีโร่^000000";
				return 1;
			case 7531:
				mes "^4d4dffจงล่า Zombie Master และ Wraith Dead อย่างละ 50 ตัวขึ้นไป^000000";
				break;
			case 7532:
				mes "^4d4dffจงล่ามอนสเตอร์ GLD_DARK_SHADOW 50 ตัว, GLD_DARK_FRAME 20 ตัว และ GLD_DARK_PRIEST 20 ตัว^000000";
				break;
			case 7533:
				mes "พวกมันคือ ^4d4dff'Kades' และ 'Rudo' ผู้พเนจรอยู่รอบเนินเขาแห่งความตาย^000000";
				return 1;
			case 7534:
				mes "^4d4dffจงล่า Gullinbursti และ Leib Olmai อย่างละ 50 ตัวขึ้นไป^000000";
				break;
			case 7535:
				mes "^4d4dffจงล่า Skeleton General 45 ตัวขึ้นไป, Am Mut 20 ตัวขึ้นไป และ Gajomart 25 ตัวขึ้นไปตามลำดับ^000000";
				break;
			case 7536:
				mes "^4d4dffพวกมันคือแม่ทัพแห่งอาณาจักรโบราณ 'Daehyon' และองครักษ์ 'Soheon'^000000";
				return 1;
			case 7537:
				mes "^4d4dffจงล่า Hell Apocalypse และ Zakudam อย่างละ 50 ตัวขึ้นไป^000000";
				break;
			case 7538:
				mes "^4d4dffจงล่า Heavy Metaling และ Cobalt Mineral อย่างละ 35 ตัวขึ้นไป^000000";
				break;
			case 7539:
				mes "^4d4dffจงล่า Banshee Master มากกว่า 50 ตัว และ Beholder Master 30 ตัว^000000";
				break;
			case 7540:
				mes "^4d4dffจงล่า Aunoe 50 ตัว และ Fanat 20 ตัว^000000";
				break;
			case 7541:
				mes "^4d4dffจงล่า Knight, Blacksmith, Assassin อย่างละ 30 ตัวขึ้นไป^000000";
				break;
			case 7542:
				mes "^4d4dffจงล่า Wizard, Priest, Hunter อย่างละ 30 ตัวขึ้นไป^000000";
				break;
			case 7561:
				mes "^4d4dffจงล่า Crusader, Rogue, Alchemist อย่างละ 30 ตัวขึ้นไป^000000";
				break;
			case 7562:
				mes "^4d4dffจงล่า Sage, Monk, Bard อย่างละ 30 ตัวขึ้นไป^000000";
				break;
		}
		return 0;
	}
	
	function calcexp {
		.@zoneid = getarg(0);
		.@index = getarg(1);
		.@maxlevel = .max_level[.@index];
		.@isboss = (.@zoneid < 4 && !((.@index+1)%3));
		
		if(!.@isboss && BaseLevel > .@maxlevel){
			return 1000 * (100 + ((.@maxlevel - 100) * 3));
		} else {
			return 1000 * (100 + ((BaseLevel - 100) * 3)) * (1 + .@isboss);
		}
	}
	
	function calcjobexp {
		.@edition = getarg(0);
		
		if(.@edition == 3){
			if(jobcanentermap("te_prtcas01")){
				if (JobLevel > 50)
					return 1000 * (50 + ((JobLevel - 50) * 3));
				else
					return 5000;
			} else {
				debugmes "Unexpected case for Job: " + Class + "(" + jobname(Class) + ") for gld_mission#core::calcjobexp";
				return 0;
			}
		} else if(.@edition == 2 ) {
			if(JobLevel > 50)
				return 1000 * (50 + ((JobLevel - 50) * 3));
			else
				return 3000;
		} else {
			return 5000 * JobLevel;
		}
		return 0;
	}
}
