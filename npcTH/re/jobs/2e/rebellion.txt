//===== rAthena Script =======================================
//= Rebellion Job Quest
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Gunslinger -> Rebellion.
//===== Changelogs: ==========================================
//= 1.0 First version. [exneval]
//= 1.1 Syntax change. [Capuche]
//============================================================

moc_fild12,261,318,3	script	Suspicious Man	4_M_MOCASS2,2,2,{
	end;
OnTouch:
	if (Class == Job_Gunslinger && BaseLevel >= 99 && JobLevel >= 70) {
		if (isbegin_quest(12340) == 0) {
			mes "[Suspicious Man]";
			mes "ในที่สุดข้าก็หาเจ้าเจอจนได้ Vagabond Spike! หัวของเจ้าเป็นของข้าล่ะ!";
			next;
			select("ว่าไงนะ?");
			mes "[Suspicious Man]";
			mes "*ฮึดฮัด* อย่ามาทำเป็นไขสือหน่อยเลย ดูประกาศจับนี่สิ! มีรูปเจ้าอยู่ในนี้ด้วย!";
			next;
			select("ข้าว่ามันก็คล้ายอยู่นะ แต่รูปลั่นไม่ใช่ข้า");
			mes "["+strcharinfo(0)+"]";
			mes "ลืมเรื่องหน้าคล้ายไปก่อนเถอะ เจ้าไปเอาประกาศจับนั่นมาจากไหน? ข้าไม่เคยทำความผิดอะไรเลยนะ";
			next;
			mes "[Suspicious Man]";
			mes "ข้าคิดไว้แล้วว่าเจ้าจะต้องพยายามโกหกเพื่อหาทางรอด";
			next;
			mes "[Suspicious Man]";
			mes "^0000ffEinbroch Private Pub^000000 เป็นคนออกประกาศจับนั่น ตอนนี้มีนักล่าค่าหัวอย่างข้าตามล่าเจ้ากันให้ควั่ก";
			next;
			mes "[Suspicious Man]";
			mes "ข้าไม่สนหรอกว่าเจ้าจะเป็นตัวจริงหรือเปล่า ตราบใดที่หน้าเจ้าเหมือน และข้าสามารถจับเจ้าไปได้ไม่ว่าจะแบบเป็นหรือตาย";
		}
		else {
			mes "[Suspicious Man]";
			mes "เจ้าอีกแล้วเหรอ! ใจกล้าดีนี่นาที่ย้อนกลับมาที่นี่!";
		}
		next;
		if (select("หนีไปจากตรงนี้:สู้กับเขา") == 1)
			mes "^0000ffตอนนี้ฉันควรหนีไปก่อน แล้วค่อยกลับมาคิดเรื่องนี้ทีหลัง^000000";
		else
			mes "^0000ffถ้าเขากำลังพูดความจริง เขาคงไม่ใช่นักล่าค่าหัวคนเดียวที่ตามล่าฉันอยู่แน่ ฉันต้องการเวลาเพื่อวางแผน^000000";
		close2;
		if (isbegin_quest(12340) == 0)
			setquest 12340;// Rebellion Job Change Quest เควสต์เปลี่ยนอาชีพ Rebellion
		warp "moc_fild12",186,83;
	}
	end;
}

einbroch,54,97,3	script	Bouncer	4_M_REBELLION,{
	if (BaseClass != Job_Gunslinger) {
		mes "[Bouncer]";
		mes "ห้ามเดินเตร็ดเตร่แถวนี้";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[Bouncer]";
		mes "กำลังปรับปรุงอยู่ เข้าไปข้างในไม่ได้";
		close;
	case 1:
		mes "[Bouncer]";
		mes "*เฮือก* เจ้านี่เอง! ข้านึกว่าเจ้าถูกจับไปแล้วเสียอีก โอ้ แต่ตอนนี้มันไม่สำคัญแล้วล่ะ เจ้าคือบัตรผ่านทางสู่ความสบายของข้าแท้ๆ!";
		next;
		select("ข้าไม่ใช่คนร้ายอย่างที่เจ้าคิดนะ!");
		mes "[Bouncer]";
		mes "ไม่ใช่เหรอ?";
		next;
		mes "^0000ffพนักงานรักษาความปลอดภัยจ้องมองใบหน้าของคุณอย่างละเอียด^000000";
		next;
		mes "[Bouncer]";
		mes "อืม... เจ้ามีหน้าที่คล้ายกันมากจริงๆ แต่เจ้าดูไม่น่ากลัวเท่าเจ้าโจร Spike นั่นแฮะ";
		next;
		select("ข้าอยากคุยกับคนออกประกาศจับ");
		mes "[Bouncer]";
		mes "ปัญหาของเจ้ามันเกินระดับที่ข้าจะจัดการได้ เข้าไปข้างในเถอะ แต่ถ้าข้าพบว่าเจ้าเป็นตัวจริงขึ้นมาเมื่อไหร่ ข้าจะทำให้เจ้าไม่ได้เห็นโลกภายนอกอีกเลย";
		close2;
		warp "job_gun",81,29;
		end;
	case 2:
		mes "[Bouncer]";
		mes "เจ้ากลับมาแล้ว มีธุระอะไรล่ะ?";
		next;
		if (select("ข้าอยากเข้าไปข้างใน:ข้าแค่เดินผ่านมา") == 2) {
			mes "[Bouncer]";
			mes "เจ้าตลกดีนะ";
			close;
		}
		mes "[Bouncer]";
		mes "เชิญสิ แต่อย่าไปก่อเรื่องล่ะ";
		close2;
		warp "job_gun",70,28;
		end;
	}
}

job_gun,68,33,3	script	Ruthless Rebellion	4_F_REBELLION,{
	mes "[Ruthless Rebellion]";
	mes "เจ้าใช้ปืนเป็นหรือเปล่า?";
	close;
}

job_gun,78,33,3	script	Suspicious Gunslinger	4_M_JOB_BLACKSMITH,{
	mes "[Suspicious Gunslinger]";
	mes "...";
	next;
	mes "[Quiet Gunslinger]";
	mes "อย่ามาทักว่าข้าหน้าเหมือน Blacksmith ที่เจ้าคุ้นหน้าล่ะ";
	close;
}

job_gun,74,24,6	script	Drunken Man	4_M_LIEMAN,{
	mes "[Drunken Man]";
	mes "*อึก* โอ้ว ข้านี่แหละเจ้าของที่นี่";
	mes "เอ๊ะ เดี๋ยวก่อนนะ ข้าอยู่ในผับหรือที่ไหนกันแน่เนี่ย? ฟี๊... Zzz...";
	close;
}

job_gun,64,33,6	script	Anxious Man	4_M_ALCHE_A,{
	mes "[Anxious Man]";
	mes "สงสัยข้าจะมาผิดที่แฮะ ที่นี่ดูไม่เหมือนผับเลยสักนิด";
	close;
}

job_gun,86,29,3	script	Kulbertinov	4_M_REBELLION,{
	if (Class == Job_Rebellion) {
		mes "[Kulbertinov]";
		mes "เจ้าต้องการพบท่านหัวหน้าใช่ไหม?";
		next;
		if (select("ใช่:ไม่") == 1) {
			close2;
			warp "job_gun",220,130;
			end;
		}
		end;
	}
	if (Class != Job_Gunslinger) {
		mes "[Kulbertinov]";
		mes "เจ้ากำลังตามหาข้าอยู่รึเปล่า?";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[Kulbertinov]";
		mes "มีอะไรให้ข้าช่วยไหม?";
		close;
	case 1:
		mes "[Kulbertinov]";
		mes "เจ้าต้องการอะไร?";
		next;
		select("ข้าถูกเข้าใจผิดว่าเป็นอาชญากรน่ะ!");
		mes "[Kulbertinov]";
		mes "เดี๋ยวขอข้าดูหน่อยนะ... มัวฮ่าฮ่าฮ่า!";
		next;
		mes "^0000ffเขาจ้องสำรวจใบหน้าของเจ้า แล้วระเบิดเสียงหัวเราะออกมา^000000";
		next;
		mes "[Kulbertinov]";
		mes "ขออภัยที ชั่ววูบหนึ่งข้านึกว่า Spike ฟื้นคืนชีพกลับมาเสียอีก เขาเพิ่งถูกพบเป็นศพเมื่อสามวันก่อนเองนะ";
		next;
		select("เขาถูกจับได้แล้วงั้นเหรอ?");
		mes "[Kulbertinov]";
		mes "ใช่ และนักล่าค่าหัวก็ได้รับเงินรางวัลไปเรียบร้อยแล้วด้วย ข่าวยังไม่แพร่ออกไปน่ะ และหน้าเจ้าก็เหมือนเขาอย่างกับแกะเลยด้วยสิ ลองนึกดูเอาเองละกัน";
		next;
		select("ท่านต้องทำอะไรสักอย่างนะ!");
		mes "[Kulbertinov]";
		mes "ข้าเสียใจด้วยนะ แต่ข้าคงไม่สามารถตามไปคุยกับนักล่าค่าหัวทุกคนเพื่อเจ้าได้หรอก แต่แน่นอนว่าข้ายินดีจะชดเชยความไม่สะดวกนี้ให้";
		next;
		mes "[Kulbertinov]";
		mes "นี่ สนใจมาเข้าร่วมกลุ่มกับพวกเราไหมล่ะ? อย่างน้อยที่สุด เจ้าจะได้ไม่โดนยิงยังไงล่ะ";
		next;
		select("ท่านอยากให้ข้ากลายเป็นนักล่าค่าหัวงั้นเหรอ?");
		mes "[Kulbertinov]";
		mes "เปล่าเลย การล่าค่าหัวน่ะมันแค่กิจกรรมยามว่าง พวกเราคือผู้เชี่ยวชาญด้านอาวุธปืนทุกชนิดในโลกต่างหาก";
		next;
		mes "[Kulbertinov]";
		mes "เหล่านักสู้ผู้รักอิสระ!";
		mes "เหล่ามือปืนเวทมนตร์ผู้ดุร้าย!";
		mes "พวกเราเรียกตัวเองว่า ^0000ffRebellion^000000";
		next;
		mes "[Kulbertinov]";
		mes "ดูเหมือนเจ้าจะใช้ปืนเป็นนะ สนใจมาเข้าร่วมกับพวกเราไหมล่ะ? เจ้าจะได้ไม่ต้องกังวลเรื่องนักล่าค่าหัวคนอื่นๆ อีกต่อไปด้วย";
		next;
		if (select("ขอข้าคิดดูก่อน:ข้าคิดว่าข้าไม่มีทางเลือกอื่นแล้วล่ะ") == 1) {
			mes "[Kulbertinov]";
			mes "เจ้าจะนอนคิดนั่งคิดยังไงก็ได้ แต่คำตอบมันก็ไม่เปลี่ยนหรอก รับข้อเสนอไปเถอะ";
		}
		else {
			mes "[Kulbertinov]";
			mes "ตัดสินใจได้ดีนี่ งั้นก็เริ่มกันเลยเถอะจะได้จบเรื่องสักที";
		}
		next;
		mes "[Kulbertinov]";
		mes "ไปคุยกับ Enwin Conick ซะ นางเป็นผู้ชี้นำวิญญาณที่เร่ร่อนอย่างเจ้าไปสู่หนทางของ Rebellion เดี๋ยวข้าจะส่งข่าวไปบอกนางเอง อ้อ แล้วอย่าริอ่านหันกระบอกปืนไปทางนางเชียวล่ะ";
		completequest 12340;
		setquest 12341;// Rebellion Job Change Quest เควสต์เปลี่ยนอาชีพ Rebellion
		close2;
		warp "job_gun",19,15;
		end;
	case 2:
		mes "[Kulbertinov]";
		mes "เจ้ามีธุระกับ Elwin Conick งั้นหรือ?";
		next;
		if (select("เปล่า:ส่งข้าไปหา Elwin Conick ที") == 1) {
			mes "[Kulbertinov]";
			mes "ตกลง รับวอดก้าสักเป๊กหน่อยไหมล่ะ?";
			close;
		}
		mes "[Kulbertinov]";
		mes "ได้เลย เดี๋ยวข้าจะบอกนางให้";
		close2;
		warp "job_gun",19,15;
		end;
	}
}

job_gun,19,24,3	script	Elwin Conick#1	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[Elwin Conick]";
		mes "ถ้าเจ้าไม่ได้อยากจะเข้าร่วมกับกลุ่ม Rebellion ก็ออกไปซะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12341) == 1) {
		mes "[Elwin Conick]";
		mes "เจ้าต้องการจะเข้าร่วมกลุ่ม Rebellion กับพวกเรางั้นรึ?";
		next;
		mes "^0000ffหญิงสาวร่างเล็กที่มีแววตาเฉียบคมจ้องลึกเข้ามาในตาของเจ้า^000000";
		next;
		mes "[Elwin Conick]";
		mes "เซ็นชื่อตรงนี้";
		next;
		mes "^0000ffผู้ลงนามฝ่าย '_____' จะไม่เรียกร้องใดๆ เกี่ยวกับการทดสอบ แม้ว่าเขาหรือเธอจะได้รับบาดเจ็บ ทุพพลภาพ หรือเสียชีวิตจากอุบัติเหตุที่ไม่คาดคิดซึ่งอาจเกิดขึ้นในระหว่างการทดสอบ^000000";
		next;
		select("อะไรนะ?!");
		mes "[Elwin Conick]";
		mes "เจ้าอยากจะเข้าร่วมกับเราหรือไม่ล่ะ?";
		next;
		if (select("ข้ายอมแพ้:...ข้าจะเซ็น") == 1) {
			mes "[Elwin Conick]";
			mes "แล้วแต่เจ้า";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[Elwin Conick]";
		mes "ดีมาก ทีนี้ตามข้ามา";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		mes "[Elwin Conick]";
		mes "เจ้าอยากจะลองใหม่อีกครั้งไหม?";
		next;
		if (select("ข้ายอมแพ้:ตกลง") == 1) {
			mes "[Elwin Conick]";
			mes "แล้วแต่เจ้า";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[Elwin Conick]";
		mes "ตามข้ามา";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12343) > 0) {
		mes "[Elwin Conick]";
		if (isbegin_quest(12343) == 1)
			mes "เจ้ายังมีการทดสอบยิงปืนของ Ivan อยู่ เจ้าอยากจะลองใหม่อีกครั้งไหม?";
		else
			mes "การทดสอบยิงปืนของ Ivan ยังไม่เสร็จสิ้น เจ้าอยากจะลองใหม่อีกครั้งไหม?";
		next;
		if (select("ข้ายอมแพ้:ตกลง") == 1) {
			mes "[Elwin Conick]";
			mes "มันไม่คุ้มที่จะยอมแพ้หรอกนะ ตอนนี้ออกไปพักผ่อนก่อนเถอะ";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[Elwin Conick]";
		mes "จำไว้ว่าต้องทำให้ดีที่สุด";
		close2;
		warp "job_gun",120,130;
		end;
	}
	if (isbegin_quest(12345) == 1) {
		mes "[Elwin Conick]";
		mes "Ivan กำลังรอเจ้าอยู่ ไปหาเขาซะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[Elwin Conick]";
	mes "เจ้าไม่มีการทดสอบอื่นต้องทำแล้ว ทำไมไม่ออกไปหาอะไรดื่มหน่อยล่ะ?";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,117,46,3	script	Elwin Conick#2	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[Elwin Conick]";
		mes "ถ้าเจ้าไม่ได้อยากจะเข้าร่วมกับกลุ่ม Rebellion ก็ออกไปซะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
	.@quest_12341 = isbegin_quest(12341);
	.@quest_12342 = isbegin_quest(12342);
	if (.@emt_chk == 3169) {
		mes "^0000ffเจ้าอยู่ในร่างสำหรับทดสอบแล้ว หากเจ้าทำการทดสอบเสร็จสิ้นทั้งหมดแล้ว ให้ไปรายงานตัวกับ Elwin Conick ที่ห้องประกอบอาวุธ (Assembly Room)^000000";
		close;
	}
	if (.@emt_chk) {
		mes "[Elwin Conick]";
		mes "ดูสภาพตัวเองสิ เจ้าอยากจะทดสอบจริงๆ งั้นเหรอ?";
		next;
		mes "^0000ffElwin เห็นเจ้ากลายร่างเป็นรูปแบบอื่นและนางโกรธมาก จงรอจนกว่าร่างปัจจุบันจะหายไป แล้วค่อยมาพบนางอีกครั้ง^000000";
		next;
		if (select("รอ:ไปจากที่นี่") == 2) {
			mes "[Elwin Conick]";
			mes "กลับมาเมื่อเจ้าไม่ได้อยู่ในร่างแปลงใดๆ แล้วนะ";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[Elwin Conick]";
		mes "มันไม่คุ้มที่จะยอมแพ้หรอกนะ ตอนนี้ออกไปพักผ่อนก่อนเถอะ";
		close;
	}
	if (.@quest_12341 == 1 || .@quest_12342 == 1) {
		if (countitem(6746)) {
			mes "[Elwin Conick]";
			mes "ชิ้นงานเหล็กที่เหลือ (Steel Artifacts) จะต้องถูกส่งคืนหลังการทดสอบ";
			delitem 6746,countitem(6746);// Steel_Article
			close;
		}
		if (countitem(6747)) {
			mes "[Elwin Conick]";
			mes "ชิ้นงานเหล็กที่เหลือ (Steel Artifacts) จะต้องถูกส่งคืนหลังการทดสอบ";
			delitem 6747,countitem(6747);// Steel_Article_
			close;
		}
		if ((countitem(13118) - isequipped(13118)) > 0) {
			mes "[Elwin Conick]";
			mes "หมดเวลาแล้ว เจ้าสอบตก ให้ข้าเก็บวัสดุการทดสอบที่เหลือของเจ้าไปซะ";
			delitem 13118,1;// Tiny_Flame
			close;
		}
		if (.@quest_12342 == 1)
			.@menu$ = "รับทราบ";
		else {
			.@menu$ = "ตกลง";
			setquest 12342;// Rebellion Job Change Quest เควสต์เปลี่ยนอาชีพ Rebellion
			completequest 12341;
		}
		mes "[Elwin Conick]";
		mes "ฟังนะ ข้าจะไม่เสียเวลาอธิบายการทดสอบให้เจ้าฟังยืดยาวหรอก";
		next;
		select( .@menu$ );
		mes "[Elwin Conick]";
		if (.@quest_12341 == 1) {
			mes "นี่คือที่สำหรับหล่อแม่พิมพ์ปืน เจ้าจะเห็นชิ้นงานเหล็กถูกผลิตขึ้นอยู่ทุกที่";
			next;
			mes "[Elwin Conick]";
			mes "พวกเรา Rebellion คือผู้เชี่ยวชาญด้านอาวุธจากใจจริง ที่นี่เจ้าจะได้เรียนรู้การสร้างและประกอบปืน";
			next;
			mes "[Elwin Conick]";
			mes "เจ้าจะต้องไปควบคุมเครื่องควบคุมเตาหลอมที่อยู่ใกล้ทางเข้า และเลือกชิ้นงานเหล็กมาตรฐาน (Steel Artifacts) มาให้ได้ 5 ชิ้น";
			next;
			mes "[Elwin Conick]";
			mes "จากนั้นให้นำพวกมันไปยังห้องประกอบ (Assembly Room) ถัดไป และประกอบพวกมันเป็นปืน เจ้าจะมีเวลา 3 นาทีในการทำทั้งหมดนั้น เจ้าควรจะทำให้เร็วและมีสมาธิ ไม่งั้นเจ้าจะสอบตก";
			next;
			mes "[Elwin Conick]";
			mes "ข้าจะอยู่ในห้องถัดไป เพื่อรอให้เจ้านำปืนที่สมบูรณ์มาให้ข้า เพื่อเป็นการตรวจสอบเวลาที่ผ่านไป เจ้าจะถูกแปลงร่างเป็นอาชีพอื่น ^0000ffหากเวลาผ่านไปเกิน 3 นาทีและเจ้ากลับสู่ร่างปกติ ให้กลับมาหาข้าเพื่อเริ่มการทดสอบใหม่อีกครั้ง^000000";
		}
		else {
			mes "เจ้าจะต้องไปควบคุมสวิตช์บนเครื่องจักร และเลือกชิ้นงานเหล็กมาตรฐานมาให้ได้ 5 ชิ้น";
			next;
			mes "[Elwin Conick]";
			mes "จากนั้นให้นำพวกมันไปยังห้องประกอบถัดไป และประกอบให้เป็นปืนที่สมบูรณ์ เจ้าจะมีเวลา 3 นาทีในการทำทั้งหมดนั้น ^0000ffเพื่อตรวจสอบเวลาที่ผ่านไป เจ้าจะถูกแปลงร่างเป็นอาชีพอื่น^000000";
			next;
			mes "[Elwin Conick]";
			mes "ข้าจะอยู่ในห้องถัดไป เพื่อรอให้เจ้านำปืนที่สมบูรณ์มาให้ข้า จำไว้ว่า ^0000ffเจ้าจะสอบตกเมื่อร่างแปลงของเจ้าหมดเวลาหลังจากผ่านไป 3 นาที^000000";
		}
		transform 3169,180000;
		close;
	}
	mes "[Elwin Conick]";
	mes "หือ? เจ้าหลงทางงั้นเหรอ? เจ้าต้องไปลงทะเบียนก่อนแล้วค่อยกลับมาที่นี่";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,198,39,3	script	Elwin Conick#3	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[Elwin Conick]";
		mes "ถ้าเจ้าไม่ได้อยากจะเข้าร่วมกับกลุ่ม Rebellion ก็ออกไปซะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@emt_chk == 3169) {
			if ((countitem(13118) - isequipped(13118)) < 1) {// ไม่นับถ้าสวมใส่อยู่
				mes "[Elwin Conick]";
				mes "เจ้าทำเสร็จแล้วงั้นเหรอ? ข้าไม่เห็นปืนที่สมบูรณ์ในกระเป๋าของเจ้าเลยนะ";
				close;
			}
			mes "[Elwin Conick]";
			mes "งานหยาบไปหน่อยแต่ก็ไม่เลวเมื่อเทียบกับเวลาที่มี เจ้าสอบผ่าน";
			delitem 13118,1;// Tiny_Flame
			next;
			mes "[Elwin Conick]";
			mes "ให้ข้าเก็บวัสดุการทดสอบที่เหลือของเจ้าไปซะ";
			delitem 6746,countitem(6746);// Iron_Artifact
			delitem 6747,countitem(6747);// Steel_Artifact
			next;
			mes "[Elwin Conick]";
			mes "ตอนนี้เจ้าสามารถไปยังบทเรียนถัดไปได้แล้ว Ivan Sidorenko จะเป็นผู้ดูแลการทดสอบของเจ้า";
			completequest 12342;
			close2;
			warp "job_gun",120,133;
			end;
		}
		if (.@emt_chk) {
			mes "[Elwin Conick]";
			mes "ดูสภาพตัวเองสิ เจ้าอยากจะทดสอบจริงๆ งั้นเหรอ?";
			next;
			mes "^0000ffElwin เห็นเจ้ากลายร่างเป็นรูปแบบอื่นและนางโกรธมาก จงรอจนกว่าร่างปัจจุบันจะหายไป แล้วค่อยมาพบนางอีกครั้ง^000000";
			next;
			if (select("รอ:ไปจากที่นี่") == 2) {
				mes "[Elwin Conick]";
				mes "กลับมาเมื่อเจ้าไม่ได้อยู่ในร่างแปลงใดๆ แล้วนะ";
				close2;
				warp "job_gun",81,29;
				end;
			}
			mes "[Elwin Conick]";
			mes "มันไม่คุ้มที่จะยอมแพ้หรอกนะ ตอนนี้ออกไปพักผ่อนก่อนเถอะ";
			close;
		}
		mes "[Elwin Conick]";
		mes "หมดเวลาแล้ว เจ้าสอบตก ให้ข้าเก็บวัสดุการทดสอบที่เหลือของเจ้าไปซะ";
		delitem 6746,countitem(6746);// Iron_Artifact
		delitem 6747,countitem(6747);// Steel_Artifact
		delitem 13118,(countitem(13118) - isequipped(13118));// Tiny_Flame
		next;
		mes "[Elwin Conick]";
		mes "มาเริ่มกันใหม่อีกครั้ง";
		close2;
		warp "job_gun",117,41;
		end;
	}
	mes "[Elwin Conick]";
	mes "หือ? เจ้าหลงทางงั้นเหรอ? เจ้าต้องไปลงทะเบียนก่อนแล้วค่อยกลับมาที่นี่";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,165,18,0	script	Furnace Controller#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "เฉพาะผู้ที่ได้รับอนุญาตเท่านั้นจึงจะดำเนินการได้";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {// J_REB_SHECIL1
		if (isbegin_quest(12342) == 1) {
			mes "เจ้าต้องการจะขุดแม่พิมพ์ขึ้นมาจากเตาหลอมใช่หรือไม่?";
			next;
			if (select("ออก:เริ่ม") == 1) {
				mes "สลับเข้าสู่โหมดสแตนด์บาย";
				close;
			}
			mes "กำลังเริ่มต้นระบบควบคุมเตาหลอม ZX-3100...";
			disablenpc "Furnace Controller#1";
			donpcevent "Furnace Controller#2::OnStart";
			close;
		}
	}
	mes "ไม่ใช่ผู้ที่ได้รับอนุญาต ยกเลิกการดำเนินการ";
	close;
}

job_gun,214,36,0	script	Auto Anvil#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "เฉพาะผู้ที่ได้รับอนุญาตเท่านั้นจึงจะดำเนินการได้";
		close;
	}
	if ((countitem(13118) - isequipped(13118)) > 0) {// Tiny_Flame
		mes "ตรวจพบการสร้างอาวุธปืนโดยไม่ได้รับอนุญาต โปรดรายงานต่อผู้จัดการ Elwin";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {
		if (countitem(6747) < 5) {// Steel_Article_
			mes "เจ้าไม่มีชิ้นงานเหล็กมาตรฐาน (Steel Artifacts)";
			close;
		}
		if (isbegin_quest(12342) == 1) {
			mes "เจ้าต้องการใช้ทั่งตีเหล็กอัตโนมัติเพื่อขึ้นรูปชิ้นงานเหล็กมาตรฐานให้เป็นปืนใช่หรือไม่?";
			next;
			if (select("ออก:เริ่ม") == 1) {
				mes "เพื่อความปลอดภัยของเจ้า โปรดถอยห่างจากทั่งตีเหล็กอัตโนมัติ";
				close;
			}
			if (rand(1,10) > 6) {
				mes "เจ้าสร้างปืนโดยใช้ทั่งตีเหล็กอัตโนมัติสำเร็จแล้ว ^ff0000โปรดไปรายงานตัวก่อนที่ร่างแปลงของเจ้าจะหมดเวลา^000000";
				specialeffect EF_REPAIRWEAPON;
				getitem 13118,1;// Tiny_Flame (ปืนที่ประกอบเสร็จ)
			}
			else {
				mes "สร้างปืนล้มเหลว";
				specialeffect EF_SUI_EXPLOSION;
			}
			delitem 6747,5;// Steel_Article_
			close;
		}
	}
	mes "ไม่ใช่ผู้ที่ได้รับอนุญาต ยกเลิกการดำเนินการ";
	close;
}

job_gun,165,18,0	script	Furnace Controller#2	CLEAR_NPC,{
	mes "ระบบกำลังยุ่งอยู่กับการขุดแม่พิมพ์ที่เสร็จสมบูรณ์ขึ้นมาจากเตาหลอม";
	close;

OnStart:
	enablenpc "Furnace Controller#2";
	sleep 3000;
	mapannounce "job_gun","Furnace Controller: กำลังขุดแม่พิมพ์ขึ้นมาจากเตาหลอม... เหล่าคนงาน โปรดเตรียมพร้อมที่สายการผลิต",bc_map,"0x00ff44";
	sleep 500;
	for ( .@i = 0; .@i < 30; ++.@i ) {
		.@eff_rnd = rand(1,100);
		if (.@eff_rnd < 33)
			donpcevent "Heating Furnace#3::OnStart";
		else if (.@eff_rnd > 66)
			donpcevent "Heating Furnace#4::OnStart";
		else
			donpcevent "Heating Furnace#5::OnStart";
		sleep 200;
		.@d_itemx = rand(127,143);
		.@d_itemy = rand(26,29);
		makeitem 6746,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article (ชิ้นงานเหล็ก)
		if (rand(1,10) > 7)
			makeitem 6747,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article_ (ชิ้นงานเหล็กมาตรฐาน)
		sleep 500;
	}
	sleep 2000;
	mapannounce "job_gun","Furnace Controller: กำลังสิ้นสุดการดำเนินการ... จะเข้าสู่โหมดสแตนด์บายใน 15 วินาที",bc_map,"0x00ff44";
	sleep 15000;
	disablenpc "Furnace Controller#2";
	enablenpc "Furnace Controller#1";
	end;
OnInit:
	disablenpc "Furnace Controller#2";
	end;
}

job_gun,130,31,0	script	Heating Furnace#3	CLEAR_NPC,{
	end;
OnStart:
	specialeffect EF_FIREPILLAR;
	sleep 500;
	specialeffect EF_FIREPILLARBOMB;
	end;
}
job_gun,136,31,0	duplicate(Heating Furnace#3)	Heating Furnace#4	CLEAR_NPC
job_gun,141,31,0	duplicate(Heating Furnace#3)	Heating Furnace#5	CLEAR_NPC

job_gun,120,138,3	script	Ivan Sidorenko#1	4_M_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[Ivan Sidorenko]";
		mes "นี่คือพื้นที่ทดสอบการยิง เจ้าไม่สามารถเข้ามาที่นี่ตามใจชอบได้";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) < 2) {
		mes "[Ivan Sidorenko]";
		mes "ดูเหมือนจะมีความผิดพลาดบางอย่างนะ เจ้าไม่สามารถอยู่ที่นี่ได้";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12343) == 0) {
		mes "[Ivan Sidorenko]";
		mes "เฮ้ มีสมาธิหน่อย--เราใช้กระสุนจริงกันที่นี่นะ และเจ้าอาจโดนยิงได้ทุกเมื่อ";
		next;
		mes "[Ivan Sidorenko]";
		mes "ข้าจะพูดให้ไว โดยพื้นฐานแล้วเจ้าจะต้องวิ่งผ่านสนามยิงปืนนี้ซึ่งแบ่งออกเป็น 5 ส่วน";
		next;
		mes "[Ivan Sidorenko]";
		mes "เป้าหมายคือการหาศัตรูท่ามกลางกลุ่มฝูงชนให้ได้อย่างรวดเร็วและไม่ผิดพลาด";
		next;
		mes "[Ivan Sidorenko]";
		mes "ท่ามกลางเป้าหมายทั้งหมด เจ้าต้องยิงเฉพาะ ^0000ffเป้าหมายมาตรฐาน (Standard-issue Targets)^000000 เท่านั้น หากเจ้าทำพลาด เจ้าจะเสียคะแนน";
		next;
		mes "[Ivan Sidorenko]";
		mes "มาลองกันเถอะ ^0000ffเป้าหมายมาตรฐาน^000000 มีจำนวนไม่มากนัก เพราะฉะนั้นจงเบิกตาดูให้ดี";
		next;
		mes "[Ivan Sidorenko]";
		mes "^0000ffเจ้ามีเวลา 3 นาทีในการทดสอบนี้ หากเจ้าไม่สามารถทำเสร็จก่อนที่ร่างแปลงของเจ้าจะหมดเวลา^000000 เจ้าจะสอบตก หากเป็นเช่นนั้น ก็แค่เดินตามทางกลับมาหาข้า";
		next;
		mes "[Ivan Sidorenko]";
		mes "^0000ffเจ้าจะสอบตกทันทีหากพลาดเป้าหมายมาตรฐานไปแม้แต่ชิ้นเดียว หรือยิงเป้าหมายที่ผิดเกิน 3 ตัว^000000 มาเริ่มกันเลย";
		setquest 12343;// Rebellion Job Change Quest (เควสต์เปลี่ยนอาชีพ Rebellion)
		setquest 12344;// Rebellion Job Change Quest (เควสต์เปลี่ยนอาชีพ Rebellion)
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	if (isbegin_quest(12343) == 1) {
		if (checkquest(12343,HUNTING) == 2)
			.@j_r_s = 10;
		if (checkquest(12344,HUNTING) == 2)
			.@j_r_f = 2;
		if (getstatus(SC_MONSTER_TRANSFORM,1) != 3169)
			.@j_r_t = 5;
		.@j_r_c = .@j_r_s - .@j_r_f - .@j_r_t;
		if (.@j_r_c > 9) {
			mes "[Ivan Sidorenko]";
			mes "ทำได้ดีมาก ข้าไม่คิดเลยว่าเจ้าจะผ่านได้เร็วขนาดนี้";
			completequest 12343;
			completequest 12344;
			setquest 12345;// Rebellion Job Change Quest (เควสต์เปลี่ยนอาชีพ Rebellion)
			next;
			mes "[Ivan Sidorenko]";
			mes "เจ้าออกไปรอข้างนอกได้เลย เดี๋ยวข้าจะนำรายงานการประเมินของผู้ฝึกสอนคนอื่นๆ ไปให้ดู";
			close2;
			warp "job_gun",210,132;
			end;
		}
		if (.@j_r_c == 8) {
			mes "[Ivan Sidorenko]";
			mes "เจ้าเร็วพอตัวนะ แต่เจ้ายิงเป้าหมายที่ผิดมากเกินไป เจ้าสอบตก";
		}
		else if (.@j_r_c == 5) {
			mes "[Ivan Sidorenko]";
			mes "เจ้ามีความแม่นยำพอตัว แต่เจ้าใช้เวลามากเกินไป Rebellion ที่เชื่องช้าจะอยู่รอดได้ไม่นานนักหรอก";
		}
		else if (.@j_r_c < 5) {
			mes "[Ivan Sidorenko]";
			mes "เจ้าสอบตกทั้งด้านความเร็วและความแม่นยำ เจ้าอยู่รอดมาได้นานขนาดนี้ได้ยังไงเนี่ย?";
		}
		else {
			mes "[Ivan Sidorenko]";
			mes "มีปัญหาเกิดขึ้น ดูเหมือนสถิติจะผิดพลาดนะ อืม... เรื่องแบบนี้เกิดขึ้นได้บางครั้ง";
		}
		erasequest 12343;
		erasequest 12344;
		next;
		mes "[Ivan Sidorenko]";
		mes "มาลองใหม่อีกครั้ง คราวนี้ระวังให้มากกว่าเดิมล่ะ";
		setquest 12343;
		setquest 12344;
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	mes "[Ivan Sidorenko]";
	mes "ทำไมเจ้ายังไม่ไปอีก?";
	next;
	mes "[Ivan Sidorenko]";
	mes "เจ้าออกไปรอข้างนอกได้เลย เดี๋ยวข้าจะนำรายงานการประเมินของผู้ฝึกสอนคนอื่นๆ ไปให้ดู";
	close2;
	warp "job_gun",210,132;
	end;
}

job_gun,1,4,0	script	#target	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#target::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#target";
	while( mobcount("job_gun","#target::OnMyMobDead") < 16 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
	}
	end;
}

job_gun,1,5,0	script	#nontarget	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#nontarget::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#nontarget";
	while( mobcount("job_gun","#nontarget::OnMyMobDead") < 56 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
	}
	end;
}

job_gun,220,132,3	script	Ivan Sidorenko#2	10062,{
	if (!checkweight(1201,1)) {
		mes "เจ้ามีไอเทมมากเกินไป โปรดลดจำนวนลงเพื่อดำเนินการต่อ";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "ไอเทมในตัวของเจ้ามีน้ำหนักมากเกินไป ลดน้ำหนักลงก่อนเถอะ";
		close;
	}
	if (isbegin_quest(12345) == 0) {
		mes "[Ivan Sidorenko]";
		mes "ต้องมีความผิดพลาดบางอย่างเกิดขึ้นแน่ เจ้าไม่ควรจะอยู่ที่นี่";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Gunslinger) {
		if (BaseLevel < 99 || JobLevel < 70) {
			mes "[Ivan Sidorenko]";
			mes "หือ? เจ้าดูอ่อนแอมากเลยนะ? ข้าไม่มีอะไรจะพูดกับเจ้าอีกแล้ว";
			close;
		}
		if (SkillPoint) {
			mes "[Ivan Sidorenko]";
			mes "โอ้ ไม่นะ~ เจ้ายังมีแต้ม Skill Points เหลืออยู่";
			close;
		}
		mes "[Ivan Sidorenko]";
		mes "Kulbertinov, Elwin และตัวข้า ได้ประเมินผลการทดสอบการเป็น Rebellion ของเจ้าแล้ว";
		next;
		mes "[Ivan Sidorenko]";
		mes "และ...";
		next;
		mes "[Ivan Sidorenko]";
		mes "เจ้าผ่านข้อกำหนดทั้งหมดของพวกเรา ทั้งด้านทัศนคติและผลการทดสอบ";
		next;
		mes "[Ivan Sidorenko]";
		mes "ถูกต้องแล้ว ตอนนี้เจ้าคือสมาชิกของ Rebellion เตรียมตัวพบกับความตื่นตาตื่นใจได้เลย";
		next;
		mes "[Ivan Sidorenko]";
		mes "และ Elwin อยากให้เจ้าได้รับสิ่งนี้ไว้ ดูแลมันให้ดีล่ะ เขาทำมันขึ้นมาเพื่อเจ้าโดยเฉพาะ";
		jobchange Job_Rebellion;
		completequest 12345;
		getitem 13119,1;// Freedom_Flame (ปืนแห่งอิสรภาพ)
		next;
		mes "[Ivan Sidorenko]";
		mes "แล้วเจอกัน";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Rebellion) {
		mes "[Ivan Sidorenko]";
		mes "ชีวิตนี้ควรจะสนุกเข้าไว้ ไม่ใช่ปล่อยให้มันน่าเบื่อ";
		next;
		mes "[Ivan Sidorenko]";
		mes "และการเป็น Rebellion ชีวิตของเจ้าควรจะได้รับการยกระดับขึ้น เพราะฉะนั้น จงออกไปสร้างการเดินทางที่ยอดเยี่ยม และทำให้พวกเราภูมิใจ!";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[Ivan Sidorenko]";
	mes "เจ้ามีอะไรจะพูดกับข้าไหมล่ะ หืม?";
	close;
}

job_gun,220,138,4	shop	Prop Vending Machine	2_VENDING_MACHINE1,13210:-1,13211:-1,13212:-1,13213:-1,13214:-1,7663:-1,7664:-1,7665:-1

job_gun,216,138,4	script	Butler Karlex	1_M_01,{
	mes "[Butler Karlex]";
	mes "ใช้บริการคลังสินค้าชั่วคราวนี้เพื่อเก็บไอเทมของเจ้าให้ปลอดภัยในระหว่างการทดสอบเปลี่ยนอาชีพ";
	next;
	.@s = select("ออก:เปิดคลังสินค้า");
	mes "[Butler Karlex]";
	mes "ขอบคุณที่ใช้บริการ";
	close2;
	if (.@s == 2)
		openstorage;
	end;
}

job_gun,56,26,0	warp	rebelroom#1	2,2,einbroch,49,97
job_gun,18,10,0	warp	rebelroom#2	2,2,job_gun,81,29
job_gun,165,22,0	warp	rebelroom#3	2,2,job_gun,196,35
job_gun,197,49,0	warp	rebelroom#4	2,2,job_gun,157,21
job_gun,88,144,0	warp	shootingrange#1	3,3,job_gun,93,123
job_gun,105,97,0	warp	shootingrange#2	3,3,job_gun,125,98
job_gun,156,115,0	warp	shootingrange#3	3,3,job_gun,157,137
job_gun,152,154,0	warp	shootingrange#4	3,3,job_gun,135,154
job_gun,130,146,0	warp	shootingrange#5	3,3,job_gun,120,130


job_gun,1,1,0	script	Admin1	CLEAR_NPC,{
	callfunc "F_GM_NPC";
	if (callfunc("F_GM_NPC",1854,0) != 1)
		end;
	mes "[Time Manager]";
	mes "ท่านต้องการย้อนเวลากลับไปช่วงไหน?";
	next;
	switch( select("ยกเลิก:อัญเชิญมอนสเตอร์ทดสอบ:เริ่มตั้งแต่ต้น:โซนเตาหลอม:โซนยิงปืน:ผ่านการทดสอบทั้งหมด") ) {
	case 1:
		end;
	case 2:
		donpcevent "#target::OnStart";
		donpcevent "#nontarget::OnStart";
		end;
	case 3:
		callsub S_Quest,0,0;
	case 4:
		callsub S_Quest,12341,12341;
	case 5:
		callsub S_Quest,12342,12342;
	case 6:
		callsub S_Quest,12345,12344;
	}
	end;

S_Quest:
	setarray .@arguments[0], getarg(0), getarg(1);
	for ( .@quest = 12340; .@quest <= 12345; ++.@quest ) {
		if (isbegin_quest(.@quest) > 0)
			erasequest .@quest;
		if (.@quest <= .@arguments[0])
			setquest .@quest;
		if (.@quest <= .@arguments[1])
			completequest .@quest;
	}
	end;
}
