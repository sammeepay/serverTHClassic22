1@ep21a,1,1,0	script	#EP21_secret_altar	-1,{
	end;

OnInstanceInit:
	set_instance_var("map$", instance_mapname("1@ep21a"));
	set_instance_var("sec", 0);
	set_instance_var("lh", 0);
	set_instance_var("event_control", 0);
	set_instance_var("summon", 0);
	set_instance_var("control$",instance_npcname(strnpcinfo(0)));
	instance_enable("#ep21a_evt01", true);
end;

OnEvent:
	set_instance_var("event_control",get_instance_var("event_control") + 1);
	instance_event(strnpcinfo(0),"OnEvent" + (get_instance_var("event_control") > 9 ? get_instance_var("event_control") : ("0" + get_instance_var("event_control"))),false);
end;

OnEvent01:
	instance_enable("#ep21a_evt01", false);
	set_instance_var("sec", 1);
	instance_enable("Lehar#lehar01", true);
	instance_enable("Nillem#nillem01", true);
	instance_enable("Nyar#nyar01", true);
	instance_event(strnpcinfo(0),"OnSummon",false);
end;

OnEvent02:
	instance_enable("Lehar#lehar01",false);
	instance_enable("Nillem#nillem01",false);
	instance_enable("Nyar#nyar01",false);
	set_instance_var("sec", get_instance_var("sec") + 1);
	if(get_instance_var("sec") == 3)
		instance_event(strnpcinfo(0),"OnEvent",false);
end;

OnEvent03:
	set_instance_var("sec", 4);
	instance_enable("Door#door01",true);
	instance_enable("Lehar#lehar02",true);
	instance_enable("Nyar#nyar02",true);
	instance_enable("Nillem#nillem02",true);
end;

OnEvent04:
	set_instance_var("sec", 5);
	npctalk "ทางนี้! เร็วเข้า! ข้าจะเปิดประตูให้!",instance_npcname("Lehar#lehar02");
	npctalk "เนี๊ยวววว!!!",instance_npcname("Nyar#nyar02");
	setnpcdisplay(instance_npcname("Lower Cult Priest#a1"),22353);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a2"),22353);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a3"),22353);
	instance_enable("Lower Cult Priest#a1",true);
	instance_enable("Lower Cult Priest#a2",true);
	instance_enable("Lower Cult Priest#a3",true);
	sleep 400;
	instance_enable("Lower Cult Priest#a4",true);
	instance_enable("Lower Cult Priest#a5",true);
	instance_enable("Lower Cult Priest#a6",true);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a4"),22353);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a5"),22353);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a6"),22353);
	sleep 400;
	instance_enable("Lower Cult Priest#a7",true);
	instance_enable("Lower Cult Priest#a8",true);
	instance_enable("Lower Cult Priest#a9",true);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a7"),22353);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a8"),22353);
	setnpcdisplay(instance_npcname("Lower Cult Priest#a9"),22353);
	sleep 900;
	npctalk "อึก! พวกศัตรูแห่กันเข้ามาเต็มไปหมด! เราควรทำยังไงดี?!",instance_npcname("Lehar#lehar02");
	sleep 2000;
	npctalk "ในที่สุด ก็ถึงเวลาที่ข้าจะได้เฉิดฉายแล้ว! Nillem พ่อมดแห่งราชสำนักผู้มากความสามารถจะจัดการตรงนี้เอง พวกเจ้าล่วงหน้าไปก่อนเลย!",instance_npcname("Nillem#nillem02");
	sleep 2000;
	npctalk "ได้เวลาโชว์เวทมนตร์โจมตีเป็นวงกว้างแบบอลังการแล้ว~!",instance_npcname("Nillem#nillem02");
	npctalk "มีประตูอยู่ตรงนี้!",instance_npcname("Lehar#lehar02");
	sleep 2000;
	npctalk "โอ้..โอ้... ไม่มีทางเลือกอื่นแล้ว! เร็วเข้า ท่านนักผจญภัย!",instance_npcname("Lehar#lehar02");
	instance_enable("Lehar#lehar03",true);
	instance_enable("Nyar#nyar03",true);
	instance_event(strnpcinfo(0),"OnSummon",false);
	set_instance_var("sec", 6);
end;

OnEvent05:
	instance_enable("Lehar#lehar03",false);
	instance_enable("Nyar#nyar03",false);
	set_instance_var("sec", get_instance_var("sec") + 1);
	if(get_instance_var("sec") == 8)
		instance_event(strnpcinfo(0),"OnEvent",false);
end;

OnEvent06:
	set_instance_var("sec", 9);
	instance_enable("Nyar#nyar04", true);
	instance_enable("Lehar#lehar04", true);
	instance_enable("Jormungandr Shaman", true);
end;

OnEvent07:
	set_instance_var("sec", 10);
	instance_enable("Jormungandr Shaman",false);
	instance_event(strnpcinfo(0),"OnSummon",false);
end;

OnEvent08:
	set_instance_var("sec", 11);
	npctalk "เมี้ยว!! เนี้ยว",instance_npcname("Nyar#nyar04");
	npctalk "เจ้านั่นมันบ้าไปแล้วจริงๆ ใช่ไหม?",instance_npcname("Lehar#lehar04");
	sleep 2000;
	npctalk "เมี้ยว เมี้ยว!",instance_npcname("Nyar#nyar04");
	sleep 2000;
	npctalk "อ๊ะ นั่นมันอะไรกัน...!",instance_npcname("Lehar#lehar04");
	for(.@i = 1; .@i < 4; .@i++){
		setnpcdisplay(instance_npcname("Intermediate Cult Priest#f" + .@i),22357);
		instance_enable("Intermediate Cult Priest#f" + .@i,true);
		sleep 200;
	}
	sleep 400;
	for(.@i = 4; .@i < 9; .@i++){
		setnpcdisplay(instance_npcname("Intermediate Cult Priest#f" + .@i),22357);
		instance_enable("Intermediate Cult Priest#f" + .@i,true);
		sleep 200;
	}
	setnpcdisplay(instance_npcname("Intermediate Cult Priest#f9"),22356);
	instance_enable("Intermediate Cult Priest#f9",true);
	sleep 100;
	setnpcdisplay(instance_npcname("Intermediate Cult Priest#f10"),22356);
	instance_enable("Intermediate Cult Priest#f10",true);
	npctalk "ศัตรูปรากฏตัวออกมาแล้ว!",instance_npcname("Lehar#lehar04");
	sleep 2000;
	npctalk "เราไม่มีทางเลือก! ท่านนักผจญภัย ข้าจะรับมือตรงนี้เอง เพราะฉะนั้นรีบเข้าไปข้างในเถอะ!",instance_npcname("Lehar#lehar04");
	sleep 1000;
	npctalk "เมี้ยว เมี้ยว เมี้ยว เมี้ยว!",instance_npcname("Nyar#nyar04");
	sleep 2000;
	npctalk "เร็วเข้า ไปหยุดพิธีกรรมหรืออะไรก็ตามที่พวกมันกำลังทำอยู่!",instance_npcname("Lehar#lehar04");
	instance_enable("Door#door02",true);
	sleep 1000;
	npctalk "ฝากตรงนี้ไว้กับข้า รีบไปเร็ว!",instance_npcname("Lehar#lehar04");
	instance_enable("Cult Shaman#evtman", true);
	instance_enable("Cult Shaman#rgan_l", true);
	instance_enable("Cult Shaman#rgan_r", true);
	instance_enable("Cult Shaman#rgan_c", true);
	set_instance_var("sec", 12);
end;

OnEvent09:
	set_instance_var("sec", 13);
	instance_enable("Cult Shaman#evtman",false);
	specialeffect EF_DQ9_CHARGE3,AREA,instance_npcname("Cult Shaman#rgan_c");
	instance_event("Cult Shaman#rgan_l","OnSummon",false);
	instance_event("Cult Shaman#rgan_r","OnSummon",false);
	instance_event("Cult Shaman#rgan_c","OnSummon",false);
	instance_event(strnpcinfo(0),"OnSummon",false);
end;

OnEvent10:
	set_instance_var("sec", 14);
	.@map$ = get_instance_var("map$");
	instance_enable("Cult Shaman#rgan_l", false);
	killmonster .@map$,instance_npcname("Cult Shaman#rgan_l") + "::OnSummon";
	instance_enable("Cult Shaman#rgan_r", false);
	killmonster .@map$,instance_npcname("Cult Shaman#rgan_r") + "::OnSummon";
	instance_enable("Cult Shaman#rgan_c", false);
	killmonster .@map$,instance_npcname("Cult Shaman#rgan_c") + "::OnSummon";
	instance_enable("Serpent Form#endman", true);
end;

OnEvent11:
	set_instance_var("sec", 15);
	npctalk "อินทรีที่ร่วงหล่นมาถึงแล้ว!!",instance_npcname("Serpent Form#endman");
	instance_enable("Tris#tris01",true);
	instance_enable("Aurelie#arr01",true);
	sleep 2000;
	npctalk "อินทรีที่ร่วงหล่น เจ้ากำลังพูดเรื่องอะไรน่ะ?",instance_npcname("Tris#tris01");
	npctalk "เราอยู่ที่ไหนกัน...",instance_npcname("Aurelie#arr01");
	sleep 2000;
	instance_enable("Nillem#nillem03",true);
	instance_enable("Lehar#lehar05",true);
	npctalk "ข้าตามมาทันจนได้! ข้าเลยลงเอยด้วยการติดมากับพวกเขาน่ะ!",instance_npcname("Nillem#nillem03");
	npctalk "จู่ๆ ทั้งสองคนก็ปรากฏตัวออกมา... สถานการณ์เป็นยังไงบ้าง?",instance_npcname("Lehar#lehar05");
	sleep 2000;
	npctalk "มีมานาที่มหาศาลอยู่แถวนี้ หรือว่าเซอร์เพนท์ที่เราจัดการไปจะไม่ใช่ตัวสุดท้าย?!",instance_npcname("Aurelie#arr01");
	sleep 2000;
	npctalk "พวกมนุษย์โง่เขลา พิษนี้จะทำหน้าที่เป็นหอกที่มองไม่เห็นของข้า",instance_npcname("Serpent Form#endman");
	sleep 2000;
	npctalk "คำสาปได้เริ่มทำงานแล้ว! ผู้ที่แบกรับนามของ Gaebolg จะต้องจมดิ่งสู่ความทุกข์ทรมานชั่วนิรันดร์!",instance_npcname("Serpent Form#endman");
	sleep 2000;
	npctalk "ตราบใดที่ชื่อ Gaebolg ยังไม่เลือนหายไปจากโลกนี้ คำสาปก็จะไม่มีวันถูกทำลาย",instance_npcname("Serpent Form#endman");
	sleep 2000;
	npctalk "โชคชะตาของเราจะพัวพันกันตราบเท่าที่ Gaebolg ยังมีตัวตนอยู่!",instance_npcname("Serpent Form#endman");
	sleep 2000;
	instance_enable("Nyar#nyar05",true);
	npctalk "เมี้ยว?",instance_npcname("Nyar#nyar05");
	sleep 500;
	specialeffect EF_SELFSCROLL,AREA,instance_npcname("Nyar#nyar05");
	sleep 2000;
	npctalk "เจ้า.. ใช่ เจ้า! คนที่แทงทะลุหัวใจของเซอร์เพนท์! คำสาปตกอยู่ที่เจ้าแล้ว!!!",instance_npcname("Serpent Form#endman");
	sleep 1500;
	npctalk "เมี้ยว?",instance_npcname("Nyar#nyar05");
	specialeffect EF_DQ9_CHARGE3,AREA,instance_npcname("Nyar#nyar05");
	npctalk "คนที่แทงทะลุ... หัวใจของเซอร์เพนท์ นี่มันกำลังพูดถึงข้าเหรอ..",instance_npcname("Tris#tris01");
	sleep 2000;
	npctalk "พลังนี้.. มานานี่ อึก จำไว้ให้ดีมนุษย์เอ๋ย พวก Rgans จะยังคงอยู่ตลอดไป...",instance_npcname("Serpent Form#endman");
	sleep 1000;
	npctalk "เนี้ยว~",instance_npcname("Nyar#nyar05");
	sleep 1000;
	specialeffect EF_SPR_LIGHTPRINT,AREA,instance_npcname("Serpent Form#endman");
	specialeffect EF_HIT1,AREA,instance_npcname("Serpent Form#endman");
	sleep 1000;
	specialeffect EF_BEGINSPELL_YB,AREA,instance_npcname("Serpent Form#endman");
	instance_enable("Serpent Form#endman",false);
	sleep 2000;
	npctalk "เมี้ยยยยยยยยย้วววว....",instance_npcname("Nyar#nyar05");
	instance_enable("Giant Egg#egg",true);
	set_instance_var("sec", 16);
end;

OnSummon:
	.@map$ = get_instance_var("map$");
	.@var = get_instance_var("summon") + 1;
	.@event$ = get_instance_var("control$") + "::OnMobKill";
	set_instance_var("summon", .@var);
	switch(.@var){
		case 1:
			setarray .@mob$,
				82,33,22353,"Lower Cult Priest",
				82,35,22353,"Lower Cult Priest",
				82,37,22353,"Lower Cult Priest",
				94,33,22354,"Lower Cult Priest",
				94,35,22354,"Lower Cult Priest",
				94,37,22354,"Lower Cult Priest",
				107,33,22353,"Lower Cult Priest",
				107,35,22353,"Lower Cult Priest",
				107,37,22353,"Lower Cult Priest",
				123,33,22354,"Lower Cult Priest",
				123,35,22354,"Lower Cult Priest",
				123,37,22354,"Lower Cult Priest",
				139,33,22353,"Lower Cult Priest",
				139,35,22353,"Lower Cult Priest",
				139,37,22353,"Lower Cult Priest",
				158,33,22354,"Lower Cult Priest",
				158,35,22354,"Lower Cult Priest",
				158,37,22354,"Lower Cult Priest",
				193,33,22353,"Lower Cult Priest",
				193,35,22353,"Lower Cult Priest",
				193,37,22353,"Lower Cult Priest";
			break;

		case 2:
			setarray .@mob$,
				165,114,22356,"Lower Cult Priest",
				165,114,22356,"Lower Cult Priest",
				163,114,22357,"Lower Cult Priest",
				163,110,22356,"Lower Cult Priest",
				163,114,22357,"Lower Cult Priest",
				163,110,22356,"Lower Cult Priest",
				160,114,22357,"Lower Cult Priest",
				160,114,22357,"Lower Cult Priest",
				145,113,22356,"Lower Cult Priest",
				145,113,22356,"Lower Cult Priest",
				142,113,22357,"Lower Cult Priest",
				142,113,22357,"Lower Cult Priest",
				136,117,22357,"Lower Cult Priest",
				136,117,22357,"Lower Cult Priest",
				132,117,22356,"Lower Cult Priest",
				132,117,22356,"Lower Cult Priest",
				119,128,22357,"Lower Cult Priest",
				115,126,22357,"Lower Cult Priest",
				113,122,22356,"Lower Cult Priest",
				110,130,22356,"Lower Cult Priest",
				110,151,22357,"Lower Cult Priest",
				115,151,22356,"Lower Cult Priest",
				105,155,22357,"Lower Cult Priest",
				110,155,22356,"Lower Cult Priest";
			break;

		case 3:
			setarray .@mob$,
				148,209,22361,"Jormungandr Shaman";
			break;

		case 4:
			setarray .@mob$,
				68,245,22360,"Hyasagand",
				68,256,22300,"Materialized Mana",
				89,252,22300,"Materialized Mana",
				48,252,22300,"Materialized Mana";
			break;
	}
	for(.@i = 0; .@i < getarraysize(.@mob$); .@i += 4)
		monster .@map$,atoi(.@mob$[.@i]),atoi(.@mob$[.@i+1]),.@mob$[.@i+3],atoi(.@mob$[.@i+2]),1,.@event$;
end;

OnMobKill:
	if(mobcount(get_instance_var("map$"),get_instance_var("control$") + "::OnMobKill")) end;
	switch(get_instance_var("summon")){
		case 1:
			set_instance_var("sec", get_instance_var("sec") + 1);
			if(get_instance_var("sec") == 3)
				instance_event("#EP21_secret_altar","OnEvent",false);
			end;

		case 2:
			set_instance_var("sec", get_instance_var("sec") + 1);
			if(get_instance_var("sec") == 8)
				instance_event("#EP21_secret_altar","OnEvent",false);
			end;

		default:
			break;
	}
	instance_event("#EP21_secret_altar","OnEvent",false);
end;
}

1@ep21a,64,35,0	script	#ep21a_evt01	HIDDEN_WARP_NPC,5,5,{
	end;

OnTouch:
	if(is_party_leader() && !get_instance_var("sec"))
		instance_event("#EP21_secret_altar","OnEvent",false);
end;
}

1@ep21a,64,35,5	script(DISABLED)	Lehar#lehar01	4_EP19_LEHAR,{
	if(is_party_leader() &&  get_instance_var("lh") == 0){
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "ที่แท้ก็มีสถานที่เร้นลับแบบนี้อยู่ด้วย เราควรจะเข้าไปข้างในต่อดีไหม?";
		npctalk "ที่แท้ก็มีสถานที่เร้นลับแบบนี้อยู่ด้วย เราควรจะเข้าไปข้างในต่อดีไหม?",instance_npcname("Lehar#lehar01");
		next;
		cutin "ep172_nillem02.png",0;
		mes "[Nillem]";
		mes "กระแสมานาเชื่อมต่อกันแล้ว เราแค่ต้องตามมันไป~!";
		npctalk "กระแสมานาเชื่อมต่อกันแล้ว เราแค่ต้องตามมันไป~!",instance_npcname("Nillem#nillem01");
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "ข้าสัมผัสได้ถึงพลังที่หยาบกร้านและมหาศาลอีกอย่างหนึ่ง ซึ่งแตกต่างจากพลังที่เรากำลังตามรอยอยู่ มันคืออะไรกันนะ?";
		npctalk "ข้าสัมผัสได้ถึงพลังที่หยาบกร้านและมหาศาลอีกอย่างหนึ่ง ซึ่งแตกต่างจากพลังที่เรากำลังตามรอยอยู่ มันคืออะไรกันนะ?",instance_npcname("Lehar#lehar01");
		next;
		cutin "ep20_nyar02.png",1;
		mes "[Nyar]";
		mes "เมี้ยวววว~~~";
		npctalk "เมี้ยวววว~~~",instance_npcname("Nyar#nyar01");
		next;
		cutin "ep172_nillem01.png",0;
		mes "[Nillem]";
		mes "โอ๊ะ? มันอาจจะเป็นพลังที่หลงเหลือจากเซอร์เพนท์ยักษ์ที่ท่านนักผจญภัยเคยต่อสู้ด้วยก็ได้";
		npctalk "โอ๊ะ? มันอาจจะเป็นพลังที่หลงเหลือจากเซอร์เพนท์ยักษ์ที่ท่านนักผจญภัยเคยต่อสู้ด้วยก็ได้",instance_npcname("Nillem#nillem01");
		next;
		cutin "ep172_nillem02.png",0;
		mes "[Nillem]";
		mes "พวกลัทธิกำลังพยายามจะชุบชีวิต Jormungandr ใช่ไหมล่ะ?";
		mes "พลังนี้น่าจะเป็นของเซอร์เพนท์ยักษ์ที่พวกมันพยายามจะใช้เลียนแบบ Jormungandr";
		npctalk "พลังนี้น่าจะเป็นของเซอร์เพนท์ยักษ์ที่พวกมันพยายามจะใช้เลียนแบบ Jormungandr",instance_npcname("Nillem#nillem01");
		next;
		cutin "ep20_nyar02.png",1;
		mes "[Nyar]";
		mes "เมี้ยว เนี้ยว ฮิสสส ม้าว?";
		npctalk "เมี้ยว เนี้ยว ฮิสสส ม้าว?",instance_npcname("Nyar#nyar01");
		next;
		cutin "ep172_nillem01.png",0;
		mes "[Nillem]";
		mes "ถูกต้อง พลังนี้มาจากตัวตนที่แท้จริงของ Jormungandr เช่นกัน";
		mes "แต่เซอร์เพนท์ยักษ์ถูกกำจัดโดยเหล่าฮีโร่ทั้งสิบสองคนก่อนที่มันจะเสร็จสมบูรณ์...";
		mes "และพลังที่เหลืออยู่กำลังถูกนำไปใช้สำหรับแผนการขั้นต่อไปแล้ว!";
		npctalk "พวกมันพยายามสร้างเซอร์เพนท์ยักษ์โดยใช้พลังของ Jormungandr แต่ก็ถูกกำจัดไปเสียก่อน ตอนนี้พลังที่เหลืออยู่กำลังถูกนำไปใช้ในแผนการถัดไปของพวกมัน!",instance_npcname("Nillem#nillem01");
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "แผนการขั้นต่อไปของพวกมันงั้นเหรอ?";
		npctalk "แผนการขั้นต่อไปของพวกมันงั้นเหรอ?",instance_npcname("Lehar#lehar01");
		next;
		cutin "ep20_nyar03.png",1;
		mes "[Nyar]";
		mes "มยา มยา มยา มยา? มยา เนี้ยง!";
		npctalk "มยา มยา มยา มยา? มยา เนี้ยง!",instance_npcname("Nyar#nyar01");
		next;
		cutin "ep172_nillem02.png",0;
		mes "[Nillem]";
		mes "บางที Lasgand อาจจะเป็นแผนสำรองของพวกมันในกรณีที่เซอร์เพนท์ยักษ์ล้มเหลวก็ได้นะ?";
		mes "นั่นคือสิ่งที่ข้าคิด";
		mes "แล้วเจ้าล่ะ คิดว่าไง?";
		npctalk "บางที Lasgand อาจจะเป็นแผนสำรองของพวกมันในกรณีที่เซอร์เพนท์ยักษ์ล้มเหลวก็ได้นะ?",instance_npcname("Nillem#nillem01");
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "เราต้องหยุดมันก่อนที่พวกมันจะรวบรวมพลังที่เหลืออยู่ได้ทั้งหมด!";
		mes "เราต้องขัดขวางไม่ให้พลังไปรวมตัวกันที่ Lasgand!";
		npctalk "เราต้องขัดขวางไม่ให้พลังไปรวมตัวกันที่ Lasgand!",instance_npcname("Lehar#lehar01");
		close2;
		set_instance_var("lh", 1);
		cutin "",255;
		sleep 1000;
		npctalk "ไปกันเถอะ",instance_npcname("Lehar#lehar01");
		sleep 1000;
		npctalk "Lehar: เราต้องรีบหน่อยก่อนที่พลังจะไปรวมตัวกันใน Lasgand!",instance_npcname("Lehar#lehar01");
		sleep 1000;
		instance_event("#EP21_secret_altar","OnEvent",false);
	}
	end;
}

1@ep21a,64,38,5	script(DISABLED)	Nillem#nillem01	4_M_NILLEM,{ end; }
1@ep21a,64,32,5	script(DISABLED)	Nyar#nyar01	4_EP20_NYAR,{ end; }

1@ep21a,190,48,3	script(DISABLED)	Door#door01	4_POINT_BLUE,{
	if(is_party_leader() && get_instance_var("sec") == 4){
		instance_event("#EP21_secret_altar","OnEvent",false);
		end;
	}
	if(get_instance_var("sec") > 5)
		warp get_instance_var("map$"),191,79;
	end;
}

1@ep21a,192,46,1	script(DISABLED)	Lehar#lehar02	4_EP19_LEHAR,{
	if(get_instance_var("sec") < 6){
		npctalk "มีประตูอยู่ตรงนี้!";
		end;
	}
	cutin "ep19_lehar05.png",2;
	mes "[Lehar]";
	mes "ไม่ต้องห่วงเรื่องพ่อมดนั่นหรอก! รีบหน่อย เปิดประตูแล้วเข้าไปข้างในกันเถอะ!";
	close3;
}

1@ep21a,190,43,3	script(DISABLED)	Nillem#nillem02	4_M_NILLEM,{
	if(get_instance_var("sec") > 5){
		cutin "ep172_nillem05.png",1;
		mes "[Nillem]";
		mes "ข้าจะจัดการตรงนี้ให้เสร็จไวๆ แล้วจะรีบตามไปหา อย่ากังวลไปเลยนะ~?";
		close3;
	}
	 end; 
}

1@ep21a,188,46,7	script(DISABLED)	Nyar#nyar02	4_EP20_NYAR,{ end; }
1@ep21a,189,39,7	script(DISABLED)	Lower Cult Priest#a1	CLEAR_NPC,{ end; }
1@ep21a,192,39,0	script(DISABLED)	Lower Cult Priest#a2	CLEAR_NPC,{ end; }
1@ep21a,195,39,1	script(DISABLED)	Lower Cult Priest#a3	CLEAR_NPC,{ end; }
1@ep21a,190,37,7	script(DISABLED)	Lower Cult Priest#a4	CLEAR_NPC,{ end; }
1@ep21a,193,37,0	script(DISABLED)	Lower Cult Priest#a5	CLEAR_NPC,{ end; }
1@ep21a,196,37,1	script(DISABLED)	Lower Cult Priest#a6	CLEAR_NPC,{ end; }
1@ep21a,191,35,7	script(DISABLED)	Lower Cult Priest#a7	CLEAR_NPC,{ end; }
1@ep21a,194,35,0	script(DISABLED)	Lower Cult Priest#a8	CLEAR_NPC,{ end; }
1@ep21a,196,35,1	script(DISABLED)	Lower Cult Priest#a9	CLEAR_NPC,{ end; }

1@ep21a,194,82,1	script(DISABLED)	Lehar#lehar03	4_EP19_LEHAR,{
	if(is_party_leader() && get_instance_var("lh") == 1){
		set_instance_var("lh", 2);
		npctalk "โอ้... สถานที่แห่งนี้ดูคุ้นๆ ไหม?",instance_npcname("Lehar#lehar03");
		sleep 2000;
		npctalk "มันดูคล้ายกับที่ที่พวก Rgans อาศัยอยู่เลยไม่ใช่เหรอ?",instance_npcname("Lehar#lehar03");
		sleep 2000;
		npctalk "เนี้ยว~ เมี้ยว~",instance_npcname("Nyar#nyar03");
		sleep 2000;
		npctalk "อืม เป็นไปได้มากว่าสมาชิกระดับสูงสุดของลัทธิจะเป็นพวก Rgans ระดับสูง",instance_npcname("Lehar#lehar03");
		sleep 2000;
		npctalk "เราเคลื่อนที่ต่อกันเลยไหม?",instance_npcname("Lehar#lehar03");
		sleep 2000;
		instance_enable("Lehar#lehar03",false);
		instance_enable("Nyar#nyar03",false);
		instance_event("#EP21_secret_altar","OnEvent",false);
	}
	end;
}

1@ep21a,190,82,7	script(DISABLED)	Nyar#nyar03	4_EP20_NYAR,{ end; }

1@ep21a,148,209,3	script(DISABLED)	Jormungandr Shaman	EP21_YORTUS_B,{
	if(is_party_leader() && get_instance_var("sec") == 9){
		cutin "ep21_yortus_b.png",1;
		mes "[Cult Shaman]";
		mes "พวกมนุษย์โง่เขลา เจ้าเดินเข้ามาที่นี่ด้วยตัวเองเพื่อเป็นเครื่องสังเวยสินะ";
		mes "มันจะฉลาดกว่านี้ถ้าพวกเจ้ายอมรับอ้อมกอดของ Jormungandr...";
		npctalk "พวกมนุษย์โง่เขลา เจ้าเดินเข้ามาที่นี่ด้วยตัวเองเพื่อเป็นเครื่องสังเวยสินะ";
		next;
		mes "[Cult Shaman]";
		mes "พวกเจ้าคงกำลังฉลองกันอยู่ล่ะสิ ที่คิดว่ากำจัดเทพเซอร์เพนท์ได้แล้ว";
		mes "ผู้ที่ครอบครองชื่อ Gaebolg และผู้ที่อยู่ที่นั่นจะต้องพินาศ";
		npctalk "คิดว่ามันจบแล้วงั้นเหรอ? ผู้ที่ครอบครองชื่อ Gaebolg และผู้ที่อยู่ที่นั่นจะต้องพินาศ";
		next;
		mes "[Cult Shaman]";
		mes "สำหรับการตัดกระดูกสันหลังของเซอร์เพนท์ ลูกหลานของพวกเจ้าทุกคนจะต้องตายตั้งแต่อายุยังน้อย";
		mes "เจ้าผู้ที่เป็นพยานในความตาย จะได้รับการย้ำเตือนอย่างไม่จบไม่สิ้น";
		npctalk "สำหรับการตัดกระดูกสันหลังของเซอร์เพนท์ ลูกหลานของพวกเจ้าทุกคนจะต้องตายตั้งแต่อายุยังน้อย เจ้าผู้ที่เป็นพยานในความตาย จะได้รับการย้ำเตือนอย่างไม่จบไม่สิ้น";
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "ช่วยอธิบายเรื่องนั้นให้ละเอียดกว่านี้หน่อยได้ไหม?";
		mes "คำสาปเหรอ? เจ้ากำลังสาปพวกเรางั้นเหรอ?";
		npctalk "ช่วยอธิบายเรื่องนั้นหน่อย คำสาปเหรอ? เจ้ากำลังสาปพวกเรางั้นเหรอ?",instance_npcname("Lehar#lehar04");
		next;
		cutin "ep21_yortus_b.png",1;
		mes "[Cult Shaman]";
		mes "คำสาปได้เริ่มทำงานแล้ว พวกโง่เอ๋ย!";
		mes "อินทรีที่สังหารเซอร์เพนท์จะร่วงหล่นสู่พื้นดินด้วยความเจ็บปวด!";
		mes "มือที่แทงทะลุหัวใจจะเปื้อนเลือด และไม่มีวันล้างออกได้!";
		npctalk "คำสาปเริ่มทำงานแล้ว อินทรีที่สังหารเซอร์เพนท์จะร่วงหล่น และมือที่แทงทะลุหัวใจจะเปื้อนเลือด ไม่มีวันล้างออกได้!";
		next;
		cutin "ep19_lehar03.png",2;
		mes "[Lehar]";
		mes "เจ้าคนบ้า! หยุดเดี๋ยวนี้!";
		npctalk "เจ้าคนบ้า! หยุดเดี๋ยวนี้!",instance_npcname("Lehar#lehar04");
		next;
		cutin "ep21_yortus_b.png",1;
		mes "[Cult Shaman]";
		mes "เจ้านั่นแหละที่ควรหยุด!";
		mes "ตายซะที่นี่! และจงกลายเป็นปุ๋ยเพื่อชีวิตนิรันดร์!!";
		mes "จงเป็นพยานให้แก่สรวงสวรรค์ของศาสนจักรและอดทนต่อความทุกข์ทรมานชั่วนิรันดร์!";
		npctalk "เจ้านั่นแหละที่ควรหยุด! ตายซะที่นี่! จงกลายเป็นปุ๋ยเพื่อชีวิตนิรันดร์!!";
		close2;
		cutin "",255;
		instance_event("#EP21_secret_altar","OnEvent",false);
	}
	end;
}

1@ep21a,145,208,5	script(DISABLED)	Nyar#nyar04	4_EP20_NYAR,{
	npctalk "เมี้ยว~เมี้ยว~เมี้ยว!";
	end;
}

1@ep21a,150,208,1	script(DISABLED)	Lehar#lehar04	4_EP19_LEHAR,{
	npctalk "อ๊าก~ ข้าคิดอะไรไม่ออกเลย!";
	end;
}

1@ep21a,148,210,3	script(DISABLED)	Door#door02	4_POINT_BLUE,{
	if(get_instance_var("sec") > 11)
		warp get_instance_var("map$"),68,229;
	end;
}

1@ep21a,144,204,7	script(DISABLED)	Intermediate Cult Priest#f1	CLEAR_NPC,{ end; }
1@ep21a,147,203,7	script(DISABLED)	Intermediate Cult Priest#f2	CLEAR_NPC,{ end; }
1@ep21a,150,203,1	script(DISABLED)	Intermediate Cult Priest#f3	CLEAR_NPC,{ end; }
1@ep21a,153,204,1	script(DISABLED)	Intermediate Cult Priest#f4	CLEAR_NPC,{ end; }
1@ep21a,143,198,7	script(DISABLED)	Intermediate Cult Priest#f5	CLEAR_NPC,{ end; }
1@ep21a,146,197,7	script(DISABLED)	Intermediate Cult Priest#f6	CLEAR_NPC,{ end; }
1@ep21a,149,197,1	script(DISABLED)	Intermediate Cult Priest#f7	CLEAR_NPC,{ end; }
1@ep21a,152,198,1	script(DISABLED)	Intermediate Cult Priest#f8	CLEAR_NPC,{ end; }
1@ep21a,140,200,7	script(DISABLED)	Intermediate Cult Priest#f9	CLEAR_NPC,{ end; }
1@ep21a,155,200,1	script(DISABLED)	Intermediate Cult Priest#f10	CLEAR_NPC,{ end; }

1@ep21a,68,245,3	script(DISABLED)	Cult Shaman#evtman	22360,5,5,{
OnTouch:
	if(is_party_leader() && get_instance_var("sec") == 12){
		cutin "ep21_yortus_a.png",1;
		mes "[Hyasagand]";
		mes "เจ้า เจ้าคือคนที่อยู่กับเจ้าพวกผู้ทรยศที่โสโครกนั่น";
		npctalk "Hyasagand : เจ้า เจ้าคือคนที่อยู่กับเจ้าพวกผู้ทรยศที่โสโครกนั่น",instance_npcname("Cult Shaman#evtman");
		next;
		mes "[Hyasagand]";
		mes "บาปของพวกผู้ทรยศได้รับการบันทึกไว้แล้ว";
		mes "การลงทัณฑ์ของ Jormungandr จะตกอยู่กับเขา";
		npctalk "Hyasagand : การลงทัณฑ์ของ Jormungandr จะตกอยู่กับผู้ที่ทรยศต่อศาสนจักร",instance_npcname("Cult Shaman#evtman");
		next;
		mes "[Hyasagand]";
		mes "เจ้ายืนหยัดอยู่ได้ยังไง? เจ้าปลอมแปลงชื่อของเจ้าเหรอ?";
		npctalk "Hyasagand : เจ้ายืนหยัดอยู่ได้ยังไง? เจ้าปลอมแปลงชื่อของเจ้าเหรอ?",instance_npcname("Cult Shaman#evtman");
		next;
		mes "[Hyasagand]";
		mes "ช่างมันเถอะ นั่นไม่ใช่เรื่องสำคัญสำหรับข้าในตอนนี้";
		mes "ผู้ที่แทงทะลุหัวใจของเซอร์เพนท์กำลังใกล้เข้ามาแล้ว";
		npctalk "Hyasagand : ผู้ที่แทงทะลุหัวใจของเซอร์เพนท์กำลังใกล้เข้ามาแล้ว",instance_npcname("Cult Shaman#evtman");
		next;
		mes "[Hyasagand]";
		mes "Gaebolg... Gaebolg... คำสาปที่ผูกพันกับชื่อนั้นได้เริ่มต้นขึ้นแล้ว";
		npctalk "Hyasagand : Gaebolg... Gaebolg... คำสาปที่ผูกพันกับชื่อนั้นได้เริ่มต้นขึ้นแล้ว",instance_npcname("Cult Shaman#evtman");
		next;
		mes "[Hyasagand]";
		mes "เจตจำนงของศาสนจักรจะไหลผ่านสายเลือดที่แข็งแกร่งและแพร่กระจายไปทั่วโลก";
		mes "ตราบเท่าที่ Gaebolg ยังคงอยู่ มันจะคงอยู่ต่อไป";
		npctalk "Hyasagand : เจตจำนงของศาสนจักรจะไหลผ่านสายเลือดที่แข็งแกร่งและแพร่กระจายไปทั่วโลก ตราบเท่าที่ Gaebolg ยังคงอยู่ มันจะคงอยู่ต่อไป",instance_npcname("Cult Shaman#evtman");
		close2;
		cutin "",255;
		instance_event("#EP21_secret_altar","OnEvent",false);
	}
	end;
}

1@ep21a,53,252,3	script(DISABLED)	Cult Shaman#rgan_l	22359,{
	end;
	
OnSummon:
	if(get_instance_var("sec") > 13) end;
	specialeffect EF_ENTRY;
	monster get_instance_var("map$"),53,252,"Cult Shaman",22359,1,instance_npcname(strnpcinfo(0)) + "::OnSummon";
end;
}

1@ep21a,84,252,7	script(DISABLED)	Cult Shaman#rgan_r	22359,{
	end;

OnSummon:
	if(get_instance_var("sec") > 13) end;
	specialeffect EF_ENTRY;
	monster get_instance_var("map$"),84,252,"Cult Shaman",22359,1,instance_npcname(strnpcinfo(0)) + "::OnSummon";
end;
}

1@ep21a,68,252,0	script(DISABLED)	Cult Shaman#rgan_c	22360,{
	end;

OnSummon:
	if(get_instance_var("sec") > 13) end;
	specialeffect EF_ENTRY;
	monster get_instance_var("map$"),68,252,"Cult Shaman",22360,1,instance_npcname(strnpcinfo(0)) + "::OnSummon";
end;
}

1@ep21a,68,254,3	script(DISABLED)	Serpent Form#endman	22300,{
	if(is_party_leader() && get_instance_var("sec") == 14)
		instance_event("#EP21_secret_altar","OnEvent",false);
	end;
}

1@ep21a,68,250,1	script(DISABLED)	Tris#tris01	4_EP21_TRIS,{
	npctalk "...คำสาปเริ่มขึ้นแล้ว...";
	end;
}

1@ep21a,70,248,1	script(DISABLED)	Aurelie#arr01	4_EP21_AURELIE,{
	npctalk "..เราต้องฟังสิ่งที่แมวนั่นกำลังพูด";
	end;
}

1@ep21a,65,248,6	script(DISABLED)	Nillem#nillem03	4_M_NILLEM,{
	cutin "ep172_nillem03.png",0;
	mes "[Nillem]";
	mes "ไข่! ข้าสัมผัสได้ถึงพลังมหาศาลจากมัน! นั่นต้องเป็นแหล่งพลังงานแน่ๆ!";
	close3;
}

1@ep21a,64,246,7	script(DISABLED)	Lehar#lehar05	4_EP19_LEHAR,{
	cutin "ep19_lehar01.png",2;
	mes "[Lehar]";
	mes "ข้าไม่รู้ว่ามันเกิดอะไรขึ้นกันแน่ แต่เราไปตรวจสอบไข่ยักษ์นั่นก่อนเถอะ!";
	close3;
}

1@ep21a,71,253,3	script(DISABLED)	Nyar#nyar05	4_EP20_NYAR,{
	mes "[Nyar]";
	mes "เมี้ยว?";
	cutin "ep20_nyar04.png",1;
	close3;
}

1@ep21a,68,262,3	script(DISABLED)	Giant Egg#egg	4_POINT_BLUE,{
	if(get_instance_var("sec") < 16) end;
	if(isbegin_quest(18352) == 1){
		cutin "ep172_nillem02.png",1;
		mes "[Nillem]";
		mes "นี่เหรอ? นี่คือทั้งหมดจริงๆ เหรอ?";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "หะ.. ฮ่าฮ่า... แล้วเราควรทำยังไงต่อดีล่ะ?";
		mes "Lasgand อยู่ที่ไหน?";
		next;
		cutin "ep20_nyar02.png",1;
		mes "[Nyar]";
		mes "มโฮรวว ม้าว เมี้ยวว";
		next;
		cutin "ep172_nillem02.png",1;
		mes "[Nillem]";
		mes "ก่อนที่ Lasgand จะฟักตัวออกมางั้นเหรอ...?";
		mes "เจ้าจะบอกว่ามันดูดซับมานาไว้เพียงพอแล้วอย่างนั้นเหรอ?";
		mes "ดูเหมือนว่าจะเป็นอย่างนั้นนะ...";
		next;
		cutin "ep20_nyar03.png",1;
		mes "[Nyar]";
		mes "เมี้ยว";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "โอ้.. จริงเหรอ? สิ่งนั้นเมื่อกี้กำลังดูดซับมานาอยู่เหรอ?";
		mes "โชคดีนะที่ Nyar สามารถชิงมันกลับมาได้บางส่วน";
		next;
		cutin "ep21_aurelie01.png",1;
		mes "[Aurelie]";
		mes "ขอโทษนะ แต่ช่วยอธิบายให้เราฟังหน่อยได้ไหมว่ามันเกิดอะไรขึ้น?";
		next;
		cutin "ep172_nillem02.png",0;
		mes "[Nillem]";
		mes "โอ้ จริงด้วย! พวกเจ้าอยู่ที่นี่ด้วยนี่นา!";
		mes "เดี๋ยวสิ พวกเจ้ามาที่นี่ได้ยังไงหลังจากเก็บกวาดการต่อสู้ครั้งสุดท้ายเสร็จแล้ว?";
		next;
		cutin "ep21_aurelie02.png",1;
		mes "[Aurelie]";
		mes "....หืม เราแค่ตามรอยพลังงานมหาศาลมาน่ะ";
		mes "ดูเหมือนว่าพวกเจ้ากำลังทำอะไรบางอย่างเป็นความลับนะ";
		mes "เป้าหมายของพวกเจ้าตั้งแต่เริ่มต้น คือสิ่งนี้สินะ?";
		next;
		cutin "ep21_aurelie01.png",1;
		mes "[Aurelie]";
		mes "มีบางอย่างไม่ค่อยสมเหตุสมผล";
		mes "ไอ้เรื่องที่พูดถึงความทุกข์ทรมานชั่วนิรันดร์ต่อผู้ที่ใช้ชื่อ Gaebolg... นั่นมันเป็นคำสาปใช่ไหม?";
		next;
		mes "[Aurelie]";
		mes "เจ้าพวกคนบ้าพวกนั้น พวกมันเชี่ยวชาญเรื่องคำสาปที่ผูกติดกับชื่อ";
		mes "พวกเราเรียกตัวเองว่า Gaebolg เพราะฉะนั้นพวกมันเลยสาป Gaebolg ทั้งหมดเลยงั้นเหรอ?";
		next;
		cutin "ep19_lehar03.png",2;
		mes "[Lehar]";
		mes "อ่า... จริงด้วย คำสาปนั่น";
		mes "ตราบใดที่ Gaebolg ยังคงอยู่ Rgan ก็จะยังคงอยู่";
		mes "นี่น่าจะเกี่ยวกับ Lasgand สินะ";
		next;
		cutin "ep172_nillem01.png",0;
		mes "[Nillem]";
		mes "มันคือไข่สดๆ ที่ยังไม่ฟักใบนั้นน่ะ";
		mes "พวกมันบอกว่าตราบใดที่ Gaebolg ยังมีตัวตนอยู่บนแผ่นดินนี้ Lasgand จะไม่มีวันตาย";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "แล้วถ้าเราแค่ทำลายไข่ใบนั้นตอนนี้เลยล่ะ...?";
		mes "มันจะไม่ช่วยแก้ปัญหาเหรอ?!";
		next;
		cutin "ep20_nyar05.png",1;
		mes "[Nyar]";
		mes "มโฮรววว....";
		next;
		cutin "ep21_aurelie02.png",1;
		mes "[Aurelie]";
		mes "นั่นเป็นไปไม่ได้หรอก";
		mes "ถ้าเราทำลายไข่ใบนี้ ทุกคนที่นี่อาจจะตายหมดก็ได้";
		mes "การฝืนทำลายมันจะทำให้เกิดการระเบิดของมานาอย่างมหาศาล";
		next;
		cutin "ep172_nillem02.png",0;
		mes "[Nillem]";
		mes "นั่นคือเรื่องจริง";
		mes "แม้แต่พลังของ Jormungandr ที่เรายึดมาได้ก็คือมานาบริสุทธิ์";
		mes "มันคือมานาที่กลายพันธุ์และถูกบีบอัดโดยซากของ Rgan";
		next;
		mes "[Nillem]";
		mes "การฝังมันไว้ในดินนี้เพื่อให้มันถูกกัดเซาะไปช้าๆ คือทางเลือกที่ดีที่สุด";
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "แล้วเรื่องคำสาปล่ะ?";
		mes "Gaebolg รวมไปถึงทุกคนที่ต่อต้านลัทธินะ!";
		next;
		cutin "ep21_tris04.png",2;
		mes "[Tris]";
		mes "งั้นก็รวมมันไว้ที่ข้า ให้คำสาปพุ่งเป้ามาที่ข้าคนเดียว";
		mes "นั่นน่าจะแก้ปัญหาได้ใช่ไหม?";
		mes "ข้าจะยืนหยัดอยู่ข้างหน้าและทำให้แน่ใจว่าหอกที่มองไม่เห็นนั่นจะแทงเข้าหาเป้าหมายที่ถูกต้อง";
		next;
		cutin "ep21_aurelie01.png",0;
		mes "[Aurelie]";
		mes "เจ้าเข้าใจสิ่งที่ตัวเองกำลังพูดอยู่หรือเปล่า?";
		mes "เจ้าคือคนที่มีความสำคัญที่สุดในตอนนี้นะ เราต้องการจุดรวมพลังอื่น";
		next;
		cutin "ep21_tris11.png",2;
		mes "[Tris]";
		mes "ข้าไม่ได้โง่นะ รู้ไหม?";
		mes "ฟังดูเหมือนว่ามันมีผลชั่วนิรันดร์และดำรงอยู่ นั่นหมายความว่ามันต้องใช้เวลา";
		mes "คำสาปนี่ก็แค่กลยุทธ์ถ่วงเวลาเท่านั้นแหละ";
		next;
		cutin "ep21_tris03.png",2;
		mes "[Tris]";
		mes "เพราะฉะนั้นก็แค่พุ่งคำสาปมาที่ข้า แล้วรอดูว่าจะเกิดอะไรขึ้นเมื่อไข่นั่นฟักออกมา";
		mes "การบีบมันให้เหลือเพียงการต่อสู้ระหว่างข้ากับสิ่งนั้นคือทางเลือกที่ดีที่สุดของเราในตอนนี้";
		next;
		mes "[Tris]";
		mes "อีกอย่าง ข้าก็หลีกเลี่ยงคำสาปนี้ไม่ได้อยู่แล้ว";
		mes "พวกมันพูดกี่ครั้งแล้วล่ะ? คนที่แทงทะลุหัวใจของเซอร์เพนท์ ก็คือข้า...";
		next;
		cutin "ep21_tris02.png",2;
		mes "[Tris]";
		mes "ข้าคือคนที่เป็นคนกระตุ้นคำสาปนี้เอง";
		next;
		cutin "ep172_nillem01.png",0;
		mes "[Nillem]";
		mes "(มันควรจะเป็นแบบนี้จริงๆ เหรอ..? เจ้ารู้เรื่องนี้อยู่แล้วหรือเปล่า Lehar?)";
		next;
		cutin "ep19_lehar01.png",2;
		mes "[Lehar]";
		mes "(ข้าก็เพิ่งเคยได้ยินเป็นครั้งแรกเหมือนกัน)";
		next;
		cutin "ep21_aurelie01.png",0;
		mes "[Aurelie]";
		mes "Tris แผนของเจ้าคืออะไรกันแน่?";
		mes "เจ้ากำลังคิดจะเปลี่ยนชื่อเหรอ?";
		next;
		cutin "ep21_tris01.png",2;
		mes "[Tris]";
		mes "ใช่ เปลี่ยนมันซะ ชื่อไงล่ะ... แล้วชื่อ Tris Gaebolg ล่ะเป็นไง?";
		mes "อา ข้าลืมเพื่อนรักตลอดกาลของข้าไม่ลงหรอกนะ";
		mes "Tristan Gaebolg เป็นชื่อที่ฟังดูดีใช่ไหมล่ะ?";
		next;
		cutin "ep21_aurelie02.png",0;
		mes "[Aurelie]";
		mes "...ข้าเห็นแล้วว่าเจ้าจริงจัง";
		mes "เฮ้อ... ก็ได้ ข้าจะเฝ้าดูไข่ใบนี้จนกว่าสิ่งที่อยู่ข้างในจะเหี่ยวเฉาไปเอง";
		next;
		mes "[Aurelie]";
		mes "Leon พูดถูก";
		mes "เรามาผนึกไข่ใบนี้ไว้ใน Isgard ในดินแดนแห่ง Snow Flower กันเถอะ";
		next;
		cutin "ep20_nyar03.png",1;
		mes "[Nyar]";
		mes "กรรรรร เพอร์รรรร....";
		next;
		cutin "ep172_nillem01.png",0;
		mes "[Nillem]";
		mes "โอ้...ว้าว................";
		next;
		cutin "ep21_tris04.png",2;
		mes "[Tris]";
		mes "ต่อจากนี้ไป พวกเจ้าไม่ใช่ Gaebolg อีกต่อไป เข้าใจไหม?";
		mes "ในโลกนี้ Gaebolg จะมีเพียงแค่ข้าคนเดียว";
		next;
		cutin "ep21_tris03.png",2;
		mes "[Tris]";
		mes "ข้าจะใช้ชีวิตในฐานะ Gaebolg จนกว่าสิ่งนั้นจะสลายไป";
		mes "และเมื่อข้าตาย ลูกหลานของข้าหรือทายาทของพวกเขาจะรอเวลาที่จะโต้กลับ";
		next;
		cutin "ep21_tris09.png",2;
		mes "[Tris]";
		mes "พวกเจ้าทุกคน กลับไปในที่ที่พวกเจ้าจากมาเถอะ";
		mes "Aurelie กับข้าจะจัดการที่เหลือเอง";
		next;
		cutin "ep21_aurelie01.png",1;
		mes "[Aurelie]";
		mes "ขอบคุณสำหรับทุกอย่าง ถึงแม้จะเป็นช่วงเวลาสั้นๆ ก็ตาม";
		mes "ข้าจะไม่ไปส่งนะ ลาก่อน เพื่อนจากแดนไกล";
		next;
		cutin "ep19_lehar05.png",2;
		mes "[Lehar]";
		mes "เดี๋ยวสิ อะไรนะ? ง่ายๆ แบบนี้เลยเหรอ?";
		mes "รอเดี๋ยวก่อนสิ?";
		completequest 18352;
		setquest 18353;
		getitem "EP21_Wigner_Ticket",60;
		getexp 224735905,55709690;
		add_reputation_points(13,50);
		close2;
		cutin "",255;
		warp "jor_crk_p",108,105;
	}
	end;

OnInstanceInit:
	questinfo QTYPE_QUEST,QMARK_YELLOW,"isbegin_quest(18352) == 1";
end;
}
