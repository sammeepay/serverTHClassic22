// ภฮฤฺต๙ภป EUC-KRทฮ ภฏม๖วฯฑโ ภงวั วัฑนพ๎ นฎภๅ.
//===== rAthena Script =======================================
//= Instance from Main quest.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Instance of episode 19.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Main Quest: Step 70
jor_que,32,37,5	script	Lehar#i19ms21::ep19_jor_que_32_37	EP19_RGAN_D,5,5,{
	if (ep19_main == 59) {
		mes "[Lehar]";
		mes "คุณมาได้จังหวะพอดีเลย!!!";
		next;
		select( "มีอะไรเกิดขึ้นงั้นเหรอ?" );
		cloakoffnpcself( "ep19_jor_que_36_35" );
		npctalk "ฉันเคยอยู่ที่นี่", "ep19_jor_que_36_35", bc_self;
		mes "[Lazy]";
		mes "สถานการณ์ที่นี่เริ่มแย่ลงเรื่อยๆ แล้วล่ะ";
		next;
		mes "[Rgan]";
		mes "ได้โปรดอย่าทำร้ายข้าเลย... ข้าขอร้องล่ะ...";
		next;
		mes "[Lazy]";
		mes "คุณเห็นมันหรือยัง?";
		next;
		select( "ทำไมถึงเป็นแบบนี้? ทำไมทางเข้าอาคารถึงถูกปิดตายล่ะ?" );
		mes "[Aurelie]";
		mes "ไม่ใช่ว่ามีการบอกกันว่าพวก Rgan ที่มีระดับสูงกว่าระดับก้าวหน้าทั้งหมด ได้ย้ายไปอยู่ในเขตพื้นที่ระดับสูงแล้วไม่ใช่เหรอ?";
		next;
		mes "[Lazy]";
		mes "นั่นแหละ? คือสิ่งที่ฉันได้ยินมาเหมือนกัน";
		mes "ดังนั้นฉันก็เลยกำลังจะไปเข้าร่วมการประชุม แล้วอะไรล่ะที่มาขวางทางอยู่นี่?";
		next;
		mes "[Lehar]";
		mes "นั่นแหละครับ ปัญหาคือพวก Rgan เหล่านี้กำลังจัดการพื้นที่ในส่วนบนที่พวก Rgan ระดับสูงย้ายเข้าไปอย่างเร่งรีบ แต่ดูเหมือนว่าจู่ๆ พวก Rgan ระดับสูงเหล่านั้นจะเข้าโจมตีที่ไหนสักแห่ง";
		next;
		mes "[Aurelie]";
		mes "อะไรนะ? จู่ๆ ก็นะเหรอ?";
		next;
		mes "[Lehar]";
		mes "จากที่ผมได้ยินมา มันไม่เหมือนกับพวก Rgan ระดับสูงทั่วไปที่คุณเคยเจอหรอกครับ";
		next;
		select( "ฉันคุ้นหูเรื่องนี้มาจากที่ไหนสักแห่งแฮะ" );
		npctalk "ไม่ใช่เหรอ?", "ep19_jor_que_34_38", bc_self;
		mes "[Lehar]";
		mes "นั่นคือสาเหตุที่พวกเขาปิดทางเข้าไว้ เพื่อไม่ให้พวก Rgan ระดับสูงกว่าสามารถเข้าออกไปมาได้";
		next;
		mes "[Aurelie]";
		mes "ถ้าอย่างนั้น ทั้ง Bagot, Rasgand และกลุ่ม Rgan ระดับสูงคนอื่นๆ ก็อยู่ที่นั่นกันหมดเลยงั้นเหรอ?";
		next;
		mes "[Lehar]";
		mes "ผมคิดว่านั่นคือแผนในตอนแรก ผมคิดว่าผมน่าจะทำอะไรได้บ้างถ้าเราฝ่าตรงนี้เข้าไปได้ก่อน";
		next;
		select( "คุณกำลังพยายามจะทำอะไรกันแน่?" );
		mes "[Lazy]";
		mes "บางทีอาจจะเป็นเรื่องที่คุณไม่สามารถพูดต่อหน้าหลานชายตัวน้อยของคุณได้งั้นสิ?";
		next;
		mes "[Aurelie]";
		mes "ถ้าคุณใช้ประโยชน์จากสิ่งนี้ให้ดี... คุณจะสามารถรวมพวกมันไว้ในที่เดียวกันแล้วกำจัดพวกมันทั้งหมดได้ในคราวเดียว";
		next;
		cloakoffnpcself( "ep19_jor_que_37_36" );
		mes "[Horr]";
		mes "นั่นคือเหตุผลที่ฉันเรียกหน่วยสนับสนุนมา";
		next;
		cloakoffnpcself( "ep19_jor_que_40_34" );
		cloakoffnpcself( "ep19_jor_que_35_34" );
		sleep2 500;
		npctalk "สถานการณ์เป็นยังไงบ้าง?", "ep19_jor_que_40_34", bc_self;
		npctalk "มันเริ่มขึ้นแล้วไม่ใช่เหรอ?", "ep19_jor_que_35_34", bc_self;
		sleep2 500;
		cloakoffnpcself( "ep19_jor_que_34_32" );
		sleep2 500;
		npctalk "พวกคุณมาสายเหรอ?", "ep19_jor_que_34_32", bc_self;
		sleep2 500;
		cloakoffnpcself( "ep19_jor_que_38_33" );
		cloakoffnpcself( "ep19_jor_que_37_31" );
		sleep2 500;
		npctalk "กองกำลัง! มาถึงแล้ว!", "ep19_jor_que_38_33", bc_self;
		npctalk "กองกำลัง! มาถึงแล้ว!", "ep19_jor_que_37_31", bc_self;
		mes "[Lazy]";
		mes "เอ้า มาสิ เข้าแถวกันให้เรียบร้อย ขอสรุปสั้นๆ นะ";
		next;
		mes "[Lazy]";
		mes "Rasgand, Bagot และพวก Lgan ที่อยู่เหนือระดับทั่วไป ถูกกล่าวว่าได้ไปรวมตัวกันที่นั่นตามคำสั่งของ Rasgand";
		mes "ทุกคนเข้าใจถึงตรงนี้ใช่ไหม?";
		next;
		mes "[Lazy]";
		mes "อีกอย่างหนึ่ง คือเบื้องหลังกำแพงนี้เป็นที่พักอาศัยของพวก Rgan ระดับสูง";
		next;
		mes "[Lazy]";
		mes "อย่างไรก็ตาม Illusion ได้ปล่อยพวกมันออกมาหลังจากทำบางอย่างที่ดูเหมือนจะเป็นการดัดแปลงร่างกายให้กับพวก Rgan ระดับสูงเหล่านั้น";
		next;
		mes "[Voglinde]";
		mes "มันจะกลายเป็นสภาวะคลุ้มคลั่ง แต่การควบคุมนั้นจะยังไม่สมบูรณ์";
		mes "สรุปคือ คุณหมายความว่าเราต้องกำจัด Rgan ทางด้านนั้นเพื่อที่จะไปพบกับ Rasgand ใช่ไหม?";
		next;
		mes "[Horr]";
		mes "พังทางเข้านั่นซะ รวมกลุ่มแล้วเข้าไปทีละคน พวกเรา Iwin จะเป็นคนนำทางเอง";
		next;
		mes "[Lehar]";
		mes "เตรียมตัวพร้อมหรือยัง? เข้าไปได้เลยตอนนี้";
		npctalk "ไปเจอกันข้างใน", "ep19_jor_que_37_36", bc_self;
		next;
		cloakonnpcself( "ep19_jor_que_37_36" );
		sleep2 500;
		cloakonnpcself( "ep19_jor_que_38_33" );
		cloakonnpcself( "ep19_jor_que_37_31" );
		mes "[Aurelie]";
		mes "คราวนี้ตาฉันแล้ว Leon ไปกันเถอะ";
		next;
		cloakonnpcself( "ep19_jor_que_35_34" );
		cloakonnpcself( "ep19_jor_que_36_35" );
		mes "[Voglinde]";
		mes "Lehar คุณควรจะอยู่ที่นี่เพื่อรอดูสถานการณ์และรอต่อไปนะ Miriam เราไปกันเถอะ?";
		next;
		npctalk "ไม่ต้องห่วง ฉันจะรออยู่กับคุณเอง", "ep19_jor_que_34_38", bc_self;
		sleep2 500;
		cloakonnpcself( "ep19_jor_que_40_34" );
		cloakonnpcself( "ep19_jor_que_34_32" );
		mes "[Lazy]";
		mes "คราวนี้ถึงตาของนักผจญภัยแล้ว ฉันกำลังเตรียมการอยู่";
		erasequest 17647;
		setquest 17648;
		ep19_main = 60;
		close;
	}
	if (ep19_main == 60) {
		.@md_name$ = "Confused Snake's Nest";
		mes "[Lehar]";
		mes "คุณพร้อมหรือยัง? เมื่อคุณพร้อมแล้ว ผมจะส่งสัญญาณเพื่อยืนยันว่าคนที่เข้าไปก่อนหน้านี้ได้เคลื่อนที่ไปแล้ว";
		next;
		switch( select( "ใช่ ส่งสัญญาณเลย", "ฉันจะเข้าไป", "ฉันขอเตรียมตัวก่อน" ) ) {
		case 1:
			mes "[Lehar]";
			mes "ผมจะเปิดทางเข้าทันทีที่มีสัญญาณจราจรเข้ามา โปรดรอสักครู่";
			instance_create(.@md_name$);
			close;
		case 2:
			switch( instance_enter(.@md_name$) ) {
			case IE_OTHER:
				mes "เกิดข้อผิดพลาดที่ไม่รู้จัก";
				close;
			case IE_NOINSTANCE:
				mes "[Lehar]";
				mes "คุณยังไม่พร้อมงั้นเหรอ? ผมจะรอคุณ";
				close;
			case IE_NOMEMBER:
				mes "[Lehar]";
				mes "^ff0000เฉพาะสมาชิกในปาร์ตี้เท่านั้นที่สามารถเข้าสู่ Memorial Dungeon ได้^000000";
				close;
			case IE_OK:
				mes "[Lehar]";
				mes "ผมเพิ่งได้รับสัญญาณว่าสามารถเข้าไปได้แล้ว! คุณเข้าไปได้เลย!";
				mapannounce "jor_que", "ปฏิบัติการ 3 " + strcharinfo(0) + " คุณและปาร์ตี้ของคุณ " + getpartyname( getcharid(1) ) + " ทั้งหมดได้เข้าสู่ Confused Snake's Nest", bc_map, 0xFF99;
				// warp "1@jorchs",272,20;
				end;
			}
			end;
		case 3:
			mes "[Lehar]";
			mes "โปรดบอกผมเพื่อให้ส่งสัญญาณเมื่อคุณพร้อม ไม่อย่างนั้นปฏิบัติการข้างในอาจจะยุ่งเหยิงได้";
			close;
		}
		end;
	}
	end;

OnTouch:
	end;

OnInit:
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(17647) == 1" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(17648) == 1" );
	cloakonnpc();
	end;
}

1@jorchs,1,1,3	script	ep19_jorchs_main	-1,{
	end;
OnInstanceInit:
	'map_jorchs$ = instance_mapname("1@jorchs");

	'event = 0;

	'area_1[0] = 'area_1[1] = 0;
	'special_area_1[0] = 'special_area_1[1] = 0;
	'area_2[0] = 'area_2[1] = 0;
	'special_area_2[0] = 'special_area_2[1] = 0;
	'count_area_3_2 = 0;
	'area_4[0] = 'area_4[1] = 'area_4[2] = 0;
	'area_5[0] = 'area_5[1] = 'area_5[2] = 0;
	'ep19_jorchs_area_5_2[0] = 'ep19_jorchs_area_5_2[1] = 'ep19_jorchs_area_5_2[2] = 0;

	// มอนสเตอร์พิเศษ Area 1
	disablenpc instance_npcname("ep19_1jorchs_249_79_A");
	disablenpc instance_npcname("ep19_1jorchs_114_20_A");
	disablenpc instance_npcname("ep19_1jorchs_249_79_B");
	disablenpc instance_npcname("ep19_1jorchs_114_20_B");

	// มอนสเตอร์พิเศษ Area 2
	disablenpc instance_npcname("ep19_1jorchs_23_57_A");
	disablenpc instance_npcname("ep19_1jorchs_21_97_A");
	disablenpc instance_npcname("ep19_1jorchs_23_57_B");
	disablenpc instance_npcname("ep19_1jorchs_21_97_B");

	// พื้นที่สุดท้าย 1
	disablenpc instance_npcname("ep19_1jorchs_140_57");
	disablenpc instance_npcname("ep19_1jorchs_142_58");
	disablenpc instance_npcname("ep19_1jorchs_143_56");
	disablenpc instance_npcname("ep19_1jorchs_143_60");
	disablenpc instance_npcname("ep19_1jorchs_145_56");
	disablenpc instance_npcname("ep19_1jorchs_145_59");
	disablenpc instance_npcname("ep19_1jorchs_146_58");

	setcell 'map_jorchs$,139,59,139,59,cell_walkable,0;
	setcell 'map_jorchs$,138,58,138,58,cell_walkable,0;
	setcell 'map_jorchs$,137,57,137,57,cell_walkable,0;

	// พื้นที่สุดท้าย 2
	disablenpc instance_npcname("ep19_1jorchs_112_119");
	disablenpc instance_npcname("ep19_1jorchs_112_116");
	disablenpc instance_npcname("ep19_1jorchs_118_127");
	disablenpc instance_npcname("ep19_1jorchs_122_126");
	disablenpc instance_npcname("ep19_1jorchs_124_124");

	setcell 'map_jorchs$,120,129,120,129,cell_walkable,0;
	setcell 'map_jorchs$,121,128,121,128,cell_walkable,0;
	setcell 'map_jorchs$,122,127,122,127,cell_walkable,0;
	setcell 'map_jorchs$,123,127,123,127,cell_walkable,0;
	setcell 'map_jorchs$,124,126,124,126,cell_walkable,0;
	setcell 'map_jorchs$,125,125,125,125,cell_walkable,0;
	setcell 'map_jorchs$,126,125,126,125,cell_walkable,0;

	// Area 3
	disablenpc instance_npcname("ep19_1jorchs_249_170");
	disablenpc instance_npcname("ep19_1jorchs_251_171");
	disablenpc instance_npcname("#e19p01");
	disablenpc instance_npcname("ep19_1jorchs_219_148");

	setcell 'map_jorchs$,242,174,242,174,cell_walkable,0;
	setcell 'map_jorchs$,244,176,244,176,cell_walkable,0;
	setcell 'map_jorchs$,247,176,247,176,cell_walkable,0;
	setcell 'map_jorchs$,248,176,248,176,cell_walkable,0;
	setcell 'map_jorchs$,243,175,243,175,cell_walkable,0;
	setcell 'map_jorchs$,245,176,245,176,cell_walkable,0;
	setcell 'map_jorchs$,246,176,246,176,cell_walkable,0;
	setcell 'map_jorchs$,249,177,249,177,cell_walkable,0;

	// Area 4
	disablenpc instance_npcname("ep19_1jorchs_191_203");
	disablenpc instance_npcname("ep19_1jorchs_226_244");
	disablenpc instance_npcname("ep19_1jorchs_151_179");
	disablenpc instance_npcname("ep19_1jorchs_192_206");

	setcell 'map_jorchs$,140,183,140,183,cell_walkable,0;
	setcell 'map_jorchs$,140,182,140,182,cell_walkable,0;
	setcell 'map_jorchs$,139,181,139,181,cell_walkable,0;
	setcell 'map_jorchs$,139,180,139,180,cell_walkable,0;
	setcell 'map_jorchs$,138,179,138,179,cell_walkable,0;
	setcell 'map_jorchs$,138,178,138,178,cell_walkable,0;

	// Area 5
	disablenpc instance_npcname("#e19p03");
	disablenpc instance_npcname("ep19_1jorchs_39_251");
	disablenpc instance_npcname("ep19_1jorchs_37_274");
	disablenpc instance_npcname("#19m60");

	setcell 'map_jorchs$,34,255,43,255,cell_walkable,0;
	end;
}

// Area 1

// เควสหลัก: ขั้นที่ 71
1@jorchs,261,34,3	script	Aurelie#19m00	4_EP19_AURELIE,5,5,{
	end;
OnTouch:
	if ('event != 0)
		end;
	'event = 1;	// หมายเหตุ: ผู้เล่นสามารถเคลื่อนที่ได้
	npctalk "ไม่มีเหตุผลที่จะต้องแสร้งทำเป็นพวก Ragan ที่นี่อีกแล้ว";
	sleep 1000;
	npctalk "เมื่อหน่วยของ Iwin บุกเข้าไปในพื้นที่แล้ว พวกเขาจะจัดการพวก Ragan ที่อยู่ใกล้เคียงทั้งหมดเอง", instance_npcname("ep19_1jorchs_258_32");
	sleep 1500;
	npctalk "ตอนนี้พวกเราต้องเข้าไปให้ลึกกว่าเดิมและทำความสะอาดพื้นที่ซะ", instance_npcname("ep19_1jorchs_257_35");
	sleep 3000;
	npctalk "เป้าหมายคือการคุ้มกัน Rasgand และ Bagot รวมถึง Heart of Ymir";
	sleep 3000;
	npctalk "คุณต้องเข้าไปและปิดกั้นเส้นทางหลบหนี ความเร็วเป็นเรื่องสำคัญมาก", instance_npcname("ep19_1jorchs_258_32");
	sleep 3000;
	npctalk "มันมีทางเข้าเดียวก็จริง แต่เราต้องค้นหาให้ทั่วเพื่อดูว่ามีทางอื่นที่เราไม่รู้อีกไหม";
	sleep 3000;
	npctalk "ฉันต้องกำจัดพวก Rgan ก่อนเพื่อตามหาร่องรอยของพวกมันงั้นเหรอ?", instance_npcname("ep19_1jorchs_258_32");
	sleep 3000;
	npctalk "ตามคำบอกเล่าของหน่วยล่วงหน้า พวกมันแตกต่างจาก Rgan ทั่วไป ดังนั้นคุณควรเตรียมตัวให้ดี", instance_npcname("ep19_1jorchs_258_32");
	sleep 3000;
	npctalk "ระวังด้วย คู่ต่อสู้ของคุณคือพวก Rgan ที่เชี่ยวชาญด้านเวทมนตร์";
	sleep 3000;
	npctalk "โชคดีนะทุกคน!", instance_npcname("ep19_1jorchs_258_32");
	sleep 500;
	disablenpc();
	disablenpc instance_npcname("ep19_1jorchs_260_35");
	disablenpc instance_npcname("ep19_1jorchs_256_34");
	disablenpc instance_npcname("ep19_1jorchs_257_35");
	disablenpc instance_npcname("ep19_1jorchs_258_32");
	disablenpc instance_npcname("ep19_1jorchs_256_31");
	disablenpc instance_npcname("ep19_1jorchs_259_31");
	donpcevent instance_npcname("ep19_jorchs_area_1") + "::OnStart";
	end;
}
1@jorchs,256,31,7	duplicate(dummy_npc)	Iwin#19m00::ep19_1jorchs_256_31	EP19_NPC_IWIN_SG
1@jorchs,256,34,5	duplicate(dummy_npc)	Miriam#19m00::ep19_1jorchs_256_34	4_EP18_MIRIAM
1@jorchs,257,35,5	duplicate(dummy_npc)	Voglinde#19m00::ep19_1jorchs_257_35	4_EP19_VOGLINDE
1@jorchs,258,32,7	duplicate(dummy_npc)	Horr#19m00::ep19_1jorchs_258_32	EP19_NPC_IWIN_SR
1@jorchs,259,31,7	duplicate(dummy_npc)	Iwin#19m01::ep19_1jorchs_259_31	EP19_NPC_IWIN_SG
1@jorchs,260,35,3	duplicate(dummy_npc)	Leon#19m00::ep19_1jorchs_260_35	4_EP19_LEON


// เควสหลัก: ขั้นที่ 72
1@jorchs,249,79,7	script	Rgan the Scared#19m00::ep19_1jorchs_249_79_A	EP19_RE_RGAN_E,5,5,{
	end;
OnTouch:
	// หมายเหตุ: ไม่จำเป็นต้องฆ่ามอนสเตอร์ในพื้นที่เพื่อกระตุ้นเหตุการณ์
	// หมายเหตุ: ผู้เล่นสามารถเคลื่อนที่ได้ในขณะที่ NPC กำลังพูด
	disablenpc();
	donpcevent instance_npcname("ep19_1jorchs_249_79_B") + "::OnEvent";
	end;
}

1@jorchs,249,79,7	script	Rgan the Scared#19m50::ep19_1jorchs_249_79_B	EP19_RE_RGAN_E,{
	end;
OnEvent:
	enablenpc();
	npctalk "ข้ากลัวเหลือเกิน... ทำไมร่างกายของข้าถึงเป็นแบบนี้?";
	sleep 1500;
	npctalk "นั่นมัน... มนุษย์งั้นเหรอ??? ทั้งหมดนี่เป็นฝีมือพวกแกใช่ไหม?? พวกแกทำอะไรกับข้ากันแน่?!";
	sleep 200;
	npctalk "เจ้าพวกมนุษย์ชั้นต่ำ! เร็วเข้า คืนร่างเดิมให้ข้าเดี๋ยวนี้...! อ๊าก...!!";
	sleep 500;
	specialeffect EF_FOOD02;
	sleep 500;
	disablenpc();
	monster 'map_jorchs$,249,79,"Rgan the Scared",21534,1, instance_npcname("ep19_jorchs_area_1") + "::OnSpecialMob1";	// EP19_MD_RGAN_E
	end;
}


// เควสหลัก: ขั้นที่ 73
1@jorchs,114,20,3	script	Rgan the Scared#19m01::ep19_1jorchs_114_20_A	EP19_RE_RGAN_E,5,5,{
	end;
OnTouch:
	disablenpc();
	donpcevent instance_npcname("ep19_1jorchs_114_20_B") + "::OnEvent";
	end;
}

1@jorchs,114,20,3	script	Rgan the Scared#19m51::ep19_1jorchs_114_20_B	EP19_RE_RGAN_E,{
	end;
OnEvent:
	enablenpc();
	npctalk "ข้า... ข้าคงไม่เป็นแบบนั้นไปด้วยใช่ไหม?...?";
	sleep 2000;
	npctalk "ข้าใช้ชีวิตมาอย่างดีแท้ๆ พวกมนุษย์ทำบ้าอะไรกับข้ากัน...";
	sleep 2000;
	npctalk "อา... มันหนาวเหลือเกิน...";
	sleep 2500;
	npctalk "...?! มนุษย์? มนุษย์?? มนุษย์คนที่ทำให้ข้าเป็นแบบนี้ใช่ไหม???";
	sleep 500;
	specialeffect EF_FOOD02;
	disablenpc();
	monster 'map_jorchs$,114,20,"Rgan the Scared",21534,1, instance_npcname("ep19_jorchs_area_1") + "::OnSpecialMob2";	// EP19_MD_RGAN_E
	end;
}


1@jorchs,1,1,3	script	ep19_jorchs_area_1	-1,{
	end;
OnStart:
	enablenpc instance_npcname("ep19_1jorchs_249_79_A");	// กระตุ้นการเกิดของมอนสเตอร์พิเศษ
	enablenpc instance_npcname("ep19_1jorchs_114_20_A");

	.@event$ = instance_npcname("ep19_jorchs_area_1") + "::OnNormalMob";
	monster 'map_jorchs$,237,61,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,221,37,"Divided Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,242,66,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,204,38,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,181,48,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,135,29,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,124,26,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	end;

OnNormalMob:
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_1") + "::OnNormalMob" ) > 0)
		end;
	mapannounce 'map_jorchs$, "รอด้วยทุกคน ฉันต้องจัดการบางอย่างก่อน", bc_map, 0xFFCC;
	'area_1[0] = 1;
	donpcevent instance_npcname("ep19_jorchs_area_1") + "::OnAreaClear";
	end;

OnSpecialMob1: callsub( S_Check, 0 );
OnSpecialMob2: callsub( S_Check, 1 );
S_Check:
	.@current_area = getarg(0);
	.@other_area = !.@current_area;

	'special_area_1[.@current_area] = 1;
	if ('special_area_1[.@other_area] == 1) {	// มอนสเตอร์พิเศษทั้ง 2 ตัวตายแล้ว
		'special_area_1[0] = 'special_area_1[1] = 2;	// เพื่อความแน่ใจ
		'area_1[1] = 1;
		mapannounce 'map_jorchs$, "คุณควรจะค้นหาให้ละเอียดกว่านี้นะ", bc_map, 0xFFCC;
		donpcevent instance_npcname("ep19_jorchs_area_1") + "::OnAreaClear";
		end;
	}
	end;

OnAreaClear:	// ตรวจสอบว่าพื้นที่เคลียร์หรือยัง
	if ('area_1[0] == 0 || 'area_1[1] == 0)
		end;
	if ('event != 1)
		end;
	'area_1[0] = 'area_1[1] = 0;	// เพื่อความแน่ใจ
	'event = 2;

	// บทสนทนาขั้นตอนสุดท้ายของ Area 1
	disablenpc instance_npcname("ep19_1jorchs_140_58");
	disablenpc instance_npcname("ep19_1jorchs_140_56");

	enablenpc instance_npcname("ep19_1jorchs_140_57");
	enablenpc instance_npcname("ep19_1jorchs_142_58");
	enablenpc instance_npcname("ep19_1jorchs_143_56");
	enablenpc instance_npcname("ep19_1jorchs_143_60");
	enablenpc instance_npcname("ep19_1jorchs_145_56");
	enablenpc instance_npcname("ep19_1jorchs_145_59");
	enablenpc instance_npcname("ep19_1jorchs_146_58");
	end;
}


// เควสหลัก: ขั้นที่ 74
1@jorchs,140,58,5	script	Iwin#19m13::ep19_1jorchs_140_58	EP19_NPC_IWIN_SB,5,5,{
	end;
OnTouch:
	if ('event < 2) {
		npctalk "พ้นจากตรงนี้ไป ยังไม่มีทีมสำรวจถูกส่งออกไปเลย", instance_npcname("ep19_1jorchs_140_56");
		sleep 1500;
		npctalk "เราจะรวบรวมข้อมูลและรวมตัวกันที่นี่ก่อนจะเข้าไป ดังนั้นขอให้โฟกัสไปที่การสำรวจรอบๆ พื้นที่นี้ก่อน";
		end;
	}
	end;
}
1@jorchs,140,56,5	duplicate(dummy_npc)	Iwin#19m12::ep19_1jorchs_140_56	EP19_NPC_IWIN_SB


1@jorchs,142,58,5	script	Horr#19m10::ep19_1jorchs_142_58	EP19_NPC_IWIN_SR,5,5,{
	end;
OnTouch:
	if ('event != 2)
		end;
	'event = 3;
	npctalk "นี่มัน Rgan บ้าอะไรกัน!! ฉันไม่เคยเห็น Rgan แบบนี้มาก่อนเลยในชีวิต!", instance_npcname("ep19_1jorchs_142_58");
	sleep 2500;
	npctalk "ดูเหมือนจะเป็น Rgan ระดับกลาง แต่ฉันยังหาข้อสรุปไม่ได้ เลยยังไม่ค่อยแน่ใจเท่าไหร่", instance_npcname("ep19_1jorchs_146_58");
	sleep 2000;
	npctalk "ทำไม Rgan ระดับกลางถึงเป็นแบบนี้? ไม่ใช่ว่ามันคือระดับสูงสุดหรอกเหรอ?", instance_npcname("ep19_1jorchs_145_59");
	sleep 2500;
	npctalk "ส่วนใหญ่พวก Illusion จะทำการดัดแปลงร่างกาย ฉันเลยคิดว่ามันน่าจะเกี่ยวข้องกับเรื่องนั้น", instance_npcname("ep19_1jorchs_145_56");
	sleep 2000;
	npctalk "ไม่ใช่ว่าการดัดแปลงควรจะทำให้มันดีขึ้นหรอกเหรอ? แต่ไม่ว่าจะมองยังไงพวกนี้มัน...", instance_npcname("ep19_1jorchs_142_58");
	sleep 2500;
	npctalk "พวกมันคงถูกหลอกมาติดกับเรื่องพวกนี้ หรือไม่ก็ถูกใช้แล้วทิ้งนั่นแหละ", instance_npcname("ep19_1jorchs_146_58");
	sleep 2000;
	npctalk "มันไม่ปกติเลย เพราะงั้นเข้าไปข้างในกันเถอะ", instance_npcname("ep19_1jorchs_143_56");
	sleep 3000;
	npctalk "ที่นี่มันซับซ้อน ทางที่ดีเราควรแยกย้ายกันค้นหา", instance_npcname("ep19_1jorchs_142_58");
	sleep 3000;
	npctalk "พวก Rgan ที่ Rasgand รวบรวมมา ส่วนใหญ่น่าจะอยู่ในที่พักของ Rasgand นั่นแหละ", instance_npcname("ep19_1jorchs_145_56");
	sleep 3000;
	npctalk "Bagot เองก็ถูกนำไปวิจัยด้วย เพราะงั้นฉันต้องไปเช็คที่ที่พักและห้องทดลองของพวก Illusion ด้วย", instance_npcname("ep19_1jorchs_142_58");
	sleep 2500;
	npctalk "มันน่าจะอยู่ลึกที่สุดข้างในนั่นแหละ", instance_npcname("ep19_1jorchs_146_58");
	sleep 2500;
	npctalk "พวกแกกำลังทำบ้าอะไรกันอยู่เนี่ย?", instance_npcname("ep19_1jorchs_145_59");
	setcell 'map_jorchs$,139,59,139,59,cell_walkable,1;
	setcell 'map_jorchs$,138,58,138,58,cell_walkable,1;
	setcell 'map_jorchs$,137,57,137,57,cell_walkable,1;
	disablenpc instance_npcname("ep19_1jorchs_140_57");
	disablenpc instance_npcname("ep19_1jorchs_142_58");
	disablenpc instance_npcname("ep19_1jorchs_143_56");
	disablenpc instance_npcname("ep19_1jorchs_143_60");
	disablenpc instance_npcname("ep19_1jorchs_145_56");
	disablenpc instance_npcname("ep19_1jorchs_145_59");
	disablenpc instance_npcname("ep19_1jorchs_146_58");
	donpcevent instance_npcname("ep19_jorchs_area_2_A") + "::OnStart";	// การเกิดของมอนสเตอร์
	end;
}
1@jorchs,143,60,5	duplicate(ep19_1jorchs_142_58)	Iwin#19m11::ep19_1jorchs_143_60	EP19_NPC_IWIN_SG,5,5

1@jorchs,140,57,5	duplicate(dummy_npc)	Iwin#19m10::ep19_1jorchs_140_57	EP19_NPC_IWIN_SG
1@jorchs,143,56,1	duplicate(dummy_npc)	Voglinde#19m10::ep19_1jorchs_143_56	4_EP19_VOGLINDE
1@jorchs,145,56,1	duplicate(dummy_npc)	Miriam#19m10::ep19_1jorchs_145_56	4_EP18_MIRIAM
1@jorchs,145,59,3	duplicate(dummy_npc)	Aurelie#19m10::ep19_1jorchs_145_59	4_EP19_AURELIE
1@jorchs,146,58,3	duplicate(dummy_npc)	Leon#19m10::ep19_1jorchs_146_58	4_EP19_LEON




// Area 2
1@jorchs,1,1,3	script	ep19_jorchs_area_2_A	-1,{
	end;
OnStart:
	enablenpc instance_npcname("ep19_1jorchs_23_57_A");	// เพื่อเกิดมอนสเตอร์พิเศษ
	enablenpc instance_npcname("ep19_1jorchs_21_97_A");

	.@event$ = instance_npcname("ep19_jorchs_area_2_A") + "::OnNormalMob";
	monster 'map_jorchs$,96,64,"Troubled Ragan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,75,23,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,51,45,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,39,76,"Uncomfortable RGAN","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,31,59,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,26,90,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,119,80,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,118,103,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	end;

OnNormalMob:
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_2_A") + "::OnNormalMob" ) > 0)
		end;
	mapannounce 'map_jorchs$, "ว้าว!!! นี่มันอะไรกันเนี่ย!!!", bc_map, 0xFFCC;
	'area_2[0] = 1;
	donpcevent instance_npcname("ep19_jorchs_area_2_A") + "::OnAreaClear";
	end;

OnSpecialMob1: callsub( S_Check, 0 );
OnSpecialMob2: callsub( S_Check, 1 );
S_Check:
	.@current_area = getarg(0);
	.@other_area = !.@current_area;

	'special_area_2[.@current_area] = 1;
	if ('special_area_2[.@other_area] == 1) {
		'special_area_2[0] = 'special_area_2[1] = 0;
		'area_2[1] = 1;
		mapannounce 'map_jorchs$, "ยังมีพวก Rgan เหลืออยู่ในพื้นที่ค้นหา เพราะงั้นเรารีบจัดการพวกมันซะดีกว่า", bc_map, 0xFFCC;
		donpcevent instance_npcname("ep19_jorchs_area_2_A") + "::OnAreaClear";
		end;
	}
	end;

OnAreaClear:	// ตรวจสอบพื้นที่เคลียร์หรือยัง
	if ('area_2[0] == 0 || 'area_2[1] == 0)
		end;
	if ('event != 3)
		end;
	'area_2[0] = 'area_2[1] = 0;
	'event = 4;

	enablenpc instance_npcname("ep19_1jorchs_112_119");
	enablenpc instance_npcname("ep19_1jorchs_112_116");
	end;
}


// เควสหลัก: ขั้นที่ 75
1@jorchs,23,57,5	script	Rgan with feet#19m10::ep19_1jorchs_23_57_A	EP19_RE_RGAN_E,5,5,{
	end;
OnTouch:
	disablenpc();
	donpcevent instance_npcname("ep19_1jorchs_23_57_B") + "::OnEvent";
	end;
}

1@jorchs,23,57,5	script	Rgan with feet tied#19m60::ep19_1jorchs_23_57_B	EP19_RE_RGAN_E,{
	end;
OnEvent:
	enablenpc();
	npctalk "ทุกคนหายไปไหนกันหมด... พวกเขาบอกให้ข้าเฝ้าไข่ไว้ แต่ข้าขยับออกไปแบบนี้ไม่ได้...";
	sleep 2000;
	npctalk "มนุษย์งั้นเหรอ? มนุษย์ไม่ได้รับอนุญาตให้เข้ามาที่นี่! ออกไปซะ!";
	sleep 1500;
	npctalk "บัดซบ!! ร่างกายแก...! มันคืออาหารเสริมงั้นเหรอ...? ไม่ใช่หรือไง? ?...!!";
	sleep 1000;
	specialeffect EF_FOOD02;
	disablenpc();
	monster 'map_jorchs$,23,57,"Rgan with Feet",21534,1, instance_npcname("ep19_jorchs_area_2_A") + "::OnSpecialMob1";	// EP19_MD_RGAN_E
	end;
}


// เควสหลัก: ขั้นที่ 76
1@jorchs,21,97,5	script	Rgan with feet#19m11::ep19_1jorchs_21_97_A	EP19_RE_RGAN_E,5,5,{
	end;
OnTouch:
	disablenpc();
	donpcevent instance_npcname("ep19_1jorchs_21_97_B") + "::OnEvent";
	end;
}

1@jorchs,21,97,5	script	Rgan with feet#19m61::ep19_1jorchs_21_97_B	EP19_RE_RGAN_E,5,5,{
	end;
OnEvent:
	enablenpc();
	npctalk "ข้าต้องรีบไปที่ใจกลางเมืองให้เร็วที่สุด แต่จะฝ่าไปได้ยังไงกัน? ข้าได้ยินเสียงแปลกๆ มาสักพักแล้ว...";
	sleep 1500;
	npctalk "เมื่อไหร่เจ้าจะแปลงร่างล่ะ? เจ้าต้องแปลงร่างก่อนนะถึงจะบุกฝ่าไปได้";
	sleep 2000;
	specialeffect EF_FOOD02;
	npctalk "โอ้! ในที่สุด!!! โอกาสที่ข้าจะได้เป็นระดับ Bishop มาถึงแล้ว...!!! ?!!!";
	disablenpc();
	monster 'map_jorchs$,21,97,"Rgans with feet",21534,1, instance_npcname("ep19_jorchs_area_2_A") + "::OnSpecialMob2";	// EP19_MD_RGAN_E
	end;
}


// เควสหลัก: ขั้นที่ 77
1@jorchs,120,127,3	duplicate(dummy_npc)	Smashed Rgan#19m21::ep19_1jorchs_120_127	EP19_MD_RGAN_D
1@jorchs,120,129,1	duplicate(dummy_npc)	Warped Rgan#19m20::ep19_1jorchs_120_129	EP19_MD_RGAN_D
1@jorchs,122,127,3	duplicate(dummy_npc)	Howling Rgan#19m22::ep19_1jorchs_122_127	EP19_MD_RGAN_D
1@jorchs,124,126,3	duplicate(dummy_npc)	Painful Rgan#19m24::ep19_1jorchs_124_126	EP19_MD_RGAN_D
1@jorchs,125,125,3	duplicate(dummy_npc)	Combined Rgan#19m25::ep19_1jorchs_125_125	EP19_MD_RGAN_D
1@jorchs,126,125,3	duplicate(dummy_npc)	Twisted Rgan#19m26::ep19_1jorchs_126_125	EP19_MD_RGAN_D
1@jorchs,123,127,1	duplicate(dummy_npc)	Modified Advanced Ligan#19m23::ep19_1jorchs_123_127	EP19_MD_RGAN_E

1@jorchs,118,127,7	duplicate(dummy_npc)	Horr#19m21::ep19_1jorchs_118_127	EP19_NPC_IWIN_SR
1@jorchs,122,126,7	duplicate(dummy_npc)	Iwin#19m22::ep19_1jorchs_122_126	EP19_NPC_IWIN_SG
1@jorchs,124,124,7	duplicate(dummy_npc)	Iwin#19m23::ep19_1jorchs_124_124	EP19_NPC_IWIN_SG

1@jorchs,114,119,3	script	Iwin#19m21::ep19_1jorchs_114_119	EP19_NPC_IWIN_SG,3,3,{
	end;
OnTouch:
	if ('event < 4) {
		npctalk "คุณมาแล้วเหรอ! พวก Rgan พวกนี้ผ่านไปไม่ได้เลย ไม่โจมตี และดูแปลกมาก พวกเขากำลังหาทางอยู่ เพราะงั้นโฟกัสไปที่การค้นหาของคุณเถอะ!";
		end;
	}
	end;
}
1@jorchs,112,116,7	duplicate(dummy_npc)	Iwin#19m20::ep19_1jorchs_112_116	EP19_NPC_IWIN_SG

1@jorchs,119,123,3	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m20::ep19_1jorchs_119_123	EP19_MD_HEARTHUNTER_AT
1@jorchs,116,124,3	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m21::ep19_1jorchs_116_124	EP19_MD_HEARTHUNTER_AT
1@jorchs,123,124,3	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m22::ep19_1jorchs_123_124	EP19_MD_HEARTHUNTER_AT

1@jorchs,112,119,5	script	Horr#19m20::ep19_1jorchs_112_119	EP19_NPC_IWIN_SR,5,5,{
	end;
OnTouch:
	if ('event == 4) {
		'event = 5;
		// หมายเหตุ: ผู้เล่นสามารถเดินได้
		.@hunter_1$ = instance_npcname("ep19_1jorchs_119_123");

		npctalk "คุณไปต่อแบบนี้ได้เลย!";
		sleep 2000;
		npctalk "เดี๋ยว!! มีพวก Rgan อยู่ข้างหน้าเพียบเลย!", instance_npcname("ep19_1jorchs_112_116");
		sleep 2000;
		npctalk "คุณฝ่ามันไปได้!";
		sleep 1000;
		disablenpc();
		sleep 500;
		disablenpc instance_npcname("ep19_1jorchs_112_116");
		disablenpc instance_npcname("ep19_1jorchs_114_119");
		sleep 1000;
		enablenpc instance_npcname("ep19_1jorchs_118_127");
		enablenpc instance_npcname("ep19_1jorchs_122_126");
		enablenpc instance_npcname("ep19_1jorchs_124_124");
		sleep 1000;
		specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("ep19_1jorchs_118_127");
		sleep 500;
		specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("ep19_1jorchs_122_126");
		specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("ep19_1jorchs_124_124");
		specialeffect EF_HFLIMOON3, AREA, instance_npcname("ep19_1jorchs_118_127");
		specialeffect EF_HFLIMOON3, AREA, instance_npcname("ep19_1jorchs_122_126");
		specialeffect EF_HFLIMOON3, AREA, instance_npcname("ep19_1jorchs_124_124");
		specialeffect EF_M03, AREA, instance_npcname("ep19_1jorchs_118_127");
		specialeffect EF_M03, AREA, instance_npcname("ep19_1jorchs_122_126");
		specialeffect EF_M03, AREA, instance_npcname("ep19_1jorchs_124_124");
		npctalk "?!!!", instance_npcname("ep19_1jorchs_118_127");
		sleep 1000;
		disablenpc instance_npcname("ep19_1jorchs_118_127");
		disablenpc instance_npcname("ep19_1jorchs_122_126");
		disablenpc instance_npcname("ep19_1jorchs_124_124");
		sleep 1000;
		enablenpc();
		sleep 500;
		enablenpc instance_npcname("ep19_1jorchs_112_116");
		enablenpc instance_npcname("ep19_1jorchs_114_119");
		npctalk "บ้าน่า! มันเยอะเกินไป!!! จะฝ่าด่านนั้นไปได้ยังไง?";
		sleep 1500;
		npctalk "แต่มันดูแปลกๆ ไปหน่อยไหม?", instance_npcname("ep19_1jorchs_112_116");
		sleep 2500;
		npctalk "พวกเราเฝ้าดูอยู่ และพวกมันไม่ยอมโจมตีเราเลย!", instance_npcname("ep19_1jorchs_114_119");
		sleep 2000;
		enablenpc .@hunter_1$;
		sleep 500;
		npctalk "สิ่งเดียวที่คุณต้องทำคือเปิดมันซะ อะไรนะ? ทุกคนมาอยู่ที่นี่กันหมดแล้วเหรอ?", .@hunter_1$;
		sleep 1500;
		npctalk "พวกแกมาช้าไปหรือเปล่า? ช่างเถอะ มันไม่สำคัญหรอก", .@hunter_1$;
		sleep 2500;
		npctalk "แล้วนั่นมันอะไรอีกล่ะ? พวกนั้นคือ Illusion ที่ Heart hunter Suhara พูดถึงงั้นเหรอ?";
		sleep 2000;
		npctalk "นี่คือแนวป้องกันที่ 5 ขอรับการสนับสนุนด้วย", .@hunter_1$;
		sleep 2500;
		npctalk "มีผู้บุกรุก มีสิ่งกีดขวางอยู่ ทำให้ขยายแนวป้องกันได้ยาก ย้ำอีกครั้ง ขอรับการสนับสนุนด้วย", .@hunter_1$;
		sleep 2500;
		npctalk "เรียกหน่วยสนับสนุนมาแล้ว!!";
		sleep 2000;
		enablenpc instance_npcname("ep19_1jorchs_116_124");
		enablenpc instance_npcname("ep19_1jorchs_123_124");
		npctalk "สิ่งกีดขวางที่ว่าคืออะไร?", instance_npcname("ep19_1jorchs_123_124");
		sleep 2000;
		npctalk "อย่างที่คุณเห็น นักผจญภัยบุกมาถึงนี่แล้ว", .@hunter_1$;
		sleep 2500;
		npctalk "อะไรนะ? พวก Rgan เหล่านี้ไม่ได้รวมกันเป็นหนึ่งจริงๆ งั้นเหรอ? อย่างที่ Bagot พูดไว้จริงๆ สินะ", instance_npcname("ep19_1jorchs_116_124");
		sleep 2000;
		npctalk "ใช่แล้วล่ะ พวก Rgan ที่ถูกดัดแปลงมาอย่างดีและแข็งแกร่งทำหน้าที่ปิดกั้นทางเข้าไว้ให้แล้ว!", .@hunter_1$;
		sleep 2500;
		npctalk "ว่าแต่ ทำไมถึงมีคนมาที่นี่เยอะนักล่ะ? ฉันกำจัดพวกมันทิ้งได้ไหม?", instance_npcname("ep19_1jorchs_123_124");
		sleep 2000;
		npctalk "ฉันพยายามจะเริ่มเดินเครื่องและปิดกั้นอุปกรณ์ก่อนที่มันจะเข้ามา แต่มันก็ทำไปแล้ว", .@hunter_1$;
		sleep 2500;
		npctalk "แกไปคุมอุปกรณ์ของแกซะ พวกเราจะจัดการเรื่องนี้เอง", .@hunter_1$;
		npctalk "รับทราบ", instance_npcname("ep19_1jorchs_123_124");
		sleep 2000;
		disablenpc instance_npcname("ep19_1jorchs_123_124");
		npctalk "หือ-ฮึ?! แยกกันเหรอ? นักผจญภัย! พวกเราจะจัดการทางด้านนั้นเอง ส่วนคุณจัดการเจ้าพวกนี้ซะ!";
		sleep 500;
		disablenpc();
		disablenpc instance_npcname("ep19_1jorchs_112_116");
		disablenpc instance_npcname("ep19_1jorchs_114_119");
		disablenpc .@hunter_1$;
		disablenpc instance_npcname("ep19_1jorchs_116_124");
		cloakonnpc instance_npcname("ep19_1jorchs_120_127");
		cloakonnpc instance_npcname("ep19_1jorchs_124_126");
		.@event$ = instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal1";
		monster 'map_jorchs$,119,123,"A modified Heart Hunter",21536,1, .@event$;	// EP19_MD_HEARTHUNTER_AT
		monster 'map_jorchs$,123,124,"A modified heart hunter",21536,1, .@event$;	// EP19_MD_HEARTHUNTER_AT
		monster 'map_jorchs$,120,126,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
		monster 'map_jorchs$,124,125,"Painful Rgan","EP19_MD_RGAN_D",1, .@event$;
		end;
	}
	if ('event == 7) {
		'event = 8;
		npctalk "เฮ้อ- จัดการพวกนั้นเรียบร้อยแล้วล่ะ อย่างไรก็ตาม ดูเหมือนจะมีอุปกรณ์ที่ไม่รู้จักติดตั้งอยู่เต็มไปหมดเลย", instance_npcname("ep19_1jorchs_112_119");
		sleep 2000;
		npctalk "เขาจับ Hearthunter ที่พยายามจะควบคุมมันได้แล้ว เจ้าพวกนั้นพยายามจะทำบ้าอะไรกันแน่นะ?", instance_npcname("ep19_1jorchs_112_119");
		sleep 2500;
		npctalk "พวก Hearthunter ผ่านกองทัพ Rgan พวกนั้นมาได้ยังไงกัน?", instance_npcname("ep19_1jorchs_112_116");
		sleep 2500;
		npctalk "บางทีพวก Hearthunter อาจจะมีบางอย่างที่ใช้หลบหลีกหรือควบคุมพวก Rgan ได้", instance_npcname("ep19_1jorchs_112_119");
		sleep 2000;
		npctalk "เพราะงั้น พอคุณจัดการ Hearthunter ได้ กำแพงของพวก Rgan ก็เลยถูกสลายไป!", instance_npcname("ep19_1jorchs_114_119");
		sleep 2500;
		npctalk "มันคงไม่ง่ายหรอก นักผจญภัย พวกเราจะแชร์เรื่องนี้กับคนอื่นๆ คุณล่วงหน้าไปก่อนเลย", instance_npcname("ep19_1jorchs_112_119");
		sleep 500;
		disablenpc instance_npcname("ep19_1jorchs_112_119");
		disablenpc instance_npcname("ep19_1jorchs_112_116");
		disablenpc instance_npcname("ep19_1jorchs_114_119");

		setcell 'map_jorchs$,120,129,120,129,cell_walkable,1;
		setcell 'map_jorchs$,121,128,121,128,cell_walkable,1;
		setcell 'map_jorchs$,122,127,122,127,cell_walkable,1;
		setcell 'map_jorchs$,123,127,123,127,cell_walkable,1;
		setcell 'map_jorchs$,124,126,124,126,cell_walkable,1;
		setcell 'map_jorchs$,125,125,125,125,cell_walkable,1;
		setcell 'map_jorchs$,126,125,126,125,cell_walkable,1;

		specialeffect EF_WIND, AREA, instance_npcname("ep19_1jorchs_120_129");
		specialeffect EF_WIND, AREA, instance_npcname("ep19_1jorchs_120_127");
		specialeffect EF_WIND, AREA, instance_npcname("ep19_1jorchs_122_127");
		specialeffect EF_WIND, AREA, instance_npcname("ep19_1jorchs_123_127");
		specialeffect EF_WIND, AREA, instance_npcname("ep19_1jorchs_124_126");
		specialeffect EF_WIND, AREA, instance_npcname("ep19_1jorchs_125_125");

		donpcevent instance_npcname("ep19_jorchs_area_3_1") + "::OnStart";	// เกิดมอนสเตอร์ใน Area 3
		end;
	}
	end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_2_B	-1,{
	end;
OnMobFinal1:
	// ตรวจสอบจำนวนมอนสเตอร์ ถ้ายังมีเหลือให้จบการทำงาน
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal1" ) > 0)
		end;
	if ('event != 5)
		end;
	'event = 6;
	mapannounce 'map_jorchs$, "- เจ้าคิดว่ามันจะจบลงแค่นี้รึ? มันไม่ใช่ว่าพวกเราไม่ได้เตรียมการอะไรไว้เลยนะ!", bc_map, 0xFFCC;
	sleep 2000;
	cloakonnpc instance_npcname("ep19_1jorchs_120_129");	// เพื่อแสดงผลบน rAthena (ปิดการใช้งานอย่างเป็นทางการ)
	cloakonnpc instance_npcname("ep19_1jorchs_122_127");
	cloakonnpc instance_npcname("ep19_1jorchs_125_125");
	disablenpc instance_npcname("ep19_1jorchs_126_125");
	cloakonnpc instance_npcname("ep19_1jorchs_123_127");

	.@event$ = instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal2";
	monster 'map_jorchs$,120,128,"Howling Lgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,122,126,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,125,124,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,126,124,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,123,126,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	end;

OnMobFinal2:
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_2_B") + "::OnMobFinal2" ) > 0)
		end;
	if ('event != 6)
		end;
	'event = 7;
	enablenpc instance_npcname("ep19_1jorchs_112_119");
	enablenpc instance_npcname("ep19_1jorchs_112_116");
	enablenpc instance_npcname("ep19_1jorchs_114_119");
	end;

}


// พื้นที่ 3 (Area 3)
1@jorchs,1,1,3	script	ep19_jorchs_area_3_1	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("ep19_jorchs_area_3_1") + "::OnNormalMob";
	monster 'map_jorchs$,142,144,"Howling RGAN","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,159,151,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,180,139,"Painful Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,200,131,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,216,135,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,226,141,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,236,151,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	end;

OnNormalMob:
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_3_1") + "::OnNormalMob" ) > 0)
		end;
	if ('event != 8)
		end;
	'event = 9;
	mapannounce 'map_jorchs$, "พวกเจ้าอยู่ที่นี่งั้นรึ?", bc_map, 0xFFCC;
	enablenpc instance_npcname("ep19_1jorchs_249_170");
	enablenpc instance_npcname("ep19_1jorchs_251_171");
	end;
}

// กำแพงมอนสเตอร์ (Wall of monsters)
1@jorchs,242,174,3	duplicate(dummy_npc)	Warped Rgan#19m37::ep19_1jorchs_242_174	EP19_MD_RGAN_D
1@jorchs,244,176,3	duplicate(dummy_npc)	Smashed Rgan#19m35::ep19_1jorchs_244_176	EP19_MD_RGAN_D
1@jorchs,247,176,3	duplicate(dummy_npc)	Combined Rgan#19m32::ep19_1jorchs_247_176	EP19_MD_RGAN_D
1@jorchs,248,176,3	duplicate(dummy_npc)	Warped Rgan#19m31::ep19_1jorchs_248_176	EP19_MD_RGAN_D

1@jorchs,243,175,3	duplicate(dummy_npc)	Modified Greater RGAN#19m36::ep19_1jorchs_243_175	EP19_MD_RGAN_E
1@jorchs,245,176,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m34::ep19_1jorchs_245_176	EP19_MD_RGAN_E
1@jorchs,246,176,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m33::ep19_1jorchs_246_176	EP19_MD_RGAN_E,5,5
1@jorchs,249,177,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m30::ep19_1jorchs_249_177	EP19_MD_RGAN_E


// พื้นที่ 3 - ขั้นตอนที่ 2 (Area 3 - Step 2)

// เควสหลัก: ขั้นตอนที่ 78 (Main Quest: Step 78)
1@jorchs,248,173,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m30::ep19_1jorchs_248_173	EP19_MD_HEARTHUNTER_AT
1@jorchs,246,173,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m31::ep19_1jorchs_246_173	EP19_MD_HEARTHUNTER_AT
1@jorchs,243,173,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m32::ep19_1jorchs_243_173	EP19_MD_HEARTHUNTER_AT

1@jorchs,249,170,1	duplicate(dummy_instance_disabled_npc)	Miriam#19m30::ep19_1jorchs_249_170	4_EP18_MIRIAM

1@jorchs,251,171,1	script	Voglinde#19m30::ep19_1jorchs_251_171	4_EP19_VOGLINDE,5,5,{
	end;
OnTouch:
	if ('event != 9)
		end;
	'event = 10;
	npctalk "นักผจญภัย! ท่านปลอดภัยดีนะ", instance_npcname("ep19_1jorchs_249_170");
	sleep 2000;
	npctalk "ท่านหมายความว่า Rgan ที่อยู่ตรงหน้าท่าน ถูกควบคุมโดย Hearthunter งั้นหรือ?", instance_npcname("ep19_1jorchs_251_171");
	// missing 1 npctalk (ข้อความพูดคุยหายไป 1 บรรทัด)
	sleep 1500;
	npctalk "หากพวกเจ้ามีมโนธรรมขนาดนั้น พวกเจ้าคงไม่สร้าง Heart Hunter ขึ้นมาตั้งแต่แรกหรอก", instance_npcname("ep19_1jorchs_251_171");
	sleep 2000;
	enablenpc instance_npcname("ep19_1jorchs_248_173");
	enablenpc instance_npcname("ep19_1jorchs_246_173");
	enablenpc instance_npcname("ep19_1jorchs_243_173");
	npctalk "เจ้ากำลังพูดถึงพวกเราเหรอ? ไม่ต้องห่วงหรอก พวกเราทำสิ่งนี้เพราะพวกเราต้องการเอง", instance_npcname("ep19_1jorchs_248_173");
	sleep 2500;
	npctalk "เหล่า Advanced Rgan พวกนี้ก็เช่นกัน", instance_npcname("ep19_1jorchs_246_173");
	sleep 1500;
	npctalk "พวกมันบอกว่าเป็นโอกาสที่จะได้กลายเป็นที่สุด ดังนั้นพวกเจ้าทั้งสองคนถึงได้มาเข้าแถวรอยังไงล่ะ", instance_npcname("ep19_1jorchs_243_173");
	sleep 2500;
	npctalk "เกิดอะไรขึ้นกับเจ้าพวกตัวระดับกลาง (Intermediate organs) ล่ะ?", instance_npcname("ep19_1jorchs_251_171");
	sleep 1500;
	npctalk "เอ่อ... พวกระดับกลางเหล่านั้นช่วยไม่ได้จริงๆ", instance_npcname("ep19_1jorchs_248_173");
	sleep 2000;
	npctalk "มันผิดนักหรือไงถ้าข้าบังเอิญไปอยู่ในที่ที่อันตรายในเวลาที่ผิดเพี้ยนน่ะ...?", instance_npcname("ep19_1jorchs_243_173");
	sleep 2000;
	npctalk "พวกเจ้าหายไปไหนกันมา? แล้วกำลังทำอะไรด้วยกันอยู่?", instance_npcname("ep19_1jorchs_249_170");
	sleep 2000;
	npctalk "ทำไมพวกเราต้องบอกเจ้าด้วยล่ะ ข้าไม่ได้ใจดีขนาดนั้นนะ", instance_npcname("ep19_1jorchs_243_173");
	sleep 2000;
	npctalk "งั้นก็หยุดพูดจาไร้สาระแล้วออกไปจากที่นี่ซะ", instance_npcname("ep19_1jorchs_249_170");
	sleep 2500;
	npctalk "ทุกคนใน Arunafeltz นี่พูดจาตรงไปตรงมากันจริงนะ", instance_npcname("ep19_1jorchs_246_173");
	npctalk "เจ้าจะปล่อยให้มันผ่านไปเฉยๆ ไม่ได้หรอก นี่คือร่างที่ได้รับคำสั่งมาแล้ว", instance_npcname("ep19_1jorchs_248_173");
	sleep 2500;
	npctalk "มันเกี่ยวกับการหยุดเจ้า ดังนั้นข้าเลยเตรียมบางอย่างไว้... เอ่อ...", instance_npcname("ep19_1jorchs_243_173");
	sleep 2500;
	npctalk "นี่เจ้ากำลังถ่วงเวลาอยู่เหรอ?", instance_npcname("ep19_1jorchs_251_171");
	sleep 2000;
	npctalk "เจ้ารู้ได้ยังไง? มันควรจะมีมากกว่านี้ แต่ข้าก็ไม่รู้สิ! แปด!!!", instance_npcname("ep19_1jorchs_248_173");
	sleep 1500;
	specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("ep19_1jorchs_243_173");
	npctalk "ข้าบอกให้เจ้าใช้สิ่งนี้ในเวลาแบบนี้ ดูเหมือนเจ้าจะใช้มันต่อหน้าข้าไม่ได้งั้นสินะ?", instance_npcname("ep19_1jorchs_248_173");
	mapannounce 'map_jorchs$, "เริ่มการทำงานของหน่วยฉีด (Injection unit started)", bc_map, 0xFFCC;
	sleep 2500;
	npctalk "พวกเขาสร้างไข่ระดับสูงออกมาในทันทีในฐานะ Modified Advanced Rgan", instance_npcname("ep19_1jorchs_243_173");
	specialeffect EF_CLOUD4, AREA, instance_npcname("ep19_1jorchs_243_173");
	sleep 1500;
	npctalk "เจ้าจะได้พบกับ Rgan จำนวนมากเชียวล่ะ?", instance_npcname("ep19_1jorchs_246_173");
	sleep 2500;
	npctalk "ตราบเท่าที่พลังงานยังคงถูกจ่ายอยู่ พวกมันก็จะออกมาเรื่อยๆ", instance_npcname("ep19_1jorchs_243_173");
	sleep 2000;
	npctalk "เจ้าพูดเหมือนกับว่าเจ้าสามารถตัดการจ่ายพลังงานได้งั้นแหละ? เจ้าใจดีกว่าที่ข้าคิดนะ", instance_npcname("ep19_1jorchs_251_171");
	emotion ET_HUK, getnpcid(0,"ep19_1jorchs_248_173");
	emotion ET_HUK, getnpcid(0,"ep19_1jorchs_246_173");
	emotion ET_HUK, getnpcid(0,"ep19_1jorchs_243_173");
	sleep 2500;
	npctalk "เฮ้ มันคงไม่หาเจอง่ายๆ ขนาดนั้นหรอกมั้ง? แถมข้ายังเรียกกำลังเสริมมาแล้วด้วย!!", instance_npcname("ep19_1jorchs_246_173");
	sleep 1500;
	npctalk "พวกเรากำลังมุ่งหน้าไปยังเป้าหมายต่อไป!", instance_npcname("ep19_1jorchs_248_173");
	sleep 2000;
	disablenpc instance_npcname("ep19_1jorchs_248_173");
	disablenpc instance_npcname("ep19_1jorchs_246_173");
	disablenpc instance_npcname("ep19_1jorchs_243_173");
	npctalk "นักผจญภัย เราจะตามรอยเจ้าไป กำจัด Rgan แล้วนำอุปกรณ์มา...!", instance_npcname("ep19_1jorchs_251_171");
	sleep 2000;
	disablenpc instance_npcname("ep19_1jorchs_251_171");
	disablenpc instance_npcname("ep19_1jorchs_249_170");
	enablenpc instance_npcname("#e19p01");
	'event = 11;
	end;
}

1@jorchs,243,166,0	script	#e19p01	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch:
	if ('event == 11) {
		'event = 12;
		donpcevent instance_npcname("ep19_jorchs_area_3_2") + "::OnStart";	// อัญเชิญมอนสเตอร์
		end;
	}
	if ('event == 12 || 'event == 13 || 'event == 14) {
		viewpoint 1,219,148,1,0xFF3355;
		end;
	}
	end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_3_2	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("ep19_jorchs_area_3_2") + "::OnNormalMob";

	disablenpc instance_npcname("ep19_1jorchs_249_177");
	disablenpc instance_npcname("ep19_1jorchs_242_174");
	monster 'map_jorchs$,250,177,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,243,174,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	sleep 3000;
	disablenpc instance_npcname("ep19_1jorchs_248_176");
	disablenpc instance_npcname("ep19_1jorchs_244_176");
	monster 'map_jorchs$,249,176,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,245,176,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	sleep 3000;
	disablenpc instance_npcname("ep19_1jorchs_247_176");
	disablenpc instance_npcname("ep19_1jorchs_245_176");
	monster 'map_jorchs$,248,176,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,246,176,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 3000;
	disablenpc instance_npcname("ep19_1jorchs_243_175");
	disablenpc instance_npcname("ep19_1jorchs_246_176");
	monster 'map_jorchs$,244,175,"Combined Logan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,247,176,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	if ('event == 12)
		'event = 13;
	end;

OnNormalMob:
	'count_area_3_2++;
	if ('count_area_3_2 < 8)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_3_2") + "::OnNormalMob" ) > 0)
		end;
	if ('event != 13)
		end;
	'event = 14;
	mapannounce 'map_jorchs$, "เติมพลังงานผสมลงในไข่ของ Greater L'gann", bc_map, 0xFFCC;
	enablenpc instance_npcname("ep19_1jorchs_219_148");

	donpcevent instance_npcname("#e19c01") + "::OnStart";	// อัญเชิญมอนสเตอร์ชั่วคราว
	end;
}

// เควสหลัก: ขั้นตอนที่ 79 (Main Quest: Step 79)
1@jorchs,219,148,5	script	Injection device#19m30::ep19_1jorchs_219_148	4_ENERGY_BLUE,3,3,{
	if ('event != 14)
		end;
	'event = 15;
	disablenpc();
	viewpoint 2,219,148,1,0xFF3355;
	npctalk "ปลดปล่อยอุปกรณ์ฉีด (Release Injection device)";
	donpcevent instance_npcname("ep19_jorchs_area_3_3") + "::OnStart";
	donpcevent instance_npcname("#e19c01") + "::OnStop";	// หยุดการอัญเชิญมอนสเตอร์ชั่วคราว
	initnpctimer;
	end;
OnTimer10000:
	stopnpctimer;
	donpcevent instance_npcname("#e19c01") + "::OnKill";
	end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_3_3	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("ep19_jorchs_area_3_3") + "::OnNormalMob";

	monster 'map_jorchs$,224,151,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	unittalk $@mobid[0], "อุปกรณ์กำลังทำงาน แต่มีเพียงอันเดียวที่ถูกดัดแปลงและกำเนิดขึ้นรึ?";
	monster 'map_jorchs$,225,141,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	unittalk $@mobid[0], "นี่เจ้าปิดเครื่องงั้นหรือ? เจ้าบอกว่าถ้าปิดมัน มันจะเหี่ยวเฉาและตายในไม่ช้าไม่ใช่รึ!!!";
	monster 'map_jorchs$,217,141,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	unittalk $@mobid[0], "อะไรนะ? พวกเจ้าหายไปไหนกันหมด? พวกเจ้าล่วงหน้าไปก่อนหลังจากเรียกมางั้นเหรอ?";
	end;
OnNormalMob:
	if ('event != 15)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_3_3") + "::OnNormalMob" ) > 0)
		end;
	'event = 16;
	mapannounce 'map_jorchs$, "หน่วยฉีดเพิ่มเติมเริ่มทำงาน (Additional injection unit up)", bc_map, 0xFFCC;

	setcell 'map_jorchs$,242,174,242,174,cell_walkable,1;
	setcell 'map_jorchs$,244,176,244,176,cell_walkable,1;
	setcell 'map_jorchs$,247,176,247,176,cell_walkable,1;
	setcell 'map_jorchs$,248,176,248,176,cell_walkable,1;
	setcell 'map_jorchs$,243,175,243,175,cell_walkable,1;
	setcell 'map_jorchs$,245,176,245,176,cell_walkable,1;
	setcell 'map_jorchs$,246,176,246,176,cell_walkable,1;
	setcell 'map_jorchs$,249,177,249,177,cell_walkable,1;

	donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnStart";	// อัญเชิญมอนสเตอร์ในพื้นที่ถัดไป
	end;
}

1@jorchs,230,160,0	script	#e19c01	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer30000:
	specialeffect EF_AGIUP;
	specialeffect EF_GUARD2;

	getmapxy .@map$,.@x,.@y, BL_NPC;
	monster 'map_jorchs$,.@x,.@y,"Unstable mod Rgan","EP19_MD_RGAN_E",1, instance_npcname( strnpcinfo(0) ) + "::OnTemporaryMob";
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
OnTemporaryMob:
	end;
OnKill:
	stopnpctimer;
	killmonster 'map_jorchs$, instance_npcname( strnpcinfo(0) ) + "::OnTemporaryMob";
	end;
}


// พื้นที่ 4 (Area 4)
1@jorchs,244,176,0	script	#e19p04	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch:
	if ('event == 16) {
		viewpoint 2,219,148,1,0xFF3355;
		if ('area_4[1] == 0)
			viewpoint 1,226,244,1,0xFF3355;
		if ('area_4[0] == 0)
			viewpoint 1,191,203,2,0xFF3355;
		end;
	}
	end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_4_1	-1,{
	end;
OnStart:
	enablenpc instance_npcname("ep19_1jorchs_191_203");
	enablenpc instance_npcname("ep19_1jorchs_226_244");

	.@event$ = instance_npcname("ep19_jorchs_area_4_1") + "::OnNormalMob";

	monster 'map_jorchs$,270,198,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,228,211,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,210,204,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,194,200,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,173,179,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,245,235,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,273,254,"Howling Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,247,278,"Combined Logan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,224,273,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,238,258,"Unstable Augmented Logan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,198,272,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;

	donpcevent instance_npcname("#e19c02") + "::OnStart";	// อัญเชิญมอนสเตอร์ชั่วคราว
	donpcevent instance_npcname("#e19c03") + "::OnStart";
	end;
OnNormalMob:
	if ('event != 16)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_4_1") + "::OnNormalMob" ) > 0)
		end;
	if ('area_4[2] != 0)
		end;
	'area_4[2] = 2;
	mapannounce 'map_jorchs$, "โอ้ เหล่าอาจารย์ครับ พูดถึงเรื่องนี้กันหน่อย", bc_map, 0xFFCC;
	donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnCheck";	// ตรวจสอบว่าควรข้ามไปยังขั้นตอนถัดไปหรือไม่
	end;
OnSpecialMob:
	if ('event != 16)
		end;
	if ('area_4[0] == 1 && 'area_4[1] == 1) {
		'area_4[0] = 'area_4[1] = 2;
		mapannounce 'map_jorchs$, "ยังมีพวก Rgan อยู่ตรงโน้นอีก!", bc_map, 0xFFCC;
		donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnCheck";	// ตรวจสอบว่าควรข้ามไปยังขั้นตอนถัดไปหรือไม่
		end;
	}
	end;
OnCheck:
	if ('area_4[0] == 2 && 'area_4[1] == 2 && 'area_4[2] == 2) {
		'event = 17;
		enablenpc instance_npcname("ep19_1jorchs_149_178");
		enablenpc instance_npcname("ep19_1jorchs_149_179");
		enablenpc instance_npcname("ep19_1jorchs_150_177");
		enablenpc instance_npcname("ep19_1jorchs_150_180");
		enablenpc instance_npcname("ep19_1jorchs_151_179");
		end;
	}
	end;
}

1@jorchs,198,205,0	duplicate(#e19c01)	#e19c03	HIDDEN_WARP_NPC

// เควสหลัก: ขั้นตอนที่ 80 (Main Quest: Step 80)
1@jorchs,191,203,7	script	Injection device#19m32::ep19_1jorchs_191_203	4_ENERGY_BLUE,{
	if ('event != 16)
		end;
	if ('area_4[0] != 0)
		end;
	'area_4[0] = 1;
	viewpoint 2,191,203,2,0xFF3355;
	npctalk "ปลดปล่อยอุปกรณ์ฉีด (Release Injection device)";
	disablenpc();
	donpcevent instance_npcname("#e19c03") + "::OnStop";	// หยุดการอัญเชิญมอนสเตอร์ชั่วคราว
	donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnSpecialMob";	// ประกาศหากจุดชั่วคราวทั้ง 2 ถูกปิดใช้งาน
	initnpctimer;
	end;
OnTimer10000:	// มอนสเตอร์ชั่วคราวจะถูกกำจัดหลังจากผ่านไป ~10 วินาที
	stopnpctimer;
	donpcevent instance_npcname("#e19c03") + "::OnKill";
	end;
}


1@jorchs,239,255,0	duplicate(#e19c01)	#e19c02	HIDDEN_WARP_NPC

// เควสหลัก: ขั้นตอนที่ 81 (Main Quest: Step 81)
1@jorchs,226,244,7	script	Injection device#19m31::ep19_1jorchs_226_244	4_ENERGY_BLUE,{
	if ('event != 16)
		end;
	if ('area_4[1] != 0)
		end;
	'area_4[1] = 1;
	viewpoint 2,226,244,1,0xFF3355;
	npctalk "ปลดปล่อยอุปกรณ์ฉีด (Release Injection device)";
	disablenpc();
	donpcevent instance_npcname("#e19c03") + "::OnStop";	// หยุดการอัญเชิญมอนสเตอร์ชั่วคราว
	donpcevent instance_npcname("ep19_jorchs_area_4_1") + "::OnSpecialMob";	// ประกาศหากจุดชั่วคราวทั้ง 2 ถูกปิดใช้งาน
	initnpctimer;
	end;
OnTimer10000:	// มอนสเตอร์ชั่วคราวจะถูกกำจัดหลังจากผ่านไป ~10 วินาที
	stopnpctimer;
	donpcevent instance_npcname("#e19c02") + "::OnKill";
	end;
}


// กำแพงมอนสเตอร์ (Wall of monsters)
1@jorchs,139,181,3	script	Modified Advanced Rgan#19m42::ep19_1jorchs_139_181	EP19_MD_RGAN_E,5,5,{
	unittalk getcharid(3), "" + strcharinfo(0) + " : ดูเหมือนเจ้าจะยังผ่านกำแพง Rgan นี้ไปไม่ได้ ข้าคงต้องค้นหาให้เสร็จสิ้นก่อน";
	end;
}
1@jorchs,138,178,3	duplicate(dummy_npc)	Modified Greater Rgan#19m45::ep19_1jorchs_138_178	EP19_MD_RGAN_E
1@jorchs,138,179,3	duplicate(dummy_npc)	Warped Rgan#19m44::ep19_1jorchs_138_179	EP19_MD_RGAN_D
1@jorchs,139,180,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m43::ep19_1jorchs_139_180	EP19_MD_RGAN_E
1@jorchs,140,182,3	duplicate(dummy_npc)	Smashed Rgan#19m41::ep19_1jorchs_140_182	EP19_MD_RGAN_D
1@jorchs,140,183,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m40::ep19_1jorchs_140_183	EP19_MD_RGAN_E


1@jorchs,149,178,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m41::ep19_1jorchs_149_178	EP19_MD_HEARTHUNTER_AT
1@jorchs,149,179,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m40::ep19_1jorchs_149_179	EP19_MD_HEARTHUNTER_AT
1@jorchs,150,180,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m42::ep19_1jorchs_150_180	EP19_MD_HEARTHUNTER_AT
1@jorchs,150,177,1	duplicate(dummy_instance_disabled_npc)	Leon#19m40::ep19_1jorchs_150_177	4_EP19_LEON,3,3

// เควสหลัก: ขั้นตอนที่ 82, 84 (Main Quest: Step 82, 84)
1@jorchs,151,179,1	script	Aurelie#19m40::ep19_1jorchs_151_179	4_EP19_AURELIE,5,5,{
	if ('event == 17) {
		'event = 18;
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_149_179");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_149_178");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_150_180");
		npctalk "พวกเจ้ามีปัญหาอะไรกัน? พวกเขาบอกว่าเข้าร่วมไม่ได้ แต่พวกเขาก็ไม่ปกปิดมันแถมยังสร้างปัญหาอีกเหรอ?", instance_npcname("ep19_1jorchs_151_179");
		sleep 3000;
		npctalk "ข้าไม่ได้พูดในสิ่งที่ข้ากำลังพูด แต่มันเป็นเพราะข้าไม่เข้าใจ", instance_npcname("ep19_1jorchs_151_179");
		sleep 1500;
		npctalk "เหล่านักผจญภัย พวกเราอยู่นี่แล้ว...", instance_npcname("ep19_1jorchs_150_177");
		mapannounce 'map_jorchs$, "หน่วยฉีดเริ่มทำงานแล้ว (Injection unit is up)", bc_map, 0xFFCC;
		sleep 3000;
		npctalk "พวกเราจะดึงความสนใจไว้ให้ ดังนั้นมันจะดีกว่าถ้าท่านหาอุปกรณ์ให้เจอเป็นอันดับแรกแล้วปิดมันซะ", instance_npcname("ep19_1jorchs_151_179");
		sleep 2000;
		npctalk "เจ้าทำอะไรไปเมื่อกี้ แล้วค่อยมาเริ่มใหม่เนี่ยนะ?", instance_npcname("ep19_1jorchs_150_177");
		sleep 2000;
		npctalk "มาคุยเรื่องนี้กันเถอะครับ เหล่าอาจารย์", instance_npcname("ep19_1jorchs_149_179");
		sleep 2000;
		npctalk "นี่มันบ้าอะไรกัน ทำไมข้าถึงขยับมันไม่ได้?", instance_npcname("ep19_1jorchs_149_178");
		sleep 2000;
		npctalk "มันเป็นเพราะข้าสติหลุดไปนิดหน่อยน่ะ", instance_npcname("ep19_1jorchs_151_179");
		sleep 2000;
		npctalk "เอาล่ะ เรามาคุยเรื่องที่ค้างไว้ให้จบดีไหม?", instance_npcname("ep19_1jorchs_150_177");
		sleep 3000;
		npctalk "ปล่อยให้เป็นหน้าที่ของพวกเราเถอะ", instance_npcname("ep19_1jorchs_151_179");
		npctalk "นักผจญภัย อย่าเพิ่งไปพยายามเกลี้ยกล่อมพวกเขาล่ะ!", instance_npcname("ep19_1jorchs_150_180");
		'event = 19;
		enablenpc instance_npcname("ep19_1jorchs_192_206");
		enablenpc instance_npcname("#e19p02");
		donpcevent instance_npcname("#e19c04") + "::OnStart";	// อัญเชิญมอนสเตอร์ชั่วคราว
		end;
	}
	if ('event == 19) {
		npctalk "ทิ้งพวกเราไว้ที่นี่แล้วไปปิดอุปกรณ์ซะ", instance_npcname("ep19_1jorchs_151_179");
		end;
	}
	if ('event == 21) {
		'event = 22;
		npctalk "มานี่สิ เจ้าพวกนี้ถูกจัดการเรียบร้อยแล้ว ท่านไปต่อได้เลย", instance_npcname("ep19_1jorchs_151_179");
		sleep 1500;
		npctalk "พวกเขาบอกว่าได้เรียกพวก Rgan ระดับสูงขึ้นไปทั้งหมดไปยังที่พักของ Rasgand แล้ว", instance_npcname("ep19_1jorchs_151_179");
		sleep 3000;
		npctalk "นั่น... อย่าบอกนะว่าพวกเราเป็นคนพูด ได้โปรด", instance_npcname("ep19_1jorchs_149_179");
		sleep 2000;
		npctalk "พวกเขาบอกว่ามีทางเข้าเพียงทางเดียว ไม่อยากจะเชื่อเลยใช่ไหม?", instance_npcname("ep19_1jorchs_150_177");
		npctalk "พวกเราพูดแต่ความจริง เชื่อข้าเถอะ", instance_npcname("ep19_1jorchs_149_178");
		sleep 2000;
		npctalk "ไม่ว่าทางไหน มันก็ชัดเจนว่าสิ่งที่ Rasgand และ Bagot ต้องการจะทำให้สำเร็จนั้นอยู่ใกล้แค่เอื้อม", instance_npcname("ep19_1jorchs_151_179");
		sleep 2000;
		npctalk "ข้าต้องรีบแล้ว", instance_npcname("ep19_1jorchs_151_179");
		sleep 1500;
		npctalk "เหล่าอาจารย์ครับ พวกเรา... ปล่อยพวกเราไปเถอะ พวกเราบอกทุกอย่างที่รู้ไปหมดแล้ว", instance_npcname("ep19_1jorchs_150_180");
		sleep 1500;
		npctalk "นอกเหนือจากนั้น...", instance_npcname("ep19_1jorchs_150_177");
		emotion ET_SURPRISE, getnpcid(0,"ep19_1jorchs_150_177");
		specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("ep19_1jorchs_150_177");
		mapannounce 'map_jorchs$, "เพิ่มพลังให้กับตัวเร่งแรงม้า (Power up the horsepower booster)", bc_map, 0xFFCC;
		sleep 2000;
		npctalk "หือ? กระแสของเวทมนตร์มันดูแปลกๆ นะ?", instance_npcname("ep19_1jorchs_150_177");
		sleep 2000;
		npctalk "ใช่ ข้าไม่คิดว่าเด็กๆ จะรับมือกับสิ่งนี้ได้", instance_npcname("ep19_1jorchs_151_179");
		sleep 2000;
		npctalk "นักผจญภัย ข้าขอฝากสิ่งนี้ไว้กับท่านได้ไหม?", instance_npcname("ep19_1jorchs_150_177");
		sleep 3000;
		npctalk "มันจะดีกว่าไหมถ้าจัดการให้เร็วที่สุด? มันไม่ปกติเลย ไปกันก่อนเถอะ", instance_npcname("ep19_1jorchs_150_177");
		sleep 2000;
		npctalk "ฮ่า! จริงๆ เลย! พวกเราดูดีขนาดนั้นเลยเหรอ?", instance_npcname("ep19_1jorchs_149_179");
		disablenpc instance_npcname("ep19_1jorchs_151_179");
		disablenpc instance_npcname("ep19_1jorchs_150_177");
		sleep 2000;
		npctalk "ถ้าเจ้าพวกพี่ชายที่น่ากลัวหายไป พวกเราก็ทำได้เหมือนกัน!", instance_npcname("ep19_1jorchs_149_178");
		sleep 2000;
		npctalk "เจ้าควรมัดปากนั่นไว้ก่อนจะดีกว่านะ งั้นข้าจะขอร้องเจ้าแล้วกัน", instance_npcname("ep19_1jorchs_151_179");
		donpcevent instance_npcname("ep19_jorchs_area_4_3") + "::OnStart";	// อัญเชิญมอนสเตอร์
		end;
	}
	end;
}


1@jorchs,156,177,0	script	#e19p02	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch:
	if ('event == 19) {
		viewpoint 1,192,206,3,0xFF3355;
		end;
	}
	end;
}

1@jorchs,199,197,0	duplicate(#e19c01)	#e19c04	HIDDEN_WARP_NPC

// เควสหลัก: ขั้นตอนที่ 83 (Main Quest: Step 83)
1@jorchs,192,206,5	script	Injection device#19m40::ep19_1jorchs_192_206	4_ENERGY_BLUE,3,3,{
	if ('event == 19) {
		'event = 20;
		viewpoint 2,192,206,3,0xFF3355;
		npctalk "นำอุปกรณ์ออก (remove device)";
		disablenpc();
		donpcevent instance_npcname("#e19c04") + "::OnKill";	// หยุดการอัญเชิญและกำจัดมอนสเตอร์ชั่วคราว
		mapannounce 'map_jorchs$, "มาที่นี่เมื่อปิดอุปกรณ์แล้ว", bc_map, 0xFFCC;
		donpcevent instance_npcname("ep19_jorchs_area_4_2") + "::OnStart";
		end;
	}
	end;
OnTouch:	// unknown effect (ผลลัพธ์ที่ไม่ทราบแน่ชัด)
	end;
}

1@jorchs,1,1,3	script	ep19_jorchs_area_4_2	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("ep19_jorchs_area_4_2") + "::OnNormalMob";

	monster 'map_jorchs$,191,199,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	unittalk $@mobid[0], "ข้าบอกให้เจ้าปิดมันทิ้งไปซะ";
	monster 'map_jorchs$,199,197,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	unittalk $@mobid[0], "มันจะดีกว่าไหมถ้าจะออกไปจากที่นั่น?";
	monster 'map_jorchs$,202,207,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	unittalk $@mobid[0], "เจ้ามันตัวปัญหาจริงๆ";
	end;
OnNormalMob:
	if ('event != 20)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_4_2") + "::OnNormalMob" ) > 0)
		end;
	'event = 21;
	end;
}

// เควสหลัก: ขั้นตอนที่ 84 (Main Quest: Step 84)
// 1@jorchs,151,179,1	script	Aureli#19m40::ep19_1jorchs_151_179	4_EP19_AURELIE,5,5,{

1@jorchs,1,1,3	script	ep19_jorchs_area_4_3	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("ep19_jorchs_area_4_3") + "::OnNormalMob";
	'ep19_jorchs_area_4_3 = 0;

	disablenpc instance_npcname("ep19_1jorchs_140_182");
	disablenpc instance_npcname("ep19_1jorchs_149_178");
	monster 'map_jorchs$,141,182,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,149,178,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	sleep 3000;
	disablenpc instance_npcname("ep19_1jorchs_140_183");
	disablenpc instance_npcname("ep19_1jorchs_138_179");
	monster 'map_jorchs$,141,183,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,139,179,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	sleep 3000;
	disablenpc instance_npcname("ep19_1jorchs_139_181");
	disablenpc instance_npcname("ep19_1jorchs_139_180");
	monster 'map_jorchs$,140,181,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,140,180,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 1000;
	disablenpc instance_npcname("ep19_1jorchs_150_180");
	disablenpc instance_npcname("ep19_1jorchs_149_179");
	monster 'map_jorchs$,150,180,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	monster 'map_jorchs$,149,179,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	sleep 1000;
	disablenpc instance_npcname("ep19_1jorchs_138_178");
	monster 'map_jorchs$,139,178,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	end;
OnNormalMob:
	if ('event != 22)
		end;
	'ep19_jorchs_area_4_3++;
	if ('ep19_jorchs_area_4_3 < 9)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_4_3") + "::OnNormalMob" ) > 0)
		end;
	'event = 23;
	mapannounce 'map_jorchs$, "เขตแดนภาพหลอน (Illusion Sector) ดูไม่ปกติแล้ว ใครก็ได้...!", bc_map, 0xFFCC;
	enablenpc instance_npcname("#e19p03");
	setcell 'map_jorchs$,140,183,140,183,cell_walkable,1;
	setcell 'map_jorchs$,140,182,140,182,cell_walkable,1;
	setcell 'map_jorchs$,139,181,139,181,cell_walkable,1;
	setcell 'map_jorchs$,139,180,139,180,cell_walkable,1;
	setcell 'map_jorchs$,138,179,138,179,cell_walkable,1;
	setcell 'map_jorchs$,138,178,138,178,cell_walkable,1;

	donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnStart";	// อัญเชิญมอนสเตอร์ในพื้นที่ถัดไป
	end;
}


// พื้นที่ 5 (Area 5)
1@jorchs,137,181,0	script	#e19p03	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch:
	if ('event == 23) {
		if ('area_5[1] == 0)
			viewpoint 1,20,195,4,0xFF3355;
		if ('area_5[0] == 0)
			viewpoint 1,24,142,5,0xFF3355;
		end;
	}
	end;
}


// เควสหลัก: ขั้นตอนที่ 85 (Main Quest: Step 85)

1@jorchs,1,1,3	script	ep19_jorchs_area_5_1	-1,{
	end;
OnStart:
	enablenpc instance_npcname("ep19_1jorchs_24_142");

	.@event$ = instance_npcname("ep19_jorchs_area_5_1") + "::OnNormalMob";

	monster 'map_jorchs$,121,190,"Smashed Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,103,215,"Modified Advanced Ligan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,82,221,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,86,165,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,79,154,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,45,153,"Twisted Rgan","EP19_MD_RGAN_D",1, .@event$;
	monster 'map_jorchs$,32,135,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,18,133,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,25,172,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,24,188,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;

	donpcevent instance_npcname("#e19c06") + "::OnStart";	// อัญเชิญมอนสเตอร์ชั่วคราว
	donpcevent instance_npcname("#e19c05") + "::OnStart";
	end;
OnNormalMob:
	if ('event != 23)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_1") + "::OnNormalMob" ) > 0)
		end;
	if ('area_5[2] != 0)
		end;
	'area_5[2] = 1;
	donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnCheck";
	end;
OnCheck:
	if ('event == 23 && 'area_5[0] == 1 && 'area_5[1] == 1 && 'area_5[2] == 1) {
		'event = 24;
		mapannounce 'map_jorchs$, "ใครจะมาถึงก่อนกัน?", bc_map, 0xFFCC;
		enablenpc instance_npcname("ep19_1jorchs_39_251");
		enablenpc instance_npcname("ep19_1jorchs_41_253");
		enablenpc instance_npcname("ep19_1jorchs_36_251");
	}
	end;
}

// Area 5 - South
1@jorchs,17,133,0	duplicate(#e19c01)	#e19c06	HIDDEN_WARP_NPC

1@jorchs,24,142,7	script	Research device#19m41::ep19_1jorchs_24_142	4_ENERGY_BLUE,{
	if ('event == 23 && 'area_5[0] == 0) {
		'area_5[0] = 1;
		disablenpc();
		viewpoint 2,24,142,5,0xFF3355;
		unittalk getcharid(3), "" + strcharinfo(0) + " : สิ่งนี้... มันสำคัญงั้นเหรอ?";
		.@event$ = instance_npcname("ep19_1jorchs_24_142") + "::OnNormalMob";
		monster 'map_jorchs$,20,136,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "แกแสร้งทำตัวเป็น ragan เพื่อที่จะมาที่นี่เพื่อซึมซับความทรงจำของแกงั้นเหรอ?";
		monster 'map_jorchs$,30,139,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "ฉันบอกแล้วไงว่าอย่าแตะต้องมัน!";
		initnpctimer;
		end;
	}
	end;
OnNormalMob:
	end;
OnTimer10000:	// เหล่า hunter และมอนสเตอร์ชั่วคราวจะถูกกำจัดหลังจากผ่านไป 10 วินาที
	stopnpctimer;
	donpcevent instance_npcname("#e19c06") + "::OnKill";
	killmonster 'map_jorchs$, instance_npcname("ep19_1jorchs_24_142") + "::OnNormalMob";
	donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnCheck";
	end;
}

1@jorchs,16,135,3	script	Laboratory trace#19m40	4_ENERGY_BLUE,{
	unittalk getcharid(3), "" + strcharinfo(0) + " : เหลืออยู่ไม่กี่อย่างแล้ว";
	end;
}


// Area 5 - ส่วนกลาง
1@jorchs,17,188,0	duplicate(#e19c01)	#e19c05	HIDDEN_WARP_NPC

1@jorchs,20,195,7	script	Research device#19m40::ep19_1jorchs_20_195	4_ENERGY_BLUE,{
	if ('event == 23 && 'area_5[1] == 0) {
		'area_5[1] = 1;
		disablenpc();
		viewpoint 2,20,195,4,0xFF3355;
		unittalk getcharid(3), "" + strcharinfo(0) + " : หัวใจของ Ymir... แน่นอนว่าไม่ใช่... ฉันไม่เห็นข้อมูลการวิจัยที่มีประโยชน์เลย";
		mapannounce 'map_jorchs$, "เรายังมีพวก Rgan อีกเพียบที่ต้องจัดการ!", bc_map, 0xFFCC;
		.@event$ = instance_npcname("ep19_1jorchs_20_195") + "::OnNormalMob";
		monster 'map_jorchs$,26,194,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "แกกำลังหาข้อมูลอยู่จริงๆ งั้นเหรอ?";
		monster 'map_jorchs$,26,188,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "ยกมือขึ้นซะ!!";
		monster 'map_jorchs$,19,185,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
		unittalk $@mobid[0], "แกมองเห็นไหม?";
		initnpctimer;
		end;
	}
	end;
OnNormalMob:
	end;
OnTimer20000:	// เหล่า hunter และมอนสเตอร์ชั่วคราวจะถูกกำจัดหลังจากผ่านไป 20 วินาที
	stopnpctimer;
	donpcevent instance_npcname("#e19c05") + "::OnKill";
	killmonster 'map_jorchs$, instance_npcname("ep19_1jorchs_20_195") + "::OnNormalMob";
	donpcevent instance_npcname("ep19_jorchs_area_5_1") + "::OnCheck";
	end;
}


// Area 5 - ทิศเหนือ

// กำแพง
1@jorchs,34,255,1	duplicate(dummy_npc)	Modified Advanced Ligan#19m50::ep19_1jorchs_34_255	EP19_MD_RGAN_E
1@jorchs,35,255,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m51::ep19_1jorchs_35_255	EP19_MD_RGAN_E
1@jorchs,36,255,3	duplicate(dummy_npc)	Modified Greater Rgan#19m52::ep19_1jorchs_36_255	EP19_MD_RGAN_E
1@jorchs,37,255,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m53::ep19_1jorchs_37_255	EP19_MD_RGAN_E
1@jorchs,38,255,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m54::ep19_1jorchs_38_255	EP19_MD_RGAN_E,5,5
1@jorchs,39,255,3	duplicate(dummy_npc)	Modified Advanced Ligan#19m55::ep19_1jorchs_39_255	EP19_MD_RGAN_E
1@jorchs,40,255,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m56::ep19_1jorchs_40_255	EP19_MD_RGAN_E
1@jorchs,41,255,3	duplicate(dummy_npc)	Modified Advanced Ligan#19m57::ep19_1jorchs_41_255	EP19_MD_RGAN_E
1@jorchs,42,255,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m58::ep19_1jorchs_42_255	EP19_MD_RGAN_E
1@jorchs,43,255,3	duplicate(dummy_npc)	Modified Advanced Lgan#19m59::ep19_1jorchs_43_255	EP19_MD_RGAN_E
1@jorchs,35,256,5	duplicate(dummy_npc)	Modified Advanced Rgan#19m61::ep19_1jorchs_35_256	EP19_MD_RGAN_E
1@jorchs,37,256,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m63::ep19_1jorchs_37_256	EP19_MD_RGAN_E

1@jorchs,38,256,5	script	Modified Advanced Rgan#19m64::ep19_1jorchs_38_256	EP19_MD_RGAN_E,5,5,{
	if ('event < 24) {
		unittalk getcharid(3), "" + strcharinfo(0) + " : นี่คือกำแพงของพวก Rgan ฉันต้องสำรวจก่อนแล้วค่อยกลับมาใหม่";
		end;
	}
	end;
}
1@jorchs,39,256,3	duplicate(dummy_npc)	Modified Advanced Rgan#19m65::ep19_1jorchs_39_256	EP19_MD_RGAN_E
1@jorchs,41,256,1	duplicate(dummy_npc)	Modified Advanced Rgan#19m67::ep19_1jorchs_41_256	EP19_MD_RGAN_E
1@jorchs,42,256,5	duplicate(dummy_npc)	Modified Advanced Rgan#19m68::ep19_1jorchs_42_256	EP19_MD_RGAN_E


1@jorchs,39,251,5	script	Heart hunter#19m50::ep19_1jorchs_39_251	EP19_MD_HEARTHUNTER_AT,5,5,{
	end;
OnTouch:
	if ('event == 24) {
		'event = 25;
		npctalk "นั่นใครน่ะ?", instance_npcname("ep19_1jorchs_39_251");
		sleep 1500;
		npctalk "นักผจญภัยงั้นเหรอ?", instance_npcname("ep19_1jorchs_41_253");
		sleep 2000;
		npctalk "นักผจญภัยมาถึงก่อนงั้นเหรอ?", instance_npcname("ep19_1jorchs_36_251");
		sleep 1500;
		npctalk "ใครจะมาถึงที่นี่ก่อนกันนะ?", instance_npcname("ep19_1jorchs_41_253");
		npctalk "ฉันชนะ", instance_npcname("ep19_1jorchs_39_251");
		sleep 2000;
		enablenpc instance_npcname("ep19_1jorchs_37_252");
		npctalk "พวกคุณเป็นนักผจญภัยเพียงกลุ่มเดียวที่อยู่ที่นี่งั้นเหรอ?", instance_npcname("ep19_1jorchs_37_252");
		sleep 2000;
		npctalk "ใช่ คนอื่นๆ ดูเหมือนกำลังจัดการกับพวก Magic Runaway Rgan อยู่", instance_npcname("ep19_1jorchs_39_251");
		sleep 2000;
		npctalk "งั้นเหรอ? ถึงอย่างนั้น ที่นี่ก็ยังอยากจะเจอนักผจญภัยบางคนอยู่ดี?", instance_npcname("ep19_1jorchs_37_252");
		sleep 3000;
		npctalk "พวกคุณคงจะหลอก Bagot ด้วยเล่ห์เหลี่ยมบ้าๆ เมื่อวันก่อนสินะ?!", instance_npcname("ep19_1jorchs_37_252");
		sleep 3000;
		npctalk "ฉันเลยเตรียมของขวัญมาให้พวกคุณ และนี่คือมัน", instance_npcname("ep19_1jorchs_37_252");
		sleep 3000;
		npctalk "ดูเหมือนว่าพวกเขาจะไปพบกับคนอื่นๆ เส้นทางมันสวนกันพอดี", instance_npcname("ep19_1jorchs_37_252");
		enablenpc instance_npcname("ep19_1jorchs_31_249");
		enablenpc instance_npcname("ep19_1jorchs_47_237");
		enablenpc instance_npcname("ep19_1jorchs_47_249");
		enablenpc instance_npcname("ep19_1jorchs_31_237");
		enablenpc instance_npcname("ep19_1jorchs_27_243");
		enablenpc instance_npcname("ep19_1jorchs_50_243");
		enablenpc instance_npcname("ep19_1jorchs_39_235");
		npctalk "อึก---", instance_npcname("ep19_1jorchs_31_249");
		npctalk "กรู๊ค--- โครกคราก---", instance_npcname("ep19_1jorchs_47_237");
		npctalk "ทำ... ไป... ก..", instance_npcname("ep19_1jorchs_47_249");
		npctalk ".........", instance_npcname("ep19_1jorchs_31_237");
		npctalk "มะ... อา... .ไม่...", instance_npcname("ep19_1jorchs_27_243");
		npctalk "อือ-อือ--- อือ---", instance_npcname("ep19_1jorchs_50_243");
		npctalk "ฉัน... ก็ด้วย...", instance_npcname("ep19_1jorchs_39_235");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_31_249");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_47_237");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_47_249");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_31_237");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_27_243");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_50_243");
		specialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_39_235");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_31_249");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_47_237");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_47_249");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_31_237");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_27_243");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_50_243");
		specialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_39_235");
		sleep 2500;
		npctalk "แต่มันไม่สำคัญหรอก ฉันดีใจที่ได้มอบมันให้กับคุณด้วยตัวเอง", instance_npcname("ep19_1jorchs_37_252");
		sleep 3000;
		npctalk "คุณคงสงสัยว่า Bagot กำลังจะทำอะไร? มันคงกระตุ้นให้คุณทำแบบนั้นเพื่อ Juncea สินะ", instance_npcname("ep19_1jorchs_37_252");
		sleep 2500;
		npctalk "แต่มันก็ไม่ได้ไปไกลกว่าแผนการตื้นๆ พวกนั้นหรอก", instance_npcname("ep19_1jorchs_37_252");
		sleep 3000;
		npctalk "เพราะ Bagot จะบอกคุณเอง เพื่อการนั้น ฉันเลยโรยเศษขนมปังที่ไม่จำเป็นทิ้งไว้แล้วตามเขาไป", instance_npcname("ep19_1jorchs_37_252");
		sleep 1500;
		npctalk "มันน่ารำคาญนิดหน่อยที่คิดว่าพวกแมลงวันจะคอยมาเกาะแกะในโลกใหม่ที่ Bagot จะสร้างขึ้น", instance_npcname("ep19_1jorchs_37_252");
		sleep 2500;
		npctalk "คุณหมายความว่า Rasgand เองก็โกรธพวกมนุษย์ที่หลอกลวงเขามากเหมือนกันงั้นเหรอ?", instance_npcname("ep19_1jorchs_37_252");
		sleep 2500;
		npctalk "เอาล่ะ มันคือของขวัญจาก Bagot เพราะงั้นฉันจะแค่มอบสิ่งนี้ให้แล้วก็ไปจากที่นี่ซะ", instance_npcname("ep19_1jorchs_37_252");
		sleep 500;
		disablenpc instance_npcname("ep19_1jorchs_37_252");
		donpcevent instance_npcname("ep19_jorchs_area_5_2") + "::OnWave1";
		end;
	}
	end;
}
1@jorchs,41,253,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m52::ep19_1jorchs_41_253	EP19_MD_HEARTHUNTER_AT
1@jorchs,36,251,5	duplicate(dummy_instance_disabled_npc)	Heart hunter#19m51::ep19_1jorchs_36_251	EP19_MD_HEARTHUNTER_AT

1@jorchs,37,252,5	duplicate(dummy_instance_disabled_npc)	Bagot#19m50::ep19_1jorchs_37_252	4_EP18_BAGOT

1@jorchs,31,249,5	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m50::ep19_1jorchs_31_249	EP19_MD_RGAN_ES
1@jorchs,47,237,1	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m51::ep19_1jorchs_47_237	EP19_MD_RGAN_ES
1@jorchs,47,249,3	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m52::ep19_1jorchs_47_249	EP19_MD_RGAN_ES
1@jorchs,31,237,7	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m53::ep19_1jorchs_31_237	EP19_MD_RGAN_ES
1@jorchs,27,243,5	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m54::ep19_1jorchs_27_243	EP19_MD_RGAN_ES
1@jorchs,50,243,3	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m55::ep19_1jorchs_50_243	EP19_MD_RGAN_ES
1@jorchs,39,235,1	duplicate(dummy_instance_disabled_npc)	Rgan the Berserk#19m56::ep19_1jorchs_39_235	EP19_MD_RGAN_ES


1@jorchs,1,1,3	script	ep19_jorchs_area_5_2	-1,{
	end;
OnWave1:
	.@event$ = instance_npcname("ep19_jorchs_area_5_2") + "::OnWave2";

	mapannounce 'map_jorchs$, "เตรียมพร้อมสำหรับการร่ายเวท การร่ายเวทเฟส 1 เริ่มต้นขึ้นแล้ว", bc_map, 0xFFCC;
	disablenpc instance_npcname("ep19_1jorchs_35_255");
	disablenpc instance_npcname("ep19_1jorchs_42_255");
	monster 'map_jorchs$,35,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,42,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_37_255");
	disablenpc instance_npcname("ep19_1jorchs_40_255");
	monster 'map_jorchs$,37,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,40,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_31_249");
	disablenpc instance_npcname("ep19_1jorchs_47_237");
	monster 'map_jorchs$,31,249,"Rgan the Wild","EP19_MD_RGAN_ES",1, .@event$;
	monster 'map_jorchs$,47,237,"Rgan","EP19_MD_RGAN_ES",1, .@event$;
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_31_249");
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_47_237");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_31_249");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_47_237");
	end;
OnWave2:
	if ('event != 25)
		end;
	'ep19_jorchs_area_5_2[0]++;
	if ('ep19_jorchs_area_5_2[0] < 6)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_2") + "::OnWave2" ) > 0)
		end;
	.@event$ = instance_npcname("ep19_jorchs_area_5_2") + "::OnWave3";

	mapannounce 'map_jorchs$, "เตรียมพร้อมสำหรับการร่ายเวท การร่ายเวทเฟส 2 เริ่มต้นขึ้นแล้ว", bc_map, 0xFFCC;
	sleep 6000;
	disablenpc instance_npcname("ep19_1jorchs_34_255");
	disablenpc instance_npcname("ep19_1jorchs_43_255");
	monster 'map_jorchs$,34,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,43,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_38_255");
	disablenpc instance_npcname("ep19_1jorchs_39_255");
	monster 'map_jorchs$,38,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,39,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_36_255");
	disablenpc instance_npcname("ep19_1jorchs_41_255");
	monster 'map_jorchs$,36,254,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,41,254,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_47_249");
	disablenpc instance_npcname("ep19_1jorchs_31_237");
	monster 'map_jorchs$,47,249,"Frenzy Rgan","EP19_MD_RGAN_ES",1, .@event$;
	monster 'map_jorchs$,31,237,"Frenzy Rgan","EP19_MD_RGAN_ES",1, .@event$;
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_47_249");
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_31_237");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_47_249");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_31_237");
	end;
OnWave3:
	if ('event != 25)
		end;
	'ep19_jorchs_area_5_2[1]++;
	if ('ep19_jorchs_area_5_2[1] < 8)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_2") + "::OnWave3" ) > 0)
		end;
	.@event$ = instance_npcname("ep19_jorchs_area_5_2") + "::OnWave4";

	mapannounce 'map_jorchs$, "เตรียมพร้อมสำหรับการร่ายเวท การร่ายเวทเฟส 3 เริ่มต้นขึ้นแล้ว", bc_map, 0xFFCC;
	sleep 6000;
	disablenpc instance_npcname("ep19_1jorchs_35_256");
	disablenpc instance_npcname("ep19_1jorchs_39_256");
	monster 'map_jorchs$,35,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,39,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_37_256");
	disablenpc instance_npcname("ep19_1jorchs_41_256");
	monster 'map_jorchs$,37,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,41,254,"Modified Advanced Lgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_38_256");
	disablenpc instance_npcname("ep19_1jorchs_42_256");
	monster 'map_jorchs$,38,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	monster 'map_jorchs$,42,254,"Modified Advanced Rgan","EP19_MD_RGAN_E",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_39_251");
	disablenpc instance_npcname("ep19_1jorchs_41_253");
	disablenpc instance_npcname("ep19_1jorchs_36_251");
	monster 'map_jorchs$,39,250,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	monster 'map_jorchs$,36,250,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	monster 'map_jorchs$,41,252,"Heart hunter","EP19_MD_HEARTHUNTER_AT",1, .@event$;
	sleep 4000;
	disablenpc instance_npcname("ep19_1jorchs_27_243");
	disablenpc instance_npcname("ep19_1jorchs_50_243");
	disablenpc instance_npcname("ep19_1jorchs_39_235");
	monster 'map_jorchs$,27,243,"Rgan","EP19_MD_RGAN_ES",1, .@event$;
	monster 'map_jorchs$,50,243,"Rgan","EP19_MD_RGAN_ES",1, .@event$;
	monster 'map_jorchs$,39,235,"Frenzy Rgan","EP19_MD_RGAN_ES",1, .@event$;
	// หมายเหตุ: เฉพาะตัวที่ 5 เท่านั้นที่รับความเสียหายเพียง 10%
	unitskilluseid $@mobid[0], 771, 9;	// NPC_RELIEVE_ON
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_27_243");
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_50_243");
	removespecialeffect EF_NPC_STOP, AREA, instance_npcname("ep19_1jorchs_39_235");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_27_243");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_50_243");
	removespecialeffect EF_GLASSWALL3, AREA, instance_npcname("ep19_1jorchs_39_235");
	end;
OnWave4:
	if ('event != 25)
		end;
	'ep19_jorchs_area_5_2[2]++;
	if ('ep19_jorchs_area_5_2[2] < 12)
		end;
	if (mobcount( 'map_jorchs$, instance_npcname("ep19_jorchs_area_5_2") + "::OnWave4" ) > 0)
		end;
	'event = 26;
	mapannounce 'map_jorchs$, "นั่นคือที่พำนักของ Rasgand งั้นเหรอ?", bc_map, 0xFFCC;
	enablenpc instance_npcname("ep19_1jorchs_37_274");
	setcell 'map_jorchs$,34,255,43,255,cell_walkable,1;
	end;
}


1@jorchs,21,254,5	duplicate(dummy_instance_disabled_npc)	Iwin#19m60::ep19_1jorchs_21_254	EP19_NPC_IWIN_SG
1@jorchs,35,271,7	duplicate(dummy_instance_disabled_npc)	Miriam#19m60::ep19_1jorchs_35_271	4_EP18_MIRIAM
1@jorchs,36,272,7	duplicate(dummy_instance_disabled_npc)	Voglinde#19m60::ep19_1jorchs_36_272	4_EP19_VOGLINDE
1@jorchs,38,272,1	duplicate(dummy_instance_disabled_npc)	Aureli#19m60::ep19_1jorchs_38_272	4_EP19_AURELIE
1@jorchs,39,271,1	duplicate(dummy_instance_disabled_npc)	Leon#19m60::ep19_1jorchs_39_271	4_EP19_LEON
1@jorchs,40,274,1	duplicate(dummy_instance_disabled_npc)	Horr#19m60::ep19_1jorchs_40_274	EP19_NPC_IWIN_SR



1@jorchs,37,274,5	script	Center door#19m61::ep19_1jorchs_37_274	4_ENERGY_BLUE,3,3,{
	if ('event == 26) {
		'event = 27;
		enablenpc instance_npcname("ep19_1jorchs_38_272");
		sleep 500;
		enablenpc instance_npcname("ep19_1jorchs_39_271");
		npctalk "นักผจญภัย คุณปลอดภัยดีนะ", instance_npcname("ep19_1jorchs_38_272");
		sleep 1000;
		npctalk "จู่ๆ Rgan ระดับสูงที่เปี่ยมไปด้วยพลังเวทมนตร์ก็โจมตีฉันจนทำให้ฉันติดอยู่ที่นี่", instance_npcname("ep19_1jorchs_39_271");
		sleep 2000;
		enablenpc instance_npcname("ep19_1jorchs_40_274");
		npctalk "พับผ่าสิ ให้ตายเถอะ! โชคชะตามันช่างโหดร้ายจริงๆ! แล้วไอ้พวกนั้นมันคืออะไรกันน่ะ?", instance_npcname("ep19_1jorchs_40_274");
		sleep 1000;
		enablenpc instance_npcname("ep19_1jorchs_36_272");
		sleep 500;
		enablenpc instance_npcname("ep19_1jorchs_35_271");
		npctalk "ดูเหมือนว่าทุกคนจะเจอมันเหมือนกันหมดเลยนะ", instance_npcname("ep19_1jorchs_36_272");
		sleep 1000;
		npctalk "พวกคุณเก็บเกี่ยวอะไรได้บ้างไหม?", instance_npcname("ep19_1jorchs_35_271");
		sleep 2000;
		npctalk "หลังจากที่ค้นไปทั่วสถานที่ที่ซับซ้อนแห่งนี้ ฉันก็ไม่พบร่องรอยอะไรเลย", instance_npcname("ep19_1jorchs_40_274");
		sleep 2000;
		npctalk "การวิจัยทั้งหมดของ Illusion ถูกจัดการเรียบร้อยแล้ว และตอนนี้ที่นี่เป็นที่เดียวที่เหลืออยู่", instance_npcname("ep19_1jorchs_38_272");
		sleep 2500;
		npctalk "แม้ว่าจะจับตัว Heart Hunter มาถาม พวกเขาทั้งหมดก็ย้ายไปที่ศูนย์กลางกันหมดแล้ว", instance_npcname("ep19_1jorchs_36_272");
		sleep 1500;
		npctalk "ไม่มีใครหนีออกจากเรือเหาะได้ พวกเขาไม่มีทางทำได้หรอก", instance_npcname("ep19_1jorchs_35_271");
		sleep 2000;
		npctalk "นี่คือที่สุดท้ายแล้วใช่ไหม?", instance_npcname("ep19_1jorchs_39_271");
		sleep 2000;
		npctalk "งั้นถ้าเปิดประตูนี้ เราก็จะสามารถจับได้ทั้ง Illusion และ Rasgand เลยงั้นเหรอ?", instance_npcname("ep19_1jorchs_40_274");
		sleep 1500;
		npctalk "ทิ้งพวก rgan ระดับสูงไว้ข้างหลัง แล้วย้ายไปยังสถานที่ที่ไม่มีใครสามารถเข้าไปได้งั้นเหรอ?", instance_npcname("ep19_1jorchs_38_272");
		sleep 2000;
		npctalk "ทำไมล่ะ?", instance_npcname("ep19_1jorchs_39_271");
		sleep 1500;
		npctalk "เปิดดูเถอะ เดี๋ยวก็ได้ถามด้วยตัวเองแล้ว", instance_npcname("ep19_1jorchs_40_274");
		sleep 1000;
		specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("ep19_1jorchs_37_274");
		sleep 500;
		npctalk "มันล็อคแน่นเกินไปงั้นเหรอ?", instance_npcname("ep19_1jorchs_39_271");
		sleep 2000;
		npctalk "นี่มันเรื่องไร้สาระอะไรกัน? งัดทุกอย่างที่คุณมีออกมาใช้ซะ", instance_npcname("ep19_1jorchs_40_274");
		sleep 2500;
		specialeffect EF_M03, AREA, instance_npcname("ep19_1jorchs_37_274");
		sleep 500;
		specialeffect EF_NPC_EARTHQUAKE, AREA, instance_npcname("ep19_1jorchs_37_274");
		sleep 500;
		specialeffect EF_HYOUSYOURAKU, AREA, instance_npcname("ep19_1jorchs_37_274");
		specialeffect EF_DESPERADO, AREA, instance_npcname("ep19_1jorchs_37_274");
		specialeffect EF_SPREADATTACK, AREA, instance_npcname("ep19_1jorchs_37_274");
		specialeffect EF_DRAGONFEAR, AREA, instance_npcname("ep19_1jorchs_37_274");
		sleep 1000;
		npctalk "แกทำบ้าอะไรกับประตูนั่นน่ะ? เปิดออกซะ! เปิดเดี๋ยวนี้!!! ฉันรู้หมดแล้วว่ามีอะไรอยู่ข้างใน!!", instance_npcname("ep19_1jorchs_40_274");
		sleep 2000;
		npctalk "ฉันไม่สามารถเปิดมันได้แม้จะใช้กุญแจที่ได้มาจาก Hearthunter ก็ตาม", instance_npcname("ep19_1jorchs_36_272");
		sleep 1500;
		npctalk "การอัดฉีดพลังเวทมนตร์ก็ให้ผลแบบเดียวกัน", instance_npcname("ep19_1jorchs_38_272");
		sleep 1500;
		npctalk "นั่นมันคือป้อมปราการที่ไม่มีวันแตกพ่ายงั้นเหรอ?", instance_npcname("ep19_1jorchs_40_274");
		sleep 2000;
		npctalk "ถ้าอย่างนั้นฉันจะเฝ้ายามที่นี่และออกจากที่นี่สำหรับวันนี้", instance_npcname("ep19_1jorchs_36_272");
		sleep 2000;
		npctalk "แล้วรังของ Rgan ล่ะ? เผามันทิ้งเลยไหม?", instance_npcname("ep19_1jorchs_40_274");
		sleep 2000;
		npctalk "อืม ปล่อยมันไว้อย่างนี้แหละดีแล้ว", instance_npcname("ep19_1jorchs_38_272");
		sleep 2500;
		npctalk "เพื่อให้พวกเขาเชื่อว่าเรายอมแพ้และจากไปงั้นเหรอ?", instance_npcname("ep19_1jorchs_39_271");
		sleep 2000;
		npctalk "ใช่ แล้วเมื่อถึงจุดหนึ่ง พวกเขาก็อาจจะเปิดประตูออกมาเอง", instance_npcname("ep19_1jorchs_38_272");
		sleep 1500;
		npctalk "ฉันไม่ค่อยชอบมันเท่าไหร่นะ แต่ไม่มีทางเลือกอื่นแล้ว ฉันคงต้องเพิ่มการเฝ้าระวังที่นี่ให้มากขึ้น", instance_npcname("ep19_1jorchs_40_274");
		sleep 2000;
		npctalk "ทำตัวตามสบายเถอะ เดี๋ยวฉันจะเปิดวาร์ปพอร์ทัลไว้ตรงข้างล่างนั่นนะ จะได้กลับกันได้อย่างสะดวก", instance_npcname("ep19_1jorchs_38_272");
		sleep 500;
		enablenpc instance_npcname("#19m60");
		specialeffect EF_ELECTRIC4, AREA, instance_npcname("#19m60");
		specialeffect EF_YELLOWFLY3, AREA, instance_npcname("#19m60");
		end;
	}
	end;
}

1@jorchs,36,257,0	script	#19m60	WARPNPC,2,2,{
	if ('event == 27 && ep19_main == 60) {
		erasequest 17648;
		setquest 17649;
		ep19_main = 61;
	}
	warp "jor_back3",67,316;
	end;
}
