// ภฮฤฺต๙ภป EUC-KRทฮ ภฏม๖วฯฑโ ภงวั วัฑนพ๎ นฎภๅ.
//===== rAthena Script =======================================
//= Episode 19 NPCs.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Related to new content.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

icas_in,192,57,3	script	Incineration Furnace#ep19	4_POINT_RED,{
	mes "มีเตาเผาที่คุณสามารถสัมผัสได้ถึงความร้อน";
	mes "หากคุณมีอาวุธ Dim Glacier ที่ไม่ได้ใช้งาน ให้ใส่มันลงไปเพื่อหลอมละลายซะ";
	close2;
	laphine_synthesis("Dim Glacier Extractor");
	end;
}

icas_in,188,60,3	script	Forr#ep19	4_EP19_IWIN,{
	cutin "ep19_iwin11.png",2;
	mes "[Forr]";
	mes "คุณรู้หรือไม่ว่ามีการค้นพบอาวุธในหลุมที่เพิ่งถูกทิ้งร้างและที่อื่นๆ ที่คล้ายกัน?";
	mes "มันถูกเรียกว่า Dim Glacier เพราะมันเป็นวัตถุที่เปี่ยมไปด้วยพลังเวทมนตร์ที่ไม่ธรรมดา";
	next;
	mes "[Forr]";
	mes "ฉันได้ทำการวิจัยร่วมกับ Vellgunde และพบว่ามันมีพลังเวทมนตร์ของ Jormungand ที่กลายพันธุ์";
	mes "หากคุณมี Dim Glacier ให้ลองปรับจูนเวทมนตร์ของคุณเพื่อเพิ่มประสิทธิภาพของอาวุธดูสิ";
	next;
	mes "[Forr]";
	mes "ฉันต้องการวัตถุดิบในการขัดเกลาที่ค่อนข้างยุ่งยาก แต่สนใจจะมาแลกเปลี่ยนพวกมันตามความเหมาะสมกับสิ่งที่ฉันขาดอยู่ไหมล่ะ?";
	next;
	switch( select( "แลกเปลี่ยนวัตถุดิบ", "ปลุกพลังแฝงของ Dim Glacier", "คุณทำอย่างไรกับอาวุธที่ไม่จำเป็น?", "ตกลง" ) ) {
	case 1:
		mes "[Forr]";
		mes "เศษอเมทิสต์ที่ Maram นำมาช่วยให้สามารถสร้างภาชนะบรรจุต่างๆ ได้";
		mes "มันเป็นสิ่งที่คุณไม่สามารถหาได้จากที่นี่ ดังนั้นฉันอาจจะขอให้คุณช่วยหามันบ่อยๆ นะ";
		close2;
		cutin "",255;
		callshop "barter_ep19_new_content";
		end;
	case 2:
		mes "[Forr]";
		mes "ขอให้คุณโชคดี";
		close2;
		cutin "",255;
		item_enchant(47);
		end;
	case 3:
		mes "[Forr]";
		mes "คุณเห็นเตาเผาที่อยู่ตรงนั้นไหม?";
		mes "ฉันสร้างมันขึ้นมาเพื่อเผาทำลายสิ่งของที่ไม่มีประโยชน์ซึ่งผ่านการวิจัยมาแล้ว";
		next;
		mes "[Forr]";
		mes "ถ้าคุณจะโยนมันทิ้ง ก็จงใช้เตาเผานั้นซะ";
		mes "Dim Glacier มีพลังเวทมนตร์อยู่ ดังนั้นถ้าคุณหลอมมัน สิ่งใดก็ตามที่จะปรากฏออกมา มันก็จะออกมาเอง";
		close3;
	case 4:
		mes "[Forr]";
		mes "ฉันจะอยู่ที่นี่สักพักเพื่อช่วยเหลือท่าน Vellgunde";
		mes "มาหาฉันได้ทุกเมื่อนะ";
		close3;
	}
}

// Storage (คลังเก็บของ)
icas_in,169,70,3	script	Card#ep19	4_POINT_BLUE,{
	.@total_storage = 1;	// โดยปกติ rAthena จะเปิดเพียงคลังพื้นฐาน (แต่เป็น 3 ใน kRO)

	cutin "ep19_iwin02.png",2;
	mes "[Hoyoyo]";
	mes "คุณกำลังจะใช้ตู้เก็บของงั้นเหรอ?";
	mes "เฮียว เฮีย เฮียว ใส่เหรียญจากทวีปมาสิ";
	mes "นี่... คุณอยากจะใส่เงินลงไปสัก 500 zeny ไหมล่ะ?";
	next;
	cutin "",255;
	for ( .@i = 0; .@i < .@total_storage; .@i++ )
		.@menu$ += "คลังเก็บของ " + (.@i+1) + ":";
	.@menu$ += "ยกเลิก";
	.@s = select( .@menu$ ) - 2;

	if (.@s == (.@total_storage - 1)) {
		mes "ฉันตัดสินใจว่าจะมาใช้ในครั้งหน้า";
		close;
	}

	if (Zeny < 500) {
		mes "ดูเหมือนว่าคุณต้องมี 500 zeny เพื่อที่จะใช้ตู้เก็บของนะ";
		close3;
	}
	mes "เมื่อใส่เหรียญลงไป ตู้เก็บของก็เปิดออกพร้อมกับเสียงดังครืดคราด";
	close2;
	Zeny -= 500;
	if (.@s == -1)	// คลังพื้นฐาน
		openstorage;
	else	// คลังแบบ "Premium" เริ่มต้นจาก index 0
		openstorage2 .@s, STOR_MODE_GET|STOR_MODE_PUT;	// หมายเหตุ: เปิดใช้งานก่อนกดปุ่มปิด
	end;
}
