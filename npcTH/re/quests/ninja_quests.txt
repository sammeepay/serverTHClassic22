//===== rAthena Script =======================================
//= Kagerou/Oboro Equipment Quests
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Quests for Kagerou/Oboro weapons and armor.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Little clean-up. [Capuche]
//============================================================

// Armor and Weapon NPCs :: kaboro_gearmix
//============================================================
que_ng,21,72,6	script	Master Armor Craftsman	4_M_RASWORD,{
	mes "[Gyo]";
	mes "เจ้าจะนิยามคำว่าชัยชนะว่าอย่างไร?";
	mes "คือการโค่นล้มศัตรู...";
	mes "อย่างไรก็ตาม สิ่งที่สำคัญที่สุดก็คือ...";
	mes "การไม่ปล่อยให้ตัวเองล้มลงยังไงล่ะ!";
	next;
	mes "[Gyo]";
	mes "เพื่อการนั้น เจ้าต้องป้องกันตัวเองให้ดียิ่งขึ้น";
	mes "เจ้าไม่สามารถปกป้อง 'ผู้อื่น' ได้ หากเจ้าละเลยการป้องกันของ 'ตัวเอง'";
	next;
	if(select("ผสมชุดเกราะ:จบการสนทนา") == 2) {
		mes "[Gyo]";
		mes "ความรอบคอบก็เป็นคุณธรรมที่สำคัญในการต่อสู้เช่นกัน";
		close;
	}
	switch(select("Wolf Armguard[1]:Crescent Armguard[1]:Ninja Scale Armor[1]:Tenebris Latitantes[1]:ออก")) {
	case 1:
		mes "[Gyo]";
		mes "Wolf Armguard... ชุดเกราะที่มีกลิ่นอายแห่งสัตว์ป่า";
		next;
		callsub L_Select,2172;
		if (countitem(2171) > 0 && countitem(6252) > 9) { //custom translation
			delitem 2171,1; //Fox_Armguard
			delitem 6252,10; //Wolf_Blood
			getitem 2172,1; //Wolf_Armguard
			mes "[Gyo]";
			mes "เจ้าสัมผัสได้ถึงสัญชาตญาณของหมาป่าหรือไม่?";
			mes "ทั้งดุดันและน่าหลงใหล...";
			close;
		}
		break;
	case 2:
		mes "[Gyo]";
		mes "Crescent Armguard... สิ่งที่รวมความงามและความแข็งแกร่งไว้ด้วยกัน... โดยไม่เป็นรองใคร";
		next;
		callsub L_Select,2173;
		if (countitem(2171) > 0 && countitem(7321) > 99) { //custom translation
			delitem 2171,1; //Fox_Armguard
			delitem 7321,100; //Fragment_Of_Crystal
			getitem 2173,1; //Crescent_Armguard
			mes "[Gyo]";
			mes "ช่างงดงามยิ่งนัก...";
			mes "ราวกับว่าทั่วทั้งร่างกายเปี่ยมล้นไปด้วยพลังเวทมนตร์";
			close;
		}
		break;
	case 3:
		mes "[Gyo]";
		mes "Ninja Scale Armor... ว่ากันว่านินจาในตำนานเคยสวมใส่มัน";
		next;
		callsub L_Select,15054;
		if (countitem(7451) > 29 && countitem(7562) > 29 && countitem(6091) > 29) { //custom translation
			delitem 7451,30; //Scale_Of_Red_Dragon
			delitem 7562,30; //Ice_Scale
			delitem 6091,30; //Dark_Red_Scale
			getitem 15054,1; //Ninja_Scale_Armor
			mes "[Gyo]";
			mes "นี่คือชุดเกราะในตำนานอย่างแท้จริง...";
			mes "แม้ว่าข้าจะไม่ค่อยเชื่อเรื่องตำนานสักเท่าไหร่ก็เถอะ...";
			close;
		}
		break;
	case 4:
		mes "[Gyo]";
		mes "Shadow King's Armor... มันถูกสร้างขึ้นเพื่อปกป้องผู้อื่น";
		next;
		callsub L_Select,15055;
		if (countitem(15056) > 0 && countitem(6089) > 9) { //custom translation
			delitem 15056,1; //Special_Ninja_Suit_
			delitem 6089,10; //Piece_Of_Darkness
			getitem 15055,1; //Tenebris_Latitantes
			mes "[Gyo]";
			mes "เป็นอย่างไรบ้าง?";
			mes "เกราะนี้ไม่ได้ดูเหมือนว่ามีสองอันซ้อนกันอยู่รึ?";
			close;
		}
		break;
	case 5:
		close;
	}
	mes "[Gyo]";
	mes "เจ้ามีวัตถุดิบไม่เพียงพอ";
	mes "ลองตรวจสอบดูอีกครั้งนะ...";
	close;

L_Select:
	.@string1$ = "ตรวจสอบวัตถุดิบ";
	.@string2$ = "ตรวจสอบคุณสมบัติ";
	while(1) {
		switch(select(.@string1$,.@string2$,"ผสมเลย","ออก")) {
		case 1:
			.@string1$ = "";
			callsub L_Ingredients, getarg(0);
			break;
		case 2:
			.@string2$ = "";
			callsub L_Stats, getarg(0);
			break;
		case 3:
			callsub L_Combine;
			return;
		case 4:
			mes "[Gyo]";
			mes "ดูเหมือนว่าเจ้าจะยังต้องการเวลาในการตัดสินใจ";
			close;
		}
	}

L_Ingredients:
	mes "[Gyo]";
	switch (getarg(0)) {
	case 2172:	//Wolf_Armguard
		mes "การจะสร้าง ^ff0000Wolf Armguard เจ้าต้องมี 1 Fox Armguard และ 10 Blood of Wolf^000000";
		mes "สัมผัสได้ไหม? แรงสั่นสะเทือนจากเลือดหมาป่า...";
		break;
	case 2173:	//Crescent_Armguard
		mes "การจะสร้าง ^ff0000Crescent Armguard เจ้าต้องมี 1 Fox Armguard และ 100 Crystal Fragments^000000";
		mes "พระจันทร์เสี้ยวที่สวยงามและใสกระจ่างราวกับคริสตัลจะถูกสลักไว้บนนั้น";
		break;
	case 15054:	//Ninja_Scale_Armor
		mes "การจะสร้าง ^ff0000Ninja Scale Armor เจ้าต้องมี 30 Fire Dragon Scales, 30 Ice Scales และ 30 Darkred Scale Pieces^000000";
		mes "เจ้าสามารถสัมผัสได้ถึงพลังแห่งตำนานของมัน";
		break;
	case 15055:	//Tenebris_Latitantes
		mes "การจะสร้าง ^ff0000Shadow King's Armor เจ้าจะต้องมี Special Ninja Suit ที่มีรู (Slot) และ 10 Dark Pieces^000000";
		mes "ด้วยความมืดมิด มันจะลบเลือนตัวตนออกไป...";
		break;
	}
	next;
	return;

L_Stats:
	mes "[" + getitemname(getarg(0)) + "]";
	switch (getarg(0)) {
	case 2172:	//Wolf_Armguard
		mes "มีโอกาสได้รับ ATK+100, FLEE -50 เป็นเวลา 5 วินาที เมื่อทำการโจมตีระยะประชิด";
		mes "ประเภทโล่, พลังป้องกัน 45, ช่องใส่การ์ด 1";
		mes "เลเวลที่ต้องการ 100, สำหรับอาชีพนินจาเท่านั้น";
		break;
	case 2173:	//Crescent_Armguard
		mes "ลดดีเลย์หลังใช้สกิลลง 2% ต่อระดับการตีบวก";
		mes "ประเภทโล่, พลังป้องกัน 70, ช่องใส่การ์ด 1";
		mes "เลเวลที่ต้องการ 100, สำหรับอาชีพนินจาเท่านั้น";
		break;
	case 15054:	//Ninja_Scale_Armor
		mes "MHP+15%, MSP-30%";
		mes "ประเภทเกราะ, พลังป้องกัน 90, ช่องใส่การ์ด 1";
		mes "เลเวลที่ต้องการ 100, สำหรับอาชีพนินจาเท่านั้น";
		break;
	case 15055:	//Tenebris_Latitantes
		mes "มีโอกาสเปิดใช้งาน Illusion - Shadow เลเวล 1 เมื่อถูกโจมตีระยะประชิด";
		mes "ประเภทเกราะ, พลังป้องกัน 60, ช่องใส่การ์ด 1";
		mes "เลเวลที่ต้องการ 100, สำหรับอาชีพนินจาเท่านั้น";
		break;
	}
	next;
	return;

L_Combine:
	mes "[Gyo]";
	mes "ตรวจสอบให้แน่ใจว่า ^ff0000เจ้ามีวัตถุดิบและอุปกรณ์ที่ถูกต้องอยู่ในมือแล้ว^000000";
	mes "ไม่มีประโยชน์ที่จะมาร้องไห้เสียใจภายหลังนะ";
	next;
	switch(select("ผสมเลย:ออก")) {
	case 1:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
			mes "- เดี๋ยวก่อน!! -";
			mes "- เจ้าไม่สามารถรับไอเทมได้ -";
			mes "- เพราะน้ำหนักตัวของเจ้ามากเกินไป -";
			mes "- โปรดลองใหม่อีกครั้ง -";
			mes "- หลังจากที่เจ้าลดน้ำหนักลงแล้ว -";
			close;
		}
		return;
	case 2:
		mes "[Gyo]";
		mes "ดูเหมือนว่าเจ้าจะยังต้องการเวลาในการตัดสินใจ";
		close;
	}
	end;
}

que_ng,23,70,3	script	Master Weapon Craftsman	4_DST_SOLDIER,{
	mes "[Ki]";
	mes "การจู่โจมคือวิธีการป้องกันที่ดีที่สุด";
	mes "อาวุธที่ทรงพลังจะทำให้เจ้าสมบูรณ์แบบอย่างแท้จริง";
	next;
	mes "[Ki]";
	mes "แน่นอนว่ามันจะดีกว่าถ้ามีบางสิ่งที่พิเศษอยู่ในมือ";
	next;
	if(select("ผสมอาวุธ:จบการสนทนา") == 2) {
		mes "[Ki]";
		mes "การระมัดระวังตัวมากเกินไปในบางครั้งอาจส่งผลร้ายถึงชีวิต";
		close;
	}
	switch(select("Raksasa Dagger[1]:Mikatsuki[1]:Huuma Swirling Petal[2]:ออก")) {
	case 1:
		mes "[Ki]";
		mes "Raksasa Dagger จะใช้งานได้ก็ต่อเมื่อผ่านการฝึกฝนอย่างหนักเท่านั้น";
		next;
		callsub L_Select,13076;
		if (countitem(13013) > 0 && countitem(718) > 0) { //custom translation
			delitem 13013,1; //Murasame_
			delitem 718,1; //Dark_Red_Jewel
			getitem 13076,1; //Raksasa_Dagger
			mes "[Ki]";
			mes "อา...";
			mes "ช่างเป็นเงาแวววาวที่งดงามเหลือเกิน...";
			close;
		}
		mes "[Ki]";
		mes "อืม... เจ้ามีของไม่เพียงพอที่จะสร้าง Raksasa Dagger";
		break;
	case 2:
		mes "[Ki]";
		mes "Mikatsuki... เส้นโค้งที่งดงามราวกับดวงจันทร์เสี้ยว...";
		mes "มีไม่กี่คนหรอกที่สามารถสร้างมันขึ้นมาได้";
		next;
		callsub L_Select,13078;
		if (countitem(13015) > 0 && countitem(727) > 0) { //custom translation
			delitem 13015,1; //Hakujin_
			delitem 727,1; //White_Jewel
			getitem 13078,1; //Mikatsuki
			mes "[Ki]";
			mes "งดงามจริงๆ...";
			mes "ข้าโชคดีเหลือเกินที่มีโอกาสได้เห็นมีดสั้นที่วิเศษเช่นนี้";
			close;
		}
		mes "[Ki]";
		mes "อืม... เจ้ามีของไม่เพียงพอที่จะสร้าง Mikatsuki";
		break;
	case 3:
		mes "[Ki]";
		mes "Huuma Swirling Petal... เจ้าเคยเห็นกลีบดอกไม้ที่หมุนวนหรือไม่?";
		next;
		callsub L_Select,13313;
		if (countitem(13304) > 0 && countitem(7156) > 99) { //custom translation
			delitem 13304,1; //Huuma_Calm_Mind
			delitem 7156,100; //Broken_Shuriken
			getitem 13313,1; //Huuma_Swirling_Petal
			mes "[Ki]";
			mes "กลีบดอกไม้... ที่ร่ายรำ!!";
			close;
		}
		mes "[Ki]";
		mes "อืม... เจ้ามีของไม่เพียงพอที่จะสร้าง Huuma Swirling Petal";
		break;
	case 4:
		close;
	}
	mes "ทำไมเจ้าไม่ลองตรวจสอบดูอีกครั้งล่ะ?";
	mes "บางคนก็ไม่สามารถตัดใจจากสิ่งที่ตนถือครองอยู่ได้";
	close;

L_Select:
	.@string1$ = "ตรวจสอบวัตถุดิบ";
	.@string2$ = "ตรวจสอบคุณสมบัติ";
	while(1) {
		switch(select(.@string1$,.@string2$,"ผสมเลย","ออก")) {
		case 1:
			.@string1$ = "";
			callsub L_Ingredients, getarg(0);
			break;
		case 2:
			.@string2$ = "";
			callsub L_Stats, getarg(0);
			break;
		case 3:
			callsub L_Combine;
			return;
		case 4:
			mes "[Ki]";
			mes "ลองคิดดูให้ดีนะ~~";
			close;
		}
	}

L_Ingredients:
	mes "[Ki]";
	switch (getarg(0)) {
	case 13076:	//Raksasa_Dagger
		mes "สำหรับการสร้าง ^ff0000Raksasa Dagger เจ้าต้องมี 1 Murasame แบบ 2 รู และ 1 Garnet^000000";
		mes "Garnet คือสิ่งที่ทำให้ Raksasa Dagger มีสีสันที่เป็นเอกลักษณ์";
		break;
	case 13078:	//Mikatsuki
		mes "สำหรับการสร้าง ^ff0000Mikatsuki เจ้าต้องมี Hakujin แบบมีรู และ 1 Opal^000000";
		mes "Opal คือสิ่งที่ทำให้เกิดแสงเรืองรองที่โปร่งแสง";
		break;
	case 13313:	//Huuma_Swirling_Petal
		mes "สำหรับการสร้าง ^ff0000Huuma Swirling Petal เจ้าต้องมี 1 Huuma Calm Mind Shuriken และ 100 Broken Shurikens^000000";
		mes "มันอาจดูเหมือนว่าต้องใช้ดาวกระจายมากเกินไป... แต่มันจำเป็นสำหรับผลลัพธ์ที่พลิ้วไหวอย่างสวยงามนะ";
		break;
	}
	next;
	return;

L_Stats:
	mes "[" + getitemname(getarg(0)) + "]";
	switch (getarg(0)) {
	case 13076:	//Raksasa_Dagger
		mes "INT+3 MATK+100";
		mes "ประเภทมีดสั้น, ATK 120, ช่องใส่การ์ด 1";
		mes "เลเวลที่ต้องการ 110, สำหรับอาชีพนินจาเท่านั้น";
		break;
	case 13078:	//Mikatsuki
		mes "MATK + 120, ลดร่ายแบบแปรผันและการใช้ MSP ลง 5% เมื่อใช้สกิล";
		mes "ประเภทมีดสั้น, ATK 50, อาวุธเลเวล 4, ช่องใส่การ์ด 1";
		mes "เลเวลที่ต้องการ 100, สำหรับอาชีพนินจาเท่านั้น";
		break;
	case 13313:	//Huuma_Swirling_Petal
		mes "MATK + 50, เพิ่มความเสียหายของสกิล Huuma Swirling Petal ขึ้น 20%";
		mes "ประเภทดาวกระจาย Huuma, ATK 150";
		mes "อาวุธเลเวล 3, ช่องใส่การ์ด 2";
		mes "เลเวลที่ต้องการ 110, สำหรับอาชีพนินจาเท่านั้น";
		break;
	}
	next;
	return;

L_Combine:
	mes "[Ki]";
	mes "ตรวจสอบให้แน่ใจว่า ^ff0000เจ้ามีวัตถุดิบและอุปกรณ์ที่ถูกต้องอยู่ในมือแล้ว^000000";
	mes "เมื่อทำเสร็จแล้ว จะไม่สามารถย้อนคืนกลับมาได้นะ";
	next;
	switch(select("ผสมเลย:ออก")) {
	case 1:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
			mes "- เดี๋ยวก่อน!! -";
			mes "- เจ้าไม่สามารถรับไอเทมได้ -";
			mes "- เพราะน้ำหนักตัวของเจ้ามากเกินไป -";
			mes "- โปรดลองใหม่อีกครั้ง -";
			mes "- หลังจากที่เจ้าลดน้ำหนักลงแล้ว -";
			close;
		}
		return;
	case 2:
		mes "[Ki]";
		mes "ลองคิดดูให้ดีนะ~~";
		close;
	}
	end;
}
