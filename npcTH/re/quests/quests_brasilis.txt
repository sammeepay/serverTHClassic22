//===== rAthena Script =======================================
//= Brasilis Quests
//===== By ===================================================
//= L0ne_W0lf
//===== Version ==============================================
//= 1.3
//===== Compatible With ======================================
//= rAthena Project
//===== Description ==========================================
//= [Official Conversion]
//= Lost Puppies (Repeatable, 24 hours.)
//= Suspicious Beach (Repeatable, 24 hours. iRO/cRO version.)
//= Guarana Candy Quest
//= Brasilis Water Lily Quest
//= Brasilis Dungeon Access Quest
//= Iara (Buff reward. Repeatable, 24 hours.)
//===== Comments =============================================
//= 1.0 First version.
//= 1.1 Hydra Ball (12408)
//= 1.2 Optimization. [Euphy]
//= 1.2a Added 'consumeitem' command. [Euphy]
//= 1.3 Added VIP features and fixed some EXP values. [Euphy]
//= 1.3a Clean-up. [Capuche]
//============================================================

// Lost Puppies :: dogdog.sc
//============================================================
brasilis,297,307,5	script	Angelo#br	1_M_04,{
	if (BaseLevel < 40) {
		mes "[Angelo]";
		mes "สัตว์เลี้ยงออกไปนอกหมู่บ้านหมดแล้ว~!!";
		mes "โธ่... ข้าจะทำยังไงดี... ?";
		close;
	}
	if (checkquest(9032,PLAYTIME) == 2) {
		erasequest 9032;
	}
	if (checkquest(9032,PLAYTIME) == 0) {
		mes "[Angelo]";
		mes "วันนี้ยังไม่จบลงเลย";
		mes "เจ้าสามารถช่วยได้แค่วันละครั้งเท่านั้นนะ ฮิฮิ";
		close;
	}
	if (checkquest(9030) == 1) {
		mes "[Angelo]";
		mes "สัตว์เลี้ยงของข้าอยู่ในทุ่งนาข้างนอกหมู่บ้าน";
		mes "ทำไมพวกเขาถึงออกไปกันนะ? ได้โปรดช่วยตามหาพวกมันที";
		close;
	}
	if (checkquest(9031) == 1) {
		mes "[Angelo]";
		mes "โอ้ ขอบคุณมาก เจ้าเจอลูกสุนัขทั้ง 3 ตัวแล้ว";
		mes "ขอบใจเจ้าจริงๆ";
		mes "ข้าหวังว่าสิ่งนี้จะมีประโยชน์กับเจ้านะ โฮ่โฮ่";
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 75000,0;
		else
			getexp 50000,0;
		erasequest 9031;
		setquest 9032;
		specialeffect2 EF_ASSUMPTIO;
		consumeitem 607; //Yggdrasilberry
		.@rand = rand(1,10);
		if (.@rand > 4 && .@rand < 9) getitem 504,2; //White_Potion
		else if (.@rand > 8) getitem 608,1; //Seed_Of_Yggdrasil
		close;
	}
	mes "[Angelo]";
	mes "เจ้าเป็นนักผจญภัยงั้นรึ? เจ้ามาได้ถูกเวลาพอดีเลย";
	mes "ลูกสุนัขพากันหายตัวไป";
	mes "และมีคนบอกว่าเห็นพวกมันอยู่ที่ทุ่งนาข้างนอกหมู่บ้านน่ะ....";
	next;
	mes "[Angelo]";
	mes "มันค่อนข้างยากและอันตรายที่จะตามหาพวกมัน";
	mes "เจ้าต้องตามหา ^0000FFลูกสุนัข 3 ตัว^000000 นะ";
	setquest 9030;
	close;

OnInit:
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "Angelo#br::OnGo";
	end;

OnGo:
	emotion ET_SURPRISE;
	initnpctimer;
	end;
}

-	script	Puppy#bra	-1,{
	mes "[Puppy]";
	mes "บ๊อก! โฮ่ง โฮ่ง!";
	if (checkquest(9030) == 1) {
		if (brazil_kid < 3) {
			next;
			brazil_kid++;
			mes "[" + strcharinfo(0) + "]";
			if (brazil_kid == 3) {
				mes "ดีมาก ข้าเจอลูกสุนัขครบทั้ง 3 ตัวแล้ว";
				mes "ตอนนี้ข้าต้องไปบอก Angelo";
				brazil_kid = 0;
				erasequest 9030;
				setquest 9031;
			}
			else {
				mes "อา... ใครเป็นเด็กดีน้า?";
				mes "โอเค ตัวอื่นๆ อยู่ที่ไหนกันนะ?";
			}
			if (compare(strnpcinfo(2),"1")) setarray .@i[0],2,3;
			else if (compare(strnpcinfo(2),"2")) setarray .@i[0],1,3;
			else setarray .@i[0],1,2;
			donpcevent "Puppy#"+charat(strnpcinfo(2),0)+.@i[rand(2)]+"::OnEnable";
			hideonnpc strnpcinfo(0);
		}
	}
	close;

OnInit:
	if (!compare(strnpcinfo(2),"1")) hideonnpc strnpcinfo(0);
	end;

OnEnable:
	hideoffnpc strnpcinfo(0);
	end;

OnDisable:
	hideonnpc strnpcinfo(0);
	end;
}

bra_fild01,98,96,3	duplicate(Puppy#bra)	Puppy#a1	4_DOG01
bra_fild01,59,116,5	duplicate(Puppy#bra)	Puppy#a2	4_DOG01
bra_fild01,62,142,3	duplicate(Puppy#bra)	Puppy#a3	4_DOG01
bra_fild01,80,163,3	duplicate(Puppy#bra)	Puppy#b1	4_DOG01
bra_fild01,73,210,3	duplicate(Puppy#bra)	Puppy#b2	4_DOG01
bra_fild01,80,210,3	duplicate(Puppy#bra)	Puppy#b3	4_DOG01
bra_fild01,38,235,3	duplicate(Puppy#bra)	Puppy#c1	4_DOG01
bra_fild01,307,64,3	duplicate(Puppy#bra)	Puppy#c2	4_DOG01
bra_fild01,260,60,3	duplicate(Puppy#bra)	Puppy#c3	4_DOG01
bra_fild01,234,101,3	duplicate(Puppy#bra)	Puppy#d1	4_DOG01
bra_fild01,200,84,3	duplicate(Puppy#bra)	Puppy#d2	4_DOG01
bra_fild01,176,63,5	duplicate(Puppy#bra)	Puppy#d3	4_DOG01

// Suspicious Beach :: Suspicious_Beach.sc
//============================================================
brasilis,192,133,6	script	Lucia#brasilis	4_F_BRZ_WOMAN,{
	/* -------------- Hydra Ball -------------------------
	if (countitem(12408) > 0) {
		delitem 12408,1; //Leaf_Cat_Ball
		getitem 12408,1; //Leaf_Cat_Ball
	}
	else if (countitem(6221) > 0) {
		delitem 6221,1; //Mystic_Leaf_Cat_Ball
		getitem 6221,1; //Mystic_Leaf_Cat_Ball
	}
	--------------------------------------------------- */
	if (BaseLevel < 40) {
		mes "[Lucia]";
		mes "สวัสดี";
		mes "ข้ากำลังกังวลเกี่ยวกับ ^FF0000Strange Hydra^000000 ที่อยู่บน";
		mes "ชายหาดทางตอนใต้";
		mes "ข้าหวังว่าจะมีนักผจญภัยที่มีประสบการณ์";
		mes "จะมาช่วยเรานะ";
		emotion ET_OHNO;
		close;
	}
	.@nQState1 = checkquest(9028);
	.@nQState2 = checkquest(9029);
	if (.@nQState1 == -1) {
		mes "[Lucia]";
		mes "สวัสดี";
		mes "เจ้ามาที่นี่เพื่อล่า ^FF0000Strange Hydra^000000 งั้นรึ?";
		next;
		switch(select("ใช่:ไม่:^006400เกิดอะไรขึ้นที่นี่งั้นรึ?^000000")) {
		case 1:
			setquest 9028;
			getitem 12408,1; //Hydra_Ball
			mes "[Lucia]";
			mes "นี่ รับ ^006400Hydra Ball^000000 นี้ไป";
			mes "ใช้มันเพื่อจับ ^FF0000Strange Hydra^8B4513.^000000";
			mes "ข้าหวังว่าเจ้าจะทำได้นะ~!";
			close;
		case 2:
			mes "[Lucia]";
			mes "อา ข้าคงเข้าใจผิดไป";
			mes "แล้วเจอกันนะ";
			close;
		case 3:
		L_WhatHappen:
			mes "[Lucia]";
			mes "วันหนึ่งพวก ^FF0000Strange Hydra^000000";
			mes "ก็มาที่นี่และล้อมเมืองเอาไว้";
			mes "พวกเราไม่แน่ใจว่าอะไรดึงดูดพวกมันมา แต่บางคนบอกว่าเป็นเพราะพวกเจ้าเหล่านักผจญภัย";
			next;
			mes "[Lucia]";
			mes "ไม่ว่ายังไงก็ตาม เพื่อที่จะควบคุมพวก ^FF0000Strange Hydra^000000,";
			mes "เจ้าต้องใช้เครื่องมือที่ออกแบบมาเป็นพิเศษ หรือที่เรียกว่า ^8B4513Hydra Ball^000000";
			next;
			mes "[Lucia]";
			mes "ถ้าเจ้ายังมี ^006400Hydra Ball^000000 อยู่,";
			mes "โปรดใช้มันกับพวก ^FF0000Strange Hydra^000000 ที่";
			mes "เจ้าสามารถพบได้ที่ชายหาด";
			mes "หากเจ้าโชคดี เครื่องมือนี้จะทำงานได้อย่างสมบูรณ์";
			next;
			mes "[Lucia]";
			mes "ข้าหวังว่าจะมีนักผจญภัยหลายคน";
			mes "อาสามาทำงานนี้";
			mes " ";
			mes "ข้าเกลียด Hydra จริงๆ!";
			emotion ET_CRY;
			close;
		}
	}
	else if ((.@nQState1 == 0) || (.@nQState1 == 1)) {
		if (countitem(6221) > 0) {
			mes "[Lucia]";
			mes "สวัสดี เจ้าทำได้จริงๆ ด้วย!";
			if (checkweight(11502,3)) {
				//mes "I don't have enough ^006400Hydra Ball^000000s to give you."; //Poorly translated by iRO?
				mes "ข้าหวังว่าเจ้าจะกลับมา";
				mes "ช่วยข้าอีกนะ";
				mes "ขอให้เป็นวันที่ดีนะ~!";
				delitem 6221,1; //Mystic_Leaf_Cat_Ball
				completequest 9028;
				//recall_completequest 9029;
				if (.@nQState2 > -1) erasequest 9029;
				setquest 9029;
				consumeitem 607; //Yggdrasilberry
				consumeitem 12070; //Luk_Dish05
				consumeitem 12055; //Vit_Dish05
				consumeitem 12065; //Dex_Dish05
				getitem 11502,3; //Light_Blue_Pot
				close;
			}
			else {
				mes " ";
				mes "ข้าอยากจะให้รางวัลเจ้า";
				mes "แต่กระเป๋าของเจ้าเต็มแล้ว";
				mes "โปรดไปจัดของให้ว่างแล้วกลับมาใหม่นะ!";
				close;
			}
		}
		else {
			mes "[Lucia]";
			if (countitem(12408) < 1) {
				mes "เจ้าต้องการ ^006400Hydra Ball^000000 อีกอันงั้นรึ?";
				mes "ข้าจะให้เจ้าอีกอันหนึ่ง";
				getitem 12408,1; //Leaf_Cat_Ball
				close;
			}
			else {
				mes "มีปัญหาอะไรหรือเปล่า?";
				next;
				switch(select("เปล่า:^006400ช่วยบอกข้าอีกครั้งว่าเกิดอะไรขึ้น^000000")) {
				case 1:
					mes "[Lucia]";
					mes "ตกลง งั้นรบกวนเจ้าด้วยนะ";
					close;
				case 2:
					goto L_WhatHappen;
				}
			}
		}
	}
	else {
		mes "[Lucia]";
		mes "โอ้ ^0000FF"+strcharinfo(0)+"^000000 เจ้ากลับมาแล้ว";
		.@nCheckTime = checkquest(9029,PLAYTIME);
		if ((.@nCheckTime == 0) || (.@nCheckTime == 1)) {
			mes "ข้าซาบซึ้งใจมากที่เจ้าช่วยเหลือ";
			mes "^006400Hydra Ball^000000 แต่ละลูกจะได้รับ ^006400ทุกๆ 24 ชั่วโมง^000000";
			mes "โปรดกลับมาในเวลาที่เหมาะสมนะ";
			close;
		}
		else {
			//recall_completequest 9028;
			if (.@nQState1 > -1) erasequest 9028;
			completequest 9029;
			mes "เจ้ามาที่นี่เพื่อล่า ^FF0000Strange Hydra^000000 งั้นรึ?";
			next;
			switch(select("ใช่:ไม่:^006400เกิดอะไรขึ้นที่นี่งั้นรึ?^000000")) {
			case 1:
				setquest 9028;
				getitem 12408,1; //Leaf_Cat_Ball
				mes "[Lucia]";
				mes "นี่ รับ ^006400Hydra Ball^000000 นี้ไป";
				mes "ใช้มันเพื่อจับ ^FF0000Strange Hydra^8B4513.^000000";
				mes "ข้าหวังว่าเจ้าจะทำได้นะ~!";
				close;
			case 2:
				mes "[Lucia]";
				mes "อา ข้าคงเข้าใจผิดไป";
				mes "แล้วเจอกันนะ";
				close;
			case 3:
				goto L_WhatHappen;
			}
		}
	}
	end;

OnInit:
	initnpctimer;
	end;

OnTimer7000:
	emotion ET_SURPRISE;
	stopnpctimer;
	initnpctimer;
	end;
}

// Guarana Quest :: brazil_tre.sc
//============================================================
brasilis,187,162,5	script	Candy Maker	4_M_BRZ_MAN1,{
	if (!checkweight(1201,1)) {
		mes "- เจ้าไม่สามารถเริ่มเควสได้ โปรดลดน้ำหนักในช่องเก็บของของเจ้า -";
		close;
	}
	if (brazil_gua == 0) {
		mes "[Candy Maker]";
		mes "โย่ เจ้าเจ้ารู้จักเบอร์รี่ที่ชื่อว่า ^FF0000Guarana^000000 หรือเปล่า?";
		next;
		mes "[Candy Maker]";
		mes "Guarana เป็นเบอร์รี่ที่พิเศษมากซึ่งปลูกในพื้นที่เฉพาะ มันช่วยคลายความเหนื่อยล้าทางร่างกาย และให้พลังแก่ร่างกาย มันยังช่วยขับของเสียออกจากร่างกายได้อีกด้วย";
		next;
		mes "[Candy Maker]";
		mes "ข้าเคยขายลูกอมที่ทำจากมันในสมัยก่อน";
		mes "ข้าเคยได้รับรางวัลใน <การประกวดผลิตภัณฑ์ยอดเยี่ยมประจำปี> บางปีด้วยนะ นั่นคือช่วงเวลาดีๆ ในอดีตเลยล่ะ";
		next;
		mes "[Candy Maker]";
		mes "ตั้งแต่นั้นมา ผลผลิตของผลไม้นั้นก็ลดลงและราคาก็สูงขึ้น ตอนนี้ส่วนผสมของลูกอมเลยถูกเปลี่ยนเป็นมะพร้าวหรือผลไม้เมืองร้อนอื่นๆ แทน ข้าคิดถึงลูกอม Guarana จัง";
		next;
		switch(select("ข้าจะชิมลูกอม Guarana นี้ได้อย่างไร?:จบการสนทนา")) {
		case 1:
			mes "[Candy Maker]";
			mes "หืม? ข้าขายสต็อกเก่าไปหมดเกลี้ยงแล้วล่ะ";
			next;
			mes "[Candy Maker]";
			mes "แต่ถ้าเจ้าสามารถหา Guarana มาได้ ข้าจะทำให้เจ้าเอง";
			next;
			switch(select("ข้าจะหา Guarana ได้อย่างไร?:จบการสนทนา")) {
			case 1:
				mes "[Candy Maker]";
				mes "เจ้าจะไปหา Guarana งั้นรึ?? โฮฮฮฮฮ~";
				next;
				mes "[Candy Maker]";
				mes "เจ้าจะหาเจอรึ?";
				mes "มันน่าจะแพงมากเลยนะ";
				mes "การค้าขายไม่ใช่แนวข้าซะด้วยสิ ขอข้าคิดก่อนนะ";
				next;
				mes "[Candy Maker]";
				mes "ให้ข้าแนะนำเจ้าให้รู้จักกับใครบางคนที่ข้าเคยทำธุรกิจ Guarana ด้วยแล้วกัน";
				mes "เขาน่าจะยังซื้อขายมันอยู่";
				next;
				mes "[Candy Maker]";
				mes "เขาชื่อ Cherto";
				mes "ถ้าเจ้าหาเขาในเมืองไม่เจอ ให้ไปที่พิพิธภัณฑ์นะ";
				mes "เขาเป็นคนหลงตัวเองนิดๆ เลยชอบทำตัวเป็นคนสำคัญ";
				mes "เขาคงกำลังเดินเตร่ไปมาในพิพิธภัณฑ์เพื่อพยายามอวดใครสักคนอยู่อย่างแน่นอน";
				brazil_gua = 1;
				setquest 2192;
				close;
			case 2:
				mes "[Candy Maker]";
				mes "เจ้าไม่อยากลองชิมลูกอม Guarana หน่อยรึ?";
				close;
			}
		case 2:
			mes "[Candy Maker]";
			mes "นั่นคือช่วงเวลาดีๆ ในวันวานจริงๆ...";
			close;
		}
	}
	else if (brazil_gua == 1) {
		mes "[Candy Maker]";
		mes "ถ้าเจ้าต้องการได้ Guarana ไปหา Cherto ซะ";
		mes "บางทีเขาอาจจะอยู่ในพิพิธภัณฑ์นะ";
		close;
	}
	else if (brazil_gua == 10) {
		if (!countitem(6237)) {
			mes "- Guarana ที่ข้ามีอยู่หายไปแล้ว -";
			close;
		}
		delitem 6237,1; //Guarana_Fruit
		mes "[Candy Maker]";
		mes "เจ้าได้ Guarana มาแล้วรึ?";
		next;
		mes "- เจ้าส่ง Guarana ให้เขา -";
		next;
		mes "[Candy Maker]";
		mes "ว้าว! เจ้ามีความสามารถพิเศษจริงๆ";
		mes "มันเป็นสิ่งที่ดีที่สุดเท่าที่ข้าเคยเห็นมาเลย เจ๋งมาก~!";
		next;
		mes "[Candy Maker]";
		mes "ดีล่ะ มาทำลูกอมกันเถอะ~!";
		mes "ไม่ได้เจอกันนานนะ ลูกอม Guarana สุดวิเศษของข้า...";
		next;
		mes "- สับ สับ สับ สับ สับ สับ -";
		mes "- สับ สับ สับ สับ สับ สับ -";
		next;
		mes "[Candy Maker]";
		mes "ดูนี่สิ! นี่คือลูกอม Guarana ยอดนิยม";
		mes "ลองลิ้มรสชาติที่น่าทึ่งของมันดูสิ เฮ้~ ใจเย็นๆ นะ ฮ่าฮ่าฮ่า!!";
		brazil_gua = 11;
		completequest 2200;
		getitem 12414,1; //Guarana_Candy
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 105000,15000;
		else
			getexp 70000,10000;
		close;
	}
	else if (brazil_gua == 11) {
		mes "[Candy Maker]";
		mes "ลูกอม Guarana นั่นคือผลงานชิ้นเอกที่โดดเด่นที่สุดในชีวิตข้าอย่างแน่นอน!";
		next;
		mes "[Candy Maker]";
		mes "ตั้งแต่เจ้าช่วยข้า ผลผลิต Guarana ก็เพิ่มขึ้นเรื่อยๆ";
		mes "ดังนั้น ข้าเลยกลับมาทำลูกอม Guarana อีกครั้งอย่างเป็นธรรมชาติ";
		next;
		mes "[Candy Maker]";
		mes "ว่าไงล่ะ? อยากซื้อสักหน่อยไหม?";
		mes "ราคาเม็ดละ 4000 zeny นะ";
		next;
		switch(select("ซื้อ Guarana Candy:ยกเลิก")) {
		case 1:
			if (Zeny > 3999) {
				mes "[Candy Maker]";
				mes "นี่คือลูกอม Guarana แสนอร่อย";
				Zeny = Zeny-4000;
				getitem 12414,1; //Guarana_Candy
				close;
			}
			else {
				mes "[Candy Maker]";
				mes "อะไรนะ? เจ้าควรจะบอกก่อนสิถ้าไม่มีเงิน!";
				mes "ถึงเจ้าจะจน แต่ข้าก็ให้ฟรีๆ ไม่ได้หรอกนะ";
				close;
			}
		case 2:
			mes "[Candy Maker]";
			mes "บางครั้งบางคนก็ไม่ชอบมันเพราะฤทธิ์กระตุ้นของมันน่ะ";
			close;
		}
		close;
	}
	else {
		mes "[Candy Maker]";
		mes "ลูกอม Guarana นั่นคือผลงานชิ้นเอกที่โดดเด่นที่สุดในชีวิตข้าอย่างแน่นอน!";
		close;
	}
}

bra_in01,95,179,3	script	Cherto	4_M_BRZ_MAN2,{
	if (brazil_gua <= 1) {
		mes "[Cherto]";
		mes "อืม... เฮ้ พ่อหนุ่ม เจ้ามาจากข้างนอกใช่ไหมล่ะ?";
		next;
		mes "[Cherto]";
		mes "Cherto ดูออกแม้จะเป็นครั้งแรก เจ้าหลอก Cherto ไม่ได้หรอก";
		mes "Cherto มีดวงตาแหลมคมเหมือนนกอินทรี! ฮ่าฮ่าฮ่า!";
		next;
		mes "[Cherto]";
		mes "โอเค โอเค ใช่ ใช่ ข้าเข้าใจแล้ว!";
		next;
		mes "[Cherto]";
		mes "ยังไงก็ตาม เจ้ามาถึง Brasilis แต่ไม่รู้จะทำอะไรดีล่ะสิ?";
		mes "ข้าพูดถูกไหม?";
		mes "เจ้านี่โชคดีจริงๆ ที่ได้เจอผู้ช่วยที่เหมาะสมอย่างข้า";
		next;
		mes "[Cherto]";
		mes "Cherto ดูจากสีหน้าของเจ้าแล้วเจ้าคงอยากจะพูดว่า ''ท่านคือปรมาจารย์!'' ใช่ไหมล่ะ?";
		mes "Cherto คนนี้ อ่านและดูออกได้ในทันที! มันเขียนไว้บนหน้าเจ้าแล้ว!";
		next;
		if (brazil_gua == 0) {
			mes "[Cherto]";
			mes "Cherto อยากจะอยู่ที่นี่เพื่ออธิบายทุกอย่างให้เจ้าฟังนะ แต่เขาเป็นคนยุ่งน่ะ";
			close;
		}
		else {// brazil_gua == 1
			mes "[Cherto]";
			mes "ถ้าเจ้ามีเรื่องสงสัยจะถาม Cherto ล่ะก็ Cherto จะใจดีพอที่จะตอบให้";
			next;
			select("Guarana?");
			mes "[Cherto]";
			mes "อะไรนะ? เจ้าอยากหา Guarana งั้นรึ?";
			next;
			mes "[Cherto]";
			mes "Guarana ปลูกเฉพาะในพื้นที่นี้เท่านั้น มันมีเนื้อในที่นุ่มและปกคลุมด้วยขนอ่อนๆ";
			mes "มันดูแปลกไปนิดแต่ดอกของมันใหญ่มากและมีกลิ่นหอม";
			next;
			mes "[Cherto]";
			mes "นานมาแล้ว Guarana ถูกใช้เพื่อรักษาโรคและแก้กระหาย แต่ช่วงหลังๆ มันเริ่มได้รับความนิยมในการฟื้นฟูพลังกายและเพิ่มการไหลเวียนโลหิต";
			next;
			mes "[Cherto]";
			mes "แม้ว่ามันจะมีผลลัพธ์ที่ดีเลิศเช่นนั้น แต่ Cherto เสียใจที่จะแจ้งให้ทราบว่าเราไม่สามารถหามันได้อีกต่อไปแล้ว";
			next;
			select("ว่าไงนะ??");
			mes "[Cherto]";
			mes "สักพักแล้วที่เบอร์รี่ Guarana ไม่เติบโตที่นี่อีก";
			next;
			mes "[Cherto]";
			mes "แม้ว่า Cherto จะพยายามหามาได้สักลูก แต่มันก็จะเน่าเสียอย่างรวดเร็ว";
			next;
			mes "[Cherto]";
			mes "ถ้าเพียงแต่มันไม่เกิดขึ้น!";
			next;
			select("ท่านกำลังพูดถึงเรื่องอะไร?");
			mes "[Cherto]";
			mes "เงียบหน่อย!!!!!!!!!!!!!!!!";
			mes "เรื่องนี้ถูกสั่งห้ามไว้! ใครบางคนอาจกำลังแอบฟังบทสนทนาของเราอยู่...";
			next;
			mes "[Cherto]";
			mes "ถ้า Cherto บอกเจ้า เจ้าอาจจะทำให้พวกเราเดือดร้อนได้ แต่เจ้าดูเหมือนอยากรู้จริงๆ งั้นข้าจะให้เบาะแสเจ้าหน่อย";
			mes "ขยับเข้ามาใกล้ๆ Cherto จะกระซิบเพื่อไม่ให้ใครแอบฟังได้";
			brazil_gua = 2;
			close;
		}
	}
	else if (brazil_gua == 2) {
		mes "[Cherto]";
		mes "เด็กน้อย Guarana ได้ถือกำเนิดขึ้น";
		next;
		select("เด็ก Guarana?");
		mes "[Cherto]";
		mes "มีผู้หญิงคนหนึ่งซึ่งเป็นนักพฤกษศาสตร์ผู้เชี่ยวชาญ";
		mes "ผู้หญิงคนนั้นเป็นที่รักของสิ่งมีชีวิตทั้งปวง";
		next;
		mes "[Cherto]";
		mes "ในช่วงเวลาที่ลูกของเธอเกิด เธอได้เริ่มทำฟาร์ม Guarana ด้วยเหตุผลบางอย่าง พี่น้องของเธอเกิดความอิจฉา พวกเขาเลยทำลายฟาร์มและหายตัวไป";
		mes "เรื่องราวประเภทนั้นแหละ...";
		next;
		mes "[Cherto]";
		mes "พวกเราไม่แน่ใจว่าเด็กคนนั้นเกิดมาบนโลกนี้หรือไม่ แต่ตั้งแต่นั้นมา Guarana ทั้งหมดใน Brasilis ก็หายไป";
		next;
		mes "[Cherto]";
		mes "ใครคือเด็ก Guarana?";
		mes "Pedro ผู้โด่งดังในฐานะคนโลภงั้นรึ?";
		mes "Meto ผู้ที่ทนไม่ได้กับผลไม้ทุกชนิด?";
		mes "Hovenue ผู้ที่ดูเศร้าหมอง?";
		mes "พวกเขาอาจจะรู้ก็ได้นะ~!";
		next;
		mes "[Cherto]";
		mes "แล้วเจ้าล่ะ?";
		mes "ใครคือเด็ก Guarana?";
		mes "เจ้าจะหาคำตอบได้ไหมนะ? โฮ่โฮ่โฮ่โฮ่~";
		brazil_gua = 3;
		changequest 2192,2193;
		close;
	}
	else if (brazil_gua == 3) {
		mes "[Cherto]";
		mes "เจ้าสามารถหาเด็ก Guarana เจอไหม?";
		mes "อาจจะใช่? หรืออาจจะไม่?";
		close;
	}
	else if (brazil_gua == 4) {
		mes "[Cherto]";
		mes "เจ้าเจอเด็ก Guarana หรือยัง?";
		next;
		mes "- ข้าบอก Cherto เกี่ยวกับเด็กที่ทำเสียงเหมือนสัตว์ -";
		next;
		mes "[Cherto]";
		mes "โฮ่ เป็นเรื่องที่เหลือเชื่อจริงๆ";
		mes "เด็กคนนั้นอาจจะเป็นเด็ก Guarana ก็ได้นะ แน่นอน...";
		mes "ตามเรื่องเล่า เด็กคนนั้นสามารถคุยกับสัตว์ได้";
		next;
		mes "[Cherto]";
		mes "ถ้าเขาสามารถทำเสียงร้องของสัตว์ได้ พวกเขาอาจจะสื่อสารกันได้ก็ได้นะ!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "คราวนี้ ข้าจะทำอย่างไรดี?";
		mes "ถ้าเขาเป็นเด็กจากตำนานจริงๆ มีวิธีไหนที่จะทำให้ Guarana เติบโตขึ้นมาได้อีกไหม?";
		next;
		mes "[Cherto]";
		mes "ฮ่าฮ่า!! Cherto เคยบอกว่าอะไรล่ะ?";
		mes "Cherto รู้ทุกอย่าง~!!";
		mes "Cherto คิดขั้นตอนต่อไปไว้แล้ว";
		next;
		mes "[Cherto]";
		mes "ใน Brasilis มีจอมเวทย์ผู้เชี่ยวชาญอยู่คนหนึ่ง";
		mes "เขาชื่อ Paje";
		mes "เอาบันทึกนี้ไปให้เขา";
		mes "เขาจะแสดงทางแก้ให้กับเจ้าและเด็กคนนั้นเอง";
		brazil_gua = 5;
		changequest 2194,2195;
		close;
	}
	else {
		mes "[Cherto]";
		mes "โฮ่โฮ่ จั๊กจี้~จั๊กจี้~~~~!!!";
		close;
	}
}

brasilis,203,64,3	script	Strange Kid#bra	4_M_KID1,{
	if (brazil_gua < 3) {
		mes "[Strange Kid]";
		mes "................";
		close;
	}
	else if (brazil_gua == 3) {
		mes "[Strange Kid]";
		mes "................";
		next;
		if(select("ลองคุยดู:แกล้งทำเป็นเดินผ่านไป") == 2) {
			mes "[Strange Kid]";
			mes "................";
			close;
		}
		mes "ข้าจะพูดอะไรกับเขาดี?";
		next;
		while(1) {
			switch(select("เจ้าชื่ออะไร?:เจ้าอายุเท่าไหร่?:เจ้ากำลังทำอะไรอยู่?:จบการสนทนา")) {
			case 1:
				mes "[Strange Kid]";
				mes "ก๊าาาาาาาโอ~";
				mes "กรูรรรรรรร - กาาาาน-";
				next;
				break;
			case 2:
				mes "[Strange Kid]";
				mes "บรู๊ววววววว-";
				mes "บรู๊ววววววว- -";
				next;
				break;
			case 3:
				mes "[Strange Kid]";
				mes "แชมเบอร์ พอท แบร๊ แชมเบอร์ พอท แบร๊ แชมเบอร์ พอท แบร๊ -";
				mes "แบ๊แแแแแบ๊แแแแ -";
				next;
				break;
			case 4:
				mes "[Strange Kid]";
				mes "ก๊าาาาาาาโอ~";
				mes "กรูรรรรรรร - กาาาาน-";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "เขาทำเสียงแปลกๆ เหมือนพวกสัตว์เลย";
				mes "ข้าควรไปขอคำแนะนำจาก Cherto ดีไหมนะ?";
				brazil_gua = 4;
				changequest 2193,2194;
				close;
			}
		}
	}
	else if (brazil_gua == 4) {
		mes "["+strcharinfo(0)+"]";
		mes "เขาทำเสียงแปลกๆ เหมือนพวกสัตว์เลย";
		mes "ข้าควรไปขอคำแนะนำจาก Cherto ดีไหมนะ?";
		close;
	}
	else if ((brazil_gua > 4) && (brazil_gua < 9)) {
		mes "[Strange Kid]";
		mes "อา...? อา.....?";
		close;
	}
	else if (brazil_gua == 9) {
		mes "[Strange Kid]";
		mes "อา... อะฮะ.....";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าไม่มีเรื่องเล่าหรอกนะ แต่มีเพื่อนๆ มากมายรออยู่ข้างนอกน่ะ";
		next;
		mes "- เจ้ามอบขนนก เนื้อสด และกิ่งองุ่นให้กับเด็กน้อย -";
		next;
		mes "[Strange Kid]";
		mes "อา.............";
		next;
		mes "[Strange Kid]";
		mes "อึน, อื้อ....";
		mes "มอออ... ม่ามี้.....";
		next;
		mes "[Strange Kid]";
		mes "อา..........";
		mes "นก....";
		mes "ลี, ลี, ลี...ง......";
		mes "บรู๊วว, บรู๊ววว...........";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "หมางั้นรึ?!";
		mes "กิ๊กิ๊กิ ใช่แล้ว เพื่อนๆ มากมายอยากจะเจอเจ้านะ";
		mes "ไม่ต้องเหงาอีกต่อไปแล้ว และมีความสุขกับเพื่อนๆ ของเจ้านะ";
		next;
		mes "[Strange Kid]";
		mes "อา....เหะ...เหะเหะ....";
		next;
		mes "- เขาเริ่มยิ้มบางๆ และหัวเราะ -";
		next;
		mes "[Strange Kid]";
		mes "ชะ......ใช่.......";
		mes "ขะ... ขอบ......ขอบ......คุ.........คุณ.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เอาล่ะ ยิ้มน่ารักจังเลย!";
		mes "ขอให้เป็นเด็กที่มีความสุขแบบนี้ตลอดไปนะ";
		next;
		mes "[Strange Kid]";
		mes "อื้อ......";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อะไรเหรอ?";
		next;
		mes "[Strange Kid]";
		mes "เฮ้..........";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เจ้าอยากจะพูดอะไรหรือเปล่า?";
		next;
		mes "- เจ้าเข้าไปใกล้ๆ และทำท่าทางระมัดระวัง -";
		next;
		emotion ET_CHUP;
		mes "(จู๊บ~)";
		next;
		mes "- เด็กน้อยหัวเราะเบาๆ อีกครั้งแล้ววางบางอย่างไว้ในมือเจ้า -";
		next;
		mes "- มันคือเบอร์รี่สดที่มีสีแดงและแข็ง -";
		next;
		mes "[Strange Kid]";
		mes "กวา...รา..........นา...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อุ๊ย เบอร์รี่ Guarana?";
		mes "อา! ขอบใจเจ้ามากนะ!";
		emotion ET_CHUPCHUP, playerattached();
		emotion ET_SMILE;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ถ้าข้ามีสิ่งนี้ ข้าก็สามารถทำลูกอม Guarana ได้แล้ว";
		mes "ข้าไปหา Candy Maker คนนั้นดีกว่า!";
		brazil_gua = 10;
		changequest 2199,2200;
		getitem 6237,1; //Guarana_Fruit
		close;
	}
	else if (brazil_gua == 10) {
		if (!countitem(6237)) {
			mes "[Strange Kid]";
			mes "หึ.........";
			getitem 6237,1; //Guarana_Fruit
			close;
		}
	}
	else {
		mes "- เด็กน้อยกำลังยิ้ม -";
		close;
	}
}

brasilis,56,224,7	script	Mage Paje#bra	4_M_BIBI,{
	mes "[Mage Paje]";
	mes "โอม มะลึกกึ๊กกึ๋ย~";
	specialeffect EF_POISONHIT,AREA,"Poring#bra";
	setarray .@display[0],800,876,909;
	setnpcdisplay "Poring#bra",.@display[rand(3)];
	if (brazil_gua != 5) close;
	next;
	mes "[Mage Paje]";
	mes "โอ้โฮ~!";
	mes "ข้ามีแขกแฮะ";
	mes "ดีใจที่ได้พบเจ้า";
	mes "ข้าคือจอมเวทย์ Paje";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "สวัสดีครับ ท่าน Cherto บอกให้ข้ามาหาท่าน";
	next;
	mes "[Mage Paje]";
	mes "อืม.. ท่าน Cherto งั้นรึ? เกิดอะไรขึ้นล่ะ?";
	mes "เจ้ามาที่นี่เพื่อถามเกี่ยวกับข่าวลือแปลกๆ มากมายงั้นรึ?";
	next;
	mes "- เจ้ามอบบันทึกให้ Paje-";
	next;
	mes "[Mage Paje]";
	mes "โอ้โฮ~";
	mes "อืม ให้ตายสิ.. นั่นคือสิ่งที่เกิดขึ้นสินะ";
	next;
	mes "[Mage Paje]";
	mes "ข้าไม่สามารถช่วยเจ้าได้โดยตรงหรอกนะ";
	mes "แต่ข้าจะให้มนต์ง่ายๆ แก่เจ้า เพื่อที่เจ้าจะได้ไปหาคำตอบด้วยตัวเอง";
	next;
	select("มนต์ประเภทไหนกันรึ?");
	mes "[Mage Paje]";
	mes "มันคือมนต์ที่จะทำให้เจ้าดูเหมือนสัตว์ในสายตาของสัตว์อื่นๆ เจ๋งไปเลยใช่ไหมล่ะ?";
	next;
	mes "[Mage Paje]";
	mes "โอเค~ ข้าจะมอบมนต์ให้เจ้า";
	mes "สัตว์ส่วนใหญ่ค่อนข้างอ่อนไหว ดังนั้นพวกมันอาจจะรู้ตัวได้ ลองไปหา Toucan ในทุ่งนาที่ดูเหมือนจะไม่สนใจเวทมนตร์นี้ดูสิ เจ้าจะรู้เองเมื่อได้คุยกับมัน";
	next;
	mes "[Mage Paje]";
	mes "โชคดีนะ~!";
	brazil_gua = 6;
	changequest 2195,2196;
	specialeffect2 EF_ASSUMPTIO;
	close;
}

brasilis,59,226,3	script	Poring#bra	4_PORING,{}

bra_fild01,75,83,5	script	Toucan#bra	2073,2,2,{
OnTouch:
	if (brazil_gua == 6) {
		mes "[Toucan]";
		mes "แบ๊แแแอ็ก!";
		mes "ข้าไม่เคยเห็นเจ้ามาก่อนเลย";
		mes "แบ๊แแแอ็ก!";
		next;
		mes "[Toucan]";
		mes "มันอยู่กึ่งกลางระหว่างทวีปใหม่และทวีปเก่า... ข้ารู้ว่าข้าไม่เคยเห็นเจ้ามาก่อน แต่เจ้าดูคุ้นๆ นะ เหมือนผู้หญิงที่กำลังเต้นแซมบ้าเลย ";
		next;
		select("ท่านกำลังพูดถึงเรื่องอะไร?");
		mes "[Toucan]";
		mes "ข้าสัมผัสได้ถึงพลังบางอย่างที่คล้ายกับเด็ก Guarana แบ๊แแแอ็กกกก!";
		next;
		mes "[Toucan]";
		mes "เด็กคนนั้นต้องผ่านช่วงเวลาที่โดดเดี่ยวจริงๆ นะ แบ๊แแอ็ก! บางทีเจ้าอาจจะเป็นเพื่อนของเขาหรือเปล่า? แบ๊แแอ็ก!!";
		next;
		select("ยังไม่ใช่... แต่ข้าอยากเป็นเพื่อนกับเขา");
		mes "[Toucan]";
		mes "เด็กน้อยที่ได้รับการดูแลจากผู้หญิง Guarana ก็เป็นเพื่อนกับสัตว์เช่นกัน แบ๊แแแอ็ก!";
		next;
		mes "[Toucan]";
		mes "ข้าอยากจะมอบสัญลักษณ์ตัวแทนของนก Toucan ให้กับเด็กคนนั้น แบ๊แแแอ็ก!";
		next;
		mes "[Toucan]";
		mes "ถ้าเจ้าต้องการคลายความเหงาของเขา เจ้าช่วยข้าหน่อยได้ไหม?";
		next;
		select("แน่นอนที่สุด!");
		mes "[Toucan]";
		mes "นี่คือขนนกของข้า";
		mes "ส่งมันไปให้เด็กคนนั้นทีนะ";
		mes "พวกเราจะรักษาคำมั่นสัญญาแห่งมิตรภาพระหว่างเด็ก Guarana และนก Toucan ตลอดไป แบ๊แแแอ็ก!";
		next;
		mes "- เจ้าหยิบขนนกจาก Toucan - ";
		next;
		mes "[Toucan]";
		mes "ที่นี่น่าจะมีตัวอื่นๆ ที่เหมือนข้าอยู่แถวนี้อีกนะ";
		mes "ทำไมเจ้าไม่ลองไปหา Jaguar ดูล่ะ แบ๊แแแอ็ก!";
		next;
		mes "[Toucan]";
		mes "ข้าจะมอบพรจาก Toucan ให้แก่เจ้า";
		next;
		mes "[Toucan]";
		mes "บินไป บินไปให้ไกลๆ แบ๊แแแอ็กกกกกก--!";
		brazil_gua = 7;
		changequest 2196,2197;
		specialeffect2 EF_SEISMICWEAPON;
		close2;
		warp "bra_fild01",68,146;
		end;
	}
	else {
		mes "[Toucan]";
		mes "แบ๊แแแอ็กก~! แบ๊แแแอ็กก~!";
		close;
	}
	end;
}

bra_fild01,34,184,5	script	Jaguar#bra	2072,2,2,{
OnTouch_:
	if (brazil_gua == 7) {
		mes "[Jaguar]";
		mes "โฮวว..โฮวว.....";
		next;
		mes "[Jaguar]";
		mes "กลิ่นนี้! กลิ่นนี้มันมาจากมนุษย์นี่!";
		mes "ที่ไหนสักแห่ง มนุษย์!";
		mes "ข้าจับได้แล้ว เจ้าเองงั้นรึ!!!";
		specialeffect EF_HIT1,AREA,"Jaguar#bra";
		emotion ET_HUK, playerattached();
		next;
		mes "[Jaguar]";
		mes "อย่ากลัวไปเลยมนุษย์";
		mes "ข้าไม่มีพลังพอจะล่ามนุษย์หรอก แค่กำลังรอเวลาที่จะจบสิ้นอายุขัยในป่าแห่งนี้เท่านั้น";
		next;
		mes "[Jaguar]";
		mes "ยังไงก็ตาม เจ้าสามารถคุยกับข้าได้ เจ้าคือเด็ก Guarana งั้นรึ?";
		next;
		select("ใช่? ม... ไม่... ที่จริงแล้ว....");
		mes "[Jaguar]";
		mes "ลูกชายของผู้หญิง Guarana กลายมาเป็นเพื่อนของเราด้วยเช่นกัน";
		mes "พวกเขาปฏิบัติต่อทุกชีวิตอย่างล้ำค่า";
		mes "ข้าหวังว่าเจ้าจะเป็นเหมือนเธอเช่นกันนะ";
		next;
		mes "[Jaguar]";
		mes "เสียงนกจิ๊บๆ บอกข้ามา";
		mes "ลูกชายของผู้หญิง Guarana มีหัวใจที่เจ็บป่วย";
		mes "พวกพี่น้องของเธอทำให้เขาโดดเดี่ยว เจ้าไม่คิดอย่างนั้นรึ?";
		next;
		mes "[Jaguar]";
		mes "นี่คือเนื้อสดที่ข้าเพิ่งล่าได้เมื่อไม่กี่วันก่อน";
		mes "เอาไปสิ และมอบมันให้กับเด็กที่น่าสงสารคนนั้น";
		next;
		mes "[Jaguar]";
		mes "ข้าสามารถมอบสิ่งเล็กน้อยนี้ให้เจ้าได้ ดังนั้นอย่าลืมมันล่ะ";
		mes "ผืนป่าจะต้อนรับเจ้าเสมอไม่ว่าเมื่อไหร่!";
		next;
		mes "- เจ้าได้รับเนื้อสดจาก Jaguar -";
		next;
		mes "[Jaguar]";
		mes "ลิง คนที่ชอบยุ่งเรื่องของคนอื่นอยู่เสมอ อยากจะเจอเจ้าอยู่เหมือนกัน";
		next;
		mes "[Jaguar]";
		mes "ข้าจะมอบพรอันสูงส่งของ Jaguar ให้แก่เจ้า";
		mes "จงไปหาลิงด้วยการล่องลอยไปตามสายลมเหมือนผึ้ง";
		mes "แล้วเจอกันใหม่นะเพื่อนข้า!";
		brazil_gua = 8;
		changequest 2197,2198;
		close2;
		consumeitem 12016; //Speed_Up_Potion
		end;
	}
	else {
		mes "[Jaguar]";
		mes "กรูรรรรร....";
		close;
	}
	end;
}

bra_fild01,245,53,3	script	#Monkeybra	1057,{}

bra_fild01,245,52,3	script	Monkey#bra	HIDDEN_NPC,{
	if (brazil_gua == 8) {
		mes "[Monkey]";
		mes "อะไรกัน??!!";
		mes "พวกเราไม่ต้อนรับมนุษย์! ออกไปซะ~!!";
		next;
		mes "[Monkey]";
		mes "ไม่ๆ... เดี๋ยวก่อน.... กลิ่นนั่น!!";
		mes "ข้าได้กลิ่น Jaguar จากตัวเจ้า เจ้าเป็นใครกัน?";
		mes "ตายจริง หรือว่าจะมี Jaguar ที่ไม่มีขนแถมรูปร่างประหลาดแบบนี้ด้วย!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "........................";
		mes "นี่เจ้ากำลังจะบอกว่าข้าดูเหมือนสัตว์งั้นรึ?!?!";
		next;
		mes "[Monkey]";
		mes "เอ๋? เจ้าไม่ใช่ Jaguar หรอกรึ?";
		next;
		mes "[Monkey]";
		mes "อ๋ออออ Jaguar ส่งเจ้ามาหาข้าล่ะสิใช่ไหม?? กิ๊กๆ กั๊กๆ";
		mes "แต่เจ้าดูไม่เหมือนเด็กน้อย guarana เลยนะ";
		next;
		select("ข้ามาที่นี่เพื่อช่วยเขา");
		mes "[Monkey]";
		mes "ข้าได้ยินมาว่าเด็กน้อย guarana เริ่มเหงาแล้ว เขาเหงาเหรอ?";
		mes "พวกเราน่ะเป็นผู้เชี่ยวชาญด้านกายกรรมนะ เด็กน้อยจะชอบไหมนะ?? เจี๊ยกๆ กิ๊กๆ!";
		next;
		mes "[Monkey]";
		mes "เอาพวงองุ่นนี่ไปให้เด็กน้อย guarana ที";
		mes "พวกเราจะทำให้เขาสนุกได้ตลอดทั้งคืนเมื่อไหร่ก็ตามที่เขามาหาพวกเรา!! เจี๊ยกๆ กิ๊กๆ!";
		next;
		mes "- คุณได้รับพวงองุ่นจาก Monkey -";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ดีล่ะ~ ถึงเวลาต้องกลับไปหาเด็กน้อยคนนั้นแล้ว~!!";
		brazil_gua = 9;
		changequest 2198,2199;
		close;
	}
	else {
		mes "[Monkey]";
		mes "เจี๊ยกๆ กิ๊กๆ!";
		close;
	}
}

// Water Lily Quest :: brazil_tre.sc
//============================================================
brasilis,203,286,3	script	Botanist Karmen#bra	4_F_HUWOMAN,{
	if (brazil_regia == 0) {
		mes "[Karmen]";
		mes "สภาพอากาศของ Brasilis นั้นพิเศษมาก";
		mes "สภาพอากาศแบบนี้ทำให้มีกรณีศึกษาพิเศษในวิชาพฤกษศาสตร์ที่แตกต่างจากภูมิภาคอื่นๆ ในโลก";
		next;
		mes "[Karmen]";
		mes "พืชพรรณที่นี่มีโครงสร้างที่แข็งแรง ชัดเจน และมีสีสันสดใส";
		mes "พืชที่นี่มีขนาดใหญ่มากจริงๆ จนเราสัมผัสได้ถึงตัวตนของมัน";
		next;
		mes "[Karmen]";
		mes "หนึ่งในนั้นคือ Water Lily มันเป็นพืชที่สวยงามและมีเอกลักษณ์มากจริงๆ";
		mes "ดอกไม้ชนิดนี้ค่อนข้างอ่อนไหว ดังนั้นมันจึงไม่ได้เบ่งบานไปทั่วทุกที่";
		next;
		if(select("น่าสนใจครับ/ค่ะ:จบการสนทนา") == 2) {
			mes "[Karmen]";
			mes "ฉันเดาว่าคุณคงไม่สนใจวิชาพฤกษศาสตร์สินะ";
			close;
		}
		mes "[Karmen]";
		mes "มันไม่ปรากฏให้เห็นง่ายๆ และเป็นดอกไม้ที่ลึกลับแม้แต่กับชาวพื้นเมือง ดังนั้นชาว Brasilis จึงเชื่อว่าใครก็ตามที่หามันพบจะได้รับโชคลาภอันยิ่งใหญ่";
		next;
		mes "[Karmen]";
		mes "ในฐานะนักพฤกษศาสตร์ ฉันป้วนเปี้ยนอยู่แถวนี้เพื่อตามหาดอกไม้แห่งโชคลาภนั่น แต่ก็อย่างที่คาดไว้ มันยังไม่ยอมปรากฏตัวออกมาเลย";
		next;
		mes "[Karmen]";
		mes "ฉันเชื่อว่าด้วยความพากเพียรที่มากพอ ดอกไม้ดอกนี้จะแสดงความงดงามอันเจิดจรัสให้ฉันเห็น";
		next;
		mes "[Karmen]";
		mes "อ้อ ถ้าคุณสนใจเรื่องราวของ Water Lily มากกว่านี้ ลองไปหาคนชื่อ Marta ดูนะคะ";
		mes "เธอเป็นคนฉลาดและรู้เรื่องราวมากมายใน Brasilis แห่งนี้";
		brazil_regia = 1;
		setquest 2201;
		close;
	}
	else if (brazil_regia == 1) {
		mes "[Karmen]";
		mes "อ้อ ถ้าคุณสนใจเรื่องราวของ Water Lily มากกว่านี้ ลองไปหาคนชื่อ Marta ดูนะคะ";
		mes "เธอเป็นคนฉลาดและรู้เรื่องราวมากมายใน Brasilis แห่งนี้";
		close;
	}
	else if (brazil_regia == 9) {
		mes "- คุณแสดงดอกบัวให้ Karmen ดู และเล่าเรื่องราวที่ผ่านมาทั้งหมดให้เธอฟัง -";
		next;
		mes "[Karmen]";
		mes "ว้าว!! คุณได้รับประสบการณ์ที่ยอดเยี่ยมจริงๆ";
		mes "สรุปว่า~~~ Water Lily อาศัยอยู่ในส่วนลึกของ Brasilis สินะคะ?";
		mes "ฉันจะพยายามออกตามหามันด้วยตัวเองอีกครั้ง ฉันจะไม่ยอมแพ้เด็ดขาด!!";
		next;
		mes "[Karmen]";
		mes "ฉันรู้สึกขอบคุณมากจริงๆ ที่ได้พบคุณ";
		mes "Water Lily ต้องเป็นดอกไม้แห่งความโชคดีจริงๆ ด้วยสินะ ฮ่าๆๆ";
		brazil_regia = 10;
		completequest 2207;
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 75000,15000;
		else
			getexp 50000,10000;
		close;
	}
	else {
		mes "[Karmen]";
		mes "สภาพอากาศแบบนี้ทำให้มีกรณีศึกษาพิเศษในวิชาพฤกษศาสตร์ที่แตกต่างจากภูมิภาคอื่นๆ ในโลก";
		next;
		mes "[Karmen]";
		mes "พืชพรรณที่นี่มีโครงสร้างที่แข็งแรง ชัดเจน และมีสีสันสดใส";
		mes "พืชที่นี่มีขนาดใหญ่มากจริงๆ จนเราสัมผัสได้ถึงตัวตนของมัน";
		next;
		mes "[Karmen]";
		mes "มันคือความฝันของนักพฤกษศาสตร์เลยล่ะ";
		close;
	}
}

bra_in01,142,27,5	script	Marta#bra	4_F_BRZ_INDOLD,{
	if (brazil_regia == 1) {
		mes "[Brasilis Boy]";
		mes "ยาย! คนๆ นี้มีกลิ่นแปลกๆ ด้วยล่ะ";
		next;
		mes "[Marta]";
		mes "คนผู้นี้ไม่ใช่คนที่นี่จ้ะ";
		mes "กล่าวทักทายแขกของเราสิ";
		next;
		mes "[Brasilis Boy]";
		mes "ฮี่ๆ~ หวัดดี!!";
		mes "ผมชื่อ Kaka!!";
		mes "พี่ชื่ออะไรเหรอ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ฉันชื่อ "+strcharinfo(0)+".";
		next;
		mes "[Brasilis Boy]";
		mes "คนนอกนี่มีชื่อแปลกจัง!";
		mes "ไม่เป็นไรหรอก! ถ้าเราคุยกันต่อไปเดี๋ยวเราก็เป็นเพื่อนกันเอง! สู้ๆ นะ!";
		next;
		mes "[Marta]";
		mes "หึๆ...";
		mes "แล้วทำไมคนแปลกหน้าอย่างเจ้าถึงมาที่นี่ล่ะ~?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าได้ยินมาว่าท่านรู้เรื่องราวต่างๆ มากมาย เป็นความจริงไหมครับ/ค่ะ?";
		next;
		mes "[Kaka]";
		mes "วู้ว! พี่รู้ได้ไงว่ายายของผมรู้เรื่องเยอะ สุดยอดไปเลย~?";
		mes "ยายฉลาดและใจดีมากเลยล่ะ ผมเลยได้ยินเรื่องต่างๆ มาเยอะเลย";
		next;
		mes "[Marta]";
		mes "หึๆ Kaka มักจะฟังเรื่องเล่ามากมายทุกคืน เขาชอบเรื่องที่ยายเล่ามากเลยล่ะ";
		mes "Kaka ทำให้ยายมีความสุขเสมอเพราะเขามักจะถามเรื่องที่น่าสงสัยสารพัด นั่นแหละคือความสุขที่บริสุทธิ์";
		next;
		mes "[Marta]";
		mes "เอาล่ะ Kaka ทำไมหลานไม่ชวนแขกของเราเข้าร่วมการพบปะเล็กๆ ของเราวันนี้ดูล่ะ?";
		next;
		mes "[Kaka]";
		mes "ตกลงครับยาย~!!";
		next;
		mes "[Marta]";
		mes "นี่~ เจ้ามีเรื่องราวพิเศษอะไรที่อยากฟังเป็นพิเศษไหม?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เกี่ยวกับ Water Lily ที่ลึกลับน่ะครับ/ค่ะ?";
		next;
		mes "[Marta]";
		mes "Water lily....";
		mes "มันเป็นเรื่องจากเมื่อนานแสนนานมาแล้ว";
		next;
		mes "[Marta]";
		mes "ก่อนที่ Brasilis จะถูกก่อตั้งขึ้น";
		mes "เผ่าหนึ่งที่อาศัยอยู่กับน้ำตกยักษ์และป่าไม้ในฐานะเพื่อนฝูง พวกเขาใช้เวลาทั้งหมดอยู่กับธรรมชาติ";
		next;
		mes "[Marta]";
		mes "หนึ่งในหัวหน้าเผ่ามีลูกสาวแสนสวยชื่อว่า 'Naia'";
		next;
		mes "[Marta]";
		mes "Naia ชอบฟังเรื่องเล่าเหมือนกับ Kaka เลย แม่ของเธอจึงเล่าเรื่องเกี่ยวกับธรรมชาติและเทพเจ้าให้ฟังทุกคืน";
		next;
		mes "[Kaka]";
		mes "โหววว เธอเหมือนผมเลย!";
		mes "บางทีเธออาจจะสวยก็ได้นะ... ฮิฮิ";
		next;
		mes "[Marta]";
		mes "ตามที่แม่ของเธอเล่า...";
		mes "ถ้าดวงจันทร์บนท้องฟ้าเกิดรักหญิงสาวคนใดบนโลกเข้า เขาจะเปลี่ยนเธอให้กลายเป็นดวงดาวเพื่อให้พวกเขาได้อยู่ด้วยกันตลอดไป";
		next;
		mes "[Marta]";
		mes "หลังจากที่ Naia ได้ยินเรื่องนี้ เธอจึงไปถามพ่อของเธอว่าเป็นเรื่องจริงหรือไม่";
		next;
		mes "[Kaka]";
		mes "แล้วพ่อของเธอว่าไงครับ?";
		next;
		mes "[Marta]";
		mes "^3131FF'ลูกรัก Naia เอ๋ย ดวงจันทร์น่ะเป็นหนึ่งในชายที่กล้าหาญที่สุด แต่เขาไม่สามารถมีเจ้าสาวได้อีกแล้ว ดังนั้นเจ้าไม่สามารถเป็นดวงดาวได้หรอก... พ่อเสียใจด้วยนะ'^000000";
		next;
		mes "[Kaka]";
		mes "Naia อยากเป็นเจ้าสาวของชายคนนั้นเหรอครับ?";
		next;
		mes "[Marta]";
		mes "ใช่แล้ว Kaka ลองจินตนาการดูสิว่าดวงจันทร์นั้นสวยงามและลึกลับเพียงใด นั่นแหละคือสิ่งที่เด็กสาวใฝ่ฝันถึง";
		next;
		mes "[Kaka]";
		mes "แต่ดวงจันทร์ไม่พบมนุษย์เพื่อมาเป็นภรรยาอีกแล้วเหรอ? แล้วเกิดอะไรขึ้นกับ Naia ต่อครับ?";
		next;
		mes "[Marta]";
		mes "Naia เป็นเด็กดีจริงๆ นะ";
		next;
		mes "[Marta]";
		mes "แม้ว่าพ่อแม่ของเธอจะพยายามขัดขวาง แต่เธอก็ยังแอบเข้าป่าเพื่อไปหาดวงจันทร์ทุกคืน";
		mes "น่าเศร้าที่แม้เธอจะพยายามเพียงใด ดวงจันทร์ก็ไม่แสดงปฏิกิริยาใดๆ ต่อเธอเลย";
		next;
		mes "[Marta]";
		mes "วันหนึ่งเธอยังขึ้นไปบนยอดเขาเพื่อให้ได้อยู่ใกล้ชิดกับเขามากขึ้น เธอตัดสินใจพักผ่อนสักครู่ที่ริมทะเลสาบ";
		next;
		mes "[Marta]";
		mes "นั่นคือตอนที่.. Naia ได้เห็นมัน";
		mes "มันคือดวงจันทร์ที่กำลังส่องแสงอย่างงดงามเหนือทะเลสาบที่กระเพื่อมไหวอย่างแผ่วเบา";
		next;
		mes "[Kaka]";
		mes "ผมรู้แล้ว มันก็แค่แสงจันทร์ที่สะท้อนบนผิวน้ำ ใช่ไหมครับ?!";
		next;
		mes "[Marta]";
		mes "ใช่จ้ะ แต่สำหรับเธอ ภาพนั้นทำให้เธอตัดสินใจกระโดดลงไปในทะเลสาบโดยไม่ลังเลและจมน้ำเสียชีวิต";
		next;
		mes "[Kaka]";
		mes "โอ้ ไม่นะ";
		next;
		mes "[Marta]";
		mes "ดวงจันทร์เองก็เฝ้ามองเธออยู่จากบนฟ้า";
		mes "เขารู้สึกเศร้าและสงสารเธอมาก เขาจึงตัดสินใจเปลี่ยนเธอให้กลายเป็นดอกไม้ที่สวยงามเพื่อขอบคุณในความรักของเธอ";
		next;
		mes "[Marta]";
		mes "นั่นคือเรื่องราวของดอกไม้ลึกลับที่ผู้คนเรียกว่า Brasilis Water Flower";
		mes "ดอกไม้ Naia นี้จะปรากฏเป็นสีขาวนวลในช่วงกลางวัน แต่ในตอนกลางคืนจะเปลี่ยนเป็นสีแดงเนื่องจากสายใยแห่งความรักที่มีต่อดวงจันทร์";
		next;
		mes "[Kaka]";
		mes "ช่างน่าเศร้าแต่ก็สวยงามจังครับ!";
		next;
		mes "[Marta]";
		mes "แล้วเจ้าล่ะคนแปลกหน้า?";
		mes "เจ้าสนุกกับเรื่องนี้ไหม?";
		mes "ถ้าเจ้าอยากฟังเรื่องอื่นอีก ก็มาหาข้าได้นะ";
		mes "ถ้าเจ้าไม่ถือสาที่จะเล่นกับหลานชายของข้าสัก... นิดล่ะก็นะ โฮ่ๆๆ";
		brazil_regia = 2;
		close;
	}
	else if (brazil_regia > 1) {
		mes "[Kaka]";
		mes "ยายของผมเพลียๆ จากการทำธุระของเผ่าอยู่น่ะครับ!";
		mes "พี่ช่วยมาวันหลังได้ไหม?";
		close;
	}
	else {
		mes "[Marta]";
		mes "เจ้าไม่ใช่คนแถวนี้";
		mes "ข้าสัมผัสได้ถึงกลิ่นไอดินที่แปลกประหลาด";
		next;
		mes "[Marta]";
		mes "แต่ดวงตาของเจ้าเปล่งประกายด้วยความแข็งแกร่ง";
		mes "จิตวิญญาณและความมุ่งมั่นกำลังแผ่ออกมาจากทั่วทั้งร่างกายของเจ้าจริงๆ";
		next;
		mes "[Marta]";
		mes "หากเจ้าหมั่นเพียร เจ้าจะเป็นผู้ที่ยิ่งใหญ่ในสักวัน";
		close;
	}
}
bra_in01,145,27,3	duplicate(Marta#bra)	Brasilis Boy#bra	472



brasilis,270,145,5	script	Brasilis Girl#bra	4_F_BRZ_INDIAN,5,5,{
	if (!checkweight(1201,1)) {
		mes "- เดี๋ยวก่อน!! -";
		mes "- เจ้ามีไอเทมมากเกินไป -";
		mes "- ดังนั้นเจ้าจึงไม่สามารถรับไอเทมเพิ่มได้อีก -";
		mes "- ทำให้ตัวเจ้าเบาลงก่อน -";
		mes "- แล้วค่อยลองใหม่อีกครั้ง -";
		close;
	}
	if (brazil_regia == 2) {
		mes "[เสียงจากที่ไกลๆ]";
		mes "Jasira!!!";
		mes "ลูกจะไปไหนอีกแล้วรึ?!!";
		mes "กลับมานะ ได้โปรด!!";
		next;
		mes "[Brasilis Girl]";
		mes "แม่คะ หนูต้องออกไปข้างนอก!!";
		next;
		mes "[เสียงจากที่ไกลๆ]";
		mes "ไม่มีทาง~!! ลูกไม่ควรไป!!";
		next;
		mes "[Brasilis Girl]";
		mes "โธ่.. วันนี้ก็ล้มเหลวอีกแล้ว";
		next;
		mes "[Brasilis Girl]";
		mes "......";
		mes "มีอะไรเหรอ? ทำไมมองฉันแบบนั้นล่ะ?";
		mes "ฉันไม่อยากเป็นโชว์เกิร์ลหรอกนะ!! ออกไปเลย!!";
		next;
		if(select("ไม่มีอะไร ขอโทษนะ:เกิดอะไรขึ้นเหรอ?") == 1) {
			mes "[Brasilis Girl]";
			mes "ฉันเศร้ามากเลย!!!";
			close;
		}
		mes "[Brasilis Girl]";
		mes "มันไม่ใช่ธุระของคุณสักหน่อย";
		mes "คุณก็เป็นแค่คนนอก!";
		next;
		if(select("เสียมารยาทจัง!:แค่พยายามจะช่วยน่ะ") == 1) {
			mes "[Brasilis Girl]";
			mes "การที่ฉันเสียมารยาทมันเกี่ยวอะไรกับคุณด้วย??!!";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "ฉันรู้ว่าฉันแค่ผ่านมา แต่ฉันอาจจะช่วยเธอได้นะ เธอคิดว่าไงล่ะ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "การพบกันแบบนี้อาจจะเป็นมากกว่าแค่เรื่องบังเอิญก็ได้นะ";
		next;
		mes "[Brasilis Girl]";
		mes "......................";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อืม บอกชื่อเธอหน่อยได้ไหม?";
		next;
		mes "[Brasilis Girl]";
		mes "จา...";
		mes "Jasira ค่ะ";
		mes "ฉันชื่อ Jasira";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ชื่อเพราะดีนะ~";
		mes "Jasira เกิดอะไรขึ้นงั้นเหรอ?";
		next;
		mes "[Jasira]";
		mes ".............";
		next;
		mes "[Jasira]";
		mes "ฉันต้องไปพบ 'Jasi' แต่ฉันออกไปไม่ได้....";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Jasi คือใครเหรอ?";
		mes "คนรัก.... ของเธอเหรอ?";
		next;
		emotion ET_HUK;
		mes "[Jasira]";
		mes "ค...น...ร...รัก... คนรักเหรอ??!!";
		mes "ไม่มีทาง~";
		next;
		mes "[Jasira]";
		mes "ถ้าเขาเป็นคนรักของฉันก็คงดี... แต่...";
		next;
		mes "[Jasira]";
		mes "Jasi คือ......";
		mes "ดวงจันทร์ผู้ยิ่งใหญ่ค่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ดวงจันทร์เหรอ?";
		mes "หรือว่า... เธอหมายถึงดวงจันทร์จากในเรื่องเล่ารึเปล่า?";
		next;
		mes "[Jasira]";
		mes "ใช่แล้วค่ะ!";
		mes "ท่าน Jasi ที่รักมาจากดวงจันทร์บนท้องฟ้า!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ทำไมเธอถึงคิดแบบนั้นล่ะ?";
		next;
		mes "[Jasira]";
		mes "เพราะ Jasi นั้นงดงามมากจริงๆ และที่สำคัญที่สุดคือเขากำลังดูแล Water Lily ใน Brasilis อยู่ค่ะ";
		next;
		emotion ET_HUK, playerattached();
		mes "["+strcharinfo(0)+"]";
		mes "Brasilis water lily??!!";
		mes "นั่นคือดอกไม้ที่มีเอกลักษณ์นั่นไม่ใช่เหรอ?";
		next;
		mes "[Jasira]";
		mes "ใช่ค่ะ มันเป็นดอกไม้ที่ลึกลับมากและหามันเจอได้ยาก";
		mes "แต่รอบๆ ตัว Jasi น่ะมี Water Lily เยอะแยะเลย";
		mes "นั่นคือเหตุผลที่ฉันเชื่อว่า Jasi คือดวงจันทร์ค่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Jasi อยู่ที่ไหนเหรอ?";
		next;
		mes "[Jasira]";
		mes "เขาอยู่ในป่าลึกค่ะ";
		mes "อย่างที่คุณเห็นฉันอ่อนแอมาก ฉันเลยได้แต่ต้องอยู่บ้าน แต่ครั้งหนึ่งฉันเคยแข็งแรงพอที่จะออกจากหมู่บ้านนี้ไปได้ค่ะ";
		next;
		mes "[Jasira]";
		mes "ฉันแค่ร่อนเร่ไปในป่าแล้วตกลงไปที่ไหนสักแห่ง และที่นั่นเองที่ฉันได้พบเขา";
		mes "เขาใจดีมาก เขาช่วยรักษาฉันและนำทางฉันกลับบ้าน";
		mes "นั่นเป็นช่วงเวลาที่ยอดเยี่ยมมากๆ เลยค่ะ";
		next;
		mes "[Jasira]";
		mes "ตั้งแต่ฉันกลับบ้าน พ่อแม่ก็ลงโทษฉัน";
		mes "ฉันเข้าใจนะว่าทำไมพวกเขาถึงเป็นห่วง แต่ฉันก็คิดถึง Jasi มากเลย!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ทำไมไม่ไปหาเขาหลังจากที่ร่างกายแข็งแรงขึ้นแล้วล่ะ?";
		next;
		mes "[Jasira]";
		mes ".................";
		mes "ฉันอยากเจอเขาตอนนี้เลยนี่นา...";
		next;
		if(select("ช่วย Jasira:เมินเธอ") == 2) {
			mes "["+strcharinfo(0)+"]";
			mes "ขอโทษนะฉันช่วยเธอไม่ได้หรอก สู้ๆ นะ!";
			next;
			mes "[Jasira]";
			mes "กำลังร้องไห้........";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "Jasira ฉันมาที่นี่เพื่อตามหา Brasilis water lily เหมือนกันนะ";
		mes "ไม่คิดเหรอว่าโชคชะตาพาเรามาพบกัน?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ถ้าเธอบอกฉันว่าต้องไปหา Jasi ที่ไหน ฉันช่วยเธอได้นะ";
		next;
		mes "[Jasira]";
		mes "จริงเหรอคะ? แต่ฉันไม่รู้แน่ชัดว่าจะไปที่นั่นได้ยังไง ฉันแค่หลงทางไปเรื่อยๆ จนได้พบเขาน่ะค่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "จำอะไรไม่ได้เลยเหรอ?";
		mes "ถ้าเธอรู้อะไรก็ต้องบอกฉันนะ";
		next;
		mes "[Jasira]";
		mes "ขอดูก่อนนะคะ... ฉันกำลังเดินวนเวียนอยู่แถวน้ำตก แล้วก็ตกลงไปในน้ำ จากนั้นฉันก็ถูกดูดเข้าไปที่ไหนสักแห่งค่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ดีเลย นั่นยังดีกว่าไม่มีอะไรเลยนะ! ฉันจะลองหาสถานที่ที่คล้ายๆ กันดู";
		next;
		mes "[Jasira]";
		mes "ฉันให้ข้อมูลคุณไปแล้ว งั้นช่วยอะไรฉันอย่างได้ไหมคะ?";
		mes "มันง่ายมากเลย...";
		next;
		mes "[Jasira]";
		mes "ฉันอยากจะมอบผลไม้ที่แสนอร่อยให้เขาค่ะ";
		mes "ที่ที่ Jas อยู่ดูอบอุ่นดีแต่ฉันไม่เห็นมีอาหารเลย... ดังนั้นถ้าเขาได้เห็นผลไม้แสนอร่อย เขาต้องมีความสุขแน่ๆ!";
		next;
		mes "[Jasira]";
		mes "ฝากเอา Banana 10 อันไปให้เขา และบอกเขาด้วยว่าฉันคิดถึงเขามากจริงๆ";
		mes "ขอโทษด้วยที่เมินคุณก่อนหน้านี้ ได้โปรดเถอะ มีแค่คุณเท่านั้นที่ช่วยฉันได้!";
		brazil_regia = 3;
		changequest 2201,2202;
		close;
	}
	else if ((brazil_regia == 3) || (brazil_regia == 4)) {
		mes "[Jasira]";
		mes "ถ้าคุณได้พบ Jasi ฝากเอา Banana 10 อันไปให้เขาด้วยนะคะ";
		mes "ขอดูก่อนนะคะ... ฉันกำลังเดินวนเวียนอยู่แถวน้ำตก แล้วก็ตกลงไปในน้ำ จากนั้นฉันก็ถูกดูดเข้าไปที่ไหนสักแห่งค่ะ";
		next;
		mes "[Jasira]";
		mes "ถ้าคุณหาทางไม่เจอ ให้ไปที่น้ำตกแล้วถามพวกเด็กๆ ในหมู่บ้าน Brasilis ดูนะคะ";
		mes "ฉันได้ยินว่ามีเด็กคนหนึ่งเคยไปยังสถานที่แปลกประหลาดมาก่อน เขาอาจจะไปที่เดียวกับฉันก็ได้!";
		close;
	}
	else if (brazil_regia == 5) {
		mes "[Jasira]";
		mes "คุณได้พบ Jasi หรือยังคะ?";
		mes "ได้พูดเรื่องของฉันไหม?";
		mes "ไม่ได้พูดเหรอ? เอ๋? ตาบ้า! โธ่เอ๊ย~!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "นี่แม่หนู~ เธอนี่ใจร้อนจังนะ";
		mes "ฉันได้เจอเขาแล้ว และฉันก็พูดเรื่องของเธอด้วย!";
		next;
		mes "[Jasira]";
		mes "พูดเหรอคะ?";
		mes "เขาว่าไงบ้าง?";
		mes "เขายังจำฉันได้ไหม?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เขาบอกว่าเธอเป็นคนที่มีจิตใจดีมากเลยล่ะ~";
		mes "ฉันบอกเขาว่าเธอจะพยายามไปหาเขาเมื่อร่างกายของเธอดีขึ้น";
		next;
		mes "[Jasira]";
		mes "เย้เยยยย!!";
		mes "ขอบคุณนะคะ! คุณพึ่งพาได้มากกว่าที่ฉันคิดไว้ซะอีก";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ว่าแต่ ฉันกำลังมองหาผลไม้ที่เป็นสีน้ำตาลและมีเปลือกแข็งๆ อยู่น่ะ";
		mes "มันมีน้ำอยู่ข้างในและสามารถใช้เป็นแก้วน้ำดื่มได้ด้วย";
		next;
		mes "[Jasira]";
		mes "โธ่! คุณหมายถึง Coconut ใช่ไหมล่ะคะ?!";
		mes "พวกมันมีอยู่ทั่วไปใน Brasilis นี่แหละค่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ขอบใจนะ Jasira!";
		brazil_regia = 6;
		changequest 2204,2205;
		close;
	}
	else if ((brazil_regia == 6) || (brazil_regia == 7)) {
		mes "[Jasira]";
		mes "ฉันควรดูแลพละกำลังของตัวเองให้ดี!";
		mes "ฉันจะเอาแต่นอนบนเตียงตลอดไปไม่ได้หรอก คุณเห็นด้วยไหมล่ะ?";
		close;
	}
	else if (brazil_regia == 8) {
		mes "[Jasira]";
		mes "อ้าว? ทำไมคุณถึงกลับมาล่ะคะ?";
		next;
		mes "- คุณบอกเธอในสิ่งที่ Jasi ฝากมาบอก -";
		next;
		mes "[Jasira]";
		mes "โอ้... จริงเหรอคะ?";
		mes "เขาพูดแบบนั้นเหรอ?";
		mes "ตายแล้ว! ตายแล้ว!!!";
		mes "กรี๊ดดดดดด - !!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เพราะเธอแท้ๆ เลย ฉันเลยได้ดอกไม้ดอกนี้มา";
		mes "ขอบใจมากนะ!!";
		next;
		mes "[Jasira]";
		mes "ว้าววว มันสวยจังเลยค่ะ";
		next;
		mes "[Jasira]";
		mes "อา... ขอดูมันแป๊บนึงได้ไหมคะ?";
		next;
		mes "[Jasira]";
		mes "เซอร์ไพรส์~!!";
		mes "ฉันแอบทำหมวกใบนี้ตอนที่คุณไม่อยู่ และตอนนี้มันก็เสร็จสมบูรณ์ด้วยดอก Water Lily แล้วล่ะค่ะ!";
		next;
		mes "[Jasira]";
		mes "ฉันรู้ค่ะ ฉันรู้! ฉันน่ะเก่งที่สุด...";
		delitem 7553,1; //Lotus_Flower - untradable
		brazil_regia = 9;
		changequest 2206,2207;
		setarray .@card[0],4195,4177,4188; //Leaf_Cat_Card, Dryad_Card, Leib_Olmai_Card
		getitem2 5302,1,1,0,0,.@card[rand(3)],0,0,0; //Lotus_Flower_Hat
		close;
	}
	else if (brazil_regia > 8) {
		mes "[Jasira]";
		mes "ฉันแค่ต้องแข็งแรงขึ้นอีกนิด!";
		mes "ฉันจะเอาแต่นอนบนเตียงตลอดไปไม่ได้หรอก Jasi ของฉันรอฉันอยู่~";
		close;
	}
	else {
		mes "[เสียงจากที่ไกลๆ]";
		mes "Jasira!!!";
		mes "ลูกจะไปไหนอีกแล้วรึ?!!";
		mes "กลับมานะ ได้โปรด!!";
		next;
		mes "[Brasilis Girl]";
		mes "โธ่ แม่คะ~!";
		mes "ให้หนูไปเถอะค่ะ!";
		close;
	}
	end;

OnTouch:
	if (brazil_regia == 2)
		emotion ET_FRET;
	end;
}

bra_dun02,67,205,5	script	Recluse#bra	4_M_BRZ_JACI,3,3,{
	if (brazil_regia == 3) {
		mes "[Recluse]";
		mes "โอ้ ข้าไม่ได้เห็นคนอื่นมานานมากแล้วนะเนี่ย";
		next;
		if(select("เดินต่อไป:ท่านคือดวงจันทร์งั้นรึ?") == 1) {
			mes "[Recluse]";
			mes "เจ้าไม่ได้มีธุระเฉพาะเจาะจงกับข้าสินะ";
			close;
		}
		mes "[Recluse]";
		mes "ดวงจันทร์?";
		mes "ข้าชื่อ Jasi";
		mes "ครอบครัวของข้าทำงานดูแล Water Lily มาหลายชั่วอายุคนแล้ว";
		next;
		mes "[Jasi]";
		mes "โดยพื้นฐานแล้ว Brasilis water lily น่ะขี้อายเกินกว่าจะปรากฏตัวต่อหน้าผู้คน ดังนั้นพวกมันจึงเบ่งบานในที่ที่หายากเท่านั้น ข้าเดาว่าพวกมันคงชอบที่นี่ล่ะนะ นั่นคือเหตุผลที่ข้าพำนักอยู่ที่นี่มานานมากแล้ว";
		next;
		mes "[Jasi]";
		mes "ครอบครัวของข้าดูแล Water Lily อย่างเงียบเชียบเพื่อป้องกันความเสียหายจากน้ำมือของมนุษย์หรือปีศาจ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ท่านจำเด็กสาวที่ชื่อ Jasira ได้ไหมครับ/ค่ะ?";
		next;
		mes "[Jasi]";
		mes "จา...สิ..........";
		mes "อ๋อ!! เด็กสาวจอมลุกลี้ลุกลนนั่นเอง";
		mes "ตายจริง.. ข้าเกือบจะเดือดร้อนเพราะแม่หนูนั่นเชียวล่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เดือดร้อนเหรอครับ/คะ?";
		next;
		mes "[Jasi]";
		mes "วันหนึ่งมีหญิงสาวที่มีรอยแผลเต็มตัวปรากฏตัวขึ้นข้าเลยช่วยรักษาเธอ ทันใดนั้นเธอก็พยายามจะเก็บ Water Lily และขอให้ข้ารับเธอเป็นเมีย หรือไม่ก็เปลี่ยนเธอให้เป็น Water Lily เสียอย่างนั้น ยัยบ้าเอ๊ย!";
		next;
		mes "[Jasi]";
		mes "ข้าแทบจะทำให้เธอสงบลงไม่ได้และส่งเธอกลับไปที่หมู่บ้าน";
		mes "ชีวิตของข้าคือ Water Lily เหล่านี้ ดังนั้นข้าจึงไม่อยากให้มีอะไรน่าอายเกิดขึ้น";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "นี่เป็นของขวัญจาก Jasira เพื่อกล่าวคำขอโทษสำหรับช่วงเวลานั้นครับ/ค่ะ";
		mes "เธอเศร้ามากที่ไม่สามารถมาที่นี่ได้ด้วยตัวเองเพราะอุปสรรคส่วนตัว";
		next;
		if (countitem(513) < 10) {
			mes "[Jasi]";
			mes "เจ้ากำลังพูดเรื่องอะไรน่ะ?";
			next;
			mes "- โอ้ ใช่แล้ว ฉันลืมนำ Banana 10 อันมาด้วย -";
			close;
		}
		mes "[Jasi]";
		mes "อ๊ะ! กล้วย! ว้าว ไม่ได้กินมานานแล้วนะเนี่ย เธอช่างเป็นคนที่มีน้ำใจจริงๆ ว่าไหม?";
		next;
		mes "[Jasi]";
		mes "ยังไงก็ตาม เจ้ามีธุระกับข้าแค่นี้ใช่ไหม?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เอ่อ บอกตามตรงครับ/ค่ะ ข้ากำลังตามหา Water Lily และได้พบกับท่านโดยบังเอิญ Jasira เล่าเรื่องเศร้าของเธอให้ข้าฟัง นั่นเลยเป็นสิ่งที่นำทางข้ามาที่นี่";
		next;
		mes "[Jasi]";
		mes "ข้าเข้าใจแล้ว";
		mes "อย่างที่เจ้าเห็น มี Brasilis water lily อยู่มากมายแถวนี้";
		mes "ถ้าเจ้าสัญญาว่าจะไม่ทำลายพวกมัน เจ้าจะชื่นชมพวกมันได้ตามที่เจ้าต้องการเลยล่ะ";
		brazil_regia = 4;
		changequest 2202,2203;
		close;
	}
	else if (brazil_regia == 4) {
		mes "[Jasi]";
		mes "เพลิดเพลินกับ Water Lily หรือยังล่ะ?";
		close;
	}
	else if (brazil_regia == 5) {
		mes "[Jasi]";
		mes "ข้าลืมไปแล้วว่าผลไม้นั่นชื่ออะไรกันนะ...";
		close;
	}
	else if (brazil_regia == 6) {
		if (countitem(11515) < 5) {
			mes "[Jasi]";
			mes "ข้าลืมไปแล้วว่าผลไม้นั่นชื่ออะไรกันนะ...";
			close;
		}
		else {
			mes "[Jasi]";
			mes "เจ้าหาผลไม้นั่นเจอแล้วรึ?";
			mes "โอ้ ใช่แล้ว นี่คือ....?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "มันเรียกว่า 'coconut' ครับ/ค่ะ";
			next;
			mes "[Jasi]";
			mes "อ้อ! มะพร้าว!!";
			mes "ตอนนี้ข้าจำได้แล้ว ขอบคุณมากนะ ข้าจำไม่ได้แล้วว่าครั้งสุดท้ายที่ได้กินผลไม้นี้คือเมื่อไหร่";
			next;
			mes "[Jasi]";
			mes "ข้าเดาว่าข้าควรรักษาสัญญา";
			mes "เจ้าสามารถเอา Water lily ไปได้ดอกหนึ่ง";
			next;
			mes "[Jasi]";
			mes "ข้าหวังว่า Brasilis water lily จะเข้าใจข้านะ";
			mes "เจ้าควรจะรีบคว้าดอกไม้ไว้ในขณะที่มีโอกาสล่ะ~";
			next;
			mes "[Jasi]";
			mes "โอ้ เจ้าช่วยบอกอะไรบางอย่างกับแม่หนู Jasira แทนข้าหน่อยได้ไหม?";
			mes "บอกเธอว่าข้าไม่ใช่ดวงจันทร์จากในเรื่องเล่าหรอกนะ แต่ข้าอยากจะเป็นดวงจันทร์ที่จะส่องแสงเพื่อเธอเพียงผู้เดียว";
			delitem 11515,5; //Coconut
			brazil_regia = 7;
			changequest 2205,2206;
			close;
		}
	}
	else {
		mes "[Jasi]";
		mes "ดอกไม้ที่เบ่งบานจาก Water lily วันนี้มันช่างยอดเยี่ยมจริงๆ";
		close;
	}
	end;

OnTouchNPC:
	unitwarp 0,"this",67,215;
	end;
}

bra_dun02,71,200,3	script	Water lily#bra	HIDDEN_NPC,{
	if (brazil_regia == 4) {
		mes "Water lily ที่ไม่ธรรมดากำลังเบ่งบานอยู่ที่นี่ คุณไม่สามารถหยุดจ้องมองมันได้เลย โดยที่รู้อยู่เต็มอกว่ามีเพียงไม่กี่คนนักที่เคยเห็นดอกไม้นี้เบ่งบาน";
		next;
		if(select("เด็ดดอกไม้:จ้องมองต่อไป") == 2) {
			mes "- คุณไม่สามารถหลีกเลี่ยงการจ้องมองความงามของมันได้เลย -";
			close;
		}
		mes "[Jasi]";
		mes "เฮ้! เจ้ากำลังจะทำอะไรน่ะ??!!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "มีคนที่ต้องการดอกไม้นี้จริงๆ ครับ/ค่ะ ข้าขอแค่ดอกเดียวได้ไหม?";
		next;
		mes "[Jasi]";
		mes "อย่างที่ข้าบอกไปก่อนหน้านี้ ข้าเป็นผู้พิทักษ์ Water lily เหล่านี้";
		mes "ข้าไม่สามารถยืนดูเฉยๆ และปล่อยให้เจ้ามาเด็ดดอกไม้ไปแม้เพียงดอกเดียวหรอกนะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "โธ่ท่าน~ ข้าอุตส่าห์นำผลไม้แสนอร่อยพวกนี้มาให้ท่าน... ลองชิมดูสิ! มันคือกล้วยที่สุกงอมและสดใหม่มากเลยนะ";
		next;
		if (countitem(513) < 10) {
			mes "[Jasi]";
			mes "เจ้ากำลังพูดเรื่องอะไรน่ะ?";
			next;
			mes "- โอ้ ใช่แล้ว ฉันลืมนำ Banana 10 อันมาด้วย -";
			close;
		}
		mes "[Jasi]";
		mes "อืม... ก็นานมากแล้วที่ข้าไม่ได้กินผลไม้นี้";
		next;
		mes "[Jasi]";
		mes "ข้าจะลองชิมดูอันนึงแล้วกัน แค่นั้นแหละ";
		next;
		mes "- ง่ำๆ -";
		mes "- งึมงำ งึมงำ งึมงำ -";
		specialeffect EF_POTION7,AREA,"Recluse#bra";
		next;
		mes "[Jasi]";
		mes "โอ้ รสชาตินี้... มัน!";
		mes "ข้าจำได้ว่าแม่ของข้าเคยอบพวกมันจนกลายเป็นขนมปังแสนอร่อย!";
		next;
		mes "[Jasi]";
		mes "มันทำให้ข้าคิดถึงวัยเด็กของข้าจริงๆ";
		next;
		emotion ET_HUK, getnpcid(0, "Recluse#bra");
		mes "[Jasi]";
		mes "โฮ่ๆๆๆ!!!!";
		mes "ข้าอยู่ที่นี่มานานเท่าที่ข้าจะจำความได้...";
		mes "ข้าไม่มีเวลาพอที่จะทำเรื่องง่ายๆ อย่างการกินผลไม้แสนอร่อยเลย";
		next;
		mes "[Jasi]";
		mes "มันเป็นกล้วยที่อร่อยมากจริงๆ";
		mes "แต่กฎก็คือกฎ!";
		mes "ข้าต้องทำหน้าที่ของข้า";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ได้โปรดเถอะ! ข้าต้องการแค่ดอกเดียวเอง~ ข้าต้องทำยังไงถึงจะโน้มน้าวท่านได้?";
		next;
		mes "[Jasi]";
		mes "กฎก็คือกฎ เจ้าต้องการอะไรจากข้าล่ะ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "กล้วยนั่นไม่ได้ทำให้ท่านคิดถึงวัยเด็กหรอกเหรอครับ/คะ? มีอะไรที่ข้าหามาให้ท่านได้อีกไหม?";
		next;
		mes "[Jasi]";
		mes "พอเจ้าพูดขึ้นมา มีผลไม้อยู่อย่างหนึ่งที่ข้าคิดถึงมันมากจริงๆ";
		mes "มันเป็นของโปรดของข้าตอนที่ข้ายังเป็นเด็ก แต่ข้าจำไม่ได้แล้วว่ามันเรียกว่าอะไร";
		next;
		mes "[Jasi]";
		mes "มันเป็นสีน้ำตาลและมีเปลือกแข็งหุ้มอยู่ รอบๆ มันมีน้ำอยู่ข้างใน และเจ้าสามารถใช้มันเป็นแก้วน้ำได้เมื่อกินเนื้อผลไม้เสร็จแล้ว";
		mes "เจ้ารู้ไหมว่ามันคืออะไร?";
		next;
		mes "[Jasi]";
		mes "ถ้าเจ้านำสิ่งนั้นมาให้ข้า 5 อัน ข้าจะลองพิจารณาข้อเสนอของเจ้าดูอีกครั้ง";
		delitem 513,10; //Banana
		brazil_regia = 5;
		changequest 2203,2204;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ตกลงครับ/ค่ะ งั้นข้าต้องไปหาผลไม้เปลือกแข็งมาให้ 5 อันสินะ";
		mes "อืม มันคืออะไรกันนะ?";
		close;
	}
	else if (brazil_regia == 5) {
		mes "[Jasi]";
		mes "มันเป็นของโปรดของข้าตอนที่ข้ายังเป็นเด็ก แต่ข้าจำไม่ได้แล้วว่ามันเรียกว่าอะไร";
		next;
		mes "[Jasi]";
		mes "มันเป็นสีน้ำตาลและมีเปลือกแข็งหุ้มอยู่ รอบๆ มันมีน้ำอยู่ข้างใน และเจ้าสามารถใช้มันเป็นแก้วน้ำได้เมื่อกินเนื้อผลไม้เสร็จแล้ว";
		mes "เจ้ารู้ไหมว่ามันคืออะไร?";
		close;
	}
	else if (brazil_regia == 7) {
		if (!checkweight(1201,1)) {
			mes "- เดี๋ยวก่อน!! -";
			mes "- เจ้ามีไอเทมมากเกินไป -";
			mes "- ดังนั้นเจ้าจึงไม่สามารถรับไอเทมเพิ่มได้อีก -";
			mes "- ทำให้ตัวเจ้าเบาลงก่อน -";
			mes "- แล้วค่อยลองใหม่อีกครั้ง -";
			close;
		}
		mes "- คุณเด็ดดอก Water lily ที่สวยงามขึ้นมาอย่างระมัดระวัง -";
		brazil_regia = 8;
		getitem 7553,1; //Lotus_Flower
		close;
	}
}

// Dungeon Access Quest :: brazil_tre.sc
//============================================================
brasilis,185,246,5	script	Pedro#bra	1_M_SIZ,{
	if (brazil_ghost == 0) {
		OnTalk:
		mes "[Pedro]";
		mes "ว้าว มันเป็นรูปปั้นที่ยอดเยี่ยมจริงๆ!";
		next;
		mes "[Mariana]";
		mes "ใช่ไหมล่ะ?";
		mes "รูปปั้นนี้เรียกว่า Verass Monument น่ะ";
		next;
		mes "[Mariana]";
		mes "กาลครั้งหนึ่งนานมาแล้ว มีนักผจญภัยที่กล้าหาญมากชื่อว่า Verass ด้วยการสำรวจที่ทุ่มเทของเขา ทำให้ Brasilis พัฒนาจนกลายเป็นเมืองที่ยิ่งใหญ่แบบนี้";
		next;
		mes "[Pedro]";
		mes "เจ๋งไปเลย!!";
		mes "ฉันอยากจะเป็นลูกผู้ชายตัวจริงแบบ Verass บ้าง";
		next;
		mes "[Mariana]";
		mes "Pedro นายจะเป็นอะไรก็ได้ที่นายต้องการนั่นแหละ";
		next;
		mes "[Pedro]";
		mes "Mariana ฉลาดจังเลยเนอะ? ฮิฮิ";
		next;
		mes "[Fabio]";
		mes "โอ้วววว! นายรักเธอใช่ไหมล่ะ!";
		next;
		mes "[Daniel]";
		mes "วู้ววววว Pedro กับ Mari นั่งจู๋จี๋กันบนต้นไม้!";
		next;
		mes "[Fabio]";
		mes "วู้วววว จ-ู-บ-ก-ั-น~!!!";
		next;
		mes "[Daniel]";
		mes "เนียะ เนียะ เนียะ!";
		next;
		mes "[Fabio]";
		mes "ฮ่าฮ่าฮ่าฮ่า";
		next;
		mes "[Pedro]";
		mes "หยุดทำตัวเหมือนเด็กๆ ได้แล้ว!";
		next;
		mes "[Mariana]";
		mes "พวกนายนี่~!";
		next;
		mes "[Daniel]";
		mes "จ้าๆ...";
		mes "เฮ้พวกนาย ได้ยินเรื่องที่เกิดขึ้นเมื่อไม่กี่วันก่อนไหม?";
		next;
		mes "[Mariana]";
		mes "อ๋อ ใช่~ ฉันได้ยินมาว่ามีเรื่องที่น่ากลัวมากๆ เกิดขึ้นล่ะ";
		next;
		mes "[Fabio]";
		mes "อา ใช่ นั่นแหละที่ทำให้ Mariana กลัวจนไม่กล้าเข้าห้องน้ำตั้ง 3 วัน จนตัวเหม็นไปหมดเลย ยี้~";
		next;
		mes "[Daniel]";
		mes "ฮ่า ฮ่า ฮ่า! Mari ตัวเหม็น!";
		next;
		mes "[Mariana]";
		mes "ฉันเกลียดพวกนาย~!";
		mes "หยุดปล่อยข่าวลือเกี่ยวกับฉันได้แล้ว ฉันไม่ได้กลัวห้องน้ำซะหน่อย";
		mes "Pedro นายคิดว่าฉันตัวเหม็นเหรอ?";
		next;
		mes "[Pedro]";
		mes "เอ๊ะ? เอ๊ะ?";
		mes "ไ......ไม่มี... ไม่มีทาง";
		mes "เฮ้พวกนาย~ ทำตัวดีๆ กับเธอหน่อยสิ~";
		next;
		mes "[Fabio]";
		mes "คิๆๆๆๆๆๆๆๆๆๆ";
		next;
		mes "[Daniel]";
		mes "คิๆๆๆๆๆๆๆๆๆ";
		next;
		if(select("เดินผ่านไป:ถามเรื่องซุบซิบ") == 1) {
			mes "[Fabio]";
			mes "Mariana~ ตัวเหม็น~ เนียะ เนียะ~";
			next;
			mes "[Daniel]";
			mes "โธ่เพื่อน นายก็เหม็นเหมือนกันนั่นแหละ~! เนียะ เนียะ~";
			close;
		}
		mes "[Fabio]";
		mes "นี่ยังไม่เคยได้ยินเหรอ?";
		mes "เรื่องผีในพิพิธภัณฑ์ศิลปะไง";
		next;
		mes "[Daniel]";
		mes "โอ้ววววว! น่ากลัวจังเลย~~~!";
		next;
		select("ช่วยเล่ารายละเอียดให้ฟังหน่อยได้ไหม?");
		mes "[Fabio]";
		mes "เมื่อสองสามวันก่อน พวกเราไปทัศนศึกษาที่พิพิธภัณฑ์ศิลปะกับโรงเรียน";
		mes "ก็รู้ใช่ไหมว่าไม่มีอะไรพิเศษ ก็แค่การทัศนศึกษาธรรมดาๆ";
		next;
		mes "[Fabio]";
		mes "พิพิธภัณฑ์มันน่าเบื่อ ฉันกับเพื่อนบางคนเลยแอบหนีออกจากกลุ่มมา~!";
		next;
		mes "[Fabio]";
		mes "นั่นแหละตอนที่เราได้ยินเสียงกรีดร้องดังก้องไปทั่วทั้งพิพิธภัณฑ์";
		next;
		mes "[Daniel]";
		mes "ก๊ากกกกกกก๊าาาาา!!";
		mes "กี๊เยยยยยย!";
		mes "เคะเคะ..";
		next;
		mes "[Mariana]";
		mes "ฉันก็ได้ยินเสียงกรีดร้องเหมือนกัน...";
		mes "พวกนายนี่ชอบส่งเสียงดังในที่ที่ไม่ควรทำอยู่เรื่อยเลย";
		next;
		mes "[Pedro]";
		mes "แล้วจะให้พวกเราทำอะไรล่ะ? ถ้าเราไม่ทำ คนอื่นก็ทำอยู่ดี";
		next;
		emotion ET_FRET, getnpcid(0, "Mariana#bra");
		mes "[Mariana]";
		mes "อ๊าก~ บางทีพวกผู้ชายนี่ก็น่าหงุดหงิดจริงๆ";
		next;
		select("แล้วหลังจากนั้นเกิดอะไรขึ้น?");
		mes "[Fabio]";
		mes "Daniel กับฉันเดาว่าต้องมีอะไรแปลกๆ เกิดขึ้นแน่ๆ เราเลยวิ่งไปทางที่คิดว่าเป็นต้นเสียงของเสียงกรีดร้องนั่น";
		next;
		mes "[Fabio]";
		mes "เสียงมันมาจากในห้องน้ำ";
		mes "เด็กบางคนกลัวมากจนเริ่มกรีดร้องตามแล้วก็หลับตาปี๋เลยล่ะ สถานการณ์ตอนนั้นแย่มาก";
		next;
		mes "[Daniel]";
		mes "ฉันว่านายนั่นแหละที่ฉี่หรืออึราดกางเกง กลิ่นนี่โคตรเหม็นเลย";
		next;
		mes "[Fabio]";
		mes "ไม่มีทาง~ แม่นายนั่นแหละฉี่ราด~ แบร่!";
		next;
		mes "[Daniel]";
		mes "ไม่มีทาง~ นายนั่นแหละ~ ฉี่ราดกางเกง~";
		next;
		mes "[Fabio]";
		mes "เอาเถอะ สรุปว่านั่นแหละคือที่มาของข่าวลือเรื่องผีในพิพิธภัณฑ์";
		next;
		mes "[Daniel]";
		mes "โกหกน่า ผีไม่มีจริงหรอก~";
		next;
		select("สรุปว่ามันคือผีเหรอ?");
		mes "[Fabio]";
		mes "ฉันจะไปรู้ได้ไงล่ะ?";
		mes "ไม่มีใครกล้ายืนยันว่าเห็นจริงๆ แล้วก็ไม่มีใครอยากถูกครูทำโทษด้วย";
		next;
		mes "[Pedro]";
		mes "ฉันได้ยินมาว่าถ้าพูดคำคาถาพิเศษ ผีจะออกมาล่ะ";
		next;
		mes "[Daniel]";
		mes "เลิกสอดเรื่องที่พวกเราคุยกันได้แล้ว Pedro";
		next;
		mes "[Fabio]";
		mes "ใช่ นายพูดเรื่องอะไรน่ะ Pedro?";
		mes "แล้วสรุปนายเห็นผีไหม?";
		next;
		mes "[Pedro]";
		mes "ม... ไม่ ฉันกลัวผีจะตาย";
		mes "แต่เพื่อนฉันบอกว่าพวกเขาเห็น และพวกเขาไม่ใช่คนโกหกนะ";
		next;
		select("มีใครบอกคำคาถามั้ย?");
		mes "[Pedro]";
		mes "ฉันได้ยินมาเป็นเหมือนเพลงน่ะ";
		mes "คำคาถาพิเศษก็คือ...";
		next;
		mes "[Pedro]";
		mes "'^3131FFแม่ครับ ประตูมันเปิดไม่ออก!^000000'";
		mes "'^FF0000ไขกุญแจตามจำนวนสีของรุ้งกินน้ำ^000000'";
		mes "'^3131FFแม่ครับ น้ำกำลังจะท่วมแล้ว!^000000'";
		mes "'^FF0000ถ้าดวงจันทร์หายไป 3 ครั้ง ก็อย่าได้กังวลไปเลย^000000'";
		mes "'^3131FFแม่ครับ ความแห้งแล้งเริ่มต้นขึ้นแล้ว!^000000'";
		mes "'^FF0000ไม่ต้องห่วงนะ น้ำตกจะช่วยมันเอง^000000'";
		mes "'^3131FFแม่ครับ เพื่อนๆ ของผมอยู่ที่ไหนกันหมด?^000000'";
		mes "'^FF0000เพื่อนทั้ง 7 ของลูกกำลังหลับไหล ได้เวลาปลุกพวกเขาแล้ว^000000'";
		mes "'^3131FFแม่ครับ แม่อยู่ที่ไหน?^000000'";
		next;
		mes "[Mariana]";
		mes "อืม ฟังดูเหมือนปริศนาคำทายเลยนะ";
		next;
		select("อยากช่วยฉันตามหาผีตัวนี้ไหม?");
		mes "[Pedro]";
		mes "นายลุยไปคนเดียวเถอะเพื่อน~";
		next;
		mes "[Mariana]";
		mes "ฉันไม่ชอบเรื่องน่ากลัวๆ!";
		next;
		mes "[Fabio]";
		mes "เหอะ ฉันไม่อยากจะเชื่อเลยว่านายจะเชื่อเรื่องนั้นจริงๆ";
		next;
		mes "[Daniel]";
		mes "ฉันจะทำตามที่ Fabio ทำ เหมือนเดิมทุกครั้งนั่นแหละ!";
		next;
		mes "[Fabio]";
		mes "บางทีนายอาจจะแค่กลัว...";
		brazil_ghost = 1;
		setquest 2208;
		close;
	}
	else if (brazil_ghost == 1) {
		mes "[Pedro]";
		mes "อยากฟังคำคาถาอีกรอบไหม?";
		next;
		mes "[Pedro]";
		mes "'^3131FFแม่ครับ ประตูมันเปิดไม่ออก!^000000'";
		mes "'^FF0000ไขกุญแจตามจำนวนสีของรุ้งกินน้ำ^000000'";
		mes "'^3131FFแม่ครับ น้ำกำลังจะท่วมแล้ว!^000000'";
		mes "'^FF0000ถ้าดวงจันทร์หายไป 3 ครั้ง ก็อย่าได้กังวลไปเลย^000000'";
		mes "'^3131FFแม่ครับ ความแห้งแล้งเริ่มต้นขึ้นแล้ว!^000000'";
		mes "'^FF0000ไม่ต้องห่วงนะ น้ำตกจะช่วยมันเอง^000000'";
		mes "'^3131FFแม่ครับ เพื่อนๆ ของผมอยู่ที่ไหนกันหมด?^000000'";
		mes "'^FF0000เพื่อนทั้ง 7 ของลูกกำลังหลับไหล ได้เวลาปลุกพวกเขาแล้ว^000000'";
		mes "'^3131FFแม่ครับ แม่อยู่ที่ไหน?^000000'";
		close;
	}
	else {
		mes "[Pedro]";
		mes "ฉันสงสัยจังว่าต้องทำยังไงถึงจะมีรูปปั้นของตัวเองบ้างนะ?";
		close;
	}
}

brasilis,187,244,1	script	Mariana#bra	1_F_MARIA,{
	if (brazil_ghost == 0) {
		doevent "Pedro#bra::OnTalk";
		end;
	}
	else if (brazil_ghost == 1) {
		mes "[Mariana]";
		mes "พวกนายเลิกพูดเรื่องผีกันได้หรือยัง?";
		mes "ฉันขนลุกไปหมดทั้งตัวแล้วเนี่ย";
		close;
	}
	else {
		mes "[Mariana]";
		mes "ทำไม Fabio กับ Daniel ชอบมารบกวนพวกเราอยู่เรื่อยเลยนะ?";
		close;
	}
}

brasilis,181,250,5	script	Fabio#bra	4_M_KID1,{
	if (brazil_ghost == 0) {
		doevent "Pedro#bra::OnTalk";
		end;
	}
	else if (brazil_ghost == 1) {
		mes "[Fabio]";
		mes "นายยังเสียเวลากับเรื่องผีหลอกเด็กนั่นอยู่อีกเหรอ?";
		close;
	}
	else {
		mes "[Fabio]";
		mes "Mariana อยากเห็นอะไรเจ๋งๆ ไหม?";
		next;
		mes "[Mariana]";
		mes "กรี๊ดดดดดดดดดด!! แมลง!! เอามันออกไปนะ!";
		close;
	}
}

brasilis,180,249,5	script	Daniel#bra	4_M_KID1,{
	if (brazil_ghost == 0) {
		doevent "Pedro#bra::OnTalk";
		end;
	}
	else if (brazil_ghost == 1) {
		mes "[Daniel]";
		mes "เนียะ เนียะ เนียะ~";
		close;
	}
	else {
		mes "[Daniel]";
		mes "คิๆ มานี่ๆ~ ฉันเจอแมลงเพิ่มอีกแล้ว~";
		close;
	}
}

bra_in01,149,184,3	script	Door#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- มีกุญแจเสียบอยู่ในประตูที่ล็อคอยู่ -";
		next;
		switch(select("ไขกุญแจ:เพิกเฉยมัน")) {
		case 1:
			mes "คุณเริ่มร่ายคาถาบทแรก";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother the door won't open!";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าคุณจะพูดอะไรบางอย่างผิดไป";
				close;
			}
			else {
				if (brazil_ghost == 2) {
					mes "[เสียงสะอื้น]";
					mes "'^FF0000ไขกุญแจตามจำนวนสีของรุ้งกินน้ำ^000000'";
					next;
					switch(select("เปิดประตู:เคาะประตู:ไขกุญแจ:ดึงกุญแจออก")) {
					case 1:
						mes "ประตูถูกล็อคอยู่";
						mes "ไม่มีอะไรเกิดขึ้น";
						close;
					case 2:
						mes "ควรจะลองเคาะกี่ครั้งดี?";
						input .@input,0,999;
						next;
						mes "คุณเคาะประตูไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 3:
						mes "ควรจะไขกุญแจกี่ครั้งดี?";
						input .@input,0,999;
						next;
						if (.@input == 7) {
							mes "คุณไขกุญแจไป 7 ครั้ง";
							next;
							mes "คลิก! คลิก! คลิก!";
							mes "คลิก! คลิก! คลิก!";
							mes "คลิก...!";
							next;
							mes "[เสียงจากระยะไกล]";
							mes "^FF0000คึกเค้- ฮิฮิฮิฮิ!!!^000000";
							mes "คุณได้ยินเสียงน้ำชักโครก";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางห้องน้ำ";
							brazil_ghost = 3;
							changequest 2208,60351;
							close;
						}
						else {
							mes "คุณไขกุญแจไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					case 4:
						mes "ควรจะเสียบกุญแจเข้าประตูอีกกี่ครั้งดี?";
						input .@input,0,999;
						next;
						mes "คุณเสียบกุญแจไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					}
				}
				else {
					mes "Mother the door won't open!";
					close;
				}
			}
		case 2:
			mes "คุณไม่ได้ทำอะไรเลย";
			close;
		}
	}
	else {
		mes "- มีกุญแจเสียบอยู่ในประตูที่ล็อคอยู่ -";
		close;
	}
}

bra_in01,144,187,3	script	Toilet#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- ดูเหมือนห้องน้ำธรรมดาๆ -";
		next;
		if (brazil_ghost > 6) {
			switch(select("กดชักโครก:ไม่ทำอะไรเลย")) {
			case 1:
				mes "หลังจากกดชักโครก คุณรู้สึกเวียนหัวทันทีและถูกพัดพาไปที่ไหนสักแห่ง";
				specialeffect2 EF_WATERFALL_SMALL_T2_90;
				close2;
				warp "bra_in01",206,102;
				end;
			case 2:
				mes "น้ำในชักโครกดูสกปรกจัง";
				close;
			}
		}
		switch(select("ใช้ห้องน้ำ:เพิกเฉย")) {
		case 1:
			mes "- คาถาบทที่สองคืออะไรแล้วนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother the water is flooding!";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าคุณจะพูดอะไรบางอย่างผิดไป";
				close;
			}
			else {
				if (brazil_ghost == 3) {
					mes "[เสียงสะอื้น]";
					mes "^FF0000ถ้าดวงจันทร์หายไป 3 ครั้ง... ก็อย่าได้กังวลไปเลย.....^000000";
					next;
					switch(select("กดชักโครก:ปิดฝาชักโครก")) {
					case 1:
						mes "ควรจะกดชักโครกกี่ครั้งดี?";
						input .@input,0,999;
						next;
						if (.@input == 3) {
							mes "คุณกดชักโครกไป 3 ครั้ง";
							next;
							mes "คว่าาาาาาาาา!";
							mes "คว่าาาาาาาาาา!";
							mes "คว่าาาาาาาาาาาาาาาาาาา!";
							next;
							mes "[เสียงจากระยะไกล]";
							mes "^FF0000คึกเค้- ฮิฮิฮิฮิ!!!^000000";
							mes "ทันใดนั้นเสียงน้ำในอ่างล้างหน้าก็ดังขึ้นเหมือนน้ำไหลออกมาเอง";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางก๊อกน้ำ";
							brazil_ghost = 4;
							changequest 60351,60352;
							close;
						}
						else {
							mes "คุณกดชักโครกไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					case 2:
						mes "คุณปิดฝาชักโครก";
						mes "ดูเหมือนจะไม่มีอะไรเกิดขึ้น";
						close;
					}
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "คุณไม่ได้ทำอะไรเลย";
			close;
		}
	}
	else {
		mes "- ดูเหมือนห้องน้ำธรรมดาๆ -";
		close;
	}
}

bra_in01,134,189,3	script	Faucet#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- มันดูเหมือนก๊อกน้ำธรรมดาๆ -";
		next;
		switch(select("ตรวจสอบมัน:เพิกเฉย")) {
		case 1:
			mes "- คาถาบทถัดไปคืออะไรแล้วนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother the drought has started!";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าคุณจะพูดอะไรบางอย่างผิดไป";
				close;
			}
			else {
				if (brazil_ghost == 4) {
					mes "[เสียงสะอื้น]";
					mes "^FF0000ไม่ต้องห่วงนะ... น้ำตกจะช่วยมันเอง....^000000";
					next;
					switch(select("เคาะที่ก๊อกน้ำ:เปิดน้ำ")) {
					case 1:
						mes "คุณจะเคาะก๊อกน้ำกี่ครั้ง?";
						input .@input,0,999;
						next;
						mes "คุณเคาะก๊อกน้ำไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 2:
						mes "ควรจะเปิดน้ำกี่ครั้งดี?";
						input .@input,0,999;
						next;
						if (.@input == 1) {
							mes "คุณเปิดก๊อกน้ำหนึ่งครั้ง";
							next;
							mes "ซว่าาาาาาาาา-";
							next;
							mes "[เสียงจากระยะไกล]";
							mes "^FF0000คึกเค้- ฮิฮิฮิฮิ!!!^000000";
							mes "คุณเห็นพรมขยับ";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางพรม";
							brazil_ghost = 5;
							changequest 60352,60353;
							close;
						}
						else {
							mes "คุณเปิดก๊อกน้ำไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					}
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "คุณไม่ได้ทำอะไรเลย";
			close;
		}
	}
	else {
		mes "- มันดูเหมือนก๊อกน้ำธรรมดาๆ -";
		close;
	}
}

bra_in01,138,184,3	script	Carpet#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- พรมที่มีลวดลายประณีต -";
		next;
		switch(select("ตรวจสอบมัน:เพิกเฉย")) {
		case 1:
			mes "- คาถาบทถัดไปคืออะไรแล้วนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother where are my friends?";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าคุณจะพูดอะไรบางอย่างผิดไป";
				close;
			}
			else {
				if (brazil_ghost == 5) {
					mes "[เสียงสะอื้น]";
					mes "^FF0000เพื่อนทั้ง 7 ของลูก....กำลัง...หลับไหล... ได้เวลา ....ปลุกพวกเขาแล้ว........^000000";
					next;
					switch(select("กระโดดบนพรม:นอนบนพรม:สะบัดพรม")) {
					case 1:
						mes "ควรจะกระโดดกี่ครั้งดี?";
						input .@input,0,999;
						next;
						mes "คุณกระโดดบนพรมไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 2:
						mes "ควรจะนอนบนพรมกี่ครั้งดี?";
						input .@input,0,999;
						next;
						mes "คุณลงไปนอนบนพรม "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 3:
						mes "ควรจะสะบัดพรมกี่ครั้งดี?";
						input .@input,0,999;
						next;
						if (.@input == 7) {
							mes "คุณสะบัดพรม 7 ครั้ง";
							next;
							mes "- ปลิว~ ปลิว~ ปลิว~ ปลิว~ ปลิว~ ปลิว~ ปลิว~ -";
							next;
							mes "[เสียงจากระยะไกล]";
							mes "^FF0000คึกเค้- ฮิฮิฮิฮิ!!!^000000";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางกระจก";
							brazil_ghost = 6;
							changequest 60353,60354;
							close;
						}
						else {
							mes "คุณสะบัดพรมไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					}
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "คุณไม่ได้ทำอะไรเลย";
			close;
		}
	}
	else {
		mes "- พรมที่มีลวดลายประณีต -";
		close;
	}
}

bra_in01,151,180,3	script	Mirror#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- คุณเห็นกระจกที่สะอาดสะอ้านไม่มีรอยขีดข่วนหรือฝุ่นเลย -";
		next;
		switch(select("ตรวจสอบมัน:เพิกเฉย")) {
		case 1:
			mes "- คาถาบทถัดไปคืออะไรแล้วนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Where are you mom?";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าคุณจะพูดอะไรบางอย่างผิดไป";
				close;
			}
			else {
				if (brazil_ghost == 6) {
					mes "[เสียงจากระยะไกล]";
					mes "^FF0000คิเฮ้! ฮิต! ฮิต! ฮิต! ฮิต!^000000";
					next;
					mes "[เสียงจากระยะไกล]";
					mes "^FF0000คิเฮ้! ฮิต! ฮิต! ฮิต! ฮิต!^000000";
					mes "^FF0000คิเฮ้! ฮิต! ฮิต! ฮิต! ฮิต!^000000";
					next;
					mes "[เสียงจากระยะไกล]";
					mes "ข้างหลังเจ้า...";
					enablenpc "Ghost#bra";
					next;
					emotion ET_HUK, playerattached();
					mes "["+strcharinfo(0)+"]";
					mes "เรื่องผีนี่มันเป็นเรื่องจริงเหรอเนี่ย~!";
					next;
					mes "[Ghost]";
					mes "^FF0000ลูกของแม่....^000000";
					next;
					mes "[Ghost]";
					mes "^FF0000แม่มองไม่เห็น.... ตาของแม่....^000000";
					mes "^FF0000มันเกิดอะไรขึ้น....?^000000";
					next;
					mes "- คุณสูดหายใจลึกๆ แล้วมองไปที่ผีตัวนั้น และสังเกตเห็นว่าเขามีผ้าปิดตาอยู่ -";
					next;
					mes "[Ghost]";
					mes "^FF0000ตาของแม่มันรัดแน่นเหลือเกิน... เจ้าช่วยเอามันออกไปได้ไหม?^000000";
					next;
					mes "คุณก้าวเข้าไปหาผีอย่างระมัดระวัง";
					next;
					mes "ใบหน้าของเขาปกคลุมไปด้วยฝุ่นและมีการบิดเบี้ยวของใบหน้าอย่างน่าประหลาด";
					next;
					mes "[Ghost]";
					mes "^FF0000มาสิ ช่วยแม่หน่อย.....^000000";
					next;
					switch(select("แกะผ้าพันแผลที่ตาออก:วิ่งหนี~")) {
					case 1:
						while(1) {
							.@cpudice = rand(1,6);
							.@pcdice = rand(1,6);
							if (.@cpudice != .@pcdice) {
								emotion (ET_OTL+.@cpudice), getnpcid(0, "Ghost#bra");
								emotion (ET_OTL+.@pcdice), playerattached();
								break;
							}
						}
						if (.@cpudice > .@pcdice) {
							specialeffect2 EF_DEVIL;
							mes "[Ghost]";
							mes "^FF0000ออกไปให้พ้น!^000000";
							brazil_ghost = 1;
							changequest 60354,2208;
							percentheal -50,-50;
							close2;
							disablenpc "Ghost#bra";
							warp "bra_in01",12,183;
							end;
						}
						else {
							emotion ET_STARE, playerattached();
							mes "[Ghost]";
							mes "^FF0000อ๊าาา!^000000";
							mes "ผีตัวนั้นหายเข้าไปในชักโครก";
							brazil_ghost = 7;
							changequest 60354,60355;
							close2;
							disablenpc "Ghost#bra";
							warp "bra_in01",206,100;
							end;
						}
					case 2:
						mes "คุณวิ่งหนีออกมาจากผี";
						close2;
						brazil_ghost = 1;
						changequest 60354,2208;
						warp "bra_in01",12,183;
						disablenpc "Ghost#bra";
						end;
					}
					close;
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "คุณไม่ได้ทำอะไรเลย";
			close;
		}
	}
	else {
		mes "- คุณเห็นกระจกที่สะอาดสะอ้านไม่มีรอยขีดข่วนหรือฝุ่นเลย -";
		close;
	}
}

bra_in01,136,180,5	script	Ghost#bra	1867,{
	end;

OnInit:
	disablenpc "Ghost#bra";
	end;
}

bra_in01,7,181,5	script	Curator#bra	4_M_BRZ_MAN2,{
	if (BaseLevel < 40) {
		mes "[Curator]";
		mes "ขออภัยด้วยครับ แต่ตอนนี้พื้นที่ส่วนนี้กำลังอยู่ระหว่างการปรับปรุง";
		close;
	}
	if ((brazil_ghost > 0) && (brazil_ghost < 7)) {
		if (countitem(11515) > 0) {
			mes "[Curator]";
			mes "มีอะไรให้ผมช่วยไหมครับ?";
			next;
			select("ฉันต้องการใช้ห้องน้ำ");
			mes "[Curator]";
			mes "ขออภัยด้วยครับ ตอนนี้ข้างในกำลังปรับปรุงใหม่ ก็เลยปิดให้บริการอยู่";
			mes "กรุณาไปใช้ห้องน้ำห้องอื่นแทนนะครับ";
			next;
			switch(select("แต่ฉันลืมของไว้ข้างในนะ:ตัดใจ")) {
			case 1:
				mes "[Curator]";
				mes "แย่จังเลยนะครับแบบนั้น";
				mes "แต่ผู้จัดการสั่งผมไว้ว่าห้ามใครเข้าไปในห้องน้ำนี้เด็ดขาด ดังนั้นผมต้องทำตามคำสั่งครับ";
				next;
				select("วันนี้อากาศร้อนจังเลยเนอะ!");
				mes "[Curator]";
				mes "ที่ Brasilis น่ะร้อนเป็นปกติอยู่แล้ว แต่วันนี้มันร้อนแบบไม่ไหวจริงๆ";
				mes "บางทีผมอาจจะต้องการน้ำมะพร้าวสักหน่อยเพื่อให้ร่างกายเย็นลง";
				next;
				mes "คุณมอบมะพร้าวให้กับ Curator";
				next;
				mes "[Curator]";
				mes "โอ้ จริงเหรอครับ ให้ผมได้จริงๆ เหรอ?";
				mes "ขอบคุณมากเลยครับ!";
				next;
				mes "[Curator]";
				mes "ทำดีได้ดีใช่ไหมล่ะครับ?";
				mes "ตกลงครับ ผมจะยอมให้คุณเข้าไปแค่ครั้งนี้ครั้งเดียวนะ";
				next;
				mes "Curator มองไปรอบๆ อย่างใจเย็นแล้วจึงเปิดประตู";
				delitem 11515,1; //Coconut
				brazil_ghost = 2;
				close2;
				warp "bra_in01",138,176;
				end;
			case 2:
				mes "คุณล้มเลิกความพยายามที่จะเข้าไปข้างใน";
				close;
			}
		}
		else {
			mes "[Curator]";
			mes "มีอะไรให้ผมช่วยไหมครับ?";
			next;
			select("ฉันต้องการใช้ห้องน้ำ");
			mes "[Curator]";
			mes "ขออภัยด้วยครับ ตอนนี้ข้างในกำลังปรับปรุงใหม่ ก็เลยปิดให้บริการอยู่";
			mes "กรุณาไปใช้ห้องน้ำห้องอื่นแทนนะครับ";
			next;
			switch(select("แต่ฉันลืมของไว้ข้างในนะ:ตัดใจ")) {
			case 1:
				mes "[Curator]";
				mes "แย่จังเลยนะครับแบบนั้น";
				mes "แต่ผู้จัดการสั่งผมไว้ว่าห้ามใครเข้าไปในห้องน้ำนี้เด็ดขาด ดังนั้นผมต้องทำตามคำสั่งครับ";
				next;
				select("วันนี้อากาศร้อนจังเลยเนอะ!");
				mes "[Curator]";
				mes "ที่ Brasilis น่ะร้อนเป็นปกติอยู่แล้ว แต่วันนี้มันร้อนแบบไม่ไหวจริงๆ";
				mes "บางทีผมอาจจะต้องการน้ำมะพร้าวสักหน่อยเพื่อให้ร่างกายเย็นลง";
				close;
			case 2:
				mes "คุณล้มเลิกความพยายามที่จะเข้าไปข้างใน";
				close;
			}
		}
	}
	else if (brazil_ghost > 6) {
		mes "[Curator]";
		mes "เฮ้ ขอบคุณสำหรับมะพร้าวก่อนหน้านี้นะครับ มันช่วยให้ผมเย็นลงได้มากเลยล่ะ";
		close;
	}
	else {
		mes "[Curator]";
		mes "ผมคิดไปเองหรือเปล่านะ? หรือว่าวันนี้มันจะร้อนกว่าที่เคยร้อนมาทั้งหมดเลย!";
		close;
	}
}

bra_in01,12,185,0	script	inbathroom#bra	WARPNPC,1,1,{
OnTouch_:
	if (brazil_ghost > 6)
		warp "bra_in01",138,176;
	else {
		mes "ทางเข้าถูกปิดกั้นไว้";
		close;
	}
	end;
}

bra_in01,138,174,0	warp	outbathroom#bra	1,1,bra_in01,12,183
bra_in01,206,98,0	warp	tobath#bra	1,1,bra_in01,138,176

bra_in01,206,188,1	script	Open Manhole#todunbra	CLEAR_NPC,{
	if (brazil_ghost == 7) {
		enablenpc "Ghost#bra_end";
		mes "[Ghost]";
		mes "ฉันคือวิญญาณที่ตายในระหว่างที่เร่ร่อนอยู่ในป่าเมื่อหลายปีก่อน";
		next;
		mes "[Ghost]";
		mes "ฉันพบท่อส่งน้ำในป่าและเดินตามเสียงของผู้ชายคนหนึ่งมาจนถึงจุดนี้";
		next;
		mes "[Ghost]";
		mes "และนั่นก็เป็นที่ที่ฉันทำดวงตาข้างหนึ่งบาดเจ็บในระหว่างที่เดินไปมาในความมืด";
		next;
		mes "[Ghost]";
		mes "ฉันเร่ร่อนอยู่ในท่อระบายน้ำเหล่านี้อยู่หลายวันจนกระทั่งเจอปลายสายที่เชื่อมต่อกับโถส้วมในพิพิธภัณฑ์ ฉันตะโกนอ้อนวอนขอความช่วยเหลือมาตลอดแต่ไม่มีใครตอบรับเสียงเรียกของฉันเลย";
		next;
		mes "[Ghost]";
		mes "ในที่สุดเธอก็ตอบรับเสียงเรียกของฉัน แต่มันก็สายเกินไปเสียแล้ว ขอบคุณที่อย่างน้อยก็ยังอุตส่าห์มาตรวจสอบดู จนถึงทุกวันนี้ไม่เคยมีใครใส่ใจเลยสักคน";
		next;
		mes "[Ghost]";
		mes "มีสิ่งมีชีวิตที่อันตรายมากมายอยู่ที่ปลายท่อระบายน้ำแห่งนี้";
		mes "แต่ดูเหมือนเธอจะเป็นคนกล้าหาญนะ ฉันพนันได้เลยว่าเธอคงไม่กังวลเรื่องมอนสเตอร์พวกนั้นหรอก";
		next;
		mes "[Ghost]";
		mes "ฉันคิดว่าตอนนี้ในที่สุดฉันก็สามารถไปสู่สุขคติได้เสียที";
		mes "ขอบคุณมากนะเพื่อนยาก";
		brazil_ghost = 8;
		//completequest 2208;
		completequest 60355;
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 135000,0;
		else
			getexp 90000,0;
		disablenpc "Ghost#bra_end";
		close;
	}
	warp "bra_dun01",87,47;
	end;
}


bra_dun01,87,43,1	script	Pipe#bra	CLEAR_NPC,{
	warp "bra_in01",206,185;
	end;
}

bra_fild01,323,136,1	script	Pipe#brafild	CLEAR_NPC,{
	if (brazil_ghost > 6) {
		mes "คุณเห็นท่อที่เป็นสนิม ดูเหมือนมันจะเชื่อมต่อไปยังที่ไหนสักแห่งใต้ป่า";
		next;
		switch(select("สำรวจมัน:เพิกเฉย")) {
		case 1:
			mes "คุณว่ายผ่านช่องว่างในท่อและถูกกระแสน้ำพัดพาไปอย่างรวดเร็ว";
			close2;
			warp "bra_in01",206,182;
			end;
		case 2:
			mes "มันอาจจะอันตราย ฉันไม่ควรทำอะไรวู่วามดีกว่า";
			close;
		}
	}
	else {
		mes "คุณเห็นท่อที่เป็นสนิม ดูเหมือนมันจะเชื่อมต่อไปยังที่ไหนสักแห่งใต้ป่า";
		close;
	}
}

bra_in01,206,190,3	script	Ghost#bra_end	1867,{
	end;

OnInit:
	disablenpc "Ghost#bra_end";
	end;
}

// Iara :: iara.sc
//============================================================
brasilis,315,334,5	script	Shaman#nk	4_F_BRZ_INDOLD,{
	.@iara_re = checkquest(4135,PLAYTIME);
	if ((.@iara_re == 0) || (.@iara_re == 1)) {
		mes "[Anori]";
		mes "ฉันกำลังเตรียมตัวอยู่";
		mes "ตอนนี้ฉันยังไม่ต้องการความช่วยเหลือจากเธอ";
		mes "กรุณากลับมาใหม่ในภายหลังนะ...";
		close;
	}
	else {
		if (checkquest(4135) > 1) erasequest 4135;
		.@iara_q = checkquest(4133);
		if ((.@iara_q == 0) || (.@iara_q == 1)) {
			mes "[Anori]";
			mes "เพื่อที่จะยับยั้ง Iara";
			mes "ที่ล่อลวงคนในเผ่า";
			mes "พวกเราต้องการยาชำระล้าง (Purifying Potion)...";
			next;
			mes "[Anori]";
			mes "เธอได้นำวัสดุสำหรับ";
			mes "สร้างยาชำระล้างมาหรือยัง?";
			next;
			if ((countitem(950) > 19) && (countitem(7172) > 9) && (countitem(1054) > 2)) {
				mes "[Anori]";
				mes "อืม... ดูเหมือนว่าจะใช้ได้นะ";
				mes "ฉันจะปรุงยาที่จะช่วย";
				mes "ลดทอนพลังของ Iara ให้";
				next;
				mes "[Anori]";
				mes "ไหนดูซิ บดเจ้านี่...";
				mes "แล้วก็ผสมเจ้านั่นลงไป...";
				mes "จากนั้นก็ใส่เวทมนตร์ลงไปสักหน่อย...";
				next;
				setquest 4135;
				erasequest 4133;
				setquest 4134;
				completequest 4134;
				delitem 950,20; //Heart_Of_Mermaid
				delitem 7172,10; //Leopard_Talon
				delitem 1054,3; //Lip_Of_Ancient_Fish
				getitem 11517,2; //Puri_Potion
				mes "[Anori]";
				mes "เอ้า เสร็จเรียบร้อยแล้ว";
				mes "รับนี่ไปสิ";
				mes "มันจะทำให้ Iara หยุด";
				mes "ฝึกฝนอยู่ที่ถ้ำนั่น";
				mes "ไปได้สักพักใหญ่เลยล่ะ";
				next;
				mes "[Anori]";
				mes "ได้โปรดยับยั้ง Iara ที่กำลังคุกคามความปลอดภัยของเผ่าด้วยนะ";
				close;
			}
			else {
				mes "[Anori]";
				mes "เธอยังนำวัสดุมาไม่ครบเลย";
				mes "พวกเราไม่สามารถปรุงยาชำระล้างด้วยของเพียงเท่านี้ได้หรอก";
				close;
			}
		}
		else {
			if (BaseLevel < 40) {
				mes "[Anori]";
				mes "อ้า... พวกเราต้องการนักผจญภัยที่แข็งแกร่ง";
				mes "เผ่าของเรากำลังเผชิญหน้ากับภัยคุกคามครั้งใหญ่";
				close;
			}
			.@re_q = checkquest(4134);
			if (.@re_q == 2) {
				mes "[Anori]";
				mes "เธอก็คือ...";
				mes "นักผจญภัยที่มาขอ";
				mes "ยาชำระล้างคนนั้นสินะ...";
				next;
				mes "[Anori]";
				mes "อาจจะเพราะยาชำระล้างนั่น...";
				mes "หลังจากนั้น Iara ก็หยุดล่อลวงคนในเผ่าไปได้พักหนึ่ง แต่ผลของมันอยู่ได้ไม่นานนัก";
				next;
				mes "[Anori]";
				mes "เธอช่วยไปหาวัสดุ";
				mes "แบบเดิมมาอีกครั้งได้ไหม...";
				mes "ฉันต้องการพลังของเธอ";
				next;
				switch(select("ไม่ละ:ตกลง ฉันจะทำ")) {
				case 1:
					mes "[Anori]";
					mes "แบบนี้เองสินะ อา...";
					mes "คงไม่มีทางอื่นแล้วจริงๆ";
					close;
				case 2:
					mes "[Anori]";
					mes "เธอช่างกล้าหาญจริงๆ!";
					mes "ในนามของคนในเผ่า";
					mes "ฉันขอขอบใจเธอมากนะ";
					next;
					mes "[Anori]";
					mes "วัสดุที่ต้องใช้นั้นเหมือนเดิมเลย";
					mes "เพียงแค่เธอไปหา^ff0000 Heart of Mermaid 20 อัน,";
					mes "Leopard Claw 10 อัน และ";
					mes "Ancient Lip 3 อัน^000000";
					mes "ฉันจะปรุงยาที่ช่วยชำระล้างวิญญาณร้ายให้เอง";
					mes "โดยใช้สูตรลับที่สืบทอดกันมาในเผ่า";
					next;
					mes "[Anori]";
					mes "ชะตากรรมของเผ่าขึ้นอยู่กับเธอแล้วนะ";
					mes "ได้โปรดหาวัสดุมาให้เร็วที่สุดด้วย";
					mes "ฉันจะเตรียมตัวปรุงยา";
					mes "ชำระล้างรออยู่ที่ตรงนี้แหละ";
					setquest 4133;
					close;
				}
			}
			else {
				mes "[Anori]";
				mes "ช่วงนี้มีคนแปลกหน้ามาแถวนี้บ่อยขึ้นนะ";
				mes "มันเป็นสัญญาณที่ดี...";
				next;
				mes "[Anori]";
				mes "นี่เธอ...";
				mes "ช่วยฟังเรื่องราวของฉันสักครู่ได้ไหม";
				mes "ตอนนี้มีเหตุฉุกเฉินเกิดขึ้นในเผ่าของเรา";
				next;
				if(select("ไม่ละ:ได้สิ") == 1) {
					mes "[Anori]";
					mes "เธอช่างเป็นคนไร้น้ำใจจริงๆ...";
					mes "เธอดูไม่ใช่พวกที่ชอบช่วยเหลือคนอื่นเลยนะ";
					mes "งั้นก็ไปตามทางของเธอเถอะ";
					close;
				}
				mes "[Anori]";
				mes "ขอบใจนะ ฉันได้พบคนใจดีเข้าแล้วล่ะ";
				mes "นี่เป็นความลับของเผ่าเราที่";
				mes "เธอห้ามนำไปบอกใครเด็ดขาดนะ";
				next;
				mes "[Anori]";
				mes "ช่วงหลังๆ มานี้ ชายหนุ่มใน";
				mes "เผ่าเริ่มหายตัวไป";
				mes "คนทั้งเผ่ากำลังตกอยู่ในอันตราย";
				next;
				mes "[Anori]";
				mes "มันน่าจะเป็นฝีมือของแม่มดที่ชื่อ Iara";
				mes "นางเป็นพรายน้ำที่คอยล่อลวงหัวใจของชายหนุ่มในเผ่าอยู่ที่ถ้ำหลังน้ำตกนั่น";
				next;
				mes "[Anori]";
				mes "ฉันจะหยุดชายหนุ่มพวกนั้นได้ยังไงกัน?";
				mes "แต่แล้วฉันก็ได้รู้วิธีปรุงยาชำระล้างเพื่อลบล้างผลของมนตร์สะกดของ Iara";
				next;
				mes "[Anori]";
				mes "ยาตัวนี้สืบทอดกันมาหลายชั่วอายุคนในเผ่าของเรา";
				mes "ยาชำระล้างนี้มีพลังที่จะช่วยปัดเป่าวิญญาณร้ายได้";
				next;
				mes "[Anori]";
				mes "ถ้าเธอช่วยหาวัสดุมาให้";
				mes "ฉันจะปรุง";
				mes "ยาชำระล้างให้เธอเอง";
				mes "เธอพอจะทำให้ฉันได้ไหม?";
				next;
				switch(select("ไม่ละ:ได้สิ ฉันทำได้")) {
				case 1:
					mes "[Anori]";
					mes "หืม...";
					mes "งั้นก็ตามใจ";
					mes "ถ้าเธอเปลี่ยนใจก็กลับมาหาฉันได้เสมอนะ";
					next;
					mes "[Anori]";
					mes "เธอห้ามเอาเรื่องที่";
					mes "ได้ยินตอนนี้ไปบอกใครนะ";
					mes "มันเป็นเรื่องที่น่าอายพอสมควรเลยล่ะ...";
					close;
				case 2:
					mes "[Anori]";
					mes "โอ้! เธอคือผู้ช่วยชีวิต";
					mes "ของเผ่าเราจริงๆ";
					mes "ในนามของคนในเผ่า ฉันขอขอบใจเธอมากนะ";
					next;
					mes "[Anori]";
					mes "เอาล่ะ สิ่งที่เราต้องการคือพวกนี้";
					mes "ล้วนเป็นของที่หาได้จากแถวๆ นี้แหละ";
					mes "จดไว้ให้ดีล่ะ";
					next;
					mes "[Anori]";
					mes "^ff0000 Heart of Mermaid 20 อัน";
					mes "Leopard Claw 10 อัน";
					mes "Ancient Lip 3 อัน^000000";
					mes "นี่คือวัสดุทั้งหมดที่จำเป็นต้องใช้";
					next;
					mes "[Anori]";
					mes "เมื่อเธอได้ของครบ ฉันจะปรุงยาที่ช่วยชำระล้างวิญญาณร้ายโดยใช้สูตรลับที่สืบทอดกันมาในเผ่าให้";
					setquest 4133;
					next;
					mes "[Anori]";
					mes "ชะตากรรมของเผ่าขึ้นอยู่กับเธอแล้วนะ";
					mes "ฉันหวังว่าเธอจะรีบลงมือล่ะ";
					mes "เพราะในขณะนี้ ชายหนุ่มในหมู่บ้านก็ยังคงถูกล่อลวงและหายตัวไปเรื่อยๆ...";
					close;
				}
			}
		}
	}
}

bra_dun02,157,74,5	script	Iara#nk	4_F_BRZ_WOMAN,2,2,{
	if (countitem(11517) > 0) {
		mes "[" + strcharinfo(0) + "]";
		mes "ฉันควรใช้ยาชำระล้าง (Purification Potion) เลยดีไหมนะ?";
		next;
		switch(select("ใช้:ไม่ใช้")) {
		case 1:
			specialeffect EF_MAPPILLAR;
			mes "[Iara]";
			mes "อา... แสงนี่มัน...";
			mes "มันเหมือนกับว่าความคิดที่ชั่วร้ายกำลังถูกชำระล้าง";
			mes "ออกไปจากส่วนลึกของหัวใจฉันเลย";
			next;
			mes "[Iara]";
			mes "ในที่สุดฉันก็สามารถลืมคำสาปที่ฉันมอบให้ตัวเองตอนที่ฉันจมน้ำได้เสียที";
			next;
			mes "[Iara]";
			mes "คุณคิดว่าฉันจะสามารถเกิดใหม่เป็นพรายน้ำที่แสนใจดีได้ไหมนะ?";
			next;
			specialeffect EF_GHOST;
			mes "[Iara]";
			mes "อา... ขอบคุณที่ช่วยให้ฉันกลับมารู้สึกตัวได้อีกครั้งในช่วงเวลาหนึ่ง";
			mes "แต่ว่า... ฉันคิดว่าคำสาปนี้มันอยู่กับฉันมานานเกินไปแล้ว";
			mes "รีบไปจากฉันเถอะ";
			delitem 11517,1; //Puri_Potion
			percentheal 100,100;
			sc_start SC_INCFLEE,3600000,20;
			sc_start SC_INCCRI,3600000,10;
			consumeitem 12043; //Str_Dish03
			consumeitem 12063; //Dex_Dish03
			consumeitem 12058; //Agi_Dish03
			consumeitem 12053; //Vit_Dish03
			consumeitem 12048; //Int_Dish03
			consumeitem 12068; //Luk_Dish03
			next;
			mes "[Iara]";
			mes "อ๊าาาา~...";
			specialeffect EF_DEVIL;
			next;
			mes "[Iara]";
			mes "คำสาปมันรุนแรงเกินกว่าที่ฉันจะกักเก็บมันเอาไว้ได้";
			mes "รีบไปเดี๋ยวนี้ในขณะที่เธอเธอยังปลอดภัย";
			close;
		case 2:
			mes "[" + strcharinfo(0) + "]";
			mes "(ฉันคิดว่าฉันควรจะเพิกเฉยต่อนางไปดีกว่า)";
			close;
		}
	}
	else {
		mes "[Iara]";
		mes "อาาาาาาาาาาาาาาาาาาาาาาาาาาห์";
		mes "อี๊อีอีอีอีอีอีอีอีอีอีอีอีอีอีอีอีอีห์";
		mes "โออออออออออออออออออออออออออห์";
		if (rand(1,2) == 1)
			sc_start SC_CURSE,60000,0;
		else
			sc_start SC_CONFUSION,60000,0;
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "อึ้ก! เสียงประหลาดนี่มันอะไรกัน?";
		close;
	}
	end;

OnTouch:
	if (countitem(11517) < 1) {
		mes "[Iara]";
		mes "อาาาาาาาาาาาาาาาาาาาาาาาาาาห์";
		mes "อี๊อีอีอีอีอีอีอีอีอีอีอีอีอีอีอีอีอีห์";
		mes "โออออออออออออออออออออออออออห์";
		if (rand(1,2) == 1)
			sc_start SC_CURSE,60000,0;
		else
			sc_start SC_CONFUSION,60000,0;
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "อึ้ก! เสียงประหลาดนี่มันอะไรกัน?";
		close;
	}
	end;
}

bra_fild01,188,301,5	script	Native Warrior#nk	4_M_BRZ_INDIAN,{
	mes "[Native Warrior]";
	mes "อา... ใบหน้าใบนั้นที่ฉันไม่มีวันลืมได้แม้แต่ในความฝัน";
	next;
	mes "[Native Warrior]";
	mes "เมื่อไหร่กันนะที่นางจะออกมาจากน้ำตกนั่นอีกครั้ง...?";
	next;
	if ((checkquest(4133) >= 0) || (checkquest(4134) >= 0)) {
		mes "[" + strcharinfo(0) + "]";
		mes "(อะ... อะไรของหมอนี่กันนะ?)";
	}
	else {
		mes "[" + strcharinfo(0) + "]";
		mes "(หมอนี่คงไม่มีทางที่จะ";
		mes "หลุดพ้นจากคำสาปของ Iara ได้เลยสินะ...)";
	}
	close;
}
