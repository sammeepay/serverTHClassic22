//===== rAthena Script =======================================
//= 2012 Headgear Quests
//===== By: ==================================================
//= Euphy, -SkittleNugget-
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Enhance gears by synthesizing them with Energy Crystals.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//=     Script is a little messy, could use some cleaning.
//============================================================

// Main NPC :: 2012_hat_quest
//============================================================
moc_para01,41,169,3	script	Reno#2012hat	64,{

	// iRO has (very) minor differences in dialogue from the original script.
	// To use iRO's version, uncomment the line below.
	//set .@features_iRO,1;

	// ตรวจสอบน้ำหนักก่อนเริ่มคุย
	if (checkweight(1301,1) == 0 || MaxWeight - Weight < 800) {
		mes "- น้ำหนักตัวหรือไอเทมในตัวมากเกินไป ไม่สามารถดำเนินขั้นตอนต่อไปได้ -";
		close;
	}

	setarray .@type$[0],"ระดับเริ่มต้น","ระดับกลาง","ระดับสูง","ระดับสูงสุด";
	setarray .@crystal$[0],"Rough","Purified","High";

	// ตรวจสอบเวลาคูลดาวน์ของเควส
	setarray .@quests_playtime[0],5161,5169,5174,5225,5226,5227;
	for (set .@i,0; .@i<6; set .@i,.@i+1) {
		if (checkquest(.@quests_playtime[.@i],PLAYTIME) == 2) {
			erasequest .@quests_playtime[.@i];
			mes "[Reno]";
			if (.@i < 3)
				mes "ระยะเวลารอคอยสำหรับเควสสะสมผลึก "+.@type$[.@i]+" สิ้นสุดลงแล้ว คุณสามารถรับเควสใหม่ได้";
			else
				mes "ระยะเวลารอคอยสำหรับการสกัดผลึก "+.@crystal$[.@i-3]+" Energy Crystal สิ้นสุดลงแล้ว";
			close;
		}
	}

	// ตรวจสอบการส่งเควสล่ามอนสเตอร์
	callsub L_CheckHunting,5161,5162,7,.@type$[0],6623,10; // Rough
	callsub L_CheckHunting,5169,5170,4,.@type$[1],6624,10; // Purified
	callsub L_CheckHunting,5174,5175,4,.@type$[2],6625,5;  // High (Advanced)
	callsub L_CheckHunting,5174,5179,4,.@type$[3],6625,10; // High (Highest)

	// ตรวจสอบการรับผลึกจาก Buff (หลังจากรอครบ 3 ชม.)
	set .@energy_buf00, getstatus(SC_QUEST_BUFF1) + getstatus(SC_QUEST_BUFF2) + getstatus(SC_QUEST_BUFF3);
	if (!.@energy_buf00) {
		for (set .@i,0; .@i<3; set .@i,.@i+1) {
			if (isbegin_quest(.@i + 5222)) {
				set .@item, 6623 + .@i;
				set .@amount, 2 + rand(3);
				mes "[Reno]";
				mes "การสะสมพลังงาน "+getitemname(.@item)+" ในร่างกายเสร็จสมบูรณ์แล้ว";
				next;
				setquest 5225 + .@i;
				erasequest 5222 + .@i;
				getitem .@item,.@amount;
				mes "[Reno]";
				mes "สกัดออกมาได้ ^0000FF"+.@amount+" "+getitemname(.@item)+"^000000";
				close;
			}
		}
	}

	mes "[Reno]";
	mes "สวัสดี? ฉันคือ Reno จากสมาคมวิจัยผลึกพลังงาน หรือ ECRA";
	next;
	switch(select("ECRA คืออะไร?:รับเควสล่า Energy Crystal:รับ Buff สะสมพลังงาน:บีบอัดผลึกพลังงาน:แลกเปลี่ยนอุปกรณ์:สุ่มรับอุปกรณ์ด้วย Crystal" + ((.@features_iRO)?":ดูรายการอุปกรณ์ทั้งหมด":""))) {
	case 1:
		mes "[Reno]";
		mes "ECRA คือสถาบันที่ทำการวิจัย '^0000FFEnergy Crystal^000000' เพื่อนำมาใช้เป็นแหล่งพลังงานทางเลือกสำหรับอาณาจักร Rune-Midgarts ที่กำลังเผชิญกับสภาวะขาดแคลนพลังงานในขณะนี้";
		next;
		select("เหตุผลที่ต้องทำเรื่องนี้คืออะไร?");
		mes "[Reno]";
		mes "ก่อนที่จะอธิบายเหตุผล ผมขอยืนยันว่าพวกเราไม่ได้พยายามหาข้ออ้างเพื่อหลีกเลี่ยงปัญหาที่กำลังเผชิญอยู่เลยนะ! ไม่ใช่อย่างนั้นแน่นอน เพราะฉะนั้นได้โปรดฟังผมให้จบก่อนนะครับ";
		next;
		mes "[Reno]";
		mes "ทางทีมวิจัยได้ส่งผลการทดสอบที่พวกเขาค้นพบมาให้ผมแล้ว";
		next;
		mes "[Reno]";
		mes "ปรากฏว่า Energy Crystal นั้น ^0000FFถูกสร้างขึ้นภายในตัวของมอนสเตอร์บางชนิด^000000 และจากผลการวิจัยพบว่า ยิ่งมอนสเตอร์แข็งแกร่งมากเท่าไหร่ พลังงานที่ถูกสร้างออกมาก็จะมีระดับสูงขึ้นตามไปด้วย";
		next;
		mes "[Reno]";
		mes "ทีมวิจัยจึงตัดสินใจที่จะรวบรวม Energy Crystal เหล่านี้ผ่านการจ้างวานเหล่านักผจญภัย โดยมีรางวัลตอบแทนที่เหมาะสม ซึ่งวิธีนี้จะช่วยให้เราเก็บรวบรวมผลึกได้ดีและมี ^FF0000ประสิทธิภาพ^000000 มากกว่าครับ";
		next;
		mes "[Reno]";
		mes "ผลึกพลังงานที่สะสมได้จากการออกล่าตามคำขอของผม จะถูกนำไปใช้ในงานวิจัย และ ^0000FFส่วนหนึ่งของผลึกเหล่านั้นจะถูกส่งคืนกลับไปยังเหล่านักผจญภัยด้วยครับ^000000";
		next;
		mes "[Reno]";
		mes "นักผจญภัยสามารถนำ Energy Crystal ที่รวบรวมมาได้ มาใช้เพื่อ ^FF0000อัปเกรดอุปกรณ์สวมใส่^000000 ผ่านทางผมได้เลยครับ";
		next;
		mes "[Reno]";
		mes "ถ้าหากทุกอย่างเป็นไปได้ด้วยดี ผมคิดว่ามันจะเป็นผลดีต่อทั้งทาง ECRA และเหล่านักผจญภัยทุกท่านครับ";
		close;
	case 2:
		mes "[Reno]";
		mes "เควสล่ามอนสเตอร์จะแบ่งตามระดับเลเวลและความยาก คุณสามารถรับได้เพียงวันละครั้งเท่านั้น";
		next;

		setarray .@quest_status[0],
			isbegin_quest(5162) + isbegin_quest(5163) + isbegin_quest(5164) + isbegin_quest(5165) + isbegin_quest(5166) + isbegin_quest(5167) + isbegin_quest(5168),
			isbegin_quest(5170) + isbegin_quest(5171) + isbegin_quest(5172) + isbegin_quest(5173),
			isbegin_quest(5175) + isbegin_quest(5176) + isbegin_quest(5177) + isbegin_quest(5178) + isbegin_quest(5179) + isbegin_quest(5180) + isbegin_quest(5181) + isbegin_quest(5182);
		setarray .@quest_index[0],5161,5169,5174;

		set .@select, select("ระดับต่ำ [Lv. 61-80]:ระดับกลาง [Lv. 80-99]:ระดับสูง [Lv. 90 ขึ้นไป]:ยกเลิกเควสปัจจุบัน") - 1;
		if (.@select < 3) {
			setarray .@min_level[0],60,80,90,100;
			if (BaseLevel < .@min_level[.@select]) {
				mes "[Reno]";
				mes "ขออภัยด้วยครับ แต่คำขอรวบรวมประเภท "+.@type$[.@select]+" นี้ รับได้เฉพาะผู้ที่มีเลเวลตั้งแต่ "+.@min_level[.@select]+" ขึ้นไปเท่านั้น";
				mes "ดูเหมือนว่าคุณจะยังไม่พร้อมสำหรับการทำภารกิจนี้นะครับ";
				close;
			}
			if (.@quest_status[.@select]) {
				mes "[Reno]";
				mes "ดูเหมือนว่าคุณกำลังทำภารกิจรวบรวมประเภท "+.@type$[.@select]+" ค้างไว้อยู่นะครับ";
				mes "ถ้าหากเควสต์มันยากเกินไป... สนใจจะ ^FF0000ยกเลิกภารกิจ^000000 ก่อนไหมครับ?";
				close;
			}
			set .@playtime, checkquest(.@quest_index[.@select],PLAYTIME);
			if (.@playtime == 0 || .@playtime == 1) {
				mes "[Reno]";
				mes "ขณะนี้กำลังอยู่ในช่วงพักภารกิจรวบรวมประเภท "+.@type$[.@select]+" ครับ";
				mes "โดยคุณจะสามารถทำภารกิจแต่ละประเภทได้เพียงวันละครั้งเท่านั้นครับ";
				close;
			}
			mes "[Reno]";
			mes "คุณได้เลือกที่จะล่ามอนสเตอร์จากกลุ่ม "+.@type$[.@select]+" นะครับ";
			mes "หลังจากกำจัดมอนสเตอร์เหล่านี้แล้ว คุณจะได้รับ "+((.@features_iRO)?"10 ":"")+"^0000FF"+.@crystal$[.@select]+" Energy Crystal^000000 เมื่อกลับมาหาผมครับ";
			next;
			mes "[Reno]";
			mes "โปรดเลือกมอนสเตอร์ที่คุณคิดว่าสามารถล่าได้สะดวกจากรายการต่อไปนี้ครับ";
			next;
			switch (.@select) {
			case 0:
				set .@quest, select(
					"[ระดับต่ำ] Requiem",
					"[ระดับต่ำ] Bathory",
					"[ระดับต่ำ] Spring Rabbit",
					"[ระดับต่ำ] Sleeper",
					"[ระดับต่ำ] Evil Druid",
					"[ระดับต่ำ] Ground Petite",
					"[ระดับต่ำ] Clock"
				);
				break;
			case 1:
				set .@quest, select(
					"[ระดับกลาง] Siroma",
					"[ระดับกลาง] Dark Priest",
					"[ระดับกลาง] Stapo",
					"[ระดับกลาง] Solider"
				);
				break;
			case 2:
				set .@quest, select(
					"[ระดับสูง] Desert Wolf",
					"[ระดับสูง] Medusa",
					"[ระดับสูง] Pinguicula",
					"[ระดับสูง] Majoruros",
					"[ระดับสูงสุด] Raydric",
					"[ระดับสูงสุด] Naga",
					"[ระดับสูงสุด] Ancient Mummy",
					"[ระดับสูงสุด] Ancient Mimic"
				);
				if (.@quest > 4 && BaseLevel < .@min_level[3]) {
					mes "[Reno]";
					mes "ขออภัยครับ แต่คำขอสำหรับระดับสูงสุดนี้ รับได้เฉพาะผู้ที่มีเลเวลตั้งแต่ "+.@min_level[3]+" ขึ้นไปเท่านั้น";
					mes "ดูเหมือนว่าคุณจะยังไม่พร้อมสำหรับภารกิจระดับนี้ครับ";
					close;
				}
				break;
			}
			setquest .@quest_index[.@select] + .@quest;
			mes "[Reno]";
			mes "บันทึกคำขอสำหรับล่ามอนสเตอร์กลุ่ม "+.@type$[.@select]+" เรียบร้อยแล้ว";
			mes "คุณสามารถตรวจสอบรายละเอียดเพิ่มเติมได้ที่หน้าต่างเควสต์ครับ";
			close;
		} else {
			setarray .@quest_count[0],7,4,8;
			for (set .@i,0; .@i<3; set .@i,.@i+1) {
				if (.@quest_status[.@i]) {
					mes "[Reno]";
					mes "ตอนนี้คุณกำลังทำภารกิจรวบรวมประเภท "+.@type$[.@i]+" อยู่นะครับ";
					mes "ถ้าคุณยกเลิกตอนนี้ ข้อมูลความคืบหน้าทั้งหมดจะหายไปทันที คุณยืนยันที่จะยกเลิกจริงๆ หรือครับ?";
					next;
					set .@erase_quest, select("ใช่, ยกเลิกเลย:ไม่, ฉันจะทำต่อ");
					switch (.@erase_quest) {
					case 1:
						for (set .@quest,.@quest_index[.@i]+1; .@quest<=.@quest_index[.@i]+.@quest_count[.@i]; set.@quest,.@quest+1) {
							if (isbegin_quest(.@quest))
								erasequest .@quest;
						}
						break;
					case 2:
						break;
					}
				}
			}
			switch (.@erase_quest) {
			case 0:
				mes "[Reno]";
				mes "คุณยังไม่ได้รับภารกิจใดๆ ไว้เลยนะครับ";
				close;
			case 1:
				mes "[Reno]";
				mes "เอกสารคำขอของคุณถูกถอนออกเรียบร้อยแล้วครับ";
				close;
			case 2:
				mes "[Reno]";
				mes "พยายามเข้านะครับ ผมยังรอความช่วยเหลือจากคุณอยู่";
				close;
			}
		}
	case 3:
		mes "[Reno]";
		mes "คุณต้องการสะสมพลังงานจาก Energy Crystal ไว้ในร่างกายเพื่อรับบัฟพิเศษไหมครับ?";
		next;
		set .@select, select("การสะสม Energy Crystal คืออะไร?:สะสม Rough Energy Crystal:สะสม Purified Energy Crystal:สะสม High Energy Crystal");
		switch (.@select) {
		case 1:
			mes "[Reno]";
			mes "โดยปกติแล้ว Energy Crystal จะถูกสะสมในร่างกายของคุณเมื่อคุณออกล่ามอนสเตอร์ตามที่เราขอครับ";
			next;
			mes "[Reno]";
			mes "แต่งานวิจัยของเราพบว่า มีบางวิธีการที่ทำให้มนุษย์สามารถสะสมพลังงานจากผลึกเหล่านี้ไว้ในร่างกายได้โดยตรงในรูปแบบของบัฟเพิ่มพลังครับ";
			next;
			mes "[Reno]";
			mes "หากคุณสละเวลาประมาณ 3 ชั่วโมง คุณจะได้รับ Energy Crystal จำนวนหนึ่งด้วย สนใจจะรับบัฟนี้ไหมครับ?";
			close;
		case 2:
			set .@playtime, checkquest(5225,PLAYTIME);
			set .@quest,5222;
			set .@min_level,60;
			set .@rate,1;
			set .@buff, SC_QUEST_BUFF1;
			break;
		case 3:
			set .@playtime, checkquest(5226,PLAYTIME);
			set .@quest,5223;
			set .@min_level,80;
			set .@rate,2;
			set .@buff, SC_QUEST_BUFF2;
			break;
		case 4:
			set .@playtime, checkquest(5227,PLAYTIME);
			set .@quest,5224;
			set .@min_level,90;
			set .@rate,3;
			set .@buff, SC_QUEST_BUFF3;
			break;
		}
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[Reno]";
			mes "ภารกิจราบรื่นดีไหมครับ? การสะสมพลังงานชนิดอื่นเพิ่มอาจจะไม่ส่งผลอะไรมากนัก แต่การสะสมพลังงานชนิดเดิมซ้ำซ้อนกันอาจส่งผลเสียต่อร่างกายของคุณได้นะครับ";
			close;
		}
		if (BaseLevel < .@min_level) {
			mes "[Reno]";
			mes "ด้วยสภาพร่างกายในตอนนี้ คุณยังไม่สามารถสะสม "+.@crystal$[.@select-2]+" ได้ครับ คุณจำเป็นต้องมีเลเวลอย่างน้อย "+.@min_level+" ไม่เช่นนั้นร่างกายของคุณจะรับภาระหนักเกินไป";
			close;
		}
		if (.@energy_buf00) {
			mes "[Reno]";
			mes "กระบวนการสะสมพลังงานได้เริ่มขึ้นแล้วครับ ขออภัยด้วยแต่คุณไม่สามารถสะสมพลังงานสองชนิดพร้อมกันในเวลาเดียวกันได้";
			close;
		}
		if (isbegin_quest(.@quest) > 0) {
			erasequest .@quest;
			mes "[Reno]";
			mes "เกิดข้อผิดพลาดบางอย่างขึ้น ต้องขออภัยในความไม่สะดวกด้วยครับ";
			close;
		}
		mes "[Reno]";
		mes "ร่างกายของคุณต้องใช้เวลา 3 ชั่วโมงในการกลั่นกรอง "+.@crystal$[.@select-2]+" จำนวน 2 ถึง 4 ก้อน คุณต้องการเริ่มเลยไหมครับ?";
		next;
		if(select("ยกเลิก:ตกลง เริ่มเลย") == 1) {
			mes "[Reno]";
			mes "ผมแนะนำให้คุณลองกลับมาสะสมพลังงานอีกครั้งเมื่อคุณมีเวลาว่างพอนะครับ";
			close;
		}
		mes "[Reno]";
		mes "คุณอาจจะรู้สึกจี๊ดๆ ตามตัวนิดหน่อยนะครับ...";
		next;
		specialeffect2 EF_BASH3D;
		percentheal .@rate * -5,0;
		//consumeitem ??; //Keep_Connection_[.@rate]
		sc_start .@buff,10800000,.@rate;	// Atk/Matk + 5*rate (+5, +10, +15)
		setquest .@quest;
		mes "[Reno]";
		mes "- ปึ้ก! -";
		next;
		mes "[Reno]";
		mes "ทุกอย่างเรียบร้อยดีครับ เริ่มกระบวนการสะสม "+.@crystal$[.@select-2]+" ได้! อย่าลืมกลับมาหาผมหลังจากผ่านไป 3 ชั่วโมงเพื่อรับผลึกพลังงานนะครับ";
		close;
	case 4:
		mes "[Reno]";
		mes "คุณต้องการควบแน่น Energy Crystal ไหมครับ?";
		next;
		switch(select("การควบแน่นคืออะไร?:ควบแน่น Rough เป็น Purified Crystal:ควบแน่น Purified เป็น High Crystal")) {
		case 1:
			mes "[Reno]";
			mes "สงสัยเรื่องการควบแน่น Energy Crystal หรือครับ? ผมสามารถนำผลึกระดับต่ำมาควบแน่นรวมกันเป็นผลึกในระดับที่สูงขึ้นได้ครับ";
			next;
			mes "[Reno]";
			mes "และนี่คืออัตราส่วนที่ผมสามารถทำได้ครับ";
			mes "^FF0000Rough Energy Crystal 10 ก้อน^000000 = ^FF0000Purified Energy Crystal 1 ก้อน^000000";
			mes "^FF0000Purified Energy Crystal 5 ก้อน^000000 = ^FF0000High Energy Crystal 1 ก้อน^000000";
			next;
			mes "[Reno]";
			mes "หากคุณต้องการผลึกระดับสูงขึ้น วิธีนี้ถือเป็นทางเลือกที่ดีมากครับ เพราะผลึกระดับล่างนั้นหาได้ง่ายกว่า";
			close;
		case 2:
			set .@crystal,6623;  //Rough_Energy_Crystal
			set .@crystal_,6624; //Purified_Energy_Crystal
			set .@rate,10;
			break;
		case 3:
			set .@crystal,6624;  //Purified_Energy_Crystal
			set .@crystal_,6625; //High_Purity_Energy_Xtal
			set .@rate,5;
			break;
		}
		set .@crystal_count, countitem(.@crystal);
		set .@crystal_get, .@crystal_count / .@rate;
		mes "[Reno]";
		mes sprintf("^FF0000%d %s^000000 = ^FF00001 %s^000000",.@rate,getitemname(.@crystal),getitemname(.@crystal_));
		next;
		mes "[Reno]";
		mes sprintf("คุณมี %s อยู่ %d ก้อน ซึ่งจะควบแน่นได้ %d %s ครับ",getitemname(.@crystal),.@crystal_count,.@crystal_get,getitemname(.@crystal_));
		mes "ต้องการเริ่มการควบแน่นเลยไหมครับ?";
		next;
		if(select("เริ่มการควบแน่น:หยุดก่อน") == 2) {
			mes "[Reno]";
			mes "เข้าใจแล้วครับ ไว้มาติดต่อใหม่ได้ทุกเมื่อถ้าคุณต้องการนะครับ";
			close;
		}
		while(1) {
			if (.@crystal_count >= .@rate) {  //custom translation
				delitem .@crystal,.@rate;
				getitem .@crystal_,1;
				set .@crystal_count, countitem(.@crystal);
				set .@crystal_get, .@crystal_count / .@rate;
				mes "[Reno]";
				mes "การควบแน่นเสร็จสิ้นและประสบความสำเร็จอย่างมากครับ!";
				mes "คุณยังเหลือ "+getitemname(.@crystal)+" อีก "+.@crystal_count+" ก้อน ซึ่งสามารถควบแน่นได้อีก "+.@crystal_get+" ก้อน ต้องการทำต่อไหมครับ?";
				next;
				if(select("ควบแน่นต่อ:หยุดแค่นี้") == 2)
					break;
			} else {
				mes "[Reno]";
				mes "คุณมีจำนวน "+getitemname(.@crystal)+" ไม่เพียงพอครับ";
				next;
				break;
			}
		}
		mes "[Reno]";
		mes "กระบวนการควบแน่นเสร็จสมบูรณ์แล้วครับ";
		close;
	case 5:
		disable_items;
		mes "[Reno]";
		mes "คุณต้องการนำ Energy Crystal มาแลกเปลี่ยนเป็นอุปกรณ์สวมใส่ไหมครับ? โปรดเลือกประเภทของผลึกที่คุณต้องการจะใช้แลกเปลี่ยนครับ";
		next;
		setarray .@crystals[0], countitem(6623), countitem(6624), countitem(6625);
		switch(select("ดูรายการอุปกรณ์ทั้งหมด:[ " + .@crystals[0] + " ] Rough Energy Crystal:[ " + .@crystals[1] + " ] Purified Energy Crystal:[ " + .@crystals[2] + " ] High Energy Crystal")) {
		case 1:
			mes "[Reno]";
			mes "นี่คือโบรชัวร์รายการอัปเกรดอุปกรณ์ที่สมาคมวิจัย Energy Crystal ของเราจัดทำขึ้นครับ เชิญเลือกดูได้ตามสบายเลย";
			close2;
			readbook 11060,1; //Energy_Xtal_Combi_Book
			end;

		// Variable descriptions
		// .@crystal     : Energy Crystal used
		// .@materials[] : equipment to upgrade
		// .@showslot[]  : display slots with equipment name (-1 to disable)
		// .@costs[]     : amount of Energy Crystals needed
		// .@rewards[]   : upgraded equipment
		case 2:
			set .@crystal,6623; //Rough_Energy_Crystal
			setarray .@materials[0], 5027, 5045, 5069, 5003, 2214, 5167, 5168, 5043;
			setarray .@showslot[0],    -1,   -1,   -1,   -1,   -1,    1,    1,   -1;
			setarray .@costs[0],      100,  100,  100,  100,  100,  100,  100,  100;
			setarray .@rewards[0],  18760,18761,18762,18763,18764,18769,18770,18771;
			break;
		case 3:
			set .@crystal,6624; //Purified_Energy_Crystal
			setarray .@materials[0], 2296, 5014, 5096, 2292,2615, 2355,2116, 2420, 2521, 5125;
			setarray .@showslot[0],    -1,   -1,   -1,   -1,  -1,    1,   1,    1,    1,    1;
			setarray .@costs[0],      200,  200,  200,  200, 200,  100, 100,  100,  100,  100;
			setarray .@rewards[0],  18772,18773,18774,18775,2956,15068,2183,22015,20710,18776;
			break;
		case 4:
			set .@crystal,6625; //High_Purity_Energy_Xtal
			setarray .@materials[0], 5019, 5162, 5025, 5022, 5353, 2423,2678,2679, 2355,2116, 2420, 2521, 5125;
			setarray .@showslot[0],    -1,    1,   -1,    0,    1,   -1,  -1,  -1,    1,   1,    1,    1,    1;
			setarray .@costs[0],      300,  300,  300, 1000,  100,  300, 300, 300,   20,  20,   20,   20,   20;
			setarray .@rewards[0],  18765,18768,18766,18767,18767,22014,2957,2958,15068,2183,22015,20710,18776;
			break;
		}
		mes "[Reno]";
		mes "โปรดเลือกอุปกรณ์ที่ต้องการแลกเปลี่ยนโดยใช้ "+getitemname(.@crystal)+" ครับ";
		next;
		set .@crystal_count, countitem(.@crystal);
		set .@menu$,"";
		for (set .@i,0; .@i<getarraysize(.@materials); set .@i,.@i+1) {
			set .@item_name$, getitemname(.@materials[.@i]) + ((.@showslot[.@i] > -1)?"["+.@showslot[.@i]+"]":"");
			set .@menu$, .@menu$ + sprintf("%s (%d/%d):",.@item_name$,.@crystal_count,.@costs[.@i]);
		}
		set .@index, select(.@menu$) - 1;
		set .@material, .@materials[.@index];
		set .@item_name$, getitemname(.@material) + ((.@showslot[.@index] > -1)?"["+.@showslot[.@index]+"]":"");
		set .@cost, .@costs[.@index];
		set .@reward, .@rewards[.@index];

		if (.@material == 2615) {  // กรณีพิเศษ: Safety Ring
			if (countitem(2615) < 1 || countitem(2621) < 1 || countitem(2622) < 1 || countitem(2624) < 1 ||
			    countitem(2625) < 1 || countitem(2623) < 1 || countitem(2626) < 1 || countitem(.@crystal) < .@cost) {
				mes "[Reno]";
				mes "วัตถุดิบในการสร้าง Safety Ring[1] ไม่เพียงพอครับ กรุณาตรวจสอบไอเทมในตัวของคุณอีกครั้ง";
				close;
			}
			mes "[Reno]";
			mes "ก่อนที่จะทำการอัปเกรด Safety Ring ของคุณ โปรดฟัง ^FF0000ข้อควรระวัง^000000 เหล่านี้ก่อนครับ";
			next;
			mes "[Reno]";
			mes "^FF0000นอกจาก Safety Ring แล้ว วัตถุดิบอื่นๆ ที่ใช้ รวมถึงระดับการตีบวกและการ์ดที่อยู่ในไอเทมเหล่านั้นจะหายไปทั้งหมดครับ^000000";
			next;
		} else {
			if (countitem(.@material) < 1 || countitem(.@crystal) < .@cost) {
				mes "[Reno]";
				mes "วัตถุดิบไม่เพียงพอครับ กรุณาตรวจสอบไอเทมของคุณอีกครั้ง";
				close;
			}
			mes "[Reno]";
			mes "ก่อนที่จะเริ่มการอัปเกรดอุปกรณ์ โปรดฟัง ^FF0000ข้อควรระวัง^000000 เหล่านี้ก่อนนะครับ";
			next;
			mes "[Reno]";
			mes "^FF0000เมื่อใช้ Energy Crystal ในการอัปเกรด ระดับการตีบวก, การออฟชั่น (Enchant) และการ์ดทั้งหมดที่มีอยู่เดิมจะถูกลบหายไปครับ^000000";
			next;
		}
		mes "[Reno]";
		mes "คุณรับทราบและยอมรับ ^FF0000ข้อควรระวัง^000000 ดังกล่าวใช่ไหมครับ?";
		next;
		if(select("ฉันรับทราบแล้ว:ไม่ตกลง") == 2) {
			mes "[Reno]";
			mes "เข้าใจแล้วครับ ไว้โอกาสหน้าค่อยกลับมาใหม่นะครับ";
			close;
		}
		mes "[Reno]";
		mes "ถ้าอย่างนั้น เรามาเริ่มการอัปเกรด "+.@item_name$+" กันเลย!";
		next;
		specialeffect2 EF_REPAIRWEAPON;
		progressbar "ffff00",2;
		delitem .@material,1;
		if (.@material == 2615) {
			delitem 2621,1; //Ring_
			delitem 2622,1; //Earring_
			delitem 2624,1; //Glove_
			delitem 2625,1; //Brooch_
			delitem 2623,1; //Necklace_
			delitem 2626,1; //Rosary_
		}
		delitem .@crystal,.@cost;
		getitem .@reward,1;
		mes "[Reno]";
		mes "การอัปเกรดเสร็จสิ้นและประสบความสำเร็จอย่างมากครับ Energy Crystal เหล่านี้จะช่วยเหลืองานวิจัยของเราได้มหาศาลเลยทีเดียว!";
		close;
	case 6:
		mes "[Reno]";
		mes "สนใจบริจาค Energy Crystal เพื่อสุ่มรับคอสตูมหรืออุปกรณ์สวมใส่ชิ้นใหม่ไหมครับ?";
		next;
		switch(select("การบริจาคคืออะไร?:บริจาค 300 Rough Energy Crystal:บริจาค 600 Purified Energy Crystal:บริจาค 900 High Energy Crystal")) {
		case 1:
			mes "[Reno]";
			mes "พวกเรากำลังรวบรวม Energy Crystal เพื่อนำไปวิจัยชุดปรับปรุงอุปกรณ์ใหม่ๆ ให้กับเหล่านักผจญภัยครับ";
			next;
			mes "[Reno]";
			mes "ต้องขอบคุณการสนับสนุนจากทุกท่าน ที่ทำให้เราสามารถนำผลึกเหล่านี้ไปต่อยอดงานวิจัยให้ก้าวหน้ายิ่งขึ้น";
			next;
			mes "[Reno]";
			mes "โดยเราจะเปิดรับบริจาคขั้นต่ำที่ 300 Rough Energy Crystal, 600 Purified Energy Crystal หรือ 900 High Energy Crystal ครับ";
			if (!.@features_iRO) {
				next;
				mes "[Reno]";
				mes "คุณอาจจะรู้สึกว่ามันเป็นภาระไปสักนิด แต่เพื่อความก้าวหน้าของงานวิจัยของเรา ได้โปรดช่วยสนับสนุนต่อไปด้วยนะครับ";
				close;
			}
			mes "สำหรับการบริจาค ผมจะมอบไอเทมสวมใส่แบบสุ่มให้เป็นรางวัลตอบแทนครับ";
			next;
			mes "[Reno]";
			mes "300 Rough Energy Crystal (รายการสุ่ม):";
			mes "- New Mage Hat";
			mes "- New Magician Hat";
			mes "- New Kitsune Mask";
			mes "- New Joker Jester";
			mes "- New Bunny Band";
			mes "- New Munak Hat";
			mes "- New Bongun Hat";
			mes "- New Phantom Opera Mask";
			next;
			mes "[Reno]";
			mes "600 Purified Energy Crystal (รายการสุ่ม):";
			mes "- Good Binoculars";
			mes "- Good Fin Helm";
			mes "- Good Assassin Mask";
			mes "- Good Welding Mask";
			mes "- Good Safety Ring";
			mes "- Good Angelic Protection";
			mes "- Good Angelic Guard";
			mes "- Good Angelic Cardigan";
			mes "- Good Angel's Reincarnation";
			next;
			mes "[Reno]";
			mes "900 High Energy Crystal (รายการสุ่ม):";
			mes "- Enhanced Corsair";
			mes "- Enhanced Bone Helm";
			mes "- Enhanced Helm of Angel";
			mes "- Enhanced Variant Shoes";
			mes "- Enhanced Ring of Flame Lord";
			mes "- Enhanced Ring of Resonance";
			close;
		case 2:
			if (countitem(6623) < 300) {
				mes "[Reno]";
				mes "ขอบคุณในความประสงค์ดีครับ แต่เราจำเป็นต้องใช้ 300 Rough Energy Crystal สำหรับการวิจัยครั้งนี้ครับ";
				close;
			}
			delitem 6623,300; //Rough_Energy_Crystal
			set .@r, rand(1,17);
			     if (.@r <= 2)  getitem 18760,1; //Remodel_Wizardry_Hat
			else if (.@r <= 4)  getitem 18761,1; //Remodel_Magician_Hat
			else if (.@r <= 6)  getitem 18762,1; //Remodel_Mask_Of_Fox
			else if (.@r <= 8)  getitem 18763,1; //Remodel_Joker_Jester
			else if (.@r <= 10) getitem 18764,1; //Remodel_Bunny_Band
			else if (.@r <= 13) getitem 18769,1; //Remodel_Munak_Turban
			else if (.@r <= 16) getitem 18770,1; //Remodel_Bongun_Hat
			else                getitem 18771,1; //Remodel_Opera_Mask
			break;
		case 3:
			if (countitem(6624) < 600) {
				mes "[Reno]";
				mes "ขอบคุณในความประสงค์ดีครับ แต่เราจำเป็นต้องใช้ 600 Purified Energy Crystal สำหรับการวิจัยครั้งนี้ครับ";
				close;
			}
			delitem 6624,600; //Purified_Energy_Crystal
			set .@r, rand(1,18);
			     if (.@r <= 3)  getitem 18772,1; //Improved_Binoculars
			else if (.@r <= 6)  getitem 18773,1; //Improved_Fin_Helm
			else if (.@r <= 9)  getitem 18774,1; //Improved_Assassin_Mask
			else if (.@r <= 12) getitem 18775,1; //Improved_Welding_Mask
			else if (.@r <= 13) getitem 2956,1;  //Safety_Ring_
			else if (.@r <= 14) getitem 15068,1; //Im_Angel's_Protection
			else if (.@r <= 15) getitem 2183,1;  //Impr_Angel's_Safeguard
			else if (.@r <= 16) getitem 22015,1; //Impr_Angel's_Arrival
			else if (.@r <= 17) getitem 20710,1; //Impr_Angel's_Warmth
			else                getitem 18776,1; //Improved_Kiss_Of_Angel
			break;
		case 4:
			if (countitem(6625) < 900) {
				mes "[Reno]";
				mes "ขอบคุณในความประสงค์ดีครับ แต่เราจำเป็นต้องใช้ 900 High Energy Crystal สำหรับการวิจัยครั้งนี้ครับ";
				close;
			}
			delitem 6625,900; //High_Purity_Energy_Xtal
			set .@r, rand(1,33);
			     if (.@r <= 10) getitem 18765,1; //Enhanced_Corsair
			else if (.@r <= 20) getitem 18768,1; //Enhanced_Bone_Helm
			else if (.@r <= 25) getitem 18766,1; //Enhanced_Helm_Of_Angel
			else if (.@r <= 30) getitem 22014,1; //Enhanced_Variant_Shoes
			else if (.@r <= 31) getitem 2957,1;  //Good_Ring_Of_Flame_Lord
			else                getitem 2958,1;  //Good_Ring_Of_Resonance
			break;
		}
		mes "[Reno]";
		mes "ขอบคุณมากครับ! Energy Crystal ที่คุณบริจาคมาจะมีประโยชน์ต่องานวิจัยของเราอย่างมหาศาล และนี่คือของรางวัลตอบแทนน้ำใจของคุณครับ";
		close;
	case 7:  // iRO only
		mes "[Reno]";
		mes "นี่คือโบรชัวร์รายการอัปเกรดอุปกรณ์ที่สมาคมวิจัย Energy Crystal ของเราจัดทำขึ้นครับ เชิญเลือกดูรายละเอียดได้ตามสบายเลยครับ";
		close2;
		readbook 11060,1; //Energy_Xtal_Combi_Book
		end;
	}

//callsub L_CheckHunting,<quest index>,<start quest>,<number of quests>,"<type>",<reward id>,<reward amount>;
L_CheckHunting:
	for (set .@quest,getarg(0)+1; .@quest<getarg(1)+getarg(2); set .@quest,.@quest+1) {
		if (checkquest(.@quest,HUNTING) == 2) {
			mes "[Reno]";
			mes "คุณทำภารกิจล่ามอนสเตอร์ในกลุ่ม "+getarg(3)+" สำเร็จเรียบร้อยแล้วครับ";
			next;
			setquest getarg(0);
			erasequest .@quest;
			getitem getarg(4),getarg(5);
			mes "คุณได้รับ "+getitemname(getarg(4))+" จำนวน "+getarg(5)+" ชิ้น เป็นรางวัลสำหรับการรวบรวมกลุ่ม "+getarg(3)+" ครับ";
			close;
		}
	}
	return;
}
