//===== rAthena Script =======================================
//= NPCs related to Aldebaran Clock Tower (Nightmare)
//===== By: ==================================================
//= Ridley8819
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Additional Comments: =================================
//= 1.0 Initial release [Ridley8819]
//= 1.1 Dialogue cleanup [Aleos]
//============================================================

c_tower1,232,222,5	script	Belljamin Button#ct	968,{
	mes "[Belljamin Button]";
	mes "มอนสเตอร์ที่นี่มีความสามารถในการควบคุมเวลา";
	if( BaseLevel < 150 ){
		mes "กลับมาใหม่เมื่อเจ้ามี ^0000FFเลเวล 150^000000 นะ";
		close;
	}
	mes "เวลา, ความทรงจำ, สภาพแวดล้อม... ทุกสิ่งสามารถบิดเบี้ยวได้ที่นี่";
	next;
	mes "[Belljamin Button]";
	mes "ข้าขอร้องล่ะ อย่าถามอะไรไปมากกว่านี้เลย มันอาจทำให้ชีวิตของเจ้าตกอยู่ในอันตราย";
	next;
	.@menu = select("ไม่ถามดีกว่า:ไปยังชั้น 2 ของหอคอยที่บิดเบี้ยว:ไปยังชั้น 3 ของหอคอยที่บิดเบี้ยว");
	switch(.@menu){
		case 1:
			break;
		case 2:
		case 3:
			if( .@menu == 2 ){
				.@map$ = "c_tower2_";
				.@x = 268;
				.@y = 26;
			}else{
				.@map$ = "c_tower3_";
				.@x = 65;
				.@y = 147;
			}
			mes "[Belljamin Button]";
			mes "ระมัดระวังในการตัดสินใจของเจ้าด้วย";
			next;
			mes "[Belljamin Button]";
			mes "ข้าต้องการ ^0000FF10,000z^000000 เพื่อให้เจ้าเข้าไปข้างใน ที่นั่นเป็นสถานที่ที่ค่อนข้างอันตรายทีเดียว";
			next;
			switch( select( "ยกเลิก:จ่าย 10,000z:ทำไมเจ้าถึงต้องการเงิน?" ) ){
				case 1:
					break;
				case 2:
					if( Zeny < 10000 ){
						mes "[Belljamin Button]";
						mes "จริงเหรอ? เจ้าไม่มีเงินแต่ยังขอให้ข้าพาส่งไปที่นั่นเนี่ยนะ?";
						close;
					}
					Zeny -= 10000;
					mes "[Belljamin Button]";
					mes "ข้าไม่แน่ใจว่าเจ้าจะรับรู้ถึงเวลาข้างในนั้นได้อย่างไร แต่จงระวังตัวให้มากที่สุดเท่าที่จะทำได้";
					close2;
					warp .@map$, .@x, .@y;
					end;
				case 3:
					mes "[Belljamin Button]";
					mes "ข้าอยู่ที่นี่ทั้งวันเพื่อความปลอดภัยของเหล่านักผจญภัย ข้าคอยเตือนพวกเขาเกี่ยวกับอันตรายเสมอ นั่นคือเหตุผลที่ข้าต้องการค่าตอบแทนบ้าง";
					emotion ET_SWEAT;
					close;
			}
			break;
	}
	mes "[Belljamin Button]";
	mes "ฟู่...เจ้าเลือกได้ดีแล้วล่ะ ที่นี่มันโหดร้ายมาก ดังนั้นเจ้าควรกลับไปยังพื้นที่ที่ปลอดภัยจะดีกว่า";
	close;
}

c_tower2_,274,26,3	script	Belljamin Button#nct1	962,{
	mes "[Belljamin Button]";
	mes "บางทีเจ้าอาจจะเคยเห็นข้ามาก่อนหรือเปล่า?";
	next;
	mes "[Belljamin Button]";
	mes "เจ้าเห็นข้าในสถานที่ที่ต่างออกไป ด้วยรูปลักษณ์ที่ต่างออกไป";
	next;
	if( select( "เจ้าเป็นใคร?:พาข้าออกไปจากที่นี่ที!" ) == 1 ){
		mes "[Belljamin Button]";
		mes "เจ้าช่วยอธิบายให้ข้าฟังหน่อยได้ไหมว่าเจ้าเห็นอะไรมาบ้าง และเจ้าทำอะไรลงไป?";
		next;
		mes "[Belljamin Button]";
		mes "ข้าก็เหมือนกัน เจ้าเองก็อาจจะเป็นคนที่สาบสูญไปคนหนึ่ง ที่ไม่รู้ตัว ไม่มีจุดมุ่งหมาย หรือแม้แต่ความคิดใดๆ";
		next;
		mes "[Belljamin Button]";
		mes "ได้เวลาต้องกลับแล้วล่ะ โปรดเห็นคุณค่าของเวลาให้มากขึ้นกว่านี้หน่อยนะ";
		next;
		if( select( "ขออยู่ต่ออีกสักพัก:โปรดพาข้ากลับไปที" ) == 1 ){
			mes "[Belljamin Button]";
			mes "เจ้าคงจะชอบที่นี่มากจริงๆ สินะ เอาเถอะ อยากทำอะไรก็ทำตามใจเจ้าเลย";
			close;
		}
	}else{
		mes "[Belljamin Button]";
		mes "ได้เวลาเดินทางต่อแล้วล่ะ เจ้ามีเครื่องดื่มแก้วโปรดไหม?";
		next;
	}
	mes "[Belljamin Button]";
	mes "ลองนึกถึงเครื่องดื่มในถ้วยดูสิ ค่อยๆ ใช้ช้อนคนมันอย่างช้าๆ แล้วจ้องมองดูขณะที่เครื่องดื่มนั้นค่อยๆ เปลี่ยนสีไป";
	close2;
	warp "c_tower1", 235, 223;
	end;
}

c_tower3_,59,148,5	duplicate(Belljamin Button#nct1)	Belljamin Button#nct2	574
