//===== rAthena Script ======================================= 
//= Magic Book Seller
//===== By: ================================================== 
//= Masao, Muad_Dib (translation)
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= An NPC which sells you different kinds of Magic Books.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Optimized. [Euphy]
//= 1.1a Updated NPC name to "Master Velofos". [Euphy]
//= 1.1b Fixed the rand part in "Mysterious Documents" to match
//=	 Aegis & fixed a bracket issue. [Capuche]
//= 1.2 Updates Magic Books gained from Lea. [Aleos]
//= 1.3 Fixes checkquest calls. [Everade]
//= 1.4 Added EAJ_WARLOCK so that jobs after Warlock such as 
//      Arch Mage can enter. [Mazvi]
//============================================================ 

// Main Quest :: war_book
//============================================================
geffen_in,176,105,4	script	Master Velofos	1_M_WIZARD,{
	if (checkweight(1201,1) == 0) {
		mes "คุณแบกไอเทมมากเกินไป กรุณาทำให้กระเป๋ามีที่ว่างก่อน";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "น้ำหนักไอเทมของคุณมากเกินไป กรุณาลดน้ำหนักไอเทมลงก่อน";
		close;
	}
	if ((Upper == 2) && (Class != Job_Baby_Warlock)) {
		mes "[Master Velofos]";
		mes "เจ้าเป็นอะไรน่ะ เด็กทารกเหรอ?";
		mes "ข้าไม่ได้มาที่นี่เพื่อเลี้ยงเด็ก ไปให้พ้น!";
		close;
	}
	if ((eaclass()&EAJ_THIRDMASK)!=EAJ_WARLOCK) {
		mes "[Master Velofos]";
		mes "ฮ่า ฮ่า เจ้าเองก็กำลังแสวงหาความรู้ใหม่ๆ อยู่เหมือนกันงั้นรึ?";
		next;
		mes "[Master Velofos]";
		mes "ข้าเสียใจด้วยนะ แต่ความรู้ของข้าไม่ได้เกี่ยวข้องกับเจ้า";
		close;
	}
	if (getskilllv(2230) < 1) {
		mes "[Master Velofos]";
		mes "อืม เจ้ามีคุณสมบัติครบทุกอย่างยกเว้นอย่างเดียวคือ เจ้าจำเป็นต้องมีคาถา Release";
		next;
		mes "[Master Velofos]";
		mes "ไปเรียนคาถานั้นก่อน ข้ามีเรื่องจะบอกเจ้าเยอะแยะเลยล่ะ";
		close;
	}
	mes "[Master Velofos]";
	mes "ยินดีต้อนรับ สหายของข้า";
	mes "ยินดีด้วยที่เจ้าได้เข้าสู่โลกใบใหม่แห่งเวทมนตร์";
	next;
	mes "[Master Velofos]";
	mes "พวกเราผู้ฝึกฝนเวทมนตร์มักจะกระหายความรู้และพลังใหม่ๆ อยู่เสมอ";
	next;
	mes "[Master Velofos]";
	mes "พวกเราอดทนต่อความยากลำบากและความเจ็บปวดอย่างมากเพื่อให้ได้สิ่งที่ต้องการ เมื่อเราทำสำเร็จ ความรู้สึกของความสำเร็จนั้นมันเกินกว่าจะบรรยายได้ นั่นคือเหตุผลที่ทั้งเจ้าและข้าเลือกวิถีแห่งเวทมนตร์";
	next;
	mes "[Master Velofos]";
	mes "ว่าแต่ อะไรทำให้เจ้ามาหาข้าในวันนี้ล่ะ?";
	next;
	switch (select("ไม่มีอะไรครับ:ท่านรู้จัก Reading Spell Book ไหม?:Freezing Spell คืออะไร?:ข้าขอสมุดเวทมนตร์ได้ไหม?:ข้าสามารถใช้ Reading Spell Book โดยไม่มี Freezing Spell ได้ไหม?:เกี่ยวกับ Ultimate Magic Book")) {
	case 1:
		mes "[Master Velofos]";
		mes "หึ! เจ้านี่ตลกดีนะ";
		close;
	case 2:
		mes "[Master Velofos]";
		mes "Reading Spell Book แปลตรงตัวว่า 'อ่านสมุดเวทมนตร์' เจ้าูรู้วิธีใช้สมุดเวทมนตร์ (Magic Books) หรือยัง?";
		next;
		switch (select("ทราบครับ:ไม่ทราบครับ")) {
		case 1:
			mes "[Master Velofos]";
			mes "ถ้าอย่างนั้น เจ้าก็ไม่จำเป็นต้องฟังคำบรรยายเพิ่มเติมแล้วล่ะ";
			close;
		case 2:
			mes "[Master Velofos]";
			mes "สมุดเวทมนตร์เป็นเวทมนตร์ประเภทสกรอลล์ขั้นสูง ในขณะที่คาถาบนสกรอลล์จะหายไปหลังใช้งานเพียงครั้งเดียว แต่คาถาในสมุดเวทมนตร์สามารถร่ายได้หลายครั้ง";
			next;
			mes "[Master Velofos]";
			mes "ลองเปรียบเทียบคาถาบนสกรอลล์เป็นเหมือนเส้นลวด เมื่อเส้นลวดถูกใช้เพื่อเปิดประตูหรือทำอย่างอื่น มันจะโค้งงอและแตกสลายจนเสียรูปทรงเดิมไป แต่คาถาจากสมุดเวทมนตร์จะเหมือนกับโลหะจำรูป (shape memory alloy) มากกว่า";
			next;
			mes "[Master Velofos]";
			mes "หืม เจ้าไม่รู้จักโลหะจำรูปงั้นรึ? เอ้อ เอาเป็นว่าจำแค่นี้พอ: ลวดที่ทำจากโลหะจำรูปจะคืนรูปทรงเดิมเมื่อเวลาผ่านไป";
			next;
			mes "[Master Velofos]";
			mes "เช่นเดียวกับโลหะผสม คาถาในสมุดเวทมนตร์จะยังคงอยู่หลังการใช้งาน แม้ว่าการใช้คาถาซ้ำจะต้องมีช่วงเวลาพักเพื่อการฟื้นฟูของมันก็ตาม";
			next;
			mes "[Master Velofos]";
			mes "...โอ้ ข้าไม่ได้ตั้งใจจะพูดนานเกินไป เอาเป็นว่าเจ้าสามารถใช้สมุดเวทมนตร์เพื่อร่ายคาถาที่เจ้าเรียนรู้มาแล้วได้ แนวคิดของมันต่างจากคาถาปกติ";
			next;
			select("มันต่างกันอย่างไรครับ?");
			mes "[Master Velofos]";
			mes "เจ้าถามว่าทำไมต้องเขียนคาถาที่มีอยู่ลงในสมุดเวทมนตร์งั้นรึ?";
			next;
			mes "[Master Velofos]";
			mes "นั่นเป็นคำถามที่ดี มันเกี่ยวข้องกับ Magic Binding (การพันธนาการเวท)";
			next;
			mes "[Master Velofos]";
			mes "เจ้าได้เรียนรู้ Release มาแล้ว ซึ่งช่วยให้เจ้ากักเก็บพลังงานของธาตุทั้งสี่ที่แตกต่างกันเอาไว้ก่อนการร่ายจริง Magic Binding ก็คล้ายกับ Release นั่นแหละ";
			next;
			mes "[Master Velofos]";
			mes "ในเมื่อเจ้าเรียนรู้ Release แล้ว เจ้าก็สามารถใช้ Magic Binding ได้เช่นกัน เอาเป็นว่าเราค่อยคุยเรื่องนั้นทีหลัง";
			next;
			mes "[Master Velofos]";
			mes "เจ้ารู้วิธีกักเก็บคาถาที่มีอยู่ แล้วจึงร่ายมันในจังหวะที่ต้องการผ่านทาง Release";
			next;
			mes "[Master Velofos]";
			mes "การถือครองลูกแก้วแห่งธาตุทั้งสี่ก็เกี่ยวข้องกับ Magic Binding เช่นกัน";
			next;
			mes "[Master Velofos]";
			mes "อย่างที่เจ้ารู้ ธาตุทั้งสี่คือ ดิน น้ำ ลม และไฟ การสามารถใช้งานพวกมันได้อย่างอิสระคือหลักการพื้นฐานของเวทมนตร์";
			next;
			mes "[Master Velofos]";
			mes "พวกเรามนุษย์ใช้คบไฟเพื่อเก็บไฟ ใช้พัดเพื่อเป่าลม และใช้ชามเพื่อใส่น้ำ พวกเรายังใช้ดินเหนียวเพื่อทำเครื่องปั้นดินเผาด้วย";
			next;
			mes "[Master Velofos]";
			mes "กิจกรรมเหล่านั้นอาจต่างจากเวทมนตร์ แต่ทั้งคู่ก็เหมือนกันในแง่ของการใช้ประโยชน์จากธาตุทั้งสี่";
			next;
			mes "[Master Velofos]";
			mes "เมื่อเจ้าใช้เวทมนตร์ที่ซับซ้อนเพื่อเรียกพายุ ฟาดสายฟ้า หรือเผาไหม้พื้นที่ นั่นจะใช้พลังงานของเจ้าอย่างมหาศาล ทั้งทางร่างกายและจิตใจ";
			next;
			mes "[Master Velofos]";
			mes "เมื่อเจ้าล้มเหลวในการพันธนาการเวทมนตร์ที่ทรงพลังเช่นนั้น หรือล้มเหลวในการควบคุมมัน เจ้าอาจได้รับความเสียหายที่แก้ไขไม่ได้ต่อทั้งร่างกายและจิตใจ";
			next;
			mes "[Master Velofos]";
			mes "นั่นคือเหตุผลที่เราใช้สมุดเวทมนตร์เพื่อลดภาระนั้น มันช่วยเรียกคาถาที่เตรียมไว้ล่วงหน้าในขณะที่เจ้ามุ่งสมาธิไปที่การพันธนาการพลังเวทของมัน";
			next;
			mes "[Master Velofos]";
			mes "แต่มีข้อเสียอย่างหนึ่งคือ: สมุดเวทมนตร์ไม่สามารถใช้กับคาถาที่เจ้ายังไม่ได้เรียนรู้ได้";
			next;
			mes "[Master Velofos]";
			mes "อย่างที่ข้าบอกก่อนหน้านี้ คาถาในสมุดเวทมนตร์มีแนวโน้มที่จะกลับคืนสู่สภาพเดิมของมัน";
			next;
			mes "[Master Velofos]";
			mes "ด้วยเหตุนี้ ในฐานะ Circler เจ้าไม่สามารถเปลี่ยนหรือสร้างคาถาในสมุดเวทมนตร์ได้";
			next;
			mes "[Master Velofos]";
			mes "ถึงเจ้าจะสร้างขึ้นมา พลังของมันก็จะเกินขีดความสามารถของเจ้าอยู่ดี";
			next;
			mes "[Master Velofos]";
			mes "ในการที่จะใช้ Reading Spell Book ได้นั้น เจ้าต้องเรียนรู้ Freezing Spell เพื่อพันธนาการเวทมนตร์เสียก่อน";
			close;
		}
	case 3:
		mes "[Master Velofos]";
		mes "Freezing Spell ใช้สำหรับ Magic Binding มันช่วยให้เจ้าบรรจุคาถาเวทมนตร์ลงในวงกลมเวท (circle)";
		next;
		mes "[Master Velofos]";
		mes "วงกลมเวทมนตร์ทำงานสอดประสานกันอย่างเป็นระบบ เมื่อเจ้าใช้ Freezing Spell เพื่อพันธนาการคาถา คาถานั้นจะไม่ทำงานจนกว่าเจ้าจะร่ายคาถา Release";
		next;
		mes "[Master Velofos]";
		mes "แน่นอนว่าการพันธนาการคาถาจะใช้มานาของเจ้าอย่างต่อเนื่อง แต่มันใช้มานาน้อยกว่าคาถาเวทมนตร์แบบดั้งเดิม";
		next;
		mes "[Master Velofos]";
		mes "Magic Binding จะยากขึ้นเมื่อเจ้าใช้เวทมนตร์ที่ทรงพลังมากขึ้น";
		next;
		mes "[Master Velofos]";
		mes "สำหรับคาถาที่อ่อนแอ เจ้าสามารถพันธนาการได้สองถึงสามอย่างในวงกลมเวทของเจ้า สำหรับคาถาที่ทรงพลังกว่า อาจจะเป็นไปได้แค่หนึ่งหรือสองอย่าง";
		next;
		mes "[Master Velofos]";
		mes "ทั้งหมดนั้นขึ้นอยู่กับความสามารถของเจ้าในการควบคุมคาถา เจ้าต้องลองและสัมผัสประสบการณ์ Magic Binding หลังจากที่ได้เรียนรู้มันครั้งแรกเพื่อที่จะเข้าใจมันจริงๆ";
		close;
	case 4:
		mes "[Master Velofos]";
		mes "ข้าขอโทษนะ แต่ข้ามีเพียงสมุดเวทมนตร์สำหรับผู้เริ่มต้น ซึ่งเห็นได้ชัดว่ามันไม่เหมาะกับเจ้า เจ้ายังต้องการมันอยู่อีกไหม?";
		next;
		switch (select("ต้องการครับ:ข้าจะหาหนังสือขั้นสูงได้จากที่ไหน?")) {
		case 1:
			mes "[Master Velofos]";
			mes "โปรดไปคุยกับ [Lea] ในหอจดหมายเหตุ บอกนางว่าเจ้าต้องการสมุดเวทมนตร์เล่มหนึ่งที่ข้าเก็บไว้ที่นั่น";
			next;
			mes "[Master Velofos]";
			mes "เพื่อข้อมูลของเจ้า นางจะไม่ยอมยกให้เจ้าโดยไม่มีสิ่งตอบแทนหรอกนะ เตรียมใจไว้เลยว่านางจะขออะไรบางอย่างจากเจ้า!";
			if (wm_book < 1) set wm_book, 1;
			close;
		case 2:
			mes "[Master Velofos]";
			mes "ข้าเสียใจด้วย แต่ฝ่ายบริหารของเราเพิ่งตัดสินใจจำกัดการเข้าถึงสมุดเวทมนตร์สำหรับ Multi Circlers เนื่องจากจำนวนของพวกเขาเพิ่มขึ้นเร็วกว่าที่เราคาดไว้";
			next;
			mes "[Master Velofos]";
			mes "เจ้าอาจจะหาเจอถ้าเจ้าพยายามมากพอ แต่ข้าไม่มีติดตัวไว้เลย";
			next;
			mes "[Master Velofos]";
			mes "หากฝ่ายบริหารยกเลิกข้อจำกัด เจ้าจะเป็นคนแรกที่ได้รู้ ไม่ต้องกังวลไป";
			close;
		}
	case 5:
		mes "[Master Velofos]";
		mes "ไม่มีอะไรเกิดขึ้นหรอกเมื่อเจ้าพยายามพันธนาการเวทโดยไม่ใช้ Magic Binding เพราะเจ้าไม่มีอะไรจะกักเก็บเวทมนตร์เอาไว้";
		next;
		mes "[Master Velofos]";
		mes "มันเหมือนกับน้ำที่ต้องมีชามเพื่อให้อยู่กับที่";
		next;
		mes "[Master Velofos]";
		mes "ปัญหาคือเมื่อเจ้าพยายามฝืนร่ายคาถาที่เจ้ายังไม่ได้เรียนรู้ มันจะส่งผลสะท้อนกลับหาเจ้าอย่างแน่นอน";
		next;
		mes "[Master Velofos]";
		mes "แน่นอนว่าความเสียหายจะไม่รุนแรงนัก: เจ้าจะหลับไปหรือตกอยู่ในความสับสน ทั้งหมดนี้เป็นเพราะเกิดความขัดแย้งระหว่างสมุดเวทมนตร์ของเจ้ากับพลังของ Magic Binding เมื่อเจ้าพยายามเรียกและกักเก็บคาถาที่ยังไม่ได้เรียนรู้";
		next;
		mes "[Master Velofos]";
		mes "อย่าริลองเพียงเพราะอยากเห็นว่าเกิดอะไรขึ้นด้วยตัวเองล่ะ! การหลับไประหว่างร่ายคาถา? ไม่มีอะไรจะน่าอับอายไปกว่านั้นอีกแล้วสำหรับผู้ฝึกฝนเวทมนตร์อย่างพวกเรา";
		close;
	case 6:
		if ((BaseLevel > 139) && (getskilllv(2217) > 0 || getskilllv(2213) > 0)) {
			if (mac_book < 1) {
				mes "[Master Velofos]";
				mes "ข้าขอโทษนะ แต่เจ้าช่วยไปถามคนอื่นได้ไหม? ตอนนี้ข้าค่อนข้างยุ่งน่ะ";
				close;
			}
			if (mac_book == 1) {
				mes "[Master Velofos]";
				mes "หืม เจ้ากำลังตามหา Ultimate Magic Book งั้นรึ? ใครส่งเจ้ามา?";
				next;
				select("คุณ Lea ครับ");
				mes "[Master Velofos]";
				mes "ข้าเสียใจนะ แต่ข้อมูลเกี่ยวกับ Ultimate Magic Book หลงเหลืออยู่ไม่มากนัก";
				next;
				mes "[Master Velofos]";
				mes "เหล่านักวิชาการไม่กี่คนในทวีป Midgard เคยพยายามรวบรวมสมุดเวทมนตร์โบราณและทำหนังสือเล่มนั้นขึ้นมาใหม่ แต่ว่า...";
				next;
				select("แต่ว่าอะไรครับ?");
				mes "[Master Velofos]";
				mes "โอ้ ใช่แล้ว บางทีเขาคนนั้นอาจจะรู้อะไรบางอย่าง";
				next;
				select("ใช่แล้ว ข้ากะไว้แล้วเชียว!");
				mes "[Master Velofos]";
				mes "น้องชายของข้าก็เหมือนกับเจ้านั่นแหละ เขาสนใจในการสร้าง Ultimate Magic Book ขึ้นมาใหม่มาก";
				next;
				mes "[Master Velofos]";
				mes "ชื่อของเขาคือ Galfos";
				mes "เขาสนใจในสมุดเวทมนตร์โบราณเป็นอย่างมาก";
				next;
				mes "[Master Velofos]";
				mes "ข้าไม่ได้สนับสนุนเขาหรือให้คำปรึกษาในการวิจัยของเขาเลย เพราะข้าคิดว่าเขาคงจะเลิกทำไปในไม่ช้า แต่ว่า...";
				next;
				mes "[Master Velofos]";
				mes "ถ้าเขาค้นพบสิ่งที่มีประโยชน์ บางทีมันอาจจะช่วยเจ้าได้";
				next;
				select("ตอนนี้ Galfos อยู่ที่ไหนครับ?");
				mes "[Master Velofos]";
				mes "ข้าได้ยินมาว่าเขาอยู่แถวๆ Expedition Camp หลังรอยแยกมิติ เพื่อค้นหาข้อมูลเกี่ยวกับสมุดเวทมนตร์โบราณ";
				next;
				mes "[Master Velofos]";
				mes "ถ้าเจ้าอยากพบเขา เจ้าควรเตรียมตัวสำหรับการเดินทางอันยาวไกลนะ";
				set mac_book,2;
				close;
			}
			if (mac_book > 1) {
				mes "[Master Velofos]";
				mes "Galfos อยู่แถวๆ Expedition Camp หลังรอยแยกมิติ";
				next;
				mes "[Master Velofos]";
				mes "ถ้าเจ้าอยากพบเขา เจ้าควรเตรียมตัวสำหรับการเดินทางอันยาวไกลนะ";
				close;
			}
		}
		mes "[Master Velofos]";
		mes "ฮ่า ฮ่า ข้าเสียใจด้วยนะ แต่เจ้าควรจะมุ่งเน้นไปที่การเสริมสร้างร่างกายของเจ้า มากกว่าการสร้างสมุดเวทมนตร์";
		close;
	}
}

geffen_in,175,112,4	script	Lea	2_F_MAGICMASTER,{
	if (checkweight(1201,1) == 0) {
		mes "คุณแบกไอเทมมากเกินไป กรุณาทำให้กระเป๋ามีที่ว่างก่อน";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "น้ำหนักไอเทมของคุณมากเกินไป กรุณาลดน้ำหนักไอเทมลงก่อน";
		close;
	}
	if (wm_book > 0 && ((eaclass()&EAJ_THIRDMASK)==EAJ_WARLOCK)) {
		mes "[Lea]";
		mes "มีอะไรให้ช่วยไหมคะ?";
		next;
		select("Master Velofos ให้ข้ามาเอาสมุดเวทมนตร์ของเขาครับ");
		mes "[Lea]";
		mes "คุณจะมายืมหนังสือเหรอคะ?";
		next;
		switch (select("ขอคิดดูก่อนครับ:ข้าต้องการ Intermediate Magic Books:ข้าต้องการ Superior Magic Books:ข้าต้องการ Ultimate Magic Books")) {
		case 1:
			mes "[Lea]";
			mes "ไม่มีปัญหาค่ะ";
			close;
		case 2:
			mes "[Lea]";
			mes "อ้อ คุณต้องการ Intermediate Magic Books สินะคะ หากคุณจ่าย ^4d4dffเงินมัดจำการยืม 100,000z^000000 เราจะให้คุณยืม Intermediate Magic Books ค่ะ";
			next;
			mes "[Lea]";
			mes "คุณต้องการสมุดเวทมนตร์เล่มไหนคะ?";
			next;
			set .@i, select("ขอคิดดูก่อนครับ:Magic Book (Storm Gust):Magic Book (Lord of Vermillion):Magic Book (Meteor Storm)")-1;
			mes "[Lea]";
			if (!.@i) {
				mes "ไม่มีปัญหาค่ะ";
				close;
			}
			setarray .@Books[0],100065,100066,100067;
			if (countitem(.@Books[.@i-1])) {
				mes "คุณมีสมุดเวทมนตร์เล่มนั้นอยู่แล้ว คุณไม่สามารถยืมเล่มใหม่ได้จนกว่าจะคืนเล่มปัจจุบันค่ะ";
				next;
				mes "[Lea]";
				mes "คุณไม่รู้เหรอว่ามีคนอื่นอีกกี่คนที่ต้องการหนังสือที่คุณมีอยู่? มันค่อนข้างเห็นแก่ตัวนะที่อยากได้มากกว่าหนึ่งเล่มน่ะค่ะ";
				close;
			}
			if (Zeny > 99999) {
				mes "ขอบคุณสำหรับเงินมัดจำค่ะ วันกำหนดส่งคืนหนังสือถูกเขียนไว้บนบัตรยืมที่ปกหลังของหนังสือแล้ว โปรดอย่าทำหนังสือหายหรือชำรุดเพื่อหลีกเลี่ยงการจ่ายค่าชดเชยนะคะ";
				next;
				mes "[Lea]";
				mes "Intermediate และ Superior Magic Books จะต้องถูกคืนเมื่อคุณยืม Ultimate Magic Book อย่าลืมว่าคุณแค่ยืมไปเท่านั้น ดังนั้นโปรดดูแลหนังสือเหล่านั้นให้ดีนะคะ";
				Zeny -= 100000;
				getitem .@Books[.@i-1],1; //WL_MB_SG, WL_MB_LOV, WL_MB_MS
				close;
			}
			mes "^4d4dffเงินมัดจำการยืมคือ 100,000z^000000 ค่ะ เงินมัดจำจะถูกปรับเปลี่ยนตามสมุดเวทมนตร์ที่เปลี่ยนไปค่ะ";
			close;
		case 3:
			mes "[Lea]";
			mes "โอ้ คุณต้องการ Superior Magic Books เหรอคะ? นโยบายได้ถูกเปลี่ยนเป็นการรับเงินมัดจำการยืมอย่างไม่มีเงื่อนไข แทนที่เงื่อนไขการยืมที่เข้มงวดแบบเดิมแล้วค่ะ";
			mes "เงินมัดจำการยืม Superior Magic Book คือ ^4d4dff500,000z^000000 ค่ะ";
			next;
			mes "[Lea]";
			mes "คุณต้องการสมุดเวทมนตร์เล่มไหนคะ?";
			next;
			set .@i, select("ขอคิดดูก่อนครับ:Magic Book (Drain Life):Magic Book (Jack Frost):Magic Book (Earth Strain):Magic Book (Crimson Rock):Magic Book (Chain Lightning)")-1;
			mes "[Lea]";
			if (!.@i) {
				mes "ไม่มีปัญหาค่ะ";
				close;
			}
			setarray .@Books[0],100068,100069,100070,100071,100072;
			if (countitem(.@Books[.@i-1])) {
				mes "คุณมีสมุดเวทมนตร์เล่มนั้นอยู่แล้ว คุณไม่สามารถยืมเล่มใหม่ได้จนกว่าจะคืนเล่มปัจจุบันค่ะ";
				next;
				mes "[Lea]";
				mes "คุณไม่รู้เหรอว่ามีคนอื่นอีกกี่คนที่ต้องการหนังสือที่คุณมีอยู่? มันค่อนข้างเห็นแก่ตัวนะที่อยากได้มากกว่าหนึ่งเล่มน่ะค่ะ";
				close;
			}
			if (Zeny > 499999) {
				mes "ขอบคุณสำหรับเงินมัดจำค่ะ วันกำหนดส่งคืนหนังสือถูกเขียนไว้บนบัตรยืมที่ปกหลังของหนังสือแล้ว โปรดอย่าทำหนังสือหายหรือชำรุดเพื่อหลีกเลี่ยงการจ่ายค่าชดเชยนะคะ";
				next;
				mes "[Lea]";
				mes "Intermediate และ Superior Magic Books จะต้องถูกคืนเมื่อคุณยืม Ultimate Magic Book อย่าลืมว่าคุณแค่ยืมไปเท่านั้น ดังนั้นโปรดดูแลหนังสือเหล่านั้นให้ดีนะคะ";
				Zeny -= 500000;
				getitem .@Books[.@i-1],1; //WL_MB_DL, WL_MB_JF, WL_MB_ES, WL_MB_CR, WL_MB_CL
				close;
			}
			mes "^4d4dffเงินมัดจำการยืมคือ 500,000z^000000 ค่ะ เงินมัดจำจะถูกปรับเปลี่ยนตามสมุดเวทมนตร์ที่เปลี่ยนไปค่ะ";
			close;
		case 4:
			if ((BaseLevel > 139) && (mac_book < 1) && ((getskilllv(2217) > 0) || (getskilllv(2213) > 0))) {
				mes "[Lea]";
				mes "เมื่อกี้... คุณเพิ่งพูดว่า Ultimate Magic Book เหรอคะ?";
				next;
				mes "[Lea]";
				mes "โอ้ คุณพระช่วย....";
				mes "ฉันไม่อยากจะเชื่อเลยว่าจะมีใครต้องการหนังสือเล่มนั้นจริงๆ";
				next;
				select("ได้โปรดอย่าบอกนะว่าคุณไม่มีมันน่ะ");
				mes "[Lea]";
				mes "พูดตามตรงนะคะ ฉันไม่เคยเห็น Ultimate Magic Book เลยด้วยซ้ำค่ะ";
				next;
				mes "[Lea]";
				mes "คุณควรไปถาม Master Velofos นะคะถ้าอยากรู้เรื่องหนังสือเล่มนั้นเพิ่มเติม";
				next;
				mes "[Lea]";
				mes "ขอโทษด้วยนะคะที่ฉันช่วยอะไรคุณไม่ได้มากกว่านี้";
				set mac_book,1;
				close;
			}
			if ((BaseLevel > 139) && (mac_book > 0) && ((getskilllv(2217) > 0) || (getskilllv(2213) > 0))) {
				mes "[Lea]";
				mes "คุณควรไปถาม Master Velofos เพื่อขอข้อมูลเกี่ยวกับหนังสือเล่มนั้นค่ะ";
				close;
			}
			mes "[Lea]";
			mes "เอ่อ... ทำไมคุณไม่ลองฝึกฝนต่ออีกสักนิดก่อนล่ะคะ?";
			next;
			mes "[Lea]";
			mes "ในการที่จะได้ Ultimate Magic Book มาครอง คุณต้องมีเลเวล 140 และเรียนรู้ Tetera Bolt และ Comet เสียก่อนค่ะ";
			next;
			mes "[Lea]";
			mes "หนังสือเล่มนั้นไม่ใช่ใครๆ ก็ใช้ได้นะคะ รู้ไหม?";
			close;
		}
	}
	mes "[Lea]";
	mes "ยินดีต้อนรับสู่หอจดหมายเหตุเวทมนตร์ค่ะ คุณสามารถอ่านและยืมหนังสือจากที่นี่ได้ภายใต้เงื่อนไขที่กำหนดค่ะ";
	close;
}

mid_camp,255,244,4	script	Galfos	4_M_JOB_WIZARD,{
	if (checkweight(1201,1) == 0) {
		mes "คุณแบกไอเทมมากเกินไป กรุณาทำให้กระเป๋ามีที่ว่างก่อน";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "น้ำหนักไอเทมของคุณมากเกินไป กรุณาลดน้ำหนักไอเทมลงก่อน";
		close;
	}
	if ((Upper == 2) && (Class != Job_Baby_Warlock)) {
		mes "[Galfos]";
		mes "เจ้าเป็นอะไรน่ะ เด็กทารกเหรอ?";
		mes "ข้าไม่ได้มาที่นี่เพื่อเลี้ยงเด็ก ไปให้พ้น!";
		close;
	}
	if ((eaclass()&EAJ_THIRDMASK)!=EAJ_WARLOCK) {
		mes "[Galfos]";
		mes "แขนขวาของข้ารู้สึกร้อน ในขณะที่แขนซ้ายรู้สึกเย็น ข้าไม่เคยชินกับสภาพอากาศประหลาดๆ นี่เลยจริงๆ!";
		close;
	}
	if (getskilllv(2230) < 1) {
		mes "[Galfos]";
		mes "อา ข้าคงจะลำบากแน่ถ้าขืนอยู่ที่นี่นานกว่านี้ หรือบางทีข้าอาจจะลำบากอยู่แล้วก็ได้...";
		close;
	}
	if (BaseLevel < 140 || (getskilllv(2217) < 1 && getskilllv(2213) < 1)) {
		mes "[Galfos]";
		mes "ข้าขอโทษนะ แต่ข้าไม่คุยกับคนที่ยังไม่พร้อมสำหรับเรื่องนี้";
		close;
	}
	if (mac_book < 2) {
		mes "[Galfos]";
		mes "เจ้าดูแข็งแกร่งนะ แต่ดูเหมือนเจ้าจะไม่มีธุระอะไรกับข้า";
		close;
	}
	if (mac_book == 2) {
		mes "[Galfos]";
		mes "อะไรนำพาเจ้ามาหาข้าล่ะ?";
		next;
		select("ข้าได้ยินมาว่าท่านศึกษาเกี่ยวกับ Ultimate Magic Book ครับ");
		mes "[Galfos]";
		mes "ทำไมล่ะ เจ้าเองก็สนใจเหมือนกันงั้นรึ?";
		mes "ข้าไม่นึกเลยว่าจะมีใครมาสนใจหนังสือเล่มนั้นจริงๆ ใช่ ข้ากำลังศึกษาสมุดเวทมนตร์เล่มนั้นอยู่ เจ้าไปรู้เรื่องนี้มาได้ยังไง?";
		next;
		select("ท่านคือน้องชายของ Master Velofos ใช่ไหมครับ?");
		mes "[Galfos]";
		mes "ใช่ ข้าคือ Galfos และ Velofos คือพี่ชายของข้าเอง";
		next;
		mes "[Galfos]";
		mes "เดี๋ยวหน้าเดี๋ยวนะ,";
		mes "ตาแก่นั่นแนะนำข้าให้เจ้าอย่างนั้นรึ?";
		next;
		select("ถูกต้องแล้วครับ");
		mes "[Galfos]";
		mes "ว้าว!";
		mes "นี่มันน่าประหลาดใจมาก เขาไม่เคยเห็นการวิจัยของข้าเป็นเรื่องจริงจังเลย ข้าดีใจนะที่ในที่สุดเขาก็เปลี่ยนใจ";
		next;
		mes "[Galfos]";
		mes "เหอะ! จังหวะของเจ้านี่มันดีสุดๆ ไปเลย: ข้ากำลังมองหาตัวทดสอบอยู่พอดี";
		next;
		select("ตัวทดสอบเหรอครับ?");
		mes "[Galfos]";
		mes "พูดตรงๆ นะ ข้าสนใจแค่การสร้าง Ultimate Magic Book เท่านั้นแหละ";
		next;
		mes "[Galfos]";
		mes "ข้าไม่เก่งเรื่องการใช้เวทมนตร์หรอก ยังไม่รวมถึงเรื่องที่ข้าชอบใช้หมัดในการต่อสู้มากกว่าด้วย อย่างไรก็ตาม ข้าพบว่าสมุดเวทมนตร์นั้นมันน่าหลงใหลอย่างเหลือเชื่อ";
		next;
		mes "[Galfos]";
		mes "ปัญหาคือข้าไม่สามารถใช้ Ultimate Magic Book ได้ ต่อให้ข้าจะสร้างมันขึ้นมาได้ก็ตาม นั่นคือเหตุผลที่ข้าไม่สามารถทดสอบมันได้ด้วยตัวเองจริงๆ";
		next;
		mes "[Galfos]";
		mes "นั่นคือเหตุผลที่ข้าเฝ้ารอผู้ฝึกฝนเวทมนตร์ตัวจริงอย่างเจ้า ที่สามารถช่วยข้าทดสอบ Ultimate Magic Book ของข้าได้";
		next;
		select("(เขากำลังพูดเรื่องอะไรของเขากันนะ?)");
		mes "[Galfos]";
		mes "การวิจัยของข้าเกือบจะเสร็จสมบูรณ์แล้ว,";
		mes "และข้าต้องแก้ปัญหาข้อสุดท้ายเพื่อสร้างสมุดเวทมนตร์เล่มนี้";
		next;
		mes "[Galfos]";
		mes "ในการสร้าง Ultimate Magic Book โบราณ ข้าต้องการสิ่งประดิษฐ์ (artifact) และคาถาบางอย่างเพื่อหลอมรวมเข้ากับมัน";
		next;
		mes "[Galfos]";
		mes "ข้าได้ฟื้นฟูคาถาตามเอกสารโบราณได้อย่างสมบูรณ์แบบแล้ว แต่ข้ายังไม่มีข้อมูลเกี่ยวกับสิ่งประดิษฐ์ที่จำเป็นเลย";
		next;
		mes "[Galfos]";
		mes "อา แต่ยังมีหนทางสว่างอยู่บ้าง!";
		next;
		select("(ตอนนี้เราเริ่มเข้าเรื่องกันแล้วสินะ!)");
		mes "[Galfos]";
		mes "ฮ่า ฮ่า...";
		mes "มีหอจดหมายเหตุขนาดใหญ่ใน El Dicastes เมืองหลวงของเหล่ายักษ์ชั่วร้าย ที่ซึ่งความรู้ทั้งหมดในโลกนี้ถูกเก็บรวบรวมไว้";
		next;
		mes "[Galfos]";
		mes "บางทีที่นั่นอาจจะเป็นที่ที่จะหาข้อมูลเกี่ยวกับสิ่งประดิษฐ์สำหรับ Ultimate Magic Book เล่มนี้ได้";
		next;
		mes "[Galfos]";
		mes "เจ้ารู้แล้วใช่ไหมว่าต้องทำอะไร? ไปหาข้อมูลเกี่ยวกับสิ่งประดิษฐ์ในหอจดหมายเหตุแห่ง El Dicastes โดยใช้ทุกอย่างที่เจ้ามี";
		next;
		mes "[Galfos]";
		mes "หลังจากนั้น สร้างและนำสิ่งประดิษฐ์นั้นมาให้ข้า แล้วเราค่อยมาคุยเรื่องการสร้างสมุดเวทมนตร์กัน";
		set mac_book,3;
		close;
	}
	if (mac_book == 3) {
		mes "[Galfos]";
		mes "เจ้ารู้แล้วใช่ไหมว่าต้องทำอะไร? ไปหาข้อมูลเกี่ยวกับสิ่งประดิษฐ์ในหอจดหมายเหตุแห่ง El Dicastes โดยใช้ทุกอย่างที่เจ้ามี";
		next;
		mes "[Galfos]";
		mes "หลังจากนั้น สร้างและนำสิ่งประดิษฐ์นั้นมาให้ข้า แล้วเราค่อยมาคุยเรื่องการสร้างสมุดเวทมนตร์กัน";
		close;
	}
	if (mac_book == 4) {
		mes "[Galfos]";
		mes "โอ้ เจ้าเจอ";
		mes "อะไรที่มีประโยชน์บ้างไหม?";
		next;
		set .@i, select("ข้าค้นพบทุกอย่างเกี่ยวกับ Comet แล้วครับ:ข้าได้บรรลุจุดสูงสุดของ Tetra Vortex แล้วครับ");
		mes "[Galfos]";
		if ((.@i == 1 && countitem(100073)) || (.@i == 2 && countitem(100074))) {
			mes "เจ้าล้อข้าเล่นรึเปล่า? เจ้ามีหนังสือเล่มนั้นอยู่แล้วนี่!";
			close;
		}
		if ((.@i == 1 && checkquest(12218) == 2) || (.@i == 2 && checkquest(12219) == 2 && checkquest(12220) == 2 && checkquest(12221) == 2 && checkquest(12222) == 2)) {
			mes "โอ้ คุณพระ ข้าสัมผัสได้ถึงพลังงานมหาศาลที่ออกมาจากตัวเจ้า ยอดเยี่ยมมาก! ตอนนี้เราพร้อมจะสร้าง Ultimate Magic Book กันแล้ว";
			next;
			mes "[Galfos]";
			mes "แต่มีปัญหาอยู่อย่างหนึ่งนะ: ข้าถังแตกมากจนไม่มีเงินพอจะซื้อเครื่องมือมาสร้างหนังสือเลยล่ะ";
			next;
			mes "[Galfos]";
			mes "ข้าต้องการอย่างน้อย ^0000aa500,000 zeny^000000 เพื่อซื้อเครื่องมือที่จำเป็นทั้งหมด เจ้ามีเงินพอไหม?";
			next;
			if(select("อะ-อะไรนะ? ไม่มีครับ!:มีครับ") == 1) {
				mes "[Galfos]";
				mes "อ้าว ไม่มีเหรอ? น่าผิดหวังจัง แต่ข้าก็ทำอะไรไม่ได้ ไว้กลับมาตอนที่มีเงินพอละกันนะ ตกลงไหม?";
				close;
			}
			mes "[Galfos]";
			if (Zeny >= 500000) {
				mes "เอาล่ะ ถ้าอย่างนั้นมาเริ่มกันเลย!";
				next;
				specialeffect2 EF_DISPELL;
				progressbar "ffff00",4;
				specialeffect2 EF_LORD;
				set Zeny, Zeny - 500000;
				getitem 100072+.@i,1; //WL_MB_CM, WL_MB_TV
				mes "[Galfos]";
				mes "พวก ให้ตายสิ ข้าเกือบจะคุมไม่อยู่เพราะพลังเวทมนตร์มหาศาลนั่นเลย! นี่จ๊ะ สมุดเวทมนตร์ของเจ้า";
				next;
				mes "[Galfos]";
				mes "ขอบคุณที่ทำให้ฝันของข้าเป็นจริงนะ กลับมาหาได้เสมอถ้าเจ้าต้องการหนังสือพวกนี้เพิ่ม";
				close;
			}
			mes "เฮ้ ข้าบอกเจ้าว่ายังไงนะ? ข้าซื้อเครื่องมือสร้างหนังสือไม่ได้ถ้าไม่มีเงินน่ะ!";
			close;
		}
		mes "เจ้าแน่ใจนะว่าเจออะไรมาจริงๆ? ดูเหมือนเจ้าจะยังไม่ได้เรียนรู้ในสิ่งที่เราต้องการเลยนะ...";
		close;
	}
	mes "[Galfos]";
	mes "มีอะไรอย่างอื่นที่เจ้าต้องการอีกไหม?";
	close;
}

dic_in01,25,190,0	script	Mysterious Documents	CLEAR_NPC,{
	if (isequipped(2782) < 1) {
		mes "ข้าไม่ควรลืมสวมแหวนของข้านะ";
		close;
	}
	if (ep13_3_invite < 5) {
		mes "เมื่อคุณเข้าใกล้เอกสาร ยักษ์ชั่วร้ายที่ดูเหมือนผู้จัดการหอจดหมายเหตุพยายามจะเรียก Guard Galton คุณควรรีบออกไปจากที่นี่โดยเร็ว";
		close;
	}
	if (checkquest(12165,PLAYTIME) == 1) {
		mes "เอกสารเหล่านี้ทำให้คุณรู้สึกคลื่นไส้ คุณควรกลับมาใหม่หลังจากที่ท้องไส้ของคุณสงบลงแล้ว";
		close;
	}
	if (checkquest(12165,PLAYTIME) == 2) {
		mes "คุณไม่รู้สึกคลื่นไส้มากเท่าเดิมแล้ว ตอนนี้น่าจะปลอดภัยที่จะดำเนินการต่อ";
		erasequest 12165;
		close;
	}
	if ((BaseLevel < 140) || (getskilllv(2217) < 1) && (getskilllv(2213) < 1)) {
		mes "เอกสารในตู้คอนเทนเนอร์นี้ไม่สามารถอ่านออกได้ ไม่มีประโยชน์ที่จะพยายามอ่านพวกมัน";
		close;
	}
	specialeffect2 EF_DISPELL;
	progressbar "ffff00",4;
	if (mac_book < 3) {
		mes "คุณได้ค้นพบบันทึกเกี่ยวกับเวทมนตร์ การเร่งความเร็ว และคลื่นอนุภาค";
		close;
	}
	if (mac_book == 3) {
		mes "คุณได้ค้นพบบันทึกเกี่ยวกับเวทมนตร์ การเร่งความเร็ว และคลื่นอนุภาค เอกสารเหล่านี้ต้องเป็นสิ่งที่ Galfos กำลังตามหาอยู่แน่ๆ";
		set mac_book, 4;
		close;
	}
	if (mac_book == 4) {
		set .@r, rand(1,10);
		if (.@r == 1) {
			if (getskilllv(2213) > 0) {
				if (checkquest(12218) == 1) {
					mes "คุณเป็นเจ้าของเอกสารนี้อยู่แล้ว";
					close;
				}
				mes "^660066ในการสร้างแก่นแท้แห่งลาวา (lava essence) คุณต้องกลั่น Burning Heart 40 อัน ในสถานที่ที่ร้อนที่สุดบนชั้น 2 ของดันเจี้ยน Thor's Volcano^000000";
				mes "^660066คุณต้องทำสิ่งนี้โดยใช้พลังที่ยิ่งใหญ่ที่สุดในสถานที่แห่งนั้น มนุษย์ใช้วิธีนี้ในการศึกษาการเร่งความเร็วของอนุภาคเวทมนตร์มาตั้งแต่สมัยโบราณ^000000";
				next;
				mes "คุณได้ค้นพบ ^990099วิธีการสร้างแก่นแท้แห่งลาวา^000000 โปรดตรวจสอบหน้าต่างเควสต์ของคุณสำหรับรายละเอียดเพิ่มเติม";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12218;
				end;
			}
			mes "ไม่มีอะไรในเอกสารนี้ที่ดูมีประโยชน์เลย";
			close;
		}
		if (.@r == 2) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12219) == 1) {
					mes "คุณเป็นเจ้าของเอกสารนี้อยู่แล้ว";
					close;
				}
				mes "^660066แก่นแท้แห่งเปลวเพลิง (essence of flame) สามารถกลั่นได้บนพื้นดินที่มั่นคงบนชั้น 1 ของดันเจี้ยน Thor's Volcano จำเป็นต้องมี Love Coal 10 อันเพื่อกลั่นแก่นแท้นี้^000000";
				mes "^660066ตามข้อมูลของมนุษย์ แก่นแท้นี้ประกอบขึ้นเป็นหนึ่งในธาตุธรรมชาติทั้งสี่^000000";
				next;
				mes "คุณได้ค้นพบ ^990099วิธีการสร้างแก่นแท้แห่งเปลวเพลิง^000000 โปรดตรวจสอบหน้าต่างเควสต์ของคุณสำหรับรายละเอียดเพิ่มเติม";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12219;
				end;
			}
			mes "ไม่มีอะไรในเอกสารนี้ที่ดูมีประโยชน์เลย";
			close;
		}
		if (.@r == 3) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12220) == 1) {
					mes "คุณเป็นเจ้าของเอกสารนี้อยู่แล้ว";
					close;
				}
				mes "^660066ฉันประสบความสำเร็จในการกลั่นแก่นแท้แห่งธารน้ำแข็ง (essence of glacier) ลึกเข้าไปในชั้นที่ 3 ของถ้ำน้ำแข็ง หากคุณต้องการลอง ให้เตรียม Ice Heart 10 อัน และไปยังสถานที่ต่อไปนี้^000000";
				next;
				mes "คุณได้ค้นพบ ^990099วิธีการสร้างแก่นแท้แห่งธารน้ำแข็ง^000000 โปรดตรวจสอบหน้าต่างเควสต์ของคุณสำหรับรายละเอียดเพิ่มเติม";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12220;
				end;
			}
			mes "ไม่มีอะไรในเอกสารนี้ที่ดูมีประโยชน์เลย";
			close;
		}
		if (.@r == 4) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12221) == 1) {
					mes "คุณเป็นเจ้าของเอกสารนี้อยู่แล้ว";
					close;
				}
				mes "^660066ฉันสามารถกลั่นแก่นแท้แห่งฟอสซิล (essence of fossil) จากหินเก่าแก่บนชั้น 2 ของดันเจี้ยนเหมือง จนถึงตอนนี้ สถานที่แห่งนั้นมีความมั่นคงที่สุดในบรรดาสนามทดสอบทั้งหมด";
				mes "หากคุณต้องการลอง ให้เตรียม Jubilee 10 อัน และไปที่ดันเจี้ยน ขอให้โชคดี^000000";
				next;
				mes "คุณได้ค้นพบ ^990099วิธีการสร้างแก่นแท้แห่งฟอสซิล^000000 โปรดตรวจสอบหน้าต่างเควสต์ของคุณสำหรับรายละเอียดเพิ่มเติม";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12221;
				end;
			}
			mes "ไม่มีอะไรในเอกสารนี้ที่ดูมีประโยชน์เลย";
			close;
		}
		if (.@r == 5) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12222) == 1) {
					mes "คุณเป็นเจ้าของเอกสารนี้อยู่แล้ว";
					close;
				}
				mes "^660066ในการกลั่นแก่นแท้แห่งพายุ (essence of storm) ฉันได้ไปเยือน Hermit's Checkerboard ใน Kunlun ที่ซึ่งวิญญาณแห่งสายลมสถิตอยู่";
				mes "กระบวนการกลั่นนั้นง่ายมาก แต่ฉันลำบากมากในการหาตัวเร่งปฏิกิริยา ซึ่งก็คือ Dragon Tooth^000000";
				next;
				mes "คุณได้ค้นพบ ^990099วิธีการสร้างแก่นแท้แห่งพายุ^000000 โปรดตรวจสอบหน้าต่างเควสต์ของคุณสำหรับรายละเอียดเพิ่มเติม";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12222;
				end;
			}
			mes "ไม่มีอะไรในเอกสารนี้ที่ดูมีประโยชน์เลย";
			close;
		}
		if (.@r == 10) {
			mes "คุณรู้สึกไม่สบายขึ้นมาทันที และอาการคลื่นไส้นี้จะไม่หายไปง่ายๆ คุณควรลองกลับมาหาเอกสารที่คุณต้องการในภายหลัง";
			setquest 12165;
			close;
		}
		mes "ไม่มีอะไรที่นี่ที่ดูพิเศษเลย";
		close;
	}
	mes "คุณไม่จำเป็นต้องสำรวจที่นี่อีกต่อไปแล้ว";
	close;
}

function	script	Magic_Book_Essence	{
	if (MaxWeight - Weight < 1000) {
		mes "กรุณาลดน้ำหนักในกระเป๋าของคุณลง";
		close;
	}
	.@item_req = getarg(1);
	.@req_amount = getarg(2);
	if (checkquest(getarg(0)) == 1 && countitem(.@item_req) >= .@req_amount) {
		specialeffect2 EF_BLIND;
		specialeffect2 EF_BEGINSPELL;
		progressbar "ffff00",4;
		delitem .@item_req,.@req_amount;
		if (rand(2)) {
			mes "คุณได้ใช้ "+ callfunc("F_InsertPlural",.@req_amount,getitemname(.@item_req)) +" เพื่อสร้าง ^999900แก่นแท้แห่ง "+ getarg(3) +"^000000";
			next;
			mes "^008800แก่นแท้แห่ง "+getarg(3)+" จะคงอยู่ถาวร เว้นแต่จะมีปัญหาเกิดขึ้น^000000";
			completequest getarg(0);
			specialeffect2 EF_LORD;
			close;
		}
		mes "^008888คุณล้มเหลวในการกลั่นแก่นแท้โดยใช้ "+ callfunc("F_InsertPlural",.@req_amount,getitemname(.@item_req)) +".^000000";
		close;
	}
	if (checkquest(getarg(0)) == 2) {
		mes "คุณได้สร้างแก่นแท้แห่ง "+getarg(3)+" ในบริเวณนี้เรียบร้อยแล้ว";
		close;
	}
	mes "มีคนอื่นใช้บริเวณนี้ไปแล้ว";
	close;
}
thor_v02,163,104,0	script	Powerful Lava Energy#33	CLEAR_NPC,{ callfunc "Magic_Book_Essence",12218,7097,40,"Lava"; end; }
thor_v01,185,91,0	script	Powerful Flame Energy#33	CLEAR_NPC,{ callfunc "Magic_Book_Essence",12219,7098,10,"Flame"; end; }
ice_dun03,137,148,0	script	Powerful Glacier Energy	CLEAR_NPC,{ callfunc "Magic_Book_Essence",12220,7561,10,"Glacier"; end; }
ein_dun02,181,124,0	script	Powerful Fossil Energy	CLEAR_NPC,{ callfunc "Magic_Book_Essence",12221,7312,10,"Fossil"; end; }
gon_dun02,252,198,0	script	Powerful Storm Energy#33	CLEAR_NPC,{ callfunc "Magic_Book_Essence",12222,7266,10,"Storm"; end; }
