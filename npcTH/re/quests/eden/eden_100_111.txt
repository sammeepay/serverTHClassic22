//===== rAthena Script =======================================
//= Eden Group Quests - Mission [100-111]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 100 - 111.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,37,95,5	script	Gelkah#1	4_F_GELKA,{
	cutin "gelca01",2;
	if (checkweight(1201,1) == 0) {
		mes "^FF0000- ข้อความแจ้งเตือน -";
		mes "- เดี๋ยวก่อน!! -";
		mes "- ไอเทมในตัวคุณเยอะเกินไป -";
		mes "- ไม่สามารถรับไอเทมเพิ่มได้ -";
		mes "- กรุณาลดน้ำหนักสัมภาระลง -";
		mes "- แล้วค่อยลองใหม่อีกครั้ง -^000000";
		close2;
		cutin "",255;
		end;
	}
	mes "[Gelkah]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "ช่วยแสดงหลักฐานให้ฉันดูหน่อยได้ไหมคะว่าคุณเป็นสมาชิก Eden Group ไม่อย่างนั้นฉันคงคุยกับคุณไม่ได้";
		mes "ฉันจะรับคำขอเฉพาะจากนักผจญภัยเลเวล ^FF0000100 - 110^000000 เท่านั้นค่ะ";
	}
	else if (BaseLevel < 100) {
		mes "คุณยังดูไม่เหมือนนักผจญภัยที่แก่ประสบการณ์พอเลยนะคะ อย่างน้อยๆ คุณควรจะมีเลเวล 100 หรือเปลี่ยนเป็นคลาส 3 ก่อนค่ะ";
		next;
		mes "[Gelkah]";
		mes "เพราะคำขอที่ฉันได้รับมานั้น มาจาก ^FF0000ดินแดนอื่น^000000 ที่นอกเหนือจากทวีป Midgard ค่ะ";
	}
	else if (BaseLevel < 111) {
		if (para_100lv01 == 0) {
			mes "เหล่านักผจญภัยเอ๋ย!";
			emotion ET_SMILE;
			next;
			cutin "gelca03",2;
			mes "[Gelkah]";
			mes "เมื่อเร็วๆ นี้ฉันได้รับคำขอมากมายจากลูกค้าที่ต้องการความช่วยเหลือจากนักผจญภัยที่มีฝีมือเช่นคุณค่ะ";
			next;
			if (select( "ฉันชอบงานแบบนี้แหละ!", "เควสต์น่าเบื่อ... ไม่เอาด้วยหรอก" ) == 2) {
				mes "[Gelkah]";
				mes "คุณก็น่าจะรู้่นะคะว่างานที่ยากขึ้น ย่อมมาพร้อมกับผลตอบแทนที่คุ้มค่ากว่า?";
				next;
			}
			mes "[Gelkah]";
			mes "อันที่จริง ฉันจะรับทุกคนที่เดินผ่านมาเลยก็ไม่ได้หรอกนะ เพราะอาจารย์ของฉันเขาเกลียดพวกคนที่ตัดสินใจไม่เด็ดขาดที่สุดเลย";
			next;
			if (select( "ใครคืออาจารย์ของคุณ?", "ฉันไม่ใช่คนโลเลนะ!" ) == 1) {
				cutin "gelca03",2;
				mes "[Gelkah]";
				mes "คนที่ยืนอยู่ข้างๆ ฉันนี่แหละค่ะคืออาจารย์ของฉัน... เขาเพิ่งกลับมาจากการเดินทางฝึกฝนอันยาวนานน่ะ";
				next;
				mes "[Gelkah]";
				mes "ฉันเข้าใจนะว่าคุณอาจจะสงสัย... ว่าทำไม Sura ถึงอยากได้ Archbishop มาเป็นอาจารย์สอนศิลปะการต่อสู้...";
			}
			else {
				cutin "gelca03",2;
				mes "[Gelkah]";
				if (BaseLevel < 110)
					mes "คุณคงคิดว่าตัวเองเป็นมือโปรแล้วเพียงเพราะได้เปลี่ยนเป็นคลาส 3 สินะ...";
				else
					mes "ฉันรู้ว่าคุณไม่ใช่พวกมือใหม่อะไรนั่นหรอก แต่อาจารย์ของฉันเขามองว่าคุณก็ไม่ได้ต่างอะไรกับพวกเด็กๆ เลย แต่ในมุมมองของฉัน คุณเป็นนักผจญภัยที่ยอดเยี่ยมมากนะคะ!";
			}
			next;
			mes "[Gelkah]";
			mes "เราต้องการนักผจญภัยที่เต็มใจจะช่วยเหลือจริงๆ คุณพอจะช่วยเราได้ไหมคะ?";
			next;
			cutin "gelca01",2;
			if (select( "ตกลง ฉันจะช่วย", "ฉันไม่สนใจจะช่วยพวกคุณหรอก" ) == 2) {
				mes "[Gelkah]";
				mes "แหม... ยังไม่ลองทำดูเลย จะรู้ได้ยังไงล่ะคะว่ามันเป็นยังไง";
			}
			else {
				mes "[Gelkah]";
				mes "นี่แหละคือจิตวิญญาณของนักผจญภัยตัวจริง!";
				next;
				mes "[Gelkah]";
				mes "ฉันขอดูหน่อยสิว่าคุณมีความสามารถพอจะรับงานจริงได้หรือเปล่า... อืม...";
				next;
				mes "[Gelkah]";
				mes "ไปรวบรวม ^ff0000Brigan มาให้ฉัน 50 อัน^000000 นี่เป็นบททดสอบที่ดูเหมือนจะง่ายแต่ก็ไม่ง่ายซะทีเดียวหรอกนะคะ";
				next;
				mes "[Gelkah]";
				mes "คุณสามารถหามันได้ง่ายๆ จากมอนสเตอร์ในเกาะเต่า (Turtle Island) หรือที่ Glastheim ค่ะ";
				next;
				mes "[Gelkah]";
				mes "เมื่อคุณผ่านบททดสอบเบื้องต้นนี้แล้ว เราค่อยมาเริ่มงานจริงๆ กันเถอะค่ะ";
				setquest 13002; // เควสต์รวบรวม Brigan
				para_100lv01 = 1;
				para_100lv02 += 1;
			}
		}
		else if (para_100lv01 == 1) {
			if (countitem(7054) > 49) { // ตรวจสอบว่ามี Brigan ครบ 50 อันหรือไม่
				mes "คุณนำ Brigan ทั้งหมดที่ฉันขอมาให้แล้วใช่ไหมคะ?";
				next;
				mes "[Gelkah]";
				mes "ฉันไม่จำเป็นต้องนับทีละชิ้นหรอกค่ะ เพราะบททดสอบนี้มีไว้เพื่อคัดเลือกนักผจญภัยที่คู่ควรที่สุดเท่านั้น";
				next;
				mes "[Gelkah]";
				mes "อย่าเพิ่งหงุดหงิดไปเลยนะคะ ที่อาจารย์ต้องเข้มงวดก็เพราะเขาต้องเป็นคนรับผิดชอบหากนักผจญภัยได้รับบาดเจ็บระหว่างทำภารกิจ";
				next;
				mes "[Gelkah]";
				mes "อาจารย์ตัดสินใจว่าความปลอดภัยของนักผจญภัยเป็นสิ่งสำคัญที่สุด เพราะคำขอใหม่ๆ ในช่วงนี้ค่อนข้างอันตรายมากทีเดียว";
				next;
				cutin "rote01",2;
				mes "[Rohtert]";
				mes "หืมมม...";
				emotion ET_HNG, getnpcid(0, "Rohtert#12"); // อาจารย์ส่งเสียงฮึดฮัดในลำคอ
				next;
				cutin "gelca01",2;
				mes "[Gelkah]";
				mes "หวังว่าคุณคงไม่ได้แอบดูตอนฉันประเมินนะ... เอาล่ะ การประเมินความสามารถสำหรับภารกิจระดับมาตรฐานเกือบจะเสร็จสมบูรณ์แล้ว";
				next;
				mes "[Gelkah]";
				mes "ไม่มีเวลามานั่งทำ ^ff0000เข็มกลัด Eden Group ขั้นสูง^000000 ให้หรอกนะคะ";
				next;
				mes "[Gelkah]";
				mes "^ff0000เวลาเป็นเงินเป็นทองนะเจ้าหนู!^000000";
				mes "แหม... ฉันพูดแบบนี้แล้วดูเหมือนเป็นอาจารย์จริงๆ บ้างไหมคะ?";
				next;
				mes "[Gelkah]";
				mes "คุณ " + strcharinfo(0) + " ฉันจะจัดเตรียมรายการภารกิจไว้ให้ ระหว่างนี้รบกวนไปคุยกับอาจารย์ของฉันและแสดงความเคารพเขาหน่อยนะ";
				delitem 7054,50; // ลบ Brigan 50 อัน
				completequest 13002; // เสร็จสิ้นเควสต์รวบรวม
				para_100lv01 = 2; // เลื่อนระดับสถานะเควสต์เพื่อไปคุยกับ Rohtert ต่อ
				getitem 6549,1; // รับ Courtesy_Ticket (บัตรสมนาคุณ)
			}
			else {
				// กรณีที่ Brigan ยังไม่ครบ 50 อัน
				cutin "gelca02",2;
				mes "ยังหา Brigan ไม่ครบ 50 อันอีกเหรอคะ? อาจารย์บอกว่ามันเป็นงานที่ง่ายแสนง่ายเลยนะนั่น...";
				next;
				mes "[Gelkah]";
				mes "ช่วยรีบหน่อยนะคะ! เราต้องการนักผจญภัยตัวจริงมาช่วยจัดการภารกิจที่ค้างอยู่ค่ะ";
			}
		}
		else if (para_100lv01 == 2)
			mes "รอสักครู่นะคะ ฉันกำลังจัดรายการภารกิจอยู่ ไม่แน่ใจว่าจะเริ่มให้งานไหนกับคุณดี... แหม ฉันนี่ช่างเป็นคนดีจริงๆ เลยนะเนี่ย";
		else if (para_100lv01 > 2) {
			mes "รายการภารกิจพร้อมแล้วค่ะ คุณอยากจะเริ่มเลยไหม?";
			next;
			switch( select( "ภารกิจล่ามอนสเตอร์", "สอบถามข้อมูลเพิ่มเติม", "แสดงบัตรสมนาคุณ" ) ) {
				mes "[Gelkah]";
			case 1:
				mes "คุณไม่จำเป็นต้องรับงานทั้งหมดพร้อมกันหรอกนะคะ เลือกพื้นที่ๆ เหมาะกับทักษะของคุณ เพื่อที่คุณจะได้สนุกกับการเดินทางไปพร้อมๆ กับการทำงานค่ะ";
				next;
				switch( select( "พื้นที่ Glastheim", "พื้นที่ Ash Vacuum (New World)", "พื้นที่ Ice Dungeon", "พื้นที่ Magma Dungeon", "พื้นที่อื่นๆ" ) ) {
					mes "[Gelkah]";
				case 1:
					mes "นี่คือรายการภารกิจในพื้นที่ Glastheim ค่ะ";
					next;
					switch( select( "ล่า Carat", "ล่า Arclouse", "ล่า Anolian", "ล่า Sting", "ล่า Majoruros" ) ) {
						mes "[Gelkah]";
					case 1:
						if (checkquest(13004,PLAYTIME) == 0)
							mes "คุณเพิ่งจะทำงานเสร็จไปเองนะ การพักผ่อนก็สำคัญพอๆ กับการทำงานค่ะ ไว้ค่อยกลับมาใหม่นะ งานไม่หนีไปไหนหรอก";
						else if (checkquest(13003,HUNTING) == 1) {
							mes "ภารกิจนี้คือการกำจัด ^0000FF30^000000 ^FF0000Carat^000000 ในปราสาท Glastheim ชั้น 1 ค่ะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าจัดการเรียบร้อยแล้ว อย่าลืมกลับมาบอกฉันด้วยนะคะ";
						}
						else if (checkquest(13003,HUNTING) == 2) {
							mes "การไปเยือนปราสาทเก่าแก่ที่น่ากลัวแบบนั้นคงลำบากแย่เลย ฉันหวังว่าสักวันมันจะเป็นประสบการณ์ที่ดีสำหรับฉันเหมือนกันนะคะ";
							callsub S_Quest,13003,13004,106000,148000;
						}
						else {
							mes "คำขอนี้มาจากพ่อค้าใน Morocc ที่ดูเหมือนจะเสียผลประโยชน์จากมอนสเตอร์พวกนี้ใน Glastheim ค่ะ";
							next;
							mes "[Gelkah]";
							mes "ทาง Eden Group รู้ดีว่าเหตุผลมันดูไม่ค่อยเข้าท่าเท่าไหร่ แต่เมื่อเป็นคำขอก็คือคำขอค่ะ ช่วยไปกำจัด ^FF0000Carat 30 ตัว^000000 ในปราสาท Glastheim ให้ทีนะ";
							callsub S_Quest,13004,13003;
						}
						break;
					case 2:
						if (checkquest(13006,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไป พักผ่อนก่อนเถอะค่ะ มีงานรอคุณอยู่เสมอแหละ";
						else if (checkquest(13005,HUNTING) == 1) {
							mes "ช่วยกำจัด ^FF0000Arclouse 22 ตัว^000000 ให้ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเสร็จแล้วกลับมาแจ้งฉันด้วยนะ";
						}
						else if (checkquest(13005,HUNTING) == 2) {
							mes "ทำได้เยี่ยมมากค่ะ!";
							emotion ET_BEST;
							callsub S_Quest,13005,13006,106000,148000;
						}
						else {
							mes "คุณรู้ไหมว่าใน Midgard เรามีถั่วที่ใช้ทำอาหารด้วยนะ? เวลาเจ้า Arclouse มันม้วนตัวน่ะ ดูเหมือนเม็ดถั่วไม่มีผิดเลย";
							next;
							mes "[Gelkah]";
							mes "สำนักงานใหญ่ของ Eden Group เลยอยากจะตรวจสอบดูว่าเจ้าตัวนี้พอจะเป็นมาสคอตได้ไหม";
							next;
							mes "[Gelkah]";
							mes "ช่วยไปกำจัด ^FF0000Arclouse 22 ตัว^000000 ให้ทีนะคะ";
							if (callsub( S_Quest,13006,13005 ) == 0) {
								mes "[Gelkah]";
								mes "ว้า... แย่จัง ทั้งที่เป็นคำขอที่น่าสนใจแท้ๆ";
							}
						}
						break;
					case 3:
						if (checkquest(13008,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไปเอง การผจญภัยน่ะสำคัญ แต่การพักผ่อนก็สำคัญไม่แพ้กันค่ะ";
						else if (checkquest(13007,HUNTING) == 1) {
							mes "มีนักบวชท่านหนึ่งกำลังจาริกแสวงบุญ และต้องการความคุ้มครองจากพวกสัตว์เลื้อยคลานที่หน้าตาเหมือนจระเข้น่ะค่ะ";
							next;
							mes "[Gelkah]";
							mes "สรุปก็คือ ภารกิจนี้ให้ไปกำจัด Anolian ในทางน้ำใต้ดิน เพื่อเปิดทางให้คณะแสวงบุญเดินทางได้อย่างปลอดภัยค่ะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเรียบร้อยแล้วกลับมาบอกฉันด้วยนะคะ";
						}
						else if (checkquest(13007,HUNTING) == 2) {
							mes "คุณจัดการกับงานนี้ได้ดีมากเลยค่ะ";
							callsub S_Quest,13007,13008,112000,186500;
						}
						else {
							mes "Glastheim ดูเหมือนจะเป็นสถานที่ศักดิ์สิทธิ์ไม่ใช่แค่กับนักผจญภัย แต่รวมถึงเหล่านักบวชด้วย ฉันก็ไม่รู้เหมือนกันว่าอะไรดึงดูดพวกเขาไปที่นั่นนักนะ";
							next;
							mes "[Gelkah]";
							mes "การแสวงบุญจากอารามไปยังทางน้ำใต้ดินคือเส้นทางนี้ล่ะค่ะ ภารกิจคือการกำจัด ^FF0000Anolian 30 ตัว^000000 ในท่อระบายน้ำ Glastheim เพื่อความปลอดภัยค่ะ";
							callsub S_Quest,13008,13007;
						}
						break;
					case 4:
						if (checkquest(13010,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไปนะ พักผ่อนบ้างก็ดีค่ะ งานยังมีมาเรื่อยๆ แน่นอน";
						else if (checkquest(13009,HUNTING) == 1) {
							mes "มีพวก Sting อยู่เต็มท่อระบายน้ำและถ้ำใต้ดินเลยค่ะ คงจะหากำจัดได้ไม่ยากนักหรอกนะ";
							next;
							mes "[Gelkah]";
							mes "ภารกิจคือกำจัด ^FF0000Sting 30 ตัว^000000 ในทางน้ำและถ้ำใต้ดิน เพื่อทางเดินที่ปลอดภัยของเหล่านักบวชค่ะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าจัดการเสร็จแล้วรบกวนบอกฉันด้วยนะคะ";
						}
						else if (checkquest(13009,HUNTING) == 2) {
							mes "การต้องลงไปในถ้ำใต้ดินที่ทั้งสกปรกและเหม็นแบบนั้นคงลำบากแย่เลยนะ แต่มันจะเป็นประสบการณ์ที่ดีของคุณแน่นอนค่ะ";
							callsub S_Quest,13009,13010,112000,186500;
						}
						else {
							mes "เคยได้ยินเรื่องการแสวงบุญของนักบวชไหมคะ? มีเจ้า Sting อยู่เยอะมากในทางน้ำใต้ดินจนพวกมันรบกวนความสงบของเหล่านักบวชน่ะค่ะ";
							next;
							mes "[Gelkah]";
							mes "อาจารย์รับงานนี้เพราะเขาเคารพพวกนักบวชเหล่านั้นค่ะ ภารกิจคือกำจัด ^FF0000Sting 30 ตัว^000000 ในทางน้ำใต้ดินนะคะ";
							callsub S_Quest,13010,13009;
						}
						break;
					case 5:
						if (checkquest(13012,PLAYTIME) == 0)
							mes "คุณเพิ่งเสร็จภารกิจไปค่ะ พักผ่อนเถอะ การแสวงบุญยังคงมีต่อไปเรื่อยๆ เดี๋ยวก็มีงานใหม่มาเองค่ะ";
						else if (checkquest(13011,HUNTING) == 1) {
							mes "ฉันสงสัยจริงๆ ว่าเหตุผลเบื้องหลังการแสวงบุญพวกนี้คืออะไร ทำไมพวกเขาถึงทำกันไม่หยุดเลยนะ... ต้องมีอะไรซ่อนอยู่แน่ๆ";
							next;
							mes "[Gelkah]";
							mes "ปกติฉันจะเป็นคนดูแลงานนี้เองแต่ฉันค่อนข้างกังวลน่ะค่ะ ภารกิจคือกำจัด ^FF0000Majoruros 30 ตัว^000000 ในถ้ำใต้ดินชั้นที่ 2 ค่ะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าจัดการเสร็จแล้ว กลับมาแจ้งฉันด้วยนะคะ";
						}
						else if (checkquest(13011,HUNTING) == 2) {
							mes "การเดินทางแสวงบุญที่ยาวนานแบบนั้นคงเหนื่อยน่าดูเลยนะ";
							mes "แต่ก็นับว่าเป็นประสบการณ์ที่ดีสำหรับคุณค่ะ";
							callsub S_Quest,13011,13012,112000,252000;
						}
						else {
							mes "จุดหมายของการแสวงบุญอยู่ที่ชั้น 2 ของถ้ำใต้ดิน Glastheim ค่ะ มันไม่ใช่การเดินทางที่ง่ายเลย แต่ก็น่าลองดูนะคะ";
							next;
							mes "[Gelkah]";
							mes "Majoruros ล่าไม่ง่ายเลย ระวังตัวด้วยนะ ภารกิจคือกำจัด ^FF0000Majoruros 30 ตัว^000000 ในถ้ำใต้ดินชั้น 2 ค่ะ";
							callsub S_Quest,13012,13011;
						}
					}
					break;
				case 2:
					mes "มีคำขอส่งมาจากคณะสำรวจ Midgard (Midgard Expedition) เยอะเลยค่ะ";
					next;
					if (select( "ภารกิจล่ามอนสเตอร์","แผลใจในอดีต" ) == 1) {
						mes "[Gelkah]";
						if (checkquest(13014,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จ พักผ่อนก่อนเถอะค่ะ งานใหม่รอคุณอยู่เสมอ";
						else if (checkquest(13013,HUNTING) == 1) {
							mes "พื้นที่ใน New World น่ะช่างลึกลับ... มอนสเตอร์ที่นั่นก็ดูแปลกตาดีนะคะ";
							next;
							mes "[Gelkah]";
							mes "นี่เป็นคำขอที่ดูพิเศษหน่อย ช่วยไปกำจัด ^FF0000Pinguicula 30 ตัว^000000 ให้ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "เสร็จแล้วมาบอกฉันด้วยนะ";
						}
						else if (checkquest(13013,HUNTING) == 2) {
							mes "การเดินทางไป Ash Vacuum คงลำบากน่าดูเลย";
							mes "แต่จะเป็นประสบการณ์ที่ดีแน่นอนค่ะ";
							callsub S_Quest,13013,13014,106000,148000;
						}
						else {
							mes "Pinguicula เป็นมอนสเตอร์ตัวแรกๆ ที่นักผจญภัยจะได้เจอเมื่อย่างก้าวเข้าสู่ Ash Vacuum ค่ะ พวกเขาอยากให้เราช่วยลดจำนวนมอนสเตอร์รอบๆ ค่ายสำรวจน่ะ";
							next;
							mes "[Gelkah]";
							mes "เป็นเรื่องธรรมดาที่จะเกิดอุบัติเหตุได้ง่ายกับมอนสเตอร์ที่เราไม่คุ้นเคย ภารกิจคือกำจัด ^FF0000Pinguicula 30 ตัว^000000 ค่ะ";
							callsub S_Quest,13014,13013;
						}
					}
					else {
						mes "[Gelkah]";
						if (checkquest(13016,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไป พักผ่อนเถอะค่ะ งานนี้ไม่คุ้มที่จะหักโหมขนาดนั้นหรอก";
						else if (checkquest(13015,HUNTING) == 1) {
							mes "นี่เป็นคำขอที่ประหลาดและเหตุผลก็พิลึกมาก... แต่เราก็ควรจะรับไว้นะคะ";
							next;
							mes "[Gelkah]";
							mes "ช่วยกำจัด ^FF0000Luciola Vespa 30 ตัว^000000 ให้ทีค่ะ";
							next;
							mes "[Gelkah]";
							mes "เสร็จแล้วมาบอกฉันด้วยนะ";
						}
						else if (checkquest(13015,HUNTING) == 2) {
							mes "การต้องทำภารกิจแปลกๆ แบบนี้คงลำบากใจแย่เลยนะคะ";
							callsub S_Quest,13015,13016,112000,186500;
						}
						else {
							mes "นี่เป็นคำขอส่วนตัวมากๆ เลยล่ะค่ะ แถมยังมาจากนักผจญภัยที่เป็นเพื่อนกับอาจารย์ Rohtert ด้วย... คนที่ชื่อ Pretty Boy น่ะค่ะ ทำไมเขาถึงเขียนมาแบบนี้กันนะ?";
							next;
							mes "[Gelkah]";
							mes "เหตุผลมันส่วนตัวสุดๆ คือเขาเคยโดนผึ้งต่อยตอนเด็กๆ จนกลายเป็นแผลใจ (Trauma) ทำให้เขาเดินทางไป Ash Vacuum ไม่ได้น่ะค่ะ";
							next;
							mes "Gelkah จ้องมองไปที่ Rohtert ด้วยสายตาแปลกๆ";
							next;
							mes "[Gelkah]";
							mes "เอาเถอะ ถือซะว่าเป็นภารกิจเพิ่มความปลอดภัยให้คนเดินทางไป New World ละกันนะคะ ช่วยกำจัด ^FF0000Luciola Vespa 30 ตัว^000000 ให้ทีค่ะ";
							callsub S_Quest,13016,13015;
						}
					}
					break;
				case 3:
					mes "มีภารกิจแถวๆ ถ้ำน้ำแข็ง (Ice Dungeon) ใน Rachel เยอะเลยล่ะค่ะ";
					next;
					switch( select( "ล่า Desert Wolf", "ล่า Snowier", "ล่า Ice Titan..." ) ) {
						mes "[Gelkah]";
					case 1:
						if (checkquest(13018,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไป การผจญภัยน่ะสำคัญ แต่การพักผ่อนก็สำคัญไม่แพ้กันค่ะ";
						else if (checkquest(13017,HUNTING) == 1) {
							mes "คุณเคยอ่านหนังสือเก่าๆ ไหม? ที่เกี่ยวกับนักผจญภัยที่สู้กับหมาป่ายักษ์จนสุดท้ายกลายเป็นเพื่อนกันน่ะ เรื่อง 'Samba's with Wolves' อะไรประมาณนั้น";
							next;
							mes "[Gelkah]";
							mes "แต่นิยายก็คือนิยายล่ะค่ะ ความจริงคือช่วยไปกำจัด ^FF0000Desert Wolf 30 ตัว^000000 แถวๆ ทางเข้า Ice Dungeon ใน Rachel ให้ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "เสร็จแล้วกลับมาบอกฉันด้วยล่ะ";
						}
						else if (checkquest(13017,HUNTING) == 2) {
							mes "เจ้าพวกนั้นก็น่ารักดีนะคะว่าไหม?";
							mes "แต่นับว่าเป็นประสบการณ์ที่ดีสำหรับคุณค่ะ";
							callsub S_Quest,13017,13018,106000,148000;
						}
						else {
							mes "ถ้ำน้ำแข็งอยู่ทางเหนือของเมือง Rachel ค่ะ ปัญหาก็คือพวกหมาป่ามักจะทำให้นักท่องเที่ยวตกใจกลัวเวลาจะไปเที่ยวชมถ้ำน่ะค่ะ";
							next;
							mes "[Gelkah]";
							mes "คงจะตลกดีนะคะถ้าเราเต้นรำกับหมาป่าได้จริงๆ! ช่วยไปกำจัด ^FF0000Desert Wolf 30 ตัว^000000 แถวถ้ำน้ำแข็งทีนะคะ";
							callsub S_Quest,13018,13017;
						}
						break;
					case 2:
						if (checkquest(13020,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จ พักผ่อนก่อนเถอะค่ะ";
						else if (checkquest(13019,HUNTING) == 1) {
							mes "ลมหายใจที่รุนแรง แววตาที่ไม่เป็นมิตร ท่ามกลางความหนาวเหน็บ... งานนี้ไม่ง่ายเลย ทำร่างกายให้อบอุ่นเข้าไว้นะคะ";
							next;
							mes "[Gelkah]";
							mes "ภารกิจคือกำจัด ^FF0000Snowier 30 ตัว^000000 บนชั้น 2 ของ Ice Dungeon ค่ะ";
							next;
							mes "[Gelkah]";
							mes "เรียบร้อยแล้วกลับมาแจ้งฉันด้วยนะคะ";
						}
						else if (checkquest(13019,HUNTING) == 2) {
							mes "การไปในถ้ำที่หนาวเหน็บแบบนั้นคงลำบากแย่เลยนะ";
							mes "แต่ก็นับว่าเป็นประสบการณ์ที่ดีสำหรับคุณค่ะ";
							callsub S_Quest,13019,13020,106000,148000;
						}
						else {
							mes "คนที่เคยไปถ้ำน้ำแข็งบอกว่ามีมอนสเตอร์ส่งเสียงหนวกหูรบกวนเวลาพวกเขาไปเที่ยวชมที่นั่นน่ะค่ะ ถ้าคุณทนเสียงพวกนั้นไหวก็ช่วยจัดการให้ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "ภารกิจคือกำจัด ^FF0000Snowier 30 ตัว^000000 บนชั้น 2 ของ Ice Dungeon ค่ะ";
							callsub S_Quest,13020,13019;
						}
						break;
					case 3:
						if (checkquest(13022,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไป พักผ่อนบ้างนะคะ";
						else if (checkquest(13021,HUNTING) == 1) {
							mes "ช่วยกำจัด ^FF0000Ice Titan 30 ตัว^000000 ในถ้ำน้ำแข็งให้ทีค่ะ";
							next;
							mes "[Gelkah]";
							mes "เสร็จแล้วมาบอกฉันด้วยนะ";
						}
						else if (checkquest(13021,HUNTING) == 2) {
							mes "การไปในถ้ำที่ทั้งหนาวทั้งมืดแบบนั้นคงลำบากน่าดูเลย";
							mes "แต่มันจะเป็นประสบการณ์ที่ดีของคุณแน่นอนค่ะ";
							callsub S_Quest,13021,13022,112000,186500;
						}
						else {
							mes "คุณเคยไปถ้ำน้ำแข็งทางเหนือของ Rachel หรือยังคะ? ลึกเข้าไปข้างในจะมีมอนสเตอร์ที่เรียกว่า Ice Titan อยู่ เหล่านักวิชาการอยากรู้ว่าพวกมันคือ Golem หรือเปล่าน่ะค่ะ";
							next;
							mes "[Gelkah]";
							mes "คำขอคือช่วยลดจำนวนพวกมันลงหน่อยเพื่อความปลอดภัยของเหล่านักวิชาการค่ะ ช่วยกำจัด ^FF0000Ice Titan 30 ตัว^000000 ในถ้ำน้ำแข็งนะคะ";
							callsub S_Quest,13022,13021;
						}
					}
					break;
				case 4:
					mes "มีคำขอให้ล่ามอนสเตอร์ใน Magma Dungeon ส่งมาจาก 'นักเดินทางสาวแสนสวย' (Pretty girl wanderer) ที่ Comodo ค่ะ";
					next;
					if (select( "ล่า Nightmare Terror","ล่า Deleter" ) == 1) {
						mes "[Gelkah]";
						if (checkquest(13024,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จ ขอบคุณที่ช่วยรักษาความทรงจำดีๆ ให้เธอนะคะ แต่ตอนนี้คุณควรพักผ่อนก่อนค่ะ";
						else if (checkquest(13023,HUNTING) == 1) {
							mes "ความทรงจำของเด็กสาวน่ะสำคัญมากนะคะ! ช่วยกำจัด ^FF0000Nightmare Terror 30 ตัว^000000 ทีค่ะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเรียบร้อยแล้วกลับมาบอกฉันด้วยนะ";
						}
						else if (checkquest(13023,HUNTING) == 2) {
							mes "จงนึกถึงความทรงจำของผู้อื่นเอาไว้... โลกนี้ยังมีความสวยงามซ่อนอยู่เสมอค่ะ นับว่าเป็นประสบการณ์ที่ดีสำหรับคุณนะ";
							callsub S_Quest,13023,13024,106000,148000;
						}
						else {
							mes "คำขอนี้มาจากลูกค้าที่กำลังเศร้าค่ะ เพราะใน Magma Dungeon ดันเต็มไปด้วยมอนสเตอร์ในตอนที่เธอกลับไปที่นั่นเพื่อรำลึกความหลังหลังจากเลิกกับแฟน เห็นว่าเธอเคยใช้เวลาร่วมกับเขาที่นั่นเยอะมากและอยากได้ความสงบน่ะค่ะ";
							next;
							mes "[Gelkah]";
							mes "ความทรงจำของเด็กสาวน่ะสำคัญนะคะ! ช่วยกำจัด ^FF0000Nightmare Terror 30 ตัว^000000 ให้เธอทีค่ะ";
							callsub S_Quest,13024,13023;
						}
					}
					else {
						mes "[Gelkah]";
						if (checkquest(13026,PLAYTIME) == 0)
							mes "คุณเพิ่งทำงานเสร็จไป ร่างกายหายร้อนหรือยังคะ? พักผ่อนให้เพียงพอนะ";
						else if (checkquest(13025,HUNTING) == 1) {
							mes "ลูกค้าท่านหนึ่งเคยเต้นรำอย่างสวยงามให้คนรักดู แต่แล้ววันหนึ่งพวกเขาก็ถูก Deleter จู่โจมระหว่างที่กำลังเต้นรำอยู่ใน Magma Dungeon ค่ะ";
							next;
							mes "[Gelkah]";
							mes "การโจมตีนั้นทำให้การเต้นรำพังพินาศ แถมความสัมพันธ์ยังจบลงด้วย เพราะฝ่ายชายดันไปโทษฝ่ายหญิงที่ทำให้พวกเขาโดนโจมตีซะงั้น!";
							next;
							mes "[Gelkah]";
							mes "ตอนนี้เธอเลยพาลไปโกรธพวก Deleter แทนและส่งคำขอนี้มาค่ะ ช่วยกำจัด ^FF0000Flying Deleter 30 ตัว^000000 ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเรียบร้อยแล้วบอกฉันด้วยนะ";
						}
						else if (checkquest(13025,HUNTING) == 2) {
							mes "Eden Group ปกป้องทั้งความรัก ความยุติธรรม และ Midgard! ไชโย!";
							mes "หวังว่าจะเป็นความทรงจำที่ดีของคุณนะคะ";
							callsub S_Quest,13025,13026,106000,148000;
						}
						else {
							mes "ลูกค้าท่านหนึ่งเคยเต้นรำอย่างสวยงามให้คนรักดู แต่แล้ววันหนึ่งพวกเขาก็ถูก Deleter จู่โจมระหว่างที่กำลังเต้นรำอยู่ใน Magma Dungeon ค่ะ";
							next;
							mes "[Gelkah]";
							mes "การโจมตีนั้นทำให้การเต้นรำพังพินาศ แถมความสัมพันธ์ยังจบลงด้วย เพราะฝ่ายชายดันไปโทษฝ่ายหญิงที่ทำให้พวกเขาโดนโจมตี...";
							next;
							mes "[Gelkah]";
							mes "ตอนนี้เธอเลยพาลไปโกรธพวก Deleter แทนและส่งคำขอนี้มาค่ะ ช่วยกำจัด ^FF0000Flying Deleter 30 ตัว^000000 ทีนะคะ";
							callsub S_Quest,13026,13025;
						}
					}
					break;
				case 5:
					mes "นี่คือรายการภารกิจอื่นๆ จากทั่วทุกมุมใน Midgard ค่ะ";
					next;
					switch( select( "ล่า Loli Ruri - Niflheim", "ล่า Medusa - Comodo", "ล่า Anubis - Sphinx" ) ) {
						mes "[Gelkah]";
					case 1:
						if (checkquest(13041,PLAYTIME) == 0)
							mes "ฉันว่าคุณควรพักผ่อนสักหน่อยนะ แม้แต่นักผจญภัยก็ต้องการเวลาพักใช่ไหมล่ะคะ?";
						else if (checkquest(13040,HUNTING) == 1) {
							mes "Eden Group กำลังวางแผนจะออกประกาศห้ามไม่ให้นักผจญภัยที่ยังไม่เก่งพอรับงานตอนกลางคืนเพราะมันอันตรายเกินไปค่ะ";
							next;
							mes "[Gelkah]";
							mes "ฉันล่ะสงสัยจริงๆ ว่าทำไมเขาถึงคิดเรื่อง ^FF0000ไร้สาระ^000000 แบบนี้ออกมาได้... สงสัยจังว่าพวกเขาเคยออกไปผจญภัยจริงๆ บ้างไหมเนี่ย";
							next;
							mes "[Gelkah]";
							mes "เราควรรับงานง่ายๆ นี้ไว้ก่อนที่จะมีคำขอไร้สาระกว่านี้ส่งมาค่ะ ช่วยกำจัด ^FF0000Loli Ruri 30 ตัว^000000 ใน Niflheim ให้ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเสร็จแล้วมาบอกฉันด้วยนะ";
						}
						else if (checkquest(13040,HUNTING) == 2) {
							mes "สภาพจิตใจยังโอเคอยู่ไหมคะ? ดีใจที่เห็นคุณกลับมาอย่างปลอดภัยนะ";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณค่ะ";
							callsub S_Quest,13040,13041,112000,186500;
						}
						else {
							mes "มีสมาคมลับแห่งหนึ่งขอความคุ้มครองจาก Eden Group เพราะพวกเขาเชื่อว่า Loli Ruri มีพลังจิตที่น่ากลัวค่ะ";
							next;
							mes "[Gelkah]";
							mes "พวกเขาบอกว่าจะช่วยรักษาเหล่านักผจญภัยที่เหนื่อยล้าเป็นการตอบแทน แต่ฉันก็ไม่แน่ใจหรอกนะว่าพวกเขาจะทำตามสัญญาไหม";
							next;
							mes "[Gelkah]";
							mes "ยังไงเราก็รับงานง่ายๆ นี้ไว้ก่อนเถอะค่ะ ช่วยกำจัด ^FF0000Loli Ruri 30 ตัว^000000 ใน Niflheim นะคะ";
							callsub S_Quest,13041,13040;
						}
						break;
					case 2:
						if (checkquest(13043,PLAYTIME) == 0)
							mes "การผจญภัยน่ะสำคัญ แต่การพักผ่อนก็สำคัญเหมือนกันนะ";
						else if (checkquest(13042,HUNTING) == 1) {
							mes "ฉันก็ไม่รู้เหมือนกันว่าทำไมอาจารย์ถึงชอบงานประเภทนี้จัง เขาชอบพูดว่า 'งานไหนก็เป็นงานที่ดีสำหรับนักผจญภัยทั้งนั้นแหละ'...";
							next;
							mes "[Gelkah]";
							mes "ช่วยไปกำจัด ^FF0000Medusa 30 ตัว^000000 ในถ้ำทางตะวันตกของ Comodo ให้ทีนะคะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเสร็จแล้วมาบอกฉันด้วยล่ะ";
						}
						else if (checkquest(13042,HUNTING) == 2) {
							mes "ชายหาด Comodo เป็นยังไงบ้างคะ? ลองเดินเที่ยวรอบๆ ดูบ้างสิ อย่ามัวแต่สนใจแต่เรื่องงานอย่างเดียวเลย";
							mes "มันจะเป็นประสบการณ์ที่ดีแน่นอนค่ะ";
							callsub S_Quest,13042,13043,106000,148000;
						}
						else {
							mes "นี่เป็นคำขอจากช่างทำผมใน Alberta ค่ะ เขาอยากรู้ว่าไอ้เจ้าพวกงูบนหัว Medusa น่ะมันคือวูจริงๆ หรือแค่ทรงผมแฟชั่นสุดล้ำกันแน่...";
							next;
							mes "[Gelkah]";
							mes "มันก็งูชัดๆ อยู่แล้วนะ แต่เราจะไปดับฝันอันบ้าคลั่งของเขาทำไมล่ะ จริงไหมคะ?";
							next;
							mes "[Gelkah]";
							mes "อาจารย์ของฉันรับงานนี้เพียงเพราะมันดูไร้สติสิ้นดีเนี่ยแหละ ช่วยกำจัด ^FF0000Medusa 30 ตัว^000000 ในถ้ำตะวันตก Comodo ให้ทีนะคะ";
							callsub S_Quest,13043,13042;
						}
						break;
					case 3:
						if (checkquest(13045,PLAYTIME) == 0)
							mes "ภารกิจที่ Sphinx เสร็จสิ้นแล้วค่ะ พักผ่อนก่อนนะคะ";
						else if (checkquest(13044,HUNTING) == 1) {
							mes "ฉันควรจะไปสืบเรื่องลูกค้าคนนี้ด้วยตัวเองนะ แต่ตอนนี้รบกวนคุณช่วยกำจัด ^FF0000Anubis 20 ตัว^000000 บนชั้น 4 ของ Sphinx Dungeon ให้ทีค่ะ";
							next;
							mes "[Gelkah]";
							mes "ถ้าเสร็จแล้วมาบอกฉันด้วยนะคะ";
						}
						else if (checkquest(13044,HUNTING) == 2) {
							mes "Sphinx Dungeon นี่เหมือนเขาวงกตเลยนะคะ";
							mes "แต่มันจะเป็นประสบการณ์ที่ดีสำหรับคุณแน่นอนค่ะ";
							callsub S_Quest,13044,13045,112000,186500;
						}
						else {
							mes "ลูกค้าที่ไม่ประสงค์ออกนามส่งคำขอมาให้ช่วยกำจัดมอนสเตอร์ใน Sphinx ค่ะ เพราะเดี๋ยวนี้นักผจญภัยแทบจะไม่ไปเที่ยว Morocc กันแล้ว มอนสเตอร์ที่นั่นก็เลยเริ่มอาละวาดหนักน่ะค่ะ";
							next;
							mes "[Gelkah]";
							mes "ช่วยกำจัด ^FF0000Anubis 20 ตัว^000000 บนชั้น 4 ของ Sphinx Dungeon ให้ทีนะคะ";
							callsub S_Quest,13045,13044;
						}
					}
				}
				break;
			case 2: // หัวข้อ: สอบถามข้อมูลเพิ่มเติม / สารทุกข์สุกดิบ
				if (para_100lv02 < 20) {
					.@ran = rand(1,3);
					if (.@ran == 1)
						mes "ช่วงนี้พวกเรายุ่งกับการจัดการคำขอที่ส่งเข้ามามากเลยค่ะ นี่เป็นครั้งแรกเลยนะที่ฉันรู้สึกกดดันขนาดนี้";
					else if (.@ran == 2)
						mes "ฉันพอจะรู้อยู่แล้วว่า Eden Group น่ะงานยุ่ง แต่ไม่คิดว่าจะยุ่งขนาดนี้! ในฐานะนักผจญภัย ฉันควรจะพยายามให้มากกว่านี้แล้วล่ะค่ะ!";
					else {
						cutin "gelca02",2;
						mes "อื้ม... ถ้าได้น้ำผลไม้สดๆ สักแก้วตอนนี้ก็คงจะดีไม่น้อยเลยนะคะ...";
					}
				}
				else if (para_100lv02 < 40) {
					.@ran = rand(1,3);
					if (.@ran == 1) {
						cutin "gelca03",2;
						mes "บางครั้งนักผจญภัยก็นำของแปลกๆ หรือของที่ระลึกจากที่ๆ พวกเขาไปทำภารกิจมาฝากด้วยล่ะค่ะ มันวิเศษมากเลย!";
					}
					else if (.@ran == 2)
						mes "ถ้าฉันมีอะไรดีๆ ละก็ ฉันจะแบ่งให้คุณ " + strcharinfo(0) + " บ้างนะคะ แต่ไม่แน่ใจว่าจะเหลือพอไหม เพราะช่วงนี้นักผจญภัยแวะเวียนมาเยอะเหลือเกิน";
					else
						mes "การจัดการงานเอกสารนี่มันยากจริงๆ นะคะ ฉันว่าการออกไปลุยภาคสนามยังง่ายซะกว่าอีก";
				}
				else if (para_100lv02 < 60) {
					if (para_100lv01 > 3) {
						.@ran = rand(1,3);
						if (.@ran == 1)
							mes "ถ้าฉันมีอะไรดีๆ ฉันจะบอกให้คุณทราบนะคะ";
						else if (.@ran == 2)
							mes "เริ่มชินกับงานหรือยังคะ? มันรู้สึกแปลกๆ เหมือนกันนะที่ฉันต้องมาคอยแนะนำนักผจญภัยคนอื่น แทนที่จะเป็นคนออกไปทำภารกิจซะเอง";
						else
							mes "ฉันว่ามันจะดีกว่านะถ้าคุณค่อยๆ จัดการภารกิจใน Glastheim ให้เสร็จทีละอย่างหลังจากรับงานมาทั้งหมดแล้ว";
					}
					else {
						cutin "gelca03",2;
						mes "ฉันกำลังยุ่งอยู่เลย แต่เห็นคุณ " + strcharinfo(0) + " ช่วยเคลียร์ภารกิจไปตั้งเยอะ ฉันขอบคุณจริงๆ ค่ะ ดูนี่สิ ฉันได้อาหารอร่อยๆ มาจากนักผจญภัยคนหนึ่งที่เขาได้มาระหว่างทำภารกิจด้วยล่ะ";
						next;
						mes "[Gelkah]";
						mes "อุ๊ย! นี่ฉันกินหมดแล้วเหรอเนี่ย? อ๊ะ เหลืออยู่อันนึงพอดี! เอาไว้คราวหน้าแวะมาใหม่นะคะ!";
						para_100lv01 = 4;
						getitem 608,1; // Yggdrasil Seed (เมล็ด Yggdrasil)
					}
				}
				else if (para_100lv02 < 80) {
					cutin "gelca03",2;
					if (para_100lv01 > 4)
						mes "สู้ๆ นะคะคุณ " + strcharinfo(0) + "! ฉันเองก็ไม่ค่อยแน่ใจเหมือนกันว่าเขามองคุณเป็นลูกศิษย์หรือเปล่า เพราะอาจารย์น่ะเป็นพวกที่เดาใจยากสุดๆ ไปเลย";
					else {
						mes "ช่วงนี้มีของขวัญส่งมาเยอะเลยค่ะเวลาที่มีคนเคลียร์ภารกิจได้อย่างรวดเร็ว เอ้า! นี่ค่ะ รับนี่ไปเป็นรางวัลตอบแทนความพยายามนะ";
						para_100lv01 = 5;
						getitem 607,1; // Yggdrasil Berry (ผล Yggdrasil)
					}
				}
				else if (para_100lv02 > 100) {
					mes "งานมันหนักก็จริง แต่การได้อยู่กับอาจารย์ก็น่าสนุกดีใช่ไหมคะคุณ " + strcharinfo(0) + "?";
					next;
					switch( select( "ฉันเกลียดตาแก่ขี้โมโหคนนั้นชะมัด", "ฉันอยากให้คุณมาเป็นอาจารย์แทนมากกว่า", "ฉันพร้อมจะตามคุณไปทุกสมรภูมิเลย" ) ) {
						mes "[Gelkah]";
					case 1:
						mes "ฉันเข้าใจคุณค่ะ... ตอนฉันเจออาจารย์ครั้งแรกที่หมู่บ้าน Mora พวกเราก็ทะเลาะกันยกใหญ่เลยล่ะ";
						next;
						mes "[Gelkah]";
						mes "ตอนนั้นเขาเกือบจะร้องไห้ออกมาแน่ะ ฉันนี่ลำบากแทบแย่ที่ต้องแกล้งทำเป็นแพ้เขา... ชู่วว เรื่องนี้เก็บไว้เป็นความลับนะคะ!";
						emotion ET_SMILE;
						break;
					case 2:
						if (Sex == SEX_MALE)
							mes "แหม... ฉันไม่แน่ใจว่าฉันจะเหมาะกับตำแหน่งนั้นหรือเปล่านะคะ อีกอย่าง...";
						else
							mes "เสียใจด้วยนะคะที่ต้องบอกว่า ฉันนี่แหละคือลูกศิษย์คนเดียวของอาจารย์ ทั้งตอนนี้และตลอดไป... เขาเคยพูดว่า ^FF0000'ข้าไม่ต้องการลูกศิษย์หญิงหน้าตาขี้เหร่หรอก'^000000 แต่ฉันก็ยังหน้าด้านเกาะติดเขาอยู่ดีนี่ล่ะค่ะ";
						break;
					case 3:
						cutin "gelca04",2;
						mes "ตายแล้ว... อย่าพูดแบบนั้นสิคะ ฉันขอร้องล่ะ ช่วยเก็บเรื่องนี้เป็นความลับจากอาจารย์ด้วยนะ ฉันไม่ได้ตั้งใจจะทำให้มันเป็นเรื่องใหญ่เลย";
						emotion ET_SHY;
						break;
					}
					next;
					cutin "gelca03",2;
					mes "[Gelkah]";
					if (para_100lv01 > 5)
						mes "โชคดีกับภารกิจที่เหลือนะคะ";
					else {
						mes "โชคดีกับภารกิจที่เหลือนนะคะ อ้อ... ^FF0000อาจารย์บอกว่าเขามีอะไรจะคุยกับคุณด้วยล่ะ^000000";
						para_100lv01 = 6;
					}
				}
				else
					mes "ขอโทษนะคะ ตอนนี้ฉันยุ่งนิดหน่อย ไว้เราค่อยคุยกันทีหลังได้ไหมคะ?";
				break;
			case 3: // หัวข้อ: แสดงบัตรสมนาคุณ
				cutin "gelca03",2;
				mes "คุณนำบัตรสมนาคุณมาด้วยหรือเปล่าคะ?";
				next;
				if (countitem(6549) > 0) { // Courtesy_Ticket
					if (select( "มันคือ ^FF0000บัตรสมนาคุณ^000000 แบบไหนกันนะ?", "นำบัตรมาแลกรางวัล" ) == 1) {
						mes "[Gelkah]";
						mes "ฉันรู้ว่ามันดูเหมือนของเล่นเด็กใช่ไหมล่ะ? รูปร่างมันเหมือนการ์ดที่เราใช้เล่นกันตอนเด็กๆ เลย";
						next;
						if (select( "การ์ดเหรอ? มันคืออะไรน่ะ?", "อ๋อ... เมื่อก่อนเล่นสนุกมากเลยล่ะ" ) == 1) {
							cutin "gelca02",2;
							mes "[Gelkah]";
							mes "ขอโทษทีค่ะ... สงสัยมันคงจะฮิตแค่ในหมู่บ้านของฉันมั้ง ที่ต้องเอามาขว้างใส่กันหรือทำให้มันลอยน่ะ";
							emotion ET_SORRY;
							next;
						}
						cutin "gelca01",2;
						mes "[Gelkah]";
						mes "อย่างที่คุณเห็น อาจารย์เป็นคนทำมันขึ้นมาเองกับมือ แถมยังเอารูปตัวเองใส่ลงไปซะด้วยนะ เขาเคยดั้นด้นเดินทางไปทั่วเพื่อตามหาวัสดุกระดาษดีๆ มาทำเจ้านี่เลยล่ะ";
						next;
						cutin "gelca02",2;
						mes "[Gelkah]";
						mes "ถ้าเขายังหาวัสดุไม่ได้ล่ะก็ สงสัยภารกิจ 'ตามหากระดาษ' คงถูกบรรจุลงในรายการเควสต์ให้คุณทำแน่ๆ ค่ะ";
						next;
						mes "[Gelkah]";
						mes "อันที่จริงอาจารย์เป็นคนใจดีนะ ถึงหน้าตาจะไม่ให้ก็เถอะ เขาตั้งใจทำบัตรนี้ไว้มอบให้นักผจญภัยเพื่อเป็นการตอบแทนความทุ่มเทค่ะ";
						next;
						cutin "gelca01",2;
						mes "[Gelkah]";
						mes "คุณสามารถนำบัตร 1, 10, 20 หรือ 30 ใบ มาแลกฟังเรื่องเล่าการผจญภัยหรือแลกของรางวัลก็ได้นะ";
						next;
						mes "[Gelkah]";
						mes "การฟังเรื่องราวจากนักผจญภัยรุ่นพี่ก็นับว่าเป็นประสบการณ์ที่ดีนะ มีเรื่องตื่นเต้นมากมายที่เราเคยได้ฟังมาจากอาจารย์ล่ะ";
						next;
						mes "[Gelkah]";
						mes "แต่ถ้าคุณเก่งกล้าจนไม่ต้องพึ่งพาภารกิจของพวกเราแล้ว บัตรพวกนี้ก็อาจจะใช้ไม่ได้ผลนะคะ";
					}
					else {
						mes "[Gelkah]";
						mes "คุณอยากจะใช้บัตรนี้ทำอะไรดีคะ?";
						next;
						if (select( "แลกรับไอเทมรางวัล", "ฟังเรื่องเล่าเพื่อรับค่าประสบการณ์ (EXP)" ) == 1) {
							cutin "gelca03",2;
							mes "[Gelkah]";
							mes "คุณสามารถสุ่มหยิบของรางวัลได้จากในกระเป๋าใบนี้ค่ะ อย่าเสียใจไปนะถ้าหยิบได้ของที่ไม่ถูกใจ... ว่าแต่คุณอยากจะแลกครั้งละกี่ใบดี?";
							next;
							.@amount = select( "แลกบัตรสมนาคุณ ^FF00001^000000 ใบ", "แลกบัตรสมนาคุณ ^FF000010^000000 ใบ" ) == 1 ? 1 : 10;
							mes "[Gelkah]";
							mes "ส่งบัตรมาให้ฉัน แล้วลองล้วงมือลงไปหยิบของในกระเป๋าได้เลยค่ะ";
							next;
							if (countitem(6549) < .@amount) {
								mes "[Gelkah]";
								mes "^FF0000บัตรสมนาคุณ^000000 ของคุณไม่พอค่ะ";
							}
							else {
								mes "*กำลังค้นหาของในกระเป๋า*";
								next;
								.@ran = rand(1,1000);
								if (.@amount == 1) {
									if (.@ran <= 100) setarray .@item[0],505,2; // Blue Potion
									else if (.@ran <= 200) setarray .@item[0],547,10; // White Slim Potion
									else if (.@ran == 333) setarray .@item[0],603,1; // Old Blue Box
									else if (.@ran == 777) setarray .@item[0],617,1; // Old Violet Box
									else setarray .@item[0],547,5; // White Slim Potion
								}
								else {
									if (.@ran <= 100) setarray .@item[0],608,1; // Seed Of Yggdrasil
									else if (.@ran <= 200) setarray .@item[0],547,100; // White Slim Potion
									else if (.@ran >= 300 && .@ran <= 399) setarray .@item[0],603,1; // Old Blue Box
									else if (.@ran >= 600 && .@ran <= 650) setarray .@item[0],616,1; // Old Card Album
									else if (.@ran >= 700 && .@ran <= 800) setarray .@item[0],617,1; // Old Violet Box
									else setarray .@item[0],547,60; // White Slim Potion
								}
								delitem 6549,.@amount;
								getitem .@item[0], .@item[1];
								mes "คุณล้วงมือลงไปในกระเป๋าและหยิบได้ ^0000FF"+ getitemname(.@item[0]) +" "+ .@item[1] +" ชิ้น^000000";
								next;
								if (.@amount == 10) cutin "gelca03",2;
								mes "[Gelkah]";
								mes "ใช้มันอย่างคุ้มค่านะคะ";
							}
						}
						else {
							// ระบบแลกค่าประสบการณ์จากการฟังเรื่องเล่า
							mes "[Gelkah]";
							mes "ได้เลยค่ะ ฉันมีเรื่องเล่าสนุกๆ มาแลกกับบัตรสมนาคุณของคุณ";
							next;
							switch( select( "ใช้บัตรสมนาคุณ ^FF000010^000000 ใบ", "ใช้บัตรสมนาคุณ ^FF000020^000000 ใบ", "ใช้บัตรสมนาคุณ ^FF000030^000000 ใบ" ) ) {
								mes "[Gelkah]";
							case 1:
								if (countitem(6549) < 10) mes "คุณมีบัตรสมนาคุณไม่เพียงพอค่ะ";
								else {
									.@story = rand(1,3);
									if (.@story == 1) {
										cutin "gelca02",2;
										mes "คุณเคยเห็นเรืออับปางแถวๆ Alberta ไหม?";
										next;
										mes "อาจารย์ Rohtert กับฉันเคยลงไปลึกถึงก้นเรือและสู้กับมอนสเตอร์ที่ชื่อ ^FF0000Drake^000000 มาแล้วล่ะ วันนั้นแหละที่ทำให้ฉันตั้งเป้าหมายว่าจะเป็นนักผจญภัยที่เก่งที่สุดให้ได้!";
										.@exp = 200000; .@jexp = 148000;
									}
									else if (.@story == 2) {
										cutin "gelca03",2;
										mes "มีครั้งหนึ่ง อาจารย์กับฉันไปที่ถ้ำทางตะวันตกของ Comodo และตอนที่เราเผชิญหน้ากับ Medusa...";
										next;
										mes "โอ้โห... คุณควรจะได้เห็นสีหน้าของอาจารย์ตอนนั้นนะ มันตลกจนประเมินค่าไม่ได้เลยล่ะ!";
										emotion ET_BIGTHROB; emotion ET_THINK, playerattached();
										.@exp = 180000; .@jexp = 168000;
									}
									else {
										mes "ฉันเคยหลงทางในเขาวงกตทางเหนือของ Prontera ด้วยล่ะ นึกว่าจะต้องเดินวนอยู่ในนั้นไปตลอดกาลซะแล้ว จนกระทั่งหาทางออกเจอในที่สุด";
										next;
										mes "เขาลือกันว่าถ้าคุณหลงอยู่ในนั้นนานเกินไป คุณจะกลายเป็นหนึ่งในมอนสเตอร์ของที่นั่นล่ะ น่ากลัวใช่ไหม?";
										.@exp = 220000; .@jexp = 128000;
									}
									if (JobLevel < 6) .@jexp = 106000;
									getexp .@exp,.@jexp; delitem 6549,10; para_100lv02 += 1;
								}
								break;
							case 2:
								if (countitem(6549) < 20) mes "คุณมีบัตรสมนาคุณไม่เพียงพอค่ะ";
								else {
									.@story = rand(1,3);
									if (.@story == 1) {
										cutin "gelca04",2;
										mes "ครั้งหนึ่งฉันเคยเจอเส้นทางลับที่ไป Jawaii ด้วยล่ะ... ที่นั่นสงวนไว้เฉพาะคู่รักเท่านั้น แต่ฉันก็ยังรู้วิธีแอบเข้าไปนะ";
										next;
										mes "ถึงจะดูไร้ประโยชน์ที่ไปที่นั่นตัวคนเดียวก็เถอะ... เฮ้อ";
										emotion ET_THROB;
										.@exp = 550000; .@jexp = 340000;
									}
									else if (.@story == 2) {
										cutin "gelca03",2;
										mes "มีอยู่ช่วงหนึ่ง อาจารย์เคยสังกัดกิลด์ที่ชื่อ 'ปีก...' อะไรสักอย่างนี่แหละ 'ปีกไก่' หรือเปล่านะ?";
										next;
										mes "มันเป็นกิลด์ที่สนุกมากเพราะไม่มีใครจริงจังกับอะไรเลย แต่อาจารย์ทนความขี้เกียจของพวกเขาไม่ไหวก็เลยลาออกมาน่ะค่ะ";
										.@exp = 400000; .@jexp = 240000;
									}
									else {
										mes "คุณเคยขึ้นไปบน ^FF0000Endless Tower^000000 หรือยัง? ฉันกับอาจารย์เคยไปที่นั่นนะ แต่ไปได้แค่ชั้น 20 เอง เพราะเข่าของอาจารย์ดันทรุดตอนกำลังปีนบันไดพอดี...";
										next;
										mes "เจ้าหอคอยนั่นอยู่กลางทะเลค่ะ คุณต้องไปหาคนที่ชื่อ Jansor หรือ Jan-อะไรสักอย่างที่ Alberta เขาจะเป็นคนล่องเรือพาส่งไปค่ะ";
										.@exp = 550000; .@jexp = 290000;
									}
									if (JobLevel < 6) .@jexp = 206000;
									getexp .@exp, .@jexp; delitem 6549,20; para_100lv02 += 1;
								}
								break;
							case 3:
								if (countitem(6549) < 30) mes "คุณมีบัตรสมนาคุณไม่เพียงพอค่ะ";
								else {
									.@story = rand(1,2);
									if (.@story == 1) {
										cutin "gelca04",2;
										mes "กิลด์ของอาจารย์ต้องยุบตัวลงที่ Ash Vacuum เพราะหัวหน้ากิลด์ของพวกเขาหายตัวไปหลังจากสู้กับพวก Cornus ค่ะ";
										next;
										mes "[Gelkah]";
										mes "นับตั้งแต่นั้นมา เขาก็เริ่มกลายเป็นคนเข้มงวดและดูใจร้ายกับนักผจญภัยที่ยังอ่อนแอน่ะค่ะ...";
										next;
										mes "[Gelkah]";
										mes "ฉันคิดว่าเขาคงไม่ได้เจอหัวหน้ากิลด์คนนั้นอีกเลยตั้งแต่วันนั้นล่ะนะ";
										getexp 650000,488000;
									}
									else {
										cutin "gelca03",2;
										mes "[Gelkah]";
										mes "อาจารย์เริ่มต้นจากการเป็นสาย Support (Monk) ที่โด่งดังมากในอาศรมที่ Prontera ค่ะ";
										next;
										mes "[Gelkah]";
										mes "แต่เขาก็ถูกมองว่าเป็นตัวปัญหาเพราะนิสัยชอบเอาชนะและไม่ยอมคน สุดท้ายเขาก็เลยเลือกเดินออกมาค่ะ";
										getexp 700000,430000;
									}
									delitem 6549,30; para_100lv02 += 1;
								}
								break;
							}
						}
					}
				}
				else {
					mes "[Gelkah]";
					mes "ถ้าคุณได้บัตรสมนาคุณมาเพิ่มเมื่อไหร่ อย่าลืมแวะมาหาฉันอีกนะคะ!";
				}
				break;
			}
		}
	}
	else {
		cutin "gelca03",2;
		// กรณีที่ทำเควสต์จนครบโควตาแล้ว
		if (para_100lv02 > 100)
			mes "คุณ " + strcharinfo(0) + "! ทำได้เยี่ยมมากเลยค่ะ! จริงๆ ยังมีคำขอเหลืออยู่นะคะ แต่ฉันว่าเว้นไว้ให้นักผจญภัยคนอื่นๆ บ้างจะดีกว่านะ";
		
		// กรณีที่ไม่ผ่านเงื่อนไข (เช่น เลเวลไม่ถึง)
		if (para_100lv02 < 101)
			mes "ฉันเกรงว่าคุณยังไม่ค่อยเหมาะกับภารกิจที่ฉันมีในตอนนี้เท่าไหร่... ลองไปหาประสบการณ์จากการผจญภัยอื่นๆ ดูก่อนนะคะ";

		// ตรวจสอบการส่งเควสต์ล่ามอนสเตอร์ (เรียกใช้ฟังก์ชัน S_Hunting ที่แปลไว้ก่อนหน้า)
		callsub S_Hunting,13003,106000,148000,"Carat";
		callsub S_Hunting,13005,106000,148000,"Arclouse";
		callsub S_Hunting,13007,112000,186500,"Anolian";
		callsub S_Hunting,13009,112000,186500,"Sting";
		callsub S_Hunting,13011,112000,252000,"Majoruros";
		callsub S_Hunting,13013,106000,148000,"Pinguicula";
		callsub S_Hunting,13015,112000,186500,"Luciola Vespa";
		callsub S_Hunting,13017,106000,148000,"Desert Wolf";
		callsub S_Hunting,13019,106000,148000,"Snowier";
		callsub S_Hunting,13021,112000,186500,"Ice Titan";
		callsub S_Hunting,13023,106000,148000,"Nightmare Terror";
		callsub S_Hunting,13025,106000,148000,"Flying Deleter";
		callsub S_Hunting,13040,112000,186500,"Loli Ruri";
		callsub S_Hunting,13042,106000,148000,"Medusa";
		callsub S_Hunting,13044,112000,186500,"Anubis";

		// ลบเควสต์ที่ติดคูลดาวน์ (Playtime) เมื่อครบกำหนดเวลา
		setarray .@quest_id[0],13004,13006,13008,130010,13012,13014,13016,13018,13020,13022,13024,13026,13041,13043,13045;
		for ( .@i = 0; .@i < 15; .@i++ ) {
			if (checkquest(.@quest_id[.@i],PLAYTIME) == 2) {
				erasequest .@quest_id[.@i];
				break;
			}
		}
	}
	close2;
	cutin "",255;
	end;
S_Quest:
	if (getarg(2,0)) {
		// กรณีส่งเควสต์สำเร็จ
		getitem 6549,1; // Courtesy_Ticket (บัตรสมนาคุณ)
		para_100lv02 += 1;
		if (JobLevel < 6)
			getexp 550000,getarg(1);
		else
			getexp 550000,getarg(2);
	}
	else {
		// กรณีคุยเพื่อรับเควสต์
		next;
		cutin "gelca01",2;
		if (select( "ยอมรับคำขอ","ปฏิเสธ" ) == 2)
			return 0;
		mes "[Gelkah]";
		mes "ถ้าคุณทำภารกิจสำเร็จแล้ว รบกวนกลับมาแจ้งให้ฉันทราบด้วยนะคะ";
		
		// กรณีพิเศษ: เควสต์ Glast Heim (13012)
		if (getarg(0) == 13012) {
			next;
			mes "[Gelkah]";
			mes "การเดินทางไปยังชั้นล่างสุดของ Glast Heim นั้นค่อนข้างไกลและอันตราย แต่ถ้าคุณไปกับปาร์ตี้ก็น่าจะสนุกนะคะ!";
		}
	}
	
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
		
	setquest getarg(1);
	return 1;

S_Hunting:
	switch( checkquest(getarg(0),HUNTING) ) {
	case -1:
		return;
	case 2:
		next;
		mes "[Gelkah]";
		mes "คุณทำภารกิจล่า";
		mes "^FF0000"+ getarg(3) +"^000000 สำเร็จแล้วสินะ!";
		mes "ฉันจะลงบันทึกให้ว่าคุณผ่านการทดสอบในส่วนนี้เรียบร้อยแล้วค่ะ";
		para_100lv02 += 1;
		if (JobLevel < 6)
			getexp 550000,getarg(1);
		else
			getexp 550000,getarg(2);
		break;
	case 0:
	case 1:// กรณีที่ยังล่าไม่ครบหรือไม่พบข้อมูลเควสต์
	}
	erasequest getarg(0);
	close2;
	cutin "",255;
	end;
}

moc_para01,41,95,5	script	Rohtert#12	4_M_ROTERT,{
	mes "[Rohtert]";
	if (!callfunc("F_HasEdenGroupMark")) {
		cutin "rote01",2;
		mes "เจ้าไม่ได้เป็นสมาชิก Eden Group ด้วยซ้ำ มาทำอะไรที่นี่?";
		mes "ข้าคุยเฉพาะกับนักผจญภัยเลเวล ^FF0000100 - 110^000000 เท่านั้นโว้ย";
	}
	else {
		cutin "rote03",2;
		mes "......";
		next;
		mes "[Rohtert]";
		if (BaseLevel < 100)
			mes "ไอ้หนูเอ๊ย... ไปฝึกมาใหม่ไป๊ อะไร? ไม่พอใจที่ข้าพูดงั้นเหรอ? งั้นก็รีบๆ เก่งขึ้นสิ";
		else if (BaseLevel > 110) {
			mes "เจ้าดูเป็นนักผจญภัยที่แก่ประสบการณ์เกินไปหน่อยนะ ไม่มีธุระอะไรกับข้าแล้วล่ะ ไปซะเถอะ ข้าจะคุยเฉพาะกับพวกคลาส 3 หน้าใหม่เลเวลไม่เกิน 110 เท่านั้น";
			if (checkquest(13047,PLAYTIME) == 2) erasequest 13047;
			if (checkquest(13049,PLAYTIME) == 2) erasequest 13049;
			if (checkquest(13048,HUNTING) > -1) erasequest 13048;
			if (checkquest(13046,HUNTING) > -1) erasequest 13046;
		}
		else if (para_100lv01 < 2) {
			mes "เอ้า ว่าไงเจ้าหนู? คิดว่าข้าจะใจดีบอกว่า 'โอเค ข้าจะเป็นอาจารย์สอนทุกอย่างให้' งั้นเหรอ?";
			next;
			mes "[Rohtert]";
			mes "ที่นี่ไม่ใช่โรงทานนะเว้ย ไปให้พ้นหน้าไป๊";
		}
		else if (para_100lv01 == 2) {
			mes "นักผจญภัยหน้าใหม่เหรอ?";
			next;
			mes "[Rohtert]";
			mes "มีเรื่องให้ทำตั้งเยอะแยะแต่อยากมาเสียเวลากับข้าเนี่ยนะ ไปคุยกับ Gelkah นู่น ยัยนั่นพูดเก่งกว่าข้าเยอะ";
			next;
			mes "[Rohtert]";
			mes "เจ้าดูใจกล้าดีนะ แต่ในสายตาข้า เจ้ามันก็แค่พวก Novice อยู่ดี";
			para_100lv01 = 3;
		}
		else if (para_100lv01 == 3) {
			mes "เมื่อก่อนข้าก็เคยเป็นนักผจญภัยของ Eden Group เหมือนเจ้านั่นแหละ แต่แน่นอนว่าข้าเก่งที่สุด";
			next;
			mes "[Rohtert]";
			mes "ตอนนี้ข้าเกษียณแล้ว เพราะฝีมือของข้ามันสูงส่งเกินกว่าจะไปลงแข่งกับพวกเด็กๆ อย่างพวกเจ้า";
		}
		else if (para_100lv01 == 4) {
			mes "เอาเถอะ ข้าจะช่วยสงเคราะห์อะไรให้ Eden Group หน่อยละกัน";
			next;
			mes "[Rohtert]";
			mes "ข้ากำลังมองหาคนที่ให้ความสำคัญกับ 'มิตรภาพ' หรือ 'ความซื่อสัตย์' เหมือนกับ Spoon Stub น่ะ";
			next;
			if (select( "Spoon อะไรนะ?", "คุณมันคนนิสัยเสีย" ) == 1) {
				cutin "rote03",2;
				mes "[Rohtert]";
				mes "ไม่รู้จักเหรอ? Spoon Stub ไง ที่ดรอปจาก Zombie Prisoner น่ะ ดูเหมือนจะมีประโยชน์นะ แต่จริงๆ มันก็ขยะดีๆ นี่เอง";
				next;
				mes "[Rohtert]";
				mes "แต่เอาเถอะ ดูเหมือนเจ้าจะเป็นพวกซื่อสัตย์อยู่บ้าง... มั้งนะ...";
			}
			else {
				mes "[Rohtert]";
				mes "พูดซะข้าทำตัวไม่ถูกเลย แต่ก็นะ ข้ามันคนเลวอยู่แล้ว และข้าก็ไม่แคร์ด้วย";
			}
			next;
			cutin "rote04",2;
			mes "[Rohtert]";
			mes "จำไว้ล่ะ จงรักษาความกล้าไว้ต่อไปนะ เจ้าคนบ้าพลัง";
		}
		else if (para_100lv01 == 5) {
			cutin "rote03",2;
			mes "โอเค! เจ้าทำได้ดีเท่าที่เจ้าจะทำได้แล้วล่ะ";
			next;
			mes "[Rohtert]";
			mes "ข้าล่ะกลัวจริงๆ ว่าเจ้าจะกลายเป็นเหมือน Gelkah ยัยนั่นเหมือนลูกหมาหลงทางยังไงยังงั้น อย่าเป็นแบบนั้นล่ะ เข้าใจมั้ย?";
		}
		else if (para_100lv01 == 6) {
			cutin "rote03",2;
			mes "มัวแต่มองไปรอบๆ อยู่นั่นแหละ";
			next;
			mes "[Rohtert]";
			if (checkquest(13046) == 2) {
				if (checkquest(13047,PLAYTIME) == 0) {
					mes "ข้าทำธุระเสร็จแล้ว";
					mes "ข้าต้องมารับผิดชอบจัดการเอกสารเนี่ย ข้าล่ะเกลียดงานพวกนี้ชะมัด";
					next;
					mes "[Rohtert]";
					mes "ข้าว่าข้าควรคุยกับเจ้านั่นอีกสักรอบ... พรุ่งนี้ค่อยมาใหม่ละกัน!";
				}
				else if (checkquest(13047,PLAYTIME) == 2) {
					mes "โอ้ นี่มันผ่านมานานแค่ไหนแล้วเนี่ย?";
					erasequest 13047;
				}
				else if (checkquest(13049,PLAYTIME) == 0)
					mes "กระดาษสีขาว ตัวหนังสือสีดำ... ข้าเกลียดพวกเอกสารราชการจริงๆ แต่ข้าต้องทำมัน เพราะงั้นช่วงนี้ข้าไม่ว่างโว้ย";
				else if (checkquest(13048,HUNTING) == 1)
					mes "ข้าว่าอีแค่ล่า Tendrilion คงไม่เหนือบ่ากว่าแรงเจ้าหรอกนะ";
				else if (checkquest(13048,HUNTING) == 2) {
					cutin "rote02",2;
					mes "ดีมาก! เจ้าเริ่มจะเป็นนักผจญภัยที่เข้าท่าแล้วนี่ พยายามต่อไปล่ะ!";
					erasequest 13048;
					setquest 13049;
					getexp 352000,253000;
				}
				else {
					cutin "rote02",2;
					mes "ข้าว่าเราควรสั่งสอนเจ้า Tendrilion นั่นอีกสักรอบ ให้มันรู้ซะบ้างว่าใครเป็นเจ้านาย!";
					next;
					if (select( "จัดไป เดี๋ยวจะสั่งสอนให้เข็ด", "ข้าว่าแค่นี้ก็พอแล้วมั้ง" ) == 2) {
						mes "[Rohtert]";
						mes "ก็เอาเถอะ ยังมีนักผจญภัยคนอื่นที่ทำเรื่องนี้ได้ตั้งเยอะแยะ";
					}
					else {
						cutin "rote04",2;
						mes "[Rohtert]";
						mes "ที่ข้าไม่ไปเองไม่ใช่เพราะข้าขี้ขลาดนะเว้ย";
						next;
						mes "[Rohtert]";
						mes "โชคดีล่ะ คราวนี้จัดการมันให้สิ้นซากล่ะ ฮ่าฮ่า!";
						if (checkquest(13049,PLAYTIME) == 2) erasequest 13049;
						setquest 13048;
					}
				}
			}
			else if (checkquest(13046,HUNTING) == 1)
				mes "ยังอยู่อีกเหรอเจ้าหนู? ถ้าทำคนเดียวไม่ไหวก็ไปเรียกเพื่อนมาช่วยซะ เพราะงานนี้มันไม่ได้ง่ายขนาดนั้น";
			else if (checkquest(13046,HUNTING) == 2) {
				mes "โอ้ กลับมาอย่างปลอดภัยสินะ!";
				mes "ดูเหมือนเจ้าจะไม่ใช่พวกกระจอกอย่างที่ข้าคิดแฮะ";
				completequest 13046;
				setquest 13047;
				getexp 352000,253000;
			}
			else {
				cutin "rote04",2;
				mes "นี่ สนใจจะไปทำธุระเล็กๆ น้อยๆ ให้หน่อยมั้ย?";
				next;
				if (select( "ได้เลย ไม่มีปัญหา!", "งานจุกจิกแบบนี้ไว้ให้พวกกระจอกทำเถอะ" ) == 2) {
					mes "[Rohtert]";
					mes "เจ้านี่เป็นนักผจญภัยที่แปลกคนจริงๆ...";
				}
				else {
					mes "[Rohtert]";
					mes "ในวันหนึ่งที่ลมพัดเย็นสบาย ข้ากำลังเก็บดอกไม้อยู่ทางตะวันตกเฉียงใต้ของค่ายพันธมิตร Midgard เพื่อส่งเควสต์ให้ Eden Group...";
					next;
					mes "[Rohtert]";
					mes "แล้วจู่ๆ 'เจ้านั่น' ก็โผล่มา";
					next;
					select("เจ้านั่น... คือตัวอะไร?");
					cutin "rote02",2;
					mes "[Rohtert]";
					mes "มันคือ ^FF0000Tens Drillion^000000 ยังไงล่ะ!";
					next;
					cutin "gelca03",2;
					mes "[^FF0000Gelkah^000000]";
					mes "อุ๊ย! จริงๆ มันชื่อ ^FF0000Tendrilion^000000 ค่ะอาจารย์!!";
					next;
					cutin "rote04",2;
					mes "[Rohtert]";
					mes "เออๆ จะชื่ออะไรก็ช่างมันเถอะน่า!";
					emotion ET_HNG;
					next;
					cutin "gelca02",2;
					mes "[^FF0000Gelkah^000000]";
					mes "......";
					next;
					cutin "rote04",2;
					mes "[Rohtert]";
					mes "ตอนที่เราจ้องตามัน ข้ารู้ได้ทันทีว่ามันเหมือนสิงโต แต่มันไม่ใช่สัตว์ธรรมดาทั่วไปแน่ๆ";
					emotion ET_HNG;
					next;
					mes "[Rohtert]";
					mes "^8008F8ข้ารู้ดีว่านี่คือสถานการณ์ 'ไม่มันตายก็ข้ามอด'^000000";
					next;
					mes "[Rohtert]";
					mes "แน่นอนว่าข้าจัดการมันได้อย่างง่ายดาย! ง่ายเหมือนแย่งขนมจาก Poring เลยล่ะ";
					next;
					mes "[Rohtert]";
					mes "แต่ที่น่าตกใจคือมันดันลุกขึ้นมาใหม่ ข้าเลยต้องอัดมันให้หมอบไปอีกตั้ง 12 รอบกว่ามันจะยอมนิ่ง ไอ้เจ้า Tens Drillion นี่ไม่ธรรมดาจริงๆ";
					next;
					mes "[Rohtert]";
					mes "ได้ยินว่ามันยังคงเดินป้วนเปี้ยนไปมาอยู่แถวนั้น แต่ตอนนี้ข้าค่อนข้างยุ่งน่ะ";
					next;
					mes "[Rohtert]";
					mes "ข้าอยากให้เจ้าไปจัดการมันแทนที สำหรับข้าการไปคนเดียวมันไม่ใช่ปัญหาหรอก แต่สำหรับเจ้า... ข้าแนะนำให้พาสมุนไปเยอะๆ หน่อยละกัน";
					next;
					select("คุณแน่ใจนะว่าทำคนเดียวจริงๆ?");
					mes "[Rohtert]";
					mes "ความจริงที่ว่าข้าเก่งน่ะมันสำคัญกว่าจำนวนคนเว้ย! ช่างมันเถอะ ไปจัดการเจ้านั่นซะ";
					next;
					mes "[Rohtert]";
					mes "เจ้าจะพบมันได้ทางตะวันตกของค่ายพันธมิตร Midgard ในเขต Ash Vacuum";
					next;
					select("ดูเหมือนผมจะไม่มีทางเลือกนะ");
					cutin "rote03",2;
					mes "[Rohtert]";
					mes "ยังอยู่อีกเหรอ? บอกแล้วไงว่าถ้าไม่ไหวก็เรียกเพื่อนมาช่วย งานนี้มันไม่ง่ายนะ";
					next;
					cutin "gelca02",2;
					mes "[^FF0000Gelkah^000000]";
					mes "....... คุณ "+ strcharinfo(0) +" เดี๋ยวสิคะ...";
					next;
					cutin "gelca01",2;
					mes "[^FF0000Gelkah^000000]";
					mes "ถ้าเจอตัวมันก็ไม่ต้องกลัวไปนะคะ อาจารย์ไม่ได้สู้กับ Tendrilion ตั้ง 12 รอบอะไรนั่นหรอก";
					next;
					cutin "gelca03",2;
					mes "[^FF0000Gelkah^000000]";
					mes "อันที่จริงตอนนั้นอาจารย์เมามากจนจำอะไรแทบไม่ได้เลยล่ะค่ะ";
					next;
					cutin "gelca04",2;
					mes "[^FF0000Gelkah^000000]";
					mes "ฉันเห็นว่ามันตลกดีที่เห็นอาจารย์สู้กับ Tendrilion แค่ตัวเดียวอย่างเอาเป็นเอาตาย เลยไม่ได้ห้าม แต่ตอนนี้มันเริ่มจะเป็นปัญหาแล้วล่ะ";
					next;
					cutin "gelca01",2;
					mes "[^FF0000Gelkah^000000]";
					mes "รบกวนช่วยไปจัดการเจ้า Tendrilion สักตัวแล้วกลับมาบอกนะคะ อ้อ! แล้วก็ช่วยเหยียบเรื่องนี้ไว้เป็นความลับจากอาจารย์ด้วยล่ะ!";
					setquest 13046;
				}
			}
		}
	}
	close2;
	cutin "",255;
	end;
}
