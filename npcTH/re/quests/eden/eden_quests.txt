//===== rAthena Script =======================================
//= Eden Group Quest - Quests NPCs
//===== Description: =========================================
//= Eden Group Headquarter NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Removed unencoded comments (Korean -> Gibberish)
//=     Readded the GM helper NPC, commented out.
//= 1.2 Some little optimization here and there. [Masao]
//= 1.3 Added Instructor Ur and the new Quests which come
//=     alongside him. Special thanks to Chilly for the base. [Masao]
//= 1.4 Partial cleaning and bug fixing. [Euphy]
//= 1.4a Added 'npcskill' command. [Euphy]
//= 1.4b Added 'disable_items' command. [Euphy]
//= 1.5 Added GM management function. [Euphy]
//= 1.6 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//= 2.0 Updated Instructor Ur's quests. [Capuche]
//= 2.1 Updated Blacksmith Thorn and Weapons Expert NPC. [Capuche]
//= 2.2 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 2.3 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,25,35,4	script	Instructor Boya#para01	4_M_KNIGHT_GOLD,{
	mes "[Boya]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "เธอไม่ได้อยู่ในกลุ่มของฉันใช่ไหม?";
		mes "ฉันไม่มีอะไรจะคุยกับคนนอกหรอกนะ";
		mes "ถ้าต้องการอะไร ก็ไปลงทะเบียนเข้ากลุ่มของเราก่อนไป";
		next;
		mes "[Boya]";
		mes "ถ้าจะลงทะเบียนเข้ากลุ่ม Eden Group ให้ไปคุยกับ Laime Evenor ที่อยู่ข้างๆ ฉันนี่";
		close;
	}
	if (para_suv01 == 0) {
		mes "มีอะไรเหรอ?";
		mes "ถ้าเธอมีภารกิจปกติทั่วไป ก็ไปดูที่กระดานประกาศนู่น";
		next;
		switch (select("หน้าที่ของคุณคืออะไร?:ไม่มีอุปกรณ์ให้เหรอ?:(เมิน)")) {
			mes "[Boya]";
		case 1:
			mes "ฉันมีหน้าที่มอบภารกิจฝึกฝนให้กับสมาชิก";
			mes "นั่นคือเหตุผลว่าทำไมพวกเขาถึงต้องมาเข้าร่วมกัน";
			mes "ถ้าไม่อยากเจอปัญหาล่ะก็ การฝึกพวกนี้ถือเป็นเรื่องสำคัญมากเลยล่ะ";
			next;
			mes "[Boya]";
			mes "การฝึกต่อสู้จะช่วยเพิ่มประสบการณ์จริงให้กับพวกเธอ";
			mes "และสมาชิกที่พิสูจน์ตัวเองได้ ก็จะได้รับรางวัลด้วยนะ";
			next;
			mes "[Boya]";
			mes "เรามีอุปกรณ์พิเศษที่เราผลิตขึ้นมาเองมอบให้";
			mes "ของขวัญเหล่านี้มีไว้สำหรับคนที่ทุ่มเทอย่างเต็มที่เท่านั้น";
			next;
			mes "[Boya]";
			mes "ถ้าเธอสนใจ ก็เข้าร่วมได้นะ";
			mes "คอร์สการต่อสู้ฝึกฝนพวกนี้ไม่ยากเท่าไหร่หรอก";
			mes "ไม่มีอะไรต้องกังวล";
			next;
			mes "[Boya]";
			mes "ถ้าอยากเข้าร่วม ก็อย่าลังเลเลย";
			next;
			switch (select("เข้าร่วมการฝึก:ไม่สนใจ")) {
				mes "[Boya]";
			case 1:
				callsub S_Quest1;
				callsub S_Quest2;
				callsub S_Quest3;
			case 2:
				mes "ก็แล้วแต่เธอจะตัดสินใจนะ";
				mes "ไม่ใช่เรื่องของฉันหรอก แต่ฉันว่าเธอควรลองกลับไปคิดดูใหม่นะ";
				close;
			}
		case 2:
			mes "หือ?";
			mes "เธอนี่ตรงไปตรงมาดีจังนะ!";
			mes "ให้ตายสิ อยากรู้เรื่องอุปกรณ์งั้นเหรอ?";
			next;
			mes "[Boya]";
			mes "ฉันมีชุดยูนิฟอร์มแจกฟรีสำหรับสมาชิกกลุ่มเราอยู่";
			mes "แต่ก็นะ ฉันคงให้ไปฟรีๆ เฉยๆ ไม่ได้";
			next;
			mes "[Boya]";
			mes "เราจะมอบให้เฉพาะผู้ที่ทุ่มเทอย่างดีที่สุดในการฝึกฝนเท่านั้น";
			next;
			mes "-Boya มองเธอตั้งแต่หัวจรดเท้า";
			mes "อืม... ดูเหมือนเขาจะคิดว่ามีบางอย่างผิดปกติ-";
			next;
			mes "[Boya]";
			mes "ขึ้นอยู่กับความพยายามน่ะนะ";
			next;
			select("อะไรนะ?!");
			mes "[Boya]";
			mes "สรุปจะเข้าร่วมการฝึกไหม?";
			mes "เห็นฉันดูชิลล์ๆ แบบนี้ จริงๆ ฉันยุ่งมากเลยนะ ฉันเคยถูกเรียกว่า Rune Knight แห่งแสงเชียวนะ";
			mes "รีบตัดสินใจเข้า เร็วๆ";
			next;
			switch (select("เข้าร่วมการฝึก:ปฏิเสธ!!")) {
				mes "[Boya]";
			case 1:
				callsub S_Quest1;
				callsub S_Quest2;
				callsub S_Quest3;
			case 2:
				mes "เสียมารยาทจริงๆ!";
				specialeffect2 EF_HIT1;
				percentheal -50,0;
				next;
				mes "-โบยาโจมตีอย่างรวดเร็วและ Rune Knight ผู้นี้ก็หายตัวไป";
				mes "โอ๊ย เจ็บชะมัดเลย-";
				close;
			}
		case 3:
			mes "อย่ามากวนฉันเลย";
			close;
		}
	}
	else if (para_suv01 < 5) {
		mes "เฮ้ ฉันอธิบายเรื่องพื้นที่ฝึกฝนไปหมดแล้วนะ";
		mes "ฉันจะบอกอีกรอบเดียว ตั้งใจฟังล่ะ";
		next;
		mes "[Boya]";
		mes "โอเอซิสทางตะวันออกเฉียงใต้ของ Morocc";
		mes "มีสุนัขตัวใหญ่อยู่ตรงกลาง";
		mes "รายละเอียดเขียนไว้ในบันทึกแล้ว ไปดูเอาสิ";
	}
	else if (para_suv01 == 5) {
		mes "โอ้ กลับมาแล้วเหรอ";
		mes "ทำได้ดีมาก";
		mes "ตอนนี้เธอเริ่มปรับตัวได้แล้วนะ";
		next;
		mes "[Boya]";
		mes "ขั้นที่ 1 เสร็จเรียบร้อย";
		mes "ยินดีด้วย";
		mes "เราจะมอบชุดยูนิฟอร์มและอุปกรณ์บางอย่างให้";
		next;
		mes "[Boya]";
		mes "เห็นประตูป้ายสีฟ้าขนาดใหญ่ข้างๆ กระดานไหม?";
		mes "เข้าไปข้างในแล้วเดินไปจนสุดทางเดินด้านขวา จะมีห้องเก็บอุปกรณ์อยู่ที่นั่น";
		next;
		mes "[Boya]";
		mes "บอกผู้ดูแลว่าฉันส่งมา เขาจะมอบของให้เธอเอง";
		mes "ไปเลย ไปๆๆ!";
		para_suv01 = 11;
		completequest 7132;
	}
	else if (para_suv01 < 10) {
		mes "ชื่อการฝึกคือ 'พิชิตท่อระบายน้ำ (Culvert)!";
		mes "เธอสำรวจท่อระบายน้ำจนทั่วหรือยัง?";
		next;
		mes "[Boya]";
		mes "กลับมาเมื่อเธอทำคอร์สจากครูฝึกในพื้นที่เสร็จเรียบร้อยแล้วนะ";
	}
	else if (para_suv01 == 10) {
		mes "โอ้ กลับมาแล้วเหรอ";
		mes "ทำได้ดีมาก";
		mes "ตอนนี้เธอเริ่มปรับตัวได้แล้วนะ";
		next;
		mes "[Boya]";
		mes "ขั้นที่ 1 เสร็จเรียบร้อย";
		mes "ยินดีด้วย";
		mes "ทีมของฉันจะมอบชุดยูนิฟอร์มและอุปกรณ์บางอย่างให้";
		next;
		mes "[Boya]";
		mes "เห็นประตูป้ายสีฟ้าขนาดใหญ่ข้างๆ กระดานไหม?";
		mes "เข้าไปข้างในแล้วเดินไปจนสุดทางเดินด้านขวา จะมีห้องเก็บอุปกรณ์อยู่ที่นั่น";
		next;
		mes "[Boya]";
		mes "บอกผู้ดูแลว่าฉันส่งมา เขาจะมอบของให้เธอเอง";
		mes "ไปเลย ไปๆๆ!";
		para_suv01 = 11;
		completequest 7137;
	}
	else if (para_suv01 == 11) {
		mes "มัวทำอะไรอยู่ล่ะ?";
		mes "ไปรับอุปกรณ์จากผู้ดูแลห้องเก็บของสิ";
		mes "ชุดยูนิฟอร์มของเราเท่สุดๆ ไปเลยนะ ฮ่าฮ่า";
	}
	else if (para_suv01 == 12) {
		mes "อืม ชอบของที่ได้ไปไหม?";
		mes "ฉันชอบหมวกแดงนะ";
		mes "โบว์สีแดงนั่นดูน่ารักดี";
		next;
		mes "[Boya]";
		if (BaseLevel > 25) {
			mes "และดูเหมือนว่าเธอ...";
			mes "สามารถเข้ารับการฝึกระดับที่สูงขึ้นได้แล้วล่ะ";
			mes "ว่าไง สนใจไหม?";
			next;
			switch (select("ไม่เอาล่ะ:แน่นอน ฉันทำแน่")) {
				mes "[Boya]";
			case 1:
				mes "จริงเหรอ?";
				mes "จริงๆ ฉันก็ไม่ได้บังคับหรอกนะ แต่ถ้าระดับสูงขึ้น ยูนิฟอร์มก็จะเปลี่ยนไปด้วยนะ";
				close;
			case 2:
				mes "เยี่ยม";
				mes "เดี๋ยวฉันเลือกสถานที่ที่เหมาะสมให้";
				next;
				mes "[Boya]";
				callsub S_Quest2;
				callsub S_Quest3;
			}
		}
		else {
			mes "การฝึกต่อสู้จะแบ่งออกเป็นขั้นๆ";
			mes "เมื่อเธอพร้อมสำหรับขั้นต่อไป ก็ไปเก็บเลเวลเพิ่มแล้วค่อยกลับมานะ";
			next;
			mes "[Boya]";
			mes "การฝึกขั้นต่อไปจะเปิดให้สำหรับผู้ที่มีเลเวล 26 ขึ้นไป";
			mes "เมื่อถึงเลเวลนั้นแล้วค่อยแวะมาใหม่นะ เข้าใจไหม?";
		}
	}
	else if (para_suv01 < 16) {
		mes "พื้นที่ฝึกฝนอยู่ที่ถ้ำทางตอนเหนือของ Payon";
		mes "เจ้าหน้าที่ของเราไปรออยู่ที่นั่นแล้ว";
		mes "ไปหาเขาแล้วทำตามคำแนะนำซะ";
	}
	else if (para_suv01 == 16) {
		mes "เธอฝึกขั้นที่สองเสร็จแล้วสินะ";
		mes "ตอนนี้เข้าใจระบบของโลกใบนี้ขึ้นมาบ้างหรือยัง?";
		next;
		mes "[Boya]";
		mes "ฉันจะรับรองว่าเธอผ่านการฝึกแล้ว";
		mes "ผู้ดูแลห้องเก็บของจะจัดหาอุปกรณ์ที่เธอต้องการให้เอง";
		next;
		mes "[Boya]";
		mes "เลือกอุปกรณ์ที่เหมาะกับสายอาชีพและทักษะของเธอล่ะ";
		para_suv01 = 22;
		completequest 7141;
	}
	else if (para_suv01 < 21) {
		mes "พื้นที่ฝึกฝนอยู่ทางตะวันตกเฉียงใต้ของ Morocc";
		mes "เข้าไปใน Saint Darmain Fortress เพื่อไปที่นั่นได้โดยตรงเลย";
		next;
		mes "[Boya]";
		mes "มีใครบางคนที่นั่นชื่อ... เอ่อ... เขาเฝ้ารอคอยให้เธอไปทำตามคำแนะนำของเขาอยู่";
	}
	else if (para_suv01 == 21) {
		mes "เธอฝึกขั้นที่สองเสร็จแล้วสินะ";
		mes "ตอนนี้เข้าใจระบบของโลกใบนี้ขึ้นมาบ้างหรือยัง?";
		next;
		mes "[Boya]";
		mes "ฉันจะรับรองว่าเธอผ่านการฝึกแล้ว";
		mes "ผู้ดูแลห้องเก็บของจะจัดหาอุปกรณ์ที่เธอต้องการให้เอง";
		para_suv01 = 22;
		completequest 7146;
	}
	else if (para_suv01 == 22) {
		mes "เรามีอุปกรณ์และชุดเกราะให้สำหรับผู้ฝึกฝนที่ผ่านขั้นที่สองแล้ว";
		mes "ไปที่ห้องเก็บของและพบกับผู้ดูแลที่นั่นซะ";
		next;
		mes "[Boya]";
		mes "ทางไปห้องเก็บของคือผ่านประตูสีฟ้าข้างกระดานภารกิจ แล้วเดินไปจนสุดทางเดินด้านขวา";
	}
	else if (para_suv01 == 23) {
		mes "เฮ้ ไม่เจอกันนานเลยนะ";
		mes "เป็นไงบ้าง?";
		next;
		switch (select("ฉันอยากเข้าร่วมการฝึก:ไม่มีอะไร")) {
			mes "[Boya]";
		case 1:
			mes "หืม... จริงเหรอ?";
			mes "ขอดูหน่อยซิ... ขั้นไหนที่เหมาะกับเธอดีนะ...";
			next;
			mes "[Boya]";
			if (BaseLevel < 40) {
				mes "ขอโทษทีนะ แต่การจะเข้าร่วมการฝึกนี้ เธอต้องมีเลเวลอย่างน้อย 40";
				mes "ไปตั้งใจเก็บเลเวลให้สูงกว่านี้แล้วค่อยกลับมา";
				close;
			}
			callsub S_Quest3;
		case 2:
			mes "แวะมาหาฉันงั้นเหรอ?";
			mes "แค่นั้น? ไม่มีอะไรติดไม้ติดมือมาเลย?";
			mes "อย่างน้อยน่าจะมีช็อกโกแลตมาฝากกันบ้างนะ...";
			next;
			mes "[Boya]";
			mes "บานาน่าโรล หรือขนมอะไรก็ได้...";
			mes "อ้อ แต่ฉันไม่กินขนมที่มีซินนามอน (อบเชย) นะ...";
			close;
		}
	}
	else if (para_suv01 < 28) {
		mes "อืมม ที่หมู่บ้าน Orc มีพนักงาน Kafra อยู่ เธอไปใช้บริการได้นะ";
		next;
		mes "[Boya]";
		mes "สมาชิกของเราคนหนึ่งรออยู่ในอาคารใกล้ๆ กับพนักงาน Kafra นั่นแหละ";
	}
	else if (para_suv01 == 28) {
		mes "เยี่ยม! เธอผ่านการฝึกขั้นที่สามแล้ว";
		mes "ฉันจะออกใบรับรองให้";
		mes "ผู้ดูแลห้องเก็บของจะมอบอุปกรณ์บางอย่างให้เธอเอง";
		next;
		mes "[Boya]";
		mes "เรามีอุปกรณ์และชุดเกราะให้สำหรับผู้ที่ผ่านการฝึกระดับนี้";
		mes "ไปที่ห้องเก็บของแล้วคุยกับคนดูแลซะ";
		next;
		mes "[Boya]";
		mes "ห้องเก็บของอยู่หลังประตูสีฟ้าสุดทางเดินด้านขวานะ";
		para_suv01 = 37;
		completequest 7151;
	}
	else if (para_suv01 < 32) {
		mes "อืมม ที่หมู่บ้าน Orc มีพนักงาน Kafra อยู่ เธอใช้บริการที่นั่นได้เลย";
		next;
		mes "[Boya]";
		mes "สมาชิกของเราคนหนึ่งรออยู่ในอาคารใกล้ๆ กับพนักงาน Kafra นั่นแหละ";
	}
	else if (para_suv01 == 32) {
		mes "เยี่ยม! เธอผ่านการฝึกขั้นที่สามแล้ว";
		mes "ฉันจะออกใบรับรองให้";
		mes "ผู้ดูแลห้องเก็บของจะมอบอุปกรณ์บางอย่างให้เธอ";
		next;
		mes "[Boya]";
		mes "เราจัดเตรียมอุปกรณ์ไว้ให้แล้วหลังจากผ่านการฝึกขั้นนี้";
		mes "ไปที่ห้องเก็บของแล้วพบกับผู้ดูแลที่นั่น";
		next;
		mes "[Boya]";
		mes "ห้องเก็บของอยู่หลังประตูสีฟ้าสุดทางเดินด้านขวา";
		para_suv01 = 37;
		completequest 7155;
	}
	else if (para_suv01 < 36) {
		mes "ก่อนอื่น นั่งเรือจาก Izlude ไปที่เกาะ Byalan นะ!";
		mes "ที่นั่นมีถ้ำใต้ดิน เข้าไปจนถึงชั้นล่างสุด เธอจะพบเมืองใต้น้ำในตำนาน...";
		next;
		mes "[Boya]";
		mes "มีผู้ฝึกฝนประจำการอยู่แถวๆ ทางเข้าเมืองใต้น้ำ (Ocean City)";
	}
	else if (para_suv01 == 36) {
		mes "เยี่ยม! เธอผ่านการฝึกขั้นที่สามแล้ว";
		mes "ฉันจะรับรองให้ว่าเธอฝึกจบแล้ว";
		mes "ผู้ดูแลห้องเก็บของจะจัดหาอุปกรณ์มาให้เธอเอง";
		next;
		mes "[Boya]";
		mes "เรามีอุปกรณ์และชุดเกราะมอบให้สำหรับผู้ฝึกฝนที่ผ่านขั้นนี้";
		mes "ไปที่ห้องเก็บของแล้วเจอผู้ดูแลที่นั่นซะ";
		next;
		mes "[Boya]";
		mes "ห้องเก็บของอยู่หลังประตูสีฟ้าสุดทางเดินด้านขวานะ";
		para_suv01 = 37;
		completequest 7159;
	}
	else if (para_suv01 == 37) {
		mes "[Boya]";
		mes "ถ้าเธอทำครบทุกขั้นตอนแล้ว ก็ไปรับเสบียงและของรางวัลเถอะ";
		mes "เรามอบอุปกรณ์ให้เฉพาะผู้ที่ฝึกจบเท่านั้น";
		next;
		mes "[Boya]";
		mes "เราอาจจะมีอย่างอื่นให้ด้วยนะ";
		mes "ถ้ามีคำถามอะไร ให้ถามคนดูแลคลังแสง (Arsenal) เอา";
		next;
		mes "[Boya]";
		mes "คลังแสงอยู่หลังประตูสีฟ้าสุดทางเดินด้านขวา";
	}
	else if (para_suv01 >= 38) {
		mes "เจ้านายของฉันเป็นคนออกแบบคอร์สฝึกฝนพวกนี้ขึ้นมาเองทั้งหมดเลย";
		mes "หลังจากที่เขาสร้างชุดยูนิฟอร์มและเสบียงเสร็จ เขาก็เปลี่ยนใจและบอกว่าให้ฟรีๆ ไม่ได้";
		next;
		mes "[Boya]";
		mes "เฉพาะคนที่แสดงความมุ่งมั่นเพื่อทีมของเราและเพื่อโลกใบนี้เท่านั้นถึงจะได้เสบียงไป";
		mes "นั่นคือเหตุผลที่คอร์สเหล่านี้ถูกสร้างขึ้นมา";
		next;
		mes "[Boya]";
		mes "จริงๆ แล้วเราควรจะมอบเสบียงพวกนี้ให้มือใหม่น่ะนะ";
		mes "แต่ถ้าพวกระดับเทพๆ อยากจะเข้าร่วมการฝึกนี้ เราก็ยินดีต้อนรับ";
		next;
		mes "[Boya]";
		mes "ถึงแม้ว่าชุดยูนิฟอร์มและอุปกรณ์อาจจะดูไร้ค่าสำหรับพวกเขา...";
		mes "แต่การเข้าร่วมการฝึกนี้ หมายความว่าพวกเขาต้องการเป็นส่วนหนึ่งของกลุ่มเรา";
		next;
		mes "[Boya]";
		mes "ใช่... แค่นั้นแหละ";
		mes "นั่นคือสาเหตุว่าตอนเราเลือกดีไซน์หมวกกันน่ะ มันถึงได้ยากลำบากขนาดนั้น";
		next;
		mes "[Boya]";
		mes "จำเรื่องนี้ไว้ให้ดีตอนที่ใช้อุปกรณ์ล่ะ";
		mes "แต่ถ้าเธอตัดสินใจจะขายหรือแลกเปลี่ยนมันทิ้งไป นั่นก็ไม่ใช่เรื่องที่ฉันต้องสนใจหรอก";
	}
	else {
		mes "เธอต้องการอะไรล่ะ?";
		mes "ฉันว่าระดับเธอไม่ต้องฝึกอะไรเพิ่มแล้วนะ";
		next;
		mes "[Boya]";
		mes "ไม่มีอะไรที่ฉันจะสอนเจ้าแห่งการต่อสู้อย่างเธอได้อีกแล้ว";
	}
	close;
S_Quest1:
	mes "จริงเหรอ? ดูเหมือนเธอจะพร้อมแล้วนะ";
	mes "เรามีการฝึกทั้งหมด 3 ขั้น";
	mes "ขอดูหน่อยซิ...";
	next;
	mes "[Boya]";
	if (BaseLevel < 12) {
		mes "อืมม เธอควรไปเก็บเลเวลเพิ่มกว่านี้หน่อยนะ!";
		mes "เธอต้องมีเลเวลอย่างน้อย 12!";
		mes "ขอโทษทีนะ แต่นี่คือกฎ";
		close;
	}
	if (BaseLevel < 20) {
		mes "ขั้นแรกคือ คอร์ส A";
		mes "ชื่อคอร์สคือ 'พิชิตทะเลทราย (Conquer the Desert!)'";
		mes "มันเป็นภารกิจที่เหมาะสมที่สุดสำหรับเลเวลของเธอตอนนี้";
		next;
		mes "[Boya]";
		mes "^4d4dffจะมีเมืองกลางทะเลทรายที่ชื่อว่า Morocc";
		mes "จากที่นั่นให้ลงไปทางใต้แล้วไปทางตะวันออก จะมีโอเอซิสเล็กๆ อยู่ตรงกลางแมพนั้น^000000";
		next;
		mes "[Boya]";
		mes "ถ้าเธอไปที่นั่น เธอจะเจอสุนัขอยู่แถวๆ โอเอซิส";
		mes "เขาลึกลับมากและพูดได้ด้วยนะ อย่าตกใจล่ะ";
		next;
		mes "[Boya]";
		mes "บอกสุนัขตัวนั้นไปว่า ^4d4dffBoya is really great (โบยาสุดยอดไปเลย)^000000";
		mes "ถ้ามีคำถามอะไรก็ถามสุนัขตัวนั้นเอา";
		next;
		mes "[Boya]";
		mes "จ้องหน้าฉันทำไม?";
		mes "ฉันก็ต้องคิดรหัสผ่านขึ้นมาเองบ้างสิ ใช่ไหมล่ะ?";
		mes "รหัสผ่านนี้มันทำไมเหรอ?";
		next;
		mes "[Boya]";
		mes "เอาเถอะ สถานที่นั้นอยู่ไม่ไกลจากที่นี่หรอก เหมาะสำหรับมือใหม่อย่างเธอแล้ว";
		mes "ขอให้ Freya อวยพรเธอนะ~!";
		para_suv01 = 1;
		setquest 7128;
		close;
	}
	if (BaseLevel < 26) {
		mes "ฉันจะส่งเธอไปคอร์ส B ขั้นแรก";
		mes "ชื่อคอร์สคือ 'พิชิตท่อระบายน้ำ (Conquer the Culvert!)'";
		mes "มันเหมาะกับเลเวลเธอที่สุดแล้ว";
		next;
		mes "[Boya]";
		mes "เธอต้องไปลงทะเบียนขอสำรวจท่อระบายน้ำที่ Knight Guild ในเมือง Prontera";
		mes "หลังจากลงทะเบียนแล้ว ให้ไปที่ประตูทางตะวันตกของ Prontera ผู้ดูแลท่อระบายน้ำจะอยู่แถวๆ ทางเข้า";
		next;
		mes "[Boya]";
		mes "เมื่อเข้าไปข้างในท่อระบายน้ำแล้ว เธอจะเจอแมวที่ประจำการอยู่ อย่าตกใจถ้ามันพูดกับเธอนะ";
		next;
		mes "[Boya]";
		mes "บอกแมวตัวนั้นไปว่า ^4d4dffBoya's help is like a giant and beautiful tuna (ความช่วยเหลือของโบยาเปรียบดั่งทูน่ายักษ์ที่สวยงาม)^000000";
		mes "เขาพูดได้และลึกลับมาก อย่าแปลกใจล่ะ";
		mes "เมื่อบอกรหัสผ่านแล้ว เขาจะมอบเป้าหมายการต่อสู้ให้";
		mes "ถ้าสงสัยอะไรก็ถามแมวตัวนั้นซะ";
		next;
		mes "[Boya]";
		mes "จ้องฉันแบบนั้นทำไมอีกล่ะ?";
		mes "มันก็แค่รหัสผ่านที่ฉันคิดขึ้นมาเองน่ะ";
		next;
		mes "[Boya]";
		mes "เอาเป็นว่า...";
		mes "ที่นั่นไม่ไกลจากที่นี่หรอก เหมาะกับมือใหม่ดี";
		mes "ขอให้ Freya อวยพรเธอนะ~!";
		para_suv01 = 6;
		setquest 7133;
		close;
	}
	return;
S_Quest2:
	if (BaseLevel < 33) {
		mes "เยี่ยม";
		mes "เดี๋ยวฉันเลือกที่ที่เหมาะกับเธอให้";
		next;
		mes "[Boya]";
		mes "ฉันจะส่งเธอไปขั้นที่สองของคอร์ส A";
		mes "คอร์สนี้ชื่อว่า 'พิชิตถ้ำผีสิง (Conquer the Ghost Cave!)'";
		mes "เหมาะกับเลเวลเธอที่สุดในตอนนี้แล้ว";
		next;
		mes "[Boya]";
		mes "จะมีหมู่บ้านนักธนู (Archer Village) เล็กๆ อยู่ทางตอนเหนือของ Payon";
		mes "มีถ้ำอยู่บนเนินเขาทางตะวันตกของหมู่บ้านนั้น";
		next;
		mes "[Boya]";
		mes "เรามีคนประจำการอยู่ที่หน้าถ้ำนั่นแล้ว";
		mes "เขาชื่อว่า... เอ่อ...";
		mes "...";
		next;
		mes "[Boya]";
		mes "เอาเป็นว่าเขาเป็นคนของเรา เขาจะรู้จักฉันเองแหละ";
		mes "เขาจะมอบภารกิจให้เธอ";
		mes "ถ้ามีคำถามก็ถามเขาได้";
		next;
		mes "[Boya]";
		mes "จ้องฉันทำไมล่ะ?";
		mes "เราไม่ได้เจอกันตั้งนานแล้ว ฉันก็ต้องมีลืมชื่อกันบ้างสิ!";
		next;
		mes "[Boya]";
		mes "เอาเป็นว่า...";
		mes "ที่นั่นไม่ไกล รีบไปรีบกลับล่ะ";
		mes "ขอให้ Freya อวยพรเธอนะ~!";
		para_suv01 = 13;
		setquest 7138;
		close;
	}
	if (BaseLevel < 40) {
		mes "ฉันจะส่งเธอไปขั้นที่สองของคอร์ส B";
		mes "คอร์สนี้ชื่อว่า 'พิชิตถ้ำมด (Conquer Anthell!)'";
		mes "เหมาะกับเลเวลเธอที่สุดแล้ว";
		next;
		mes "[Boya]";
		mes "เดินทางไปทางตะวันตกเฉียงใต้ของเมือง Morocc";
		mes "ที่นั่นเธอจะเห็นรูบนพื้นดินที่นำไปสู่ถ้ำที่ชื่อว่า Anthell";
		next;
		mes "[Boya]";
		mes "ในนั้นมีมดเยอะมากเลยล่ะ เข้าใจไหม?";
		mes "แล้วมันก็เต็มไปด้วยทราย ระวังตัวด้วยล่ะ";
		next;
		mes "[Boya]";
		mes "นั่นแหละคือสาเหตุที่เขาเรียกมันว่า ขุมนรกมด (Ant Hell)";
		mes "สมาชิกของเราคนหนึ่งรออยู่ที่นั่น";
		mes "เขาชื่อว่า... K? M? หืม? เอาเถอะ ฉันจำไม่ได้";
		next;
		mes "[Boya]";
		mes "เขาเป็นคนของเรา เขาจะรู้จักฉันแน่นอน";
		mes "เขาจะมอบภารกิจให้";
		mes "สงสัยอะไรก็ถามเขาได้เลย";
		next;
		mes "[Boya]";
		mes "จ้องฉันอีกแล้วนะ?";
		mes "ก็บอกว่าไม่ได้เจอกันนานเลยลืมชื่อไงล่ะ!";
		next;
		mes "[Boya]";
		mes "เอาเป็นว่า...";
		mes "ที่นั่นไม่ไกล รีบไปรีบกลับ";
		mes "ขอให้อวยพรนะ~!!";
		para_suv01 = 17;
		setquest 7142;
		close;
	}
	return;
S_Quest3:
	if (BaseLevel < 50) {
		mes "เธอมาถึงขั้นที่สามของคอร์ส A แล้ว";
		mes "คอร์สนี้ชื่อว่า 'พิชิตหมู่บ้าน Orc (Conquer Orc village!)'";
		mes "ขอดูซิ...";
		next;
		mes "[Boya]";
		mes "ออกทางประตูเมือง Prontera แล้วเดินไปทางตะวันตกเรื่อยๆ หมู่บ้าน Orc อยู่ทางนั้นแหละ";
		mes "หรือจะออกทางประตูตะวันตกของ Geffen แล้วมุ่งหน้าไปทางตะวันออกเฉียงใต้ก็ได้นะ";
		next;
		mes "[Boya]";
		mes "อืมม ที่หมู่บ้าน Orc มีพนักงาน Kafra ประจำการอยู่ เธอใช้บริการของพวกเธอได้";
		mes "แล้วแต่เธอจะเลือกเลย";
		next;
		mes "[Boya]";
		mes "สมาชิกของเราคนหนึ่งรออยู่ในอาคารใกล้ๆ กับพนักงาน Kafra นั่นแหละ";
		next;
		mes "[Boya]";
		mes "เธอจะอธิบายเองว่าต้องทำอะไรบ้าง";
		mes "สงสัยอะไรก็ถามเธอได้เลย";
		next;
		mes "[Boya]";
		mes "ขอให้ Freya อวยพรเธอนะ!";
		para_suv01 = 24;
		setquest 7147;
	}
	else if (BaseLevel < 60) {
		mes "เธอมาถึงขั้นที่สามของคอร์ส B แล้ว";
		mes "คอร์สนี้ชื่อว่า 'พิชิตดันเจี้ยน Orc (Conquer Orc dungeon!)'";
		mes "เหมาะกับเลเวลเธอที่สุดแล้ว";
		next;
		mes "[Boya]";
		mes "ออกทางประตูเมือง Prontera แล้วเดินไปทางตะวันตกเรื่อยๆ หมู่บ้าน Orc อยู่ทางนั้น";
		mes "หรือจะออกทางประตูตะวันตกของ Geffen แล้วมุ่งหน้าไปทางตะวันออกเฉียงใต้ก็ได้";
		next;
		mes "[Boya]";
		mes "ที่นั่นมีพนักงาน Kafra อยู่ เธอใช้บริการพวกเธอได้นะ";
		mes "แล้วแต่สะดวกเลย";
		next;
		mes "[Boya]";
		mes "สมาชิกของเราคนหนึ่งรออยู่ในอาคารใกล้ๆ กับพนักงาน Kafra นั่นแหละ";
		next;
		mes "[Boya]";
		mes "เธอจะอธิบายภารกิจให้ฟังเอง";
		mes "สงสัยอะไรก็ถามเธอซะ";
		next;
		mes "[Boya]";
		mes "ขอให้ Freya อวยพรเธอนะ!";
		para_suv01 = 29;
		setquest 7152;
	}
	else {
		mes "เธอมาถึงขั้นสุดท้ายแล้วนะ";
		mes "คอร์สนี้ชื่อว่า 'พิชิตเมืองใต้สมุทร (Conquer the Ocean City!)'";
		mes "ฉันไม่แน่ใจว่ามันเหมาะกับเธอไหม แต่ก็นะ มันเป็นคอร์สสุดท้ายของการฝึกเราแล้วล่ะ";
		next;
		mes "[Boya]";
		mes "ก่อนอื่น นั่งเรือจาก Izlude ไปที่เกาะ Byalan นะ!";
		mes "มีถ้ำใต้ดินอยู่ เข้าไปจนถึงชั้นล่างสุด เธอจะพบเมืองใต้น้ำในตำนาน...";
		next;
		mes "[Boya]";
		mes "ถึงมันจะอยู่ใต้น้ำ แต่เธอหายใจได้ปกติ ไม่ต้องห่วง";
		mes "มีผู้ฝึกฝนประจำการอยู่แถวๆ ทางเข้าเมืองใต้น้ำนั่นแหละ";
		next;
		mes "[Boya]";
		mes "บอกเขาว่าฉันส่งมา แล้วทำตามที่เขาสั่งซะ";
		mes "ขอให้ Freya อวยพรเธอนะ!";
		para_suv01 = 33;
		setquest 7156;
	}
	close;
}

moc_fild11,180,253,5	script	Talking Dog#para03	972,{
	if (para_suv01 == 1) {
		mes "[Talking Dog]";
		mes "กรู้ววววววววว...";
		mes "บรื๊ออออออออ...";
		mes "โฮ่ง โฮ่ง..";
		next;
		mes "[Talking Dog]";
		mes "ว่าไง?";
		mes "นั่นเธอสินะ!";
		mes "สมาชิกของกลุ่ม Eden Group";
		mes "เอาข้าวมาให้ฉันหรือเปล่า? ฉันไม่ชอบกินเครื่องดื่มนะ";
		next;
		switch (select("Boya is really perfect (โบยาสมบูรณ์แบบที่สุด):Boya is really great (โบยาสุดยอดไปเลย):Boya is really soft (โบยานุ่มนิ่มไปหมด)")) {
		case 1:
			mes "[Talking Dog]";
			mes "หือ...";
			mes "เธอพูดเรื่องอะไรของเธอน่ะ";
			close;
		case 2:
			mes "[Talking Dog]";
			mes "อ๊ะ... อืม...";
			mes "ให้ตายสิ เธอมาที่นี่เพื่อเข้าร่วมการฝึกงั้นเหรอ?";
			mes "ถ้าโบยาส่งเธอมา เธอก็คงรู้แล้วสินะว่ามันคือการฝึกต่อสู้";
			mes "บรู๊วววว...";
			next;
			mes "[Talking Dog]";
			mes "ถ้าอย่างนั้น ฉันควรเริ่มทำให้โอเอซิสแห่งนี้ปลอดภัยขึ้นกว่านี้ดีไหม?";
			mes "เห็นพวก Condor ที่บินอยู่นั่นไหม?";
			next;
			mes "[Talking Dog]";
			mes "ช่วยไล่พวกมันไปให้ฉันหน่อยได้ไหมล่ะ?";
			mes "คนจะได้มาใช้โอเอซิสแห่งนี้ได้อย่างปลอดภัยและสบายใจขึ้น";
			next;
			mes "[Talking Dog]";
			mes "เราควรจะล่าพวก Condor อย่างน้อย 10 ตัวนะ ตกลงไหม?";
			mes "ฉันขอแอบงีบสักพักล่ะ";
			mes "ครอกกกก... โฮ่ง...";
			set para_suv01,2;
			changequest 7128,7129;
			close;
		case 3:
			mes "[Talking Dog]";
			mes "เธอโอเคเปล่าเนี่ย?";
			mes "ไม่เคยเห็นสุนัขพูดได้หรือไง?";
			mes "พูดเรื่องอะไรนุ่มๆ นิ่มๆ ของเธอเนี่ย?";
			close;
		}
	}
	if (para_suv01 == 2) {
		if (checkquest(7129,HUNTING) == 2) {
			mes "[Talking Dog]";
			mes "โฮ่งงงงง...";
			mes "เยี่ยมไปเลย!";
			mes "ฉันสัมผัสได้ถึงพลังวัยหนุ่มจากตัวเธอเลยนะเนี่ย";
			next;
			mes "[Talking Dog]";
			mes "ช่างมันเถอะ";
			mes "หาเป้าหมายต่อไปกันดีกว่า!";
			mes "โอเค... มาช่วยกันไล่พวก Desert Wolf ออกไปจากที่นี่กัน";
			next;
			mes "[Talking Dog]";
			mes "ถ้าพวกมันโตขึ้น มันจะกลายเป็นตัวอันตรายมากเลยนะ";
			mes "......";
			next;
			mes "[Talking Dog]";
			mes "ทำไม... ทำไมมองฉันแบบนั้นล่ะ?";
			mes "พวกนั้นมันหมาป่า ส่วนฉันน่ะหมาใจดี";
			mes "แต่อยากจะบอกว่าฉันไม่ได้เป็นหมาแบบนี้มาทั้งชีวิตหรอกนะ";
			next;
			mes "[Talking Dog]";
			mes "ฉันจะพิสูจน์ให้ดูว่าเธอไม่ต้องมาเวทนาฉันเลยสักนิด";
			next;
			mes "[Talking Dog]";
			mes "พวกหมาป่านั่นน่ะฉี่ไปทั่วแถมไม่มียางอายเลย";
			mes "ตอนเด็กๆ ก็ทำเป็นกระดิกหาง พอโตขึ้นมาหน่อยก็กัดคนไปทั่วไม่เลือกหน้า!";
			next;
			mes "[Talking Dog]";
			mes "เธอต้องล่าพวกมันอย่างน้อย 10 ตัว!";
			mes "10 ตัวเป๊ะๆ เลยนะ!";
			mes "ไปเลยๆๆ!";
			set para_suv01,3;
			changequest 7129,7130;
			close;
		}
		mes "[Talking Dog]";
		mes "ก่อนอื่น ไปจัดการเจ้านกหัวล้านที่ส่งเสียงดังพวกนั้นก่อน";
		mes "ใช่แล้ว Condor ไงล่ะ";
		mes "ฆ่า Condor 10 ตัว ดูเหมือนจะง่ายไปใช่ไหม?";
		mes "อ้อ ถ้าเหนื่อยล่ะก็ ฉันช่วยเธอได้นะ";
		next;
		mes "-เมื่อสุนัขเห่า HP และ SP ของคุณก็ฟื้นฟูจนเต็ม-";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 3) {
		if (checkquest(7130,HUNTING) == 2) {
			mes "[Talking Dog]";
			mes "เธอนี่มันสมบูรณ์แบบจริงๆ";
			next;
			mes "[Talking Dog]";
			mes "หรือเปล่านะ ฮี่ฮี่ เอาเป็นว่าขอบใจมากที่ช่วย";
			mes "ตอนนี้โอเอซิสเกือบจะปลอดภัยร้อยเปอร์เซ็นต์แล้ว";
			mes "โอเค ขั้นตอนสุดท้าย!";
			next;
			mes "[Talking Dog]";
			mes "มีมอนสเตอร์น่ารำคาญตัวหนึ่งที่ชอบซ่อนตัวอยู่ในทราย แล้วก็โผล่มาฉีดพิษใส่คนแบบไม่ทันตั้งตัว";
			next;
			mes "[Talking Dog]";
			mes "สิ่งที่สวยงามมักมีพิษสงเสมอ แต่เจ้านี่น่ะถึงตายเลยล่ะ";
			mes "ไปจัดการเจ้า Scorpion ที่ได้ชื่อว่าเป็นยาพิษแห่งทะเลทรายซะ!";
			next;
			mes "[Talking Dog]";
			mes "มันเป็นขั้นสุดท้ายแล้ว งั้นเอาแบบง่ายๆ เลยละกัน";
			mes "ล่าแค่ 5 ตัวพอ!";
			mes "โฮ่ง โฮ่ง!";
			set para_suv01,4;
			changequest 7130,7131;
			close;
		}
		mes "[Talking Dog]";
		mes "ฉันไม่อยากให้เธอมาทำหน้าสงสารฉันนะ";
		mes "ฉันเป็นหมาที่มีหัวใจทองคำเชียวนะจะบอกให้";
		next;
		mes "[Talking Dog]";
		mes "หลังจากล่า Desert Wolf ครบ 10 ตัวแล้วค่อยกลับมานะ";
		mes "อ้อ ถ้าเหนื่อยล่ะก็ ฉันช่วยเธอได้";
		next;
		mes "-เมื่อสุนัขเห่า HP และ SP ของคุณก็ฟื้นฟูจนเต็ม-";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 4) {
		if (checkquest(7131,HUNTING) == 2) {
			mes "[Talking Dog]";
			mes "อืม... ยอดเยี่ยม";
			mes "เธอนี่มันเจ๋งจริงๆ!";
			next;
			mes "[Talking Dog]";
			mes "ขอบคุณในความพยายามของเธอนะ ตอนนี้โอเอซิสปลอดภัยแล้ว";
			mes "ไม่ต้องหันไปมองรอบๆ เลย!";
			mes "ถ้าฉันบอกว่าปลอดภัย มันก็คือปลอดภัย!";
			mes "งึดๆๆๆๆ อ๊างงง!";
			next;
			mes "[Talking Dog]";
			mes "นี่ๆ ฟังนะ";
			mes "ถ้าฉันขุดลึกลงไปเรื่อยๆ ฉันอาจจะเจอ Scorpion อีกก็ได้ แต่...";
			mes "ที่แน่ๆ โอเอซิสนี้ปลอดภัยขึ้นแน่นอน";
			next;
			mes "[Talking Dog]";
			mes "จงภูมิใจและมั่นใจในตัวเองเข้าไว้ แล้วทำให้เต็มที่ล่ะ";
			mes "เธอช่วยให้การพักผ่อนของฉันสบายขึ้นเยอะเลย";
			next;
			mes "[Talking Dog]";
			mes "เธอได้ช่วยพิชิตทะเลทรายนี้แล้ว";
			mes "และผ่านการฝึกระดับเริ่มต้นเรียบร้อย ฉันจะประทับรอยเท้าให้ละกัน";
			mes "แฮ่รรรรรร... ฮึบ!";
			next;
			mes "[Talking Dog]";
			mes "กลับไปที่สำนักงานใหญ่ของ Eden Group แล้วเอารอยเท้านี้ไปให้ Rune Knight มาดเท่นั่นดูซะ";
			mes "ขอย้ำอีกครั้งว่าเธอสุดยอดมาก!";
			mes "โฮ้ววววววววววว~";
			set para_suv01,5;
			changequest 7131,7132;
			close;
		}
		mes "[Talking Dog]";
		mes "ล่า Scorpion แค่ 5 ตัวเท่านั้น";
		mes "เราจะได้คืนความสงบสุขให้กับโอเอซิสแห่งนี้เสียที";
		next;
		mes "[Talking Dog]";
		mes "เวลาฉันงีบหลับ พวกมันจะได้ไม่มาแอบกัดหางฉันอีก";
		mes "แล้วเท้าของฉันจะได้ไม่เจ็บเพราะพวกมันด้วย";
		next;
		mes "[Talking Dog]";
		mes "เธอไปสู้เถอะ";
		mes "ส่วนฉันจะได้พักผ่อนได้สบายใจขึ้น";
		mes "คนอื่นๆ ก็จะได้ปลอดภัยด้วยเหมือนกัน";
		next;
		mes "[Talking Dog]";
		mes "ทุกคนจะต้องนึกถึงกลุ่ม Eden Group ในแง่ดีแน่ๆ";
		mes "อ้อ ถ้าเหนื่อยล่ะก็ ฉันช่วยเธอได้";
		next;
		mes "-เมื่อสุนัขเห่า HP และ SP ของคุณก็ฟื้นฟูจนเต็ม-";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 5) {
		mes "[Talking Dog]";
		mes "กึงงงงงงงงงง...";
		mes "อุ๊ยยย.. แฮ่ก";
		next;
		mes "[Talking Dog]";
		mes "ยังอยู่แถวนี้อีกเหรอ?";
		mes "ธุระของเธอที่นี่เสร็จแล้วนะ";
		mes "โฮ้ววว โฮ่ง โฮ่ง";
		close;
	}
	if (para_suv01 > 5) {
		mes "[Talking Dog]";
		mes "เฮ้ เพื่อน~ เป็นไงบ้าง?";
		mes "ทาง Rune Knight ว่าไงบ้างล่ะ?";
		mes "แฮ่รรร...";
		mes "แฮ่ก แฮ่ก...";
		next;
		mes "[Talking Dog]";
		mes "Eden Group นี่มันเจ๋งจริงๆ";
		mes "เป็นกลุ่มที่ดีมากเลยนะ";
		mes "พวกเขายอมรับสุนัขพูดได้พเนจรแบบฉันด้วย";
		mes "โชคดีนะ ดูแลตัวเองด้วยล่ะ";
		close;
	}
	mes "เฮ้ ดูนี่สิ";
	mes "ฉันเป็นหมาพูดได้นะ";
	mes "ไม่ใช่หมาป่าด้วย";
	mes "จริงๆ แล้วแต่ก่อนฉันไม่ได้เป็นหมาหรอกนะ...";
	next;
	if (callfunc("F_HasEdenGroupMark")) {
		mes "[Talking Dog]";
		mes "เอาเป็นว่าเธอเป็นสมาชิกของ Eden Group งั้นเหรอ?";
		mes "โอ้ ยินดีที่ได้รู้จัก";
		mes "ฉันเองก็เป็นหนึ่งในสมาชิกเหมือนกัน";
		mes "โชคดีนะ เดินทางปลอดภัยล่ะ";
		close;
	}
	mes "[Talking Dog]";
	mes "มองฉันแบบนั้นทำไมเหรอ?";
	mes "......";
	close;
}

prt_sewb1,131,262,3	script	Timid Cat#para04	422,{
	if (para_suv01 < 6) {
		mes "[Timid Cat]";
		mes "เมี๊ยว...";
		mes "คุณเป็นใครเมี๊ยว?";
		mes "มาทำอะไรที่นี่เมี๊ยว?";
		close;
	}
	if (para_suv01 == 6) {
		mes "[Timid Cat]";
		mes "มาเถอะเมี๊ยว...";
		mes "ถึงฉันจะมายืนคุยกับคุณแบบนี้เมี๊ยว แต่ฉันก็ยังเป็นแค่แมวนะเมี๊ยว...";
		next;
		mes "[Timid Cat]";
		mes "คุณมนุษย์ผู้ใจดี คุณคือ...";
		mes "สมาชิกในกลุ่มของฉันใช่ไหม?";
		mes "จะ... จริงเหรอ...!";
		mes "ความช่วยเหลือของโบยาผู้แสนใจดีเปรียบดั่งยักษ์และ...";
		next;
		switch (select("ปลาซัมมะที่แสนสวย:ปลาแมคเคอเรลที่สดใหม่:ปลาทูน่าที่แสนสวย")) {
		case 1:
			mes "[Timid Cat]";
			mes "ปลาซัมมะตัวใหญ่และแสนสวย...";
			mes "ฉันขี้อายนะเมี๊ยว";
			close;
		case 2:
			mes "[Timid Cat]";
			mes "ฉันอยากกินปลาแมคเคอเรล";
			mes "ปลาแมคเคอเรลตัวใหญ่ๆ และสดใหม่อยู่ที่ไหนเมี๊ยว?";
			close;
		case 3:
			mes "[Timid Cat]";
			mes "คุณรู้จักปลาทูน่าตัวใหญ่และแสนสวยด้วยเหรอเมี๊ยว?";
			mes "โบยาผู้แสนใจดีส่งคุณมาที่นี่แน่นอนเลย";
			mes "ฉันจะอธิบายยังไงดีนะ...?";
			next;
			mes "[Timid Cat]";
			mes "เมื่อกี้มีอะไรวิ่งผ่านเท้าฉันไปหรือเปล่าเมี๊ยว?";
			mes "คุณอยากฆ่าแมวหรือไงเมี๊ยว";
			mes "สภาพแวดล้อมที่นี่มันแย่สุดๆ ไปเลยเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "เอาล่ะ ฉันจะทำตามงานที่ได้รับมอบหมายมาละกัน";
			mes "งั้นก็... หวัดดี ยินดีต้อนรับนะเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "คุณมาที่นี่เพื่อฝึกต่อสู้ใช่ไหมเมี๊ยว?";
			mes "รีบไปจัดการเจ้า Thief Bug พวกนั้นเร็วเข้า!";
			mes "ล่ามาอย่างน้อย 10 ตัวนะ!";
			mes "เมี๊ยววว!";
			next;
			mes "[Timid Cat]";
			mes "ฉันไม่ชอบพวกแมลงยั้วเยี้ยน่ารังเกียจพวกนั้นเลยจริงๆ...";
			mes "เมี๊ยว~!";
			next;
			mes "^4d4dffเจ้าแมวตกใจกลัว";
			mes "Thief Bug จนแข็งทื่อ";
			mes "ไปล่าพวก Thief Bug";
			mes "แถวๆ นี้ให้เขาหน่อยเถอะ^000000";
			set para_suv01,7;
			changequest 7133,7134;
			close;
		}
	}
	if (para_suv01 == 7) {
		if (checkquest(7134,HUNTING) == 2) {
			mes "[Timid Cat]";
			mes "ตอนนี้คุณเข้าใจถึงความสกปรกและชื้นแฉะของท่อระบายน้ำใต้ดินหรือยังเมี๊ยว?";
			mes "แหวะ... มาดูเป้าหมายต่อไปกันเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "มันคือสัญลักษณ์แห่งความสกปรกพอๆ กับเจ้า Thief Bug เลยล่ะ";
			mes "ไปล่าเจ้า Tarou เพื่อทำให้ท่อระบายน้ำสะอาดขึ้นหน่อยเถอะเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "สำหรับสมาชิกที่เข้าร่วมภารกิจนี้";
			mes "ไปล่า Tarou 10 ตัวนะเมี๊ยว";
			mes "ง่ายใช่ไหมล่ะเมี๊ยว?";
			next;
			mes "[Timid Cat]";
			mes "ทำไมฉันไม่สั่งทีเดียวให้หมดน่ะเหรอ? คิกคิก..??";
			mes "อืมมม........";
			mes "ก็เพราะมันคือภารกิจฝึกฝนยังไงล่ะเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "ภารกิจฝึกฝนน่ะ ทั้งเหนื่อยและน่ารำคาญแบบนี้แหละเมี๊ยว";
			mes "เพราะฉะนั้นรีบไปล่า Tarou 10 ตัวได้แล้วเมี๊ยว";
			set para_suv01,8;
			changequest 7134,7135;
			close;
		}
		mes "[Timid Cat]";
		mes "มาทำความสะอาดท่อระบายน้ำกัน ขั้นแรกคือล่า Thief Bug";
		mes "ง่ายๆ แค่นั้นเองเมี๊ยว ใช่ไหมล่ะ?";
		next;
		mes "[Timid Cat]";
		mes "ระวังและคอยเช็คแผนที่ด้วยนะเมี๊ยว จะได้ไม่หลงทาง";
		mes "นี่คือบริการพิเศษเมี๊ยววว";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 8) {
		if (checkquest(7135,HUNTING) == 2) {
			mes "[Timid Cat]";
			mes "เก่งมากเลยเมี๊ยว~!";
			mes "คุณกำจัดพวกแมลงสกปรกและ Tarou ไปได้หมดเลย คุณนี่กล้าหาญจังเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "ทีนี้รวบรวมความกล้าไว้ล่ะ เพราะฉันจะส่งคุณไปหาคู่ต่อสู้ที่แข็งแกร่งขึ้นเมี๊ยว";
			mes "แต่ก่อนอื่น เพื่อทดสอบความกล้าของคุณ ไปล่า Familiar ซะเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "พวก Familiar มันกัดด้วยนะ ระวังตัวด้วยเมี๊ยว";
			mes "พวกมันนิสัยเสียเมี๊ยว";
			mes "พวกมันทำให้ฉันกลัว เพราะฉะนั้นล่าแค่ 5 ตัวก็พอแล้วเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "นั่นจะพิสูจน์ให้ฉันเห็นว่าคุณน่ะกล้าหาญเมี๊ยว";
			mes "เมี๊ยว~!";
			next;
			mes "[Timid Cat]";
			mes "ฉันไม่มีอะไรจะให้หรอกนะ...";
			mes "......";
			mes "มองอะไรเมี๊ยว? Familiar รอสู้กับคุณอยู่ รีบไปสิ ไป๊~!";
			set para_suv01,9;
			changequest 7135,7136;
			close;
		}
		mes "[Timid Cat]";
		mes "ถ้าคุณจัดการ Tarou ไม่ได้ คุณอาจจะติดเชื้อโรคสกปรกทุกชนิดเลยนะเมี๊ยว";
		mes "เพราะฉะนั้นจงภูมิใจในตัวเองและพยายามจัดการพวกมันให้ได้เมี๊ยว";
		next;
		mes "[Timid Cat]";
		mes "ฉันจะช่วยคุณนิดหน่อยละกัน";
		mes "นี่แน่ะ ฉันฟื้นฟูพลังให้แล้วนะเมี๊ยว..";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 9) {
		if (checkquest(7136,HUNTING) == 2) {
			mes "[Timid Cat]";
			mes "คุณสุดยอดไปเลยเมี๊ยว~";
			mes "จัดการพวกมันได้เร็วมาก!";
			mes "เมี๊ยว...";
			next;
			mes "[Timid Cat]";
			mes "ตอนนี้คุณคงไม่กลัวพวกแมลงหรือหนู Tarou แล้วล่ะเมี๊ยว";
			mes "ขอบคุณที่เข้าร่วมภารกิจฝึกฝนพิชิตท่อระบายน้ำนะเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "ทีนี้กลับไปที่สำนักงานใหญ่แล้วถามครูฝึกโบยาให้แน่ใจนะ ว่าเขามีปลาทูน่าแสนสวยเตรียมไว้ให้ฉัน...";
			mes "ฉันจะรออยู่ที่นี่นะเมี๊ยว";
			next;
			mes "[Timid Cat]";
			mes "รู้จักทางกลับสำนักงานใหญ่ Eden Group ใช่ไหมเมี๊ยว?";
			mes "เมืองที่ใกล้ที่สุดคือ Prontera";
			mes "ไปที่ Prontera แล้วหาเจ้าหน้าที่วาร์ปของ Eden Group ซะเมี๊ยว";
			set para_suv01,10;
			changequest 7136,7137;
			close;
		}
		mes "[Timid Cat]";
		mes "พวก Familiar น่ากลัวจริงๆ เมี๊ยว";
		mes "พวกมันบินว่อนไปหมดเลย";
		next;
		mes "[Timid Cat]";
		mes "นี่เป็นคอร์สสุดท้ายแล้ว สู้ๆ นะเมี๊ยว";
		mes "ฉันจะช่วยคุณนิดหน่อยละกัน";
		mes "นี่แน่ะ ฉันฟื้นฟูพลังให้แล้วนะเมี๊ยว..";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 >= 10) {
		mes "[Timid Cat]";
		mes "คุณทำภารกิจ 'พิชิตท่อระบายน้ำ' เสร็จสิ้นแล้วเมี๊ยว";
		mes "กลับไปที่สำนักงานใหญ่ Eden Group เพื่อรายงานตัวกับโบยานะเมี๊ยว";
		next;
		mes "[Timid Cat]";
		mes "โบยาอาจจะแอบกินปลาทูน่าของฉันไประหว่างที่รอคุณก็ได้เมี๊ยว";
		close;
	}
	mes "[Timid Cat]";
	mes "เป็นยังไงบ้างเมี๊ยว?";
	mes "คุณอาสามาพิชิตท่อระบายน้ำงั้นเหรอเมี๊ยว?";
	mes "คุณคือสมาชิกของ Eden Group แน่นอนเลยเมี๊ยว";
	mes "คุณกำลังช่วยทำให้โลกนี้ดีขึ้นนะเมี๊ยว";
	close;
}

pay_arche,41,136,3	script	Eden Member Karl#para05	904,{
	if (para_suv01 < 13) {
		if (callfunc("F_HasEdenGroupMark")) {
			mes "[Karl]";
			mes "เฮ้ เป็นยังไงบ้าง?";
			mes "ยินดีที่ได้เจอนะ~";
			mes "กำลังจะเข้าไปข้างในเหรอ?";
			close;
		}
		mes "[Karl]";
		mes "หืม...?";
		mes "เธอไม่ใช่คนที่ฉันกำลังรออยู่นี่นา";
		close;
	}
	if (para_suv01 == 13) {
		mes "[Karl]";
		mes "สวัสดีครับ?";
		mes "ฉันได้รับรายงานมาแล้ว และกำลังรอเธออยู่พอดีเลย";
		mes "เธอมาที่นี่เพื่อเข้าร่วมภารกิจฝึกฝนใช่ไหมล่ะ?";
		next;
		mes "[Karl]";
		mes "เคยเข้าไปในถ้ำนี้มาก่อนหรือเปล่า?";
		mes "ไม่รู้ว่าเธอเคยได้ยินตำนานของหมู่บ้านนี้มาบ้างไหมนะ";
		next;
		mes "[Karl]";
		mes "ที่นี่มีวิญญาณคนตายมากมายที่แปลงกายมาหลอกลวงผู้คนเหมือนยังมีชีวิตอยู่ แล้วก็คอยข่มขวัญชาวบ้านน่ะ";
		mes "แย่สุดๆ ไปเลยว่าไหมล่ะ?";
		next;
		mes "[Karl]";
		mes "นั่นคือเหตุผลที่เราเลือกที่นี่เป็นขั้นที่ 2 ของภารกิจฝึกต่อสู้";
		mes "ขั้นนี้มีชื่อว่า 'พิชิตถ้ำผีสิง (Conquer Ghost Cave)~'";
		mes "มาช่วยทำให้ชาวบ้านได้นอนหลับพักผ่อนอย่างสบายใจกันเถอะ";
		next;
		mes "[Karl]";
		mes "ข้างในนั้นมีอันตรายอยู่เยอะเลยล่ะ อืม...";
		mes "แต่สำหรับเธอ แค่ชั้น 1 ก็น่าจะเพียงพอแล้ว";
		next;
		mes "[Karl]";
		mes "เอาล่ะ มาพิชิตถ้ำผีสิงอันตรายนี่กัน...";
		mes "ไปกำจัดเจ้า Skeleton ที่เป็นกระดูกพวกนั้นซะ";
		next;
		mes "[Karl]";
		mes "พวกกระดูกนั่นก็มาจาก Skeleton นั่นแหละ";
		mes "จริงๆ Skeleton กับกระดูกธรรมดามันก็เหมือนกันละนะ แต่ว่า...";
		next;
		mes "[Karl]";
		mes "Skeleton ถือเป็นมอนสเตอร์ประเภท Undead (ผีดิบ) ขั้นพื้นฐาน";
		mes "จำไว้ว่าพวกผีดิบไม่มีวันตาย... ไปล่า Skeleton 15 ตัวซะ";
		next;
		mes "[Karl]";
		mes "ถ้ารู้สึกว่าตกอยู่ในอันตราย อย่าลังเลที่จะถอยออกมานะ";
		mes "เธอไม่ได้กังวลเรื่องจะบาดเจ็บใช่ไหมล่ะ?";
		set para_suv01,14;
		changequest 7138,7139;
		close;
	}
	if (para_suv01 == 14) {
		if (checkquest(7139,HUNTING) == 2) {
			mes "[Karl]";
			mes "พอจะเข้าใจการทำงานของพวก Undead หรือยัง?";
			mes "อย่างที่รู้ว่าผีดิบไม่มีวันตาย ดังนั้น พรจากผู้ที่มีชีวิตอยู่จึงเปรียบเสมือนคำสาปสำหรับพวกมัน";
			next;
			mes "[Karl]";
			mes "ดังนั้น... ทักษะที่ใช้ช่วยเหลือคนอย่าง Heal หรือ Resurrection (ชุบชีวิต)";
			mes "สิ่งเหล่านั้นจึงเป็นการโจมตีที่รุนแรงมากสำหรับพวกประเภท Undead";
			next;
			mes "[Karl]";
			mes "เอาล่ะ... เรื่องนั้นช่างมันก่อน..";
			mes "ต่อไป... ฉันเดาว่าเธอคงเห็นตอนที่กำลังสู้กับ Skeleton แล้วล่ะ";
			mes "มีเจ้าตัวเขียวๆ บางตัวคอยไล่เก็บของที่ดรอปจากมอนสเตอร์ที่ตายแล้วไปทั่วเลย";
			next;
			mes "[Karl]";
			mes "รู้จัก Poring ไหม? เจ้าพวกนั้นน่าจะเป็นญาติๆ กันนะ";
			mes "หน้าตามันดูไม่ค่อยดีเท่าไหร่ สงสัยจะกินยาพิษหรืออะไรเข้าไปแน่ๆ";
			next;
			mes "[Karl]";
			mes "โอเค ถ้าพร้อมแล้วก็ไปจัดการพวก Poporing ซะ";
			mes "ล่ามาให้ได้ 10 ตัวนะ";
			next;
			set para_suv01,15;
			changequest 7139,7140;
			close;
		}
		mes "[Karl]";
		mes "Skeleton เป็นผีดิบขั้นพื้นฐาน";
		mes "ใช้ทักษะ Heal หรือ Resurrection จัดการมันสิ";
		mes "ถ้าเธอทำได้นะ";
		next;
		mes "[Karl]";
		mes "เพื่อความปลอดภัย ฉันจะฟื้นฟูพลังให้เธอเอง";
		mes "ระวังตัวด้วยล่ะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 15) {
		if (checkquest(7140,HUNTING) == 2) {
			mes "[Karl]";
			mes "ได้ของที่ Poporing ขโมยไปคืนมาไหม?";
			mes "ถ้ำนี้ลึกมาก และมีของมีค่ามากมายที่พวกมันอาจจะเก็บไปได้";
			next;
			mes "[Karl]";
			mes "เธอทำได้ดีมากจริงๆ ยอดเยี่ยมไปเลย";
			mes "ตอนนี้ชั้น 1 ปลอดภัยขึ้นแล้ว ขอบใจที่ช่วยนะ";
			next;
			mes "[Karl]";
			mes "เธอทำภารกิจฝึกฝนครบหมดแล้ว กลับไปที่สำนักงานใหญ่และไปรับการอนุมัติจาก Rune Knight มาดเท่คนนั้นซะ";
			next;
			mes "[Karl]";
			mes "เธออาจจะได้ชุดยูนิฟอร์มใหม่ด้วยนะ";
			mes "ฮ่าๆ ฉันจะคอยติดตามดูความก้าวหน้าของเธอนะ";
			set para_suv01,16;
			changequest 7140,7141;
			close;
		}
		mes "[Karl]";
		mes "Poporing น่ะแข็งแกร่งกว่าที่เธอคิดนะ";
		mes "ถ้าเธอปฏิบัติกับมันเหมือน Poring ทั่วไป เธอจะลำบากเอาได้";
		next;
		mes "[Karl]";
		mes "เพื่อความปลอดภัย ฉันจะฟื้นฟูพลังให้เธอเอง";
		mes "ระวังตัวด้วยล่ะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 >= 16) {
		mes "[Karl]";
		mes "ฉันแจ้งเรื่องไปทางโบยาที่สำนักงานใหญ่ Eden Group แล้วล่ะ";
		mes "ถ้าเธอไปที่นั่น เขาคงจะต้อนรับเธออย่างดีแน่นอน";
		next;
		mes "[Karl]";
		mes "เธอจะได้ชุดใหม่แล้วนะ ไม่ตื่นเต้นเหรอ?";
		mes "ฮ่าๆๆ..";
		close;
	}
	mes "[Karl]";
	mes "กำลังกำจัดพวกผีดิบอยู่เหรอ?";
	mes "พยายามเข้าล่ะ เพื่อทำให้โลกนี้ปลอดภัยขึ้น";
	mes "นั่นคือหนึ่งในเป้าหมายของกลุ่ม Eden Group ของเรา";
	close;
}

anthell01,29,264,5	script	Eden Member Cloud#para06	899,{
	if (para_suv01 < 17) {
		if (callfunc("F_HasEdenGroupMark")) {
			mes "[Cloud]";
			mes "อุ๊ย";
			mes "เธอเป็นสมาชิกในกลุ่มของฉันนี่นา";
			mes "ทำไมถึงมาที่นี่ล่ะ มาตามหาอันตรายหรือไง?";
			next;
			mes "[Cloud]";
			mes "หนึ่งในมอนสเตอร์ระดับบอสที่แข็งแกร่งที่สุดอยู่ที่นี่นะ";
			mes "ระวังตัวด้วยล่ะเวลาสำรวจแถวนี้";
			close;
		}
		mes "[Cloud ]";
		mes "มองอะไรเหรอ?";
		mes "เราไม่ได้รู้จักกันใช่ไหม...";
		close;
	}
	if (para_suv01 == 17) {
		mes "[Cloud]";
		mes "สวัสดีครับ?";
		mes "มาทำอะไรที่นี่เหรอ อยากเจออันตรายหรือไง?";
		next;
		switch (select("พิชิต Ant Hell:แค่อยากมาเจอคุณน่ะ:ที่นี่คือที่ไหน?")) {
		case 1:
			mes "[Cloud]";
			mes "หือ พิชิต Ant Hell งั้นเหรอ? เธอพูดแบบนั้นใช่ไหม?";
			mes "เธอคือผู้รับการฝึกสินะ";
			mes "มาถูกที่แล้วล่ะ";
			next;
			mes "[Cloud]";
			mes "ก่อนอื่น ฉันจะอธิบายเกี่ยวกับ Ant Hell ให้ฟัง";
			mes "พวกมดพากันขุดถ้ำในทรายจนเชื่อมต่อกับพื้นผิวโลก นั่นแหละคือ Ant Hell";
			next;
			mes "[Cloud]";
			mes "ทรายจะค่อยๆ ไหลลงไปในรูนั้นช้าๆ";
			mes "ถ้าใครเผลอไปเหยียบโดนรูเข้า ก็จะตกลงไปในถ้ำผ่านรูทรายพวกนั้น..";
			next;
			mes "[Cloud]";
			mes "มันยากนะที่จะรอดชีวิตออกมาจากที่นั่นได้";
			mes "แต่สำหรับพวกเรามันดูเหมือนเรื่องปกติไปแล้วล่ะ ไม่ได้คอขาดบาดตายเหมือนคนทั่วไป";
			next;
			mes "[Cloud]";
			mes "มีมดหลายชนิดมาอาศัยอยู่ที่นี่";
			mes "แถมยังมีบอสมอนสเตอร์ที่ชื่อว่า Maya ด้วย เพราะฉะนั้นเธอต้องระมัดระวังให้มาก";
			next;
			mes "[Cloud]";
			mes "โอเค งั้นลองมาล่ามดตัวที่อ่อนแอที่สุดก่อนละกัน";
			mes "Pierre (ปิแอร์) คือชื่อมดที่อ่อนแอที่สุด";
			mes "พวกมันอยู่แถวๆ ข้างล่างนี้แหละ";
			next;
			mes "[Cloud]";
			mes "สำหรับพวกมดเนี่ย ถ้าพวกมันตัวใดตัวหนึ่งถูกโจมตี ตัวอื่นจะเข้ามาช่วยกันทันทีนะ";
			mes "ระวังตัวด้วยล่ะ แล้วไปกำจัด Pierre 15 ตัว";
			set para_suv01,18;
			changequest 7142,7143;
			close;
		case 2:
			mes "[Cloud]";
			mes "......";
			mes "ฮ่าๆ... ล้อเล่นหรือเปล่าเนี่ย?";
			mes "เธอคงจะเพลียเพราะอากาศที่ร้อนจัดในทะเลทรายสินะ";
			next;
			mes "[Cloud]";
			mes "ฉันเป็นคนยุ่งนะ";
			mes "มีเด็กฝึกคนหนึ่งวางแผนจะมาหาฉัน แต่ป่านนี้ยังไม่โผล่มาเลย ฉันเริ่มจะกังวลแล้วเนี่ย";
			close;
		case 3:
			mes "[Cloud]";
			mes "ที่นี่ที่ไหนงั้นเหรอ?";
			mes "เธออยู่ที่ Ant Hell ทางตะวันตกเฉียงใต้ของเมือง Morocc ไง";
			mes "Morocc คือเมืองที่ใกล้ที่สุด อยู่ทางตะวันออกเฉียงเหนือนี่เอง";
			close;
		}
	}
	if (para_suv01 == 18) {
		if (checkquest(7143,HUNTING) == 2) {
			mes "[Cloud]";
			mes "เฮ้ เป็นยังไงบ้าง?";
			mes "ตอนที่ล่า Pierre เธอคงจะเห็นเจ้า Andre (อองเดร) บ้างใช่ไหม";
			next;
			mes "[Cloud]";
			mes "เอาล่ะ ทีนี้เรามาล่า Andre กันด้วยเลย";
			mes "ฮะๆ สายตาที่เธอมองฉันเหมือนจะถามว่า ''ทำไมไม่สั่งทีเดียวให้หมด'' ใช่ไหมล่ะ?";
			next;
			mes "[Cloud]";
			mes "แบบนี้มันไม่สนุกกว่าเหรอ?";
			mes "เป้าหมายต่อไปของเธอคือ Andre 15 ตัว!";
			mes "เธอแข็งแกร่งอยู่แล้ว ไม่เป็นไรหรอก!";
			next;
			mes "[Cloud]";
			mes "ถ้าหา Andre ไม่เจอ ก็ลองเข้าไปลึกกว่านี้หน่อยนะ";
			mes "อ้อ แล้วระวัง Maya ด้วยล่ะ";
			set para_suv01,19;
			changequest 7143,7144;
			close;
		}
		mes "[Cloud]";
		mes "คิดยังไงกับ Ant Hell บ้างล่ะ?";
		mes "ยังไหวอยู่ไหม?";
		mes "โอเค เดี๋ยวฉันจะฟื้นฟูพลังให้ ลุยต่อไปนะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 19) {
		if (checkquest(7144,HUNTING) == 2) {
			mes "[Cloud]";
			mes "เยี่ยม เธอจัดการ Andre ไปหมดแล้วสินะ";
			mes "รู้สึกยังไงบ้างกับการสู้กับพวกมด?";
			mes "สนุกไหมล่ะ?";
			next;
			mes "[Cloud]";
			mes "เฮ้ ร่าเริงหน่อย";
			mes "เหลืออีกแค่ขั้นตอนเดียวเธอก็จะพิชิต Ant Hell ได้แล้ว";
			mes "เธอคงเดาเป้าหมายต่อไปได้แล้วล่ะสิ";
			next;
			mes "[Cloud]";
			mes "Vitata (วิตาต้า)!";
			mes "จะว่ายังไงดีนะ... เจ้านี่มันเหมือนน้ำผึ้งเลยล่ะ";
			mes "Pierre เป็นมดงาน Andre เป็นองครักษ์... ส่วน Vitata ก็เปรียบเสมือนแม่";
			next;
			mes "[Cloud]";
			mes "พวกมันใจดีกับเด็กและผู้หญิงมาก แต่พวกมดด้วยกันไม่ได้ใจดีกับมนุษย์หรอกนะ";
			mes "ไปล่า Vitata ที่คอยดูแลไข่มดซะ";
			next;
			mes "[Cloud]";
			mes "ถ้าหลังจากนี้มีใครตกลงมาใน Ant Hell ที่นี่ก็น่าจะปลอดภัยขึ้นกว่าเดิมหน่อย";
			mes "เอาล่ะ ขั้นตอนสุดท้ายแล้ว สู้ๆ นะ ไปจัดการ Vitata 10 ตัว";
			next;
			mes "[Cloud]";
			mes "ถ้ารู้สึกว่าอันตรายเกินไป ก็กลับมาหาฉันนะ";
			mes "ฉันจะรักษาให้เอง";
			set para_suv01,20;
			changequest 7144,7145;
			close;
		}
		mes "[Cloud]";
		mes "เอาละ เพื่อเป็นกำลังใจให้ ฉันจะฮีลให้เธอจนกว่าจะฝึกเสร็จนะ";
		mes "ยืดอกเข้าไว้ สู้ๆ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 20) {
		if (checkquest(7145,HUNTING) == 2) {
			mes "[Cloud]";
			mes "โอ้... มันคงเป็นการต่อสู้ที่น่ารำคาญไม่น้อยเลยนะ";
			mes "แต่เธอทำเสร็จเร็วมาก";
			next;
			mes "[Cloud]";
			mes "ทำได้ดีมาก กลับไปรายงานที่สำนักงานใหญ่ได้เลย";
			mes "เธอผ่านการฝึกนี้ได้อย่างรวดเร็วเชียวล่ะ";
			next;
			mes "[Cloud]";
			mes "เธอจะได้รับข่าวดีแน่นอน";
			mes "พยายามได้ดีมากแม้จะต้องสู้ท่ามกลางอากาศร้อนแบบนี้";
			set para_suv01,21;
			changequest 7145,7146;
			close;
		}
		mes "[Cloud]";
		mes "เอาละ เพื่อเป็นการให้กำลังใจ ฉันจะฮีลให้เธอจนกว่าจะฝึกเสร็จ";
		mes "ยืดอกเข้าไว้ สู้ๆ";
		next;
		mes "[Cloud]";
		mes "แค่ Vitata เอง ไม่ใช่คู่ต่อสู้ที่ยากไปใช่ไหม?";
		mes "ถ้าเจอ Maya ก็แค่เผ่นให้ไวละกัน";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 >= 21) {
		mes "[Cloud]";
		mes "พูดได้เต็มปากเลยว่าเธอพิชิต Ant Hell ได้แล้ว";
		mes "เริ่มจะชินกับที่นี่แล้วใช่ไหมล่ะ?";
		next;
		mes "[Cloud]";
		mes "กลับไปรายงานที่สำนักงานใหญ่ว่าทำภารกิจสำเร็จแล้วซะ รีบไปเถอะ";
		mes "เธอจะได้รับข่าวดีแน่";
		mes "จะได้ชุดยูนิฟอร์มของ Eden Group ด้วยนะ";
		close;
	}
	mes "[Cloud]";
	mes "ฉันภูมิใจในกลุ่ม Eden Group จริงๆ";
	mes "ชุดยูนิฟอร์มนี่ดูเหมาะกับฉันไหม?";
	mes "ฮ่าๆ...";
	close;
}

in_orcs01,38,175,3	script	Eden Member Hooksha	803,{
	if (para_suv01 < 24) {
		if (callfunc("F_HasEdenGroupMark")) {
			mes "[Hooksha]";
			mes "ไม่อยากจะเชื่อเลย เธอมาที่นี่ทำไมกัน?";
			mes "อืม... เธอยังไม่ถึงขั้นที่ 3 ของการฝึกไม่ใช่เหรอ?";
			mes "เย้~ ตื่นเต้นจังเลยนะ~";
			next;
			mes "[Hooksha]";
			mes "ฉันเริ่มเบื่อแล้วนะที่ต้องมารอนักฝึกหัดเนี่ย หายไปไหนกันหมดนะ?";
			close;
		}
		mes "[Hooksha]";
		mes "สวัสดี";
		mes "มาที่นี่ทำไมเหรอ?";
		mes "อืม... อ่าาาา...";
		next;
		mes "[Hooksha]";
		mes "สนใจมาเข้ากลุ่มของฉันไหมล่ะ?";
		mes "ถ้าเธอสนใจจะเข้าร่วมกับเราล่ะก็";
		mes "เธอจะไม่เสียใจที่ตัดสินใจแบบนี้แน่นอน";
		close;
	}
	if (para_suv01 == 24) {
		mes "[Hooksha]";
		mes "สวัสดี";
		mes "เธอคือนักฝึกหัดใช่ไหม?";
		mes "กะแล้วเชียว ดูจากหน้าตาก็รู้";
		mes "งั้นเรามาเริ่มกันเลยดีไหม?";
		next;
		mes "[Hooksha]";
		mes "จริงๆ ฉันก็ไม่ได้ชอบภารกิจฝึกหัดนี่เท่าไหร่นะ แต่เอาเถอะ เดี๋ยวฉันจะอธิบายให้ฟัง";
		next;
		mes "[Hooksha]";
		mes "อย่างที่รู้กัน ที่นี่คือหมู่บ้านออร์ค (Orc Village)";
		mes "พวกออร์คไม่ชอบมนุษย์";
		mes "พวกมันไม่พยายามจะสื่อสารกับเราหรอก พอเห็นมนุษย์ปุ๊บก็พุ่งเข้าทำร้ายทันที";
		next;
		mes "[Hooksha]";
		mes "ซึ่งก็น่าเศร้าที่มนุษย์เราก็ไม่อยากจะสื่อสารกับพวกมันเหมือนกัน";
		mes "เราเลยตัดสินใจที่จะใช้วิธีการที่เด็ดขาดกับพวกมัน";
		mes "จริงๆ คือเราอยากจะกำจัดพวกมันให้หมดไปเลยน่ะ";
		next;
		mes "[Hooksha]";
		mes "เรากำลังพยายามจะพิชิตหมู่บ้านออร์คแห่งนี้";
		mes "ที่นี่เป็นสถานที่ที่เหมาะที่สุดสำหรับการจบภารกิจฝึกหัดขั้นต้นของนักผจญภัยแบบเธอ";
		next;
		mes "[Hooksha]";
		mes "นั่นคือเหตุผลที่เราเลือกที่นี่สำหรับภารกิจนี้";
		mes "เอาล่ะ เรามีเวลาไม่มาก เริ่มกันเลยเถอะ";
		mes "ก่อนอื่น ไปกำจัดตัวที่อ่อนแอที่สุดก่อน";
		next;
		mes "[Hooksha]";
		mes "จัดการพวกมันตอนที่ยังไม่โตเนี่ยแหละดีที่สุด";
		mes "ไปล่า Orc Baby มา 10 ตัว";
		mes "ไม่ต้องไปสงสารพวกมันหรอก เพราะพอพวกมันโตขึ้น มันจะกลายเป็น Orc Warrior ที่ดุร้าย";
		next;
		mes "[Hooksha]";
		mes "จะดีจะร้ายยังไง มันก็ต้องเป็นไปตามนี้แหละ";
		mes "ถึงจะเป็นเด็กออร์คก็อย่าลังเลล่ะ";
		set para_suv01,25;
		changequest 7147,7148;
		close;
	}
	if (para_suv01 == 25) {
		if (checkquest(7148,HUNTING) == 2) {
			mes "[Hooksha]";
			mes "เยี่ยมมาก สุดยอดเลย";
			mes "มันอาจจะไม่ใช่เรื่องน่าอภิรมย์เท่าไหร่ งั้นเราไปต่อกันเถอะ";
			next;
			mes "[Hooksha]";
			mes "ขั้นตอนต่อไป เราจะไปสู้กับ Orc Warrior";
			mes "เมื่อพวก Orc Baby โตขึ้น พวกมันจะกลายเป็น Orc Warrior ที่แข็งแกร่ง";
			next;
			mes "[Hooksha]";
			mes "พวกมันเป็นนักรบที่ทรงพลังกันทั้งนั้น";
			mes "ตอนที่เธอสู้กับ Orc Baby เธอคงจะโดนพวกมันรุมโจมตีมาบ้างแล้วสินะ";
			next;
			mes "[Hooksha]";
			mes "คราวนี้ถึงเวลาล่า Orc Warrior 10 ตัวแล้ว";
			mes "ถ้าเธอตกที่นั่งลำบาก ก็รีบหนีกลับมาที่นี่เพื่อความปลอดภัยนะ";
			mes "เข้าใจไหม?";
			set para_suv01,26;
			changequest 7148,7149;
			close;
		}
		mes "[Hooksha]";
		mes "ในหมู่บ้านนี้มีทั้ง Orc Warrior, Orc Lady และ Orc Baby";
		mes "พวกมันดุร้ายมากจริงๆ";
		next;
		mes "[Hooksha]";
		mes "เธอดูเหนื่อยนะ เดี๋ยวฉันจะรักษาแผลให้";
		mes "ถ้ามีปัญหาอะไร ก็รีบกลับมาที่นี่เพื่อความปลอดภัยล่ะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 26) {
		if (checkquest(7149,HUNTING) == 2) {
			mes "[Hooksha]";
			mes "ตอนนี้เธอสามารถไปต่อขั้นต่อไปได้แล้ว";
			next;
			mes "[Hooksha]";
			mes "ใช่แล้ว... ถึงเวลาล่า Orc Lady แล้วล่ะ";
			mes "ถ้าเธออยากจะพิชิตหมู่บ้านออร์คให้สิ้นซาก เธอต้องจัดการพวกหล่อนก่อนที่จะมี Orc Baby เพิ่มขึ้นมาอีก";
			next;
			mes "[Hooksha]";
			mes "แต่เธอไม่จำเป็นต้องฆ่าพวกหล่อนทั้งหมดหรอก";
			mes "ไปล่า Orc Lady มาสัก 10 ตัวพอ";
			next;
			mes "[Hooksha]";
			mes "เธอคงสัมผัสได้ถึงพลังที่แข็งแกร่งจากข้างนอกนั่นแล้วสินะ...";
			mes "อย่าลังเลที่จะโจมตีพวกมันล่ะ";
			set para_suv01,27;
			changequest 7149,7150;
			close;
		}
		mes "[Hooksha]";
		mes "เธอคงจะหมดแรงแล้วล่ะสิตอนนี้";
		mes "แต่เธอยังเหลือเป้าหมายอื่นอีกนะ เข้าใจใช่ไหม";
		next;
		mes "[Hooksha]";
		mes "ดูท่าจะเหนื่อยนะ เดี๋ยวฉันฟื้นฟูพลังให้";
		mes "ถ้ามีอันตราย ก็กลับมาที่นี่นะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 27) {
		if (checkquest(7150,HUNTING) == 2) {
			mes "[Hooksha]";
			mes "ทำได้ดีมาก";
			mes "ตอนนี้เธอคงเข้าใจวิถีของเผ่าออร์คในหมู่บ้านนี้แล้วล่ะ";
			mes "เธอผ่านการฝึกมาได้อย่างดีแม้จะอยู่ในสภาพอากาศที่ร้อนชื้นแบบนี้";
			mes "เธอทำภารกิจ 'พิชิตหมู่บ้านออร์ค!' สำเร็จแล้ว ยินดีด้วยนะ";
			next;
			mes "[Hooksha]";
			mes "กลับไปรายงานผลที่สำนักงานใหญ่ Eden Group ได้เลย";
			mes "ฉันมั่นใจว่าพวกเขาจะมีข่าวดีรอเธออยู่";
			set para_suv01,28;
			changequest 7150,7151;
			close;
		}
		mes "[Hooksha]";
		mes "Orc Lady คือเป้าหมายสุดท้ายแล้ว";
		mes "สู้ๆ นะ~!";
		next;
		mes "[Hooksha]";
		mes "ดูท่าจะเหนื่อย เดี๋ยวฉันรักษาให้";
		mes "ถ้าไม่ไหวก็รีบหนีกลับมาล่ะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 28) {
		mes "[Hooksha]";
		mes "เธอฝึกสำเร็จแล้วล่ะ";
		mes "กลับไปรายงานตัวที่สำนักงานใหญ่ Eden Group ได้เลย";
		close;
	}
	if (para_suv01 == 29) {
		mes "[Hooksha]";
		mes "เธอมาที่นี่เพื่อรับการฝึกงั้นเหรอ?";
		mes "ดูเหมือนมือโปรเลยนะเนี่ย";
		next;
		mes "[Hooksha]";
		mes "ที่นี่ไม่ใช่ที่ที่จะมาคุยกันนานๆ รีบหน่อยเถอะ";
		mes "ทางนี้เชื่อมต่อไปยังดันเจี้ยนของหมู่บ้านออร์ค (Orc Dungeon)";
		next;
		mes "[Hooksha]";
		mes "เห็นทางนั้นไหม";
		mes "มีถ้ำอยู่ใต้ดิน พวกออร์คปกติจะไม่ลงไปที่นั่น";
		next;
		mes "[Hooksha]";
		mes "ที่นี่คือที่ที่ปลอดภัยที่สุดแล้ว";
		mes "เพราะข้างล่างนั่นมีมอนสเตอร์ประเภท Undead เต็มไปหมด";
		next;
		mes "[Hooksha]";
		mes "พวก Undead นั้นอันตรายและน่ากลัวมาก";
		mes "อย่าลังเลตอนที่สู้กับพวกมันล่ะ";
		next;
		mes "[Hooksha]";
		mes "คราวนี้ไปล่าพวก Undead ข้างล่างนั่นซะ";
		next;
		mes "[Hooksha]";
		mes "ลองไปล่า Orc Zombie ในดันเจี้ยนมาสัก 20 ตัวนะ";
		mes "พวกมันทำงานกันเป็นระบบมาก";
		next;
		mes "[Hooksha]";
		mes "พวกมันจะรุมทึ้งเธอทุกที่โดยไม่ลังเลเลยล่ะ";
		mes "ขอให้เทพี Freya อวยพรเธอ";
		set para_suv01,30;
		changequest 7152,7153;
		close;
	}
	if (para_suv01 == 30) {
		if (checkquest(7153,HUNTING) == 2) {
			mes "[Hooksha]";
			mes "มันต่างจากที่เธอเห็นใน Payon ใช่ไหมล่ะ?";
			mes "ถึงเธอจะจัดการ Orc Zombie ได้ แต่ก็อย่าเพิ่งชะล่าใจไปว่าตัวเองเก่งแล้วนะ..";
			next;
			mes "[Hooksha]";
			mes "ห้ามลงไปลึกกว่านั้นเด็ดขาด ไม่งั้นเธอได้ตายแน่ๆ";
			next;
			mes "[Hooksha]";
			mes "เป้าหมายต่อไปคือ Orc Skeleton ที่เดินปะปนอยู่กับพวก Orc Zombie นั่นแหละ";
			mes "ล่า Orc Skeleton 20 ตัว";
			next;
			mes "[Hooksha]";
			mes "นี่คือขั้นตอนสุดท้ายของการฝึกใน Orc Dungeon แล้ว";
			mes "ที่เหลือก็ขึ้นอยู่กับเธอแล้วล่ะ";
			next;
			mes "[Hooksha]";
			mes "โอเค สู้ๆ นะ แล้วเจอกัน";
			mes "ล่า Orc Skeleton 20 ตัว";
			set para_suv01,31;
			changequest 7153,7154;
			close;
		}
		mes "[Hooksha]";
		mes "ยังไหวอยู่ไหม?";
		mes "เธอดูเหนื่อยนะ เดี๋ยวฉันจะฟื้นฟูพลังให้";
		mes "ถ้ามีอันตราย ก็กลับมาที่นี่ซะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 31) {
		if (checkquest(7154,HUNTING) == 2) {
			mes "[Hooksha]";
			mes "การฝึกของเธอเสร็จสิ้นสมบูรณ์แล้ว";
			mes "กลับไปรายงานตัวที่สำนักงานใหญ่ Eden Group ได้เลย";
			mes "ฉันอยากจะอธิบายรายละเอียดมากกว่านี้อยู่นะ แต่ฉันชักจะเริ่มเพลียแล้วสิ";
			next;
			mes "[Hooksha]";
			mes "ช่วงนี้มีนักฝึกหัดแห่กันมาที่นี่เยอะขึ้นเรื่อยๆ จนฉันแทบไม่ได้งีบเลย";
			mes "อ้อ ฉันไม่ได้หมายความว่าเพราะเธอนะ";
			mes "เอาเป็นว่า เดี๋ยวฉันจะแจ้งทางกลุ่มไว้ให้ เธอไปรายงานตัวได้เลย";
			next;
			set para_suv01,32;
			changequest 7154,7155;
			close;
		}
		mes "[Hooksha]";
		mes "ยังไหวอยู่ใช่ไหม?";
		mes "เธอดูเหนื่อยนะ เดี๋ยวฉันฟื้นฟูพลังให้";
		mes "ถ้ามีปัญหา ก็กลับมาหาฉันล่ะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 32) {
		mes "[Hooksha]";
		mes "เธอยังไม่ต้องกลับไปรายงานตัวที่สำนักงานใหญ่เหรอ?";
		mes "ฉันเหนื่อยแล้วล่ะ ปล่อยให้ฉันอยู่คนเดียวเถอะ";
		close;
	}
	if (para_suv01 > 32) {
		mes "[Hooksha]";
		mes "หือ? ออร์คอะไรนะ? สำรวจดันเจี้ยนเหรอ?";
		mes "เธอนี่ก็ลำบากเหมือนกันนะเนี่ย";
		next;
		mes "[Hooksha]";
		mes "ที่นี่เป็นที่พักผ่อนที่ดีมากเลยล่ะ เพราะพวกออร์คจะไม่เข้ามาที่นี่";
		mes "เฮ้ออออ...";
		close;
	}
}

iz_dun04,43,46,3	script	Eden Member Callandiva	745,{
	if (para_suv01 < 33) {
		if (callfunc("F_HasEdenGroupMark")) {
			mes "[Callandiva]";
			mes "ลงมาลึกถึงเมืองใต้สมุทรนี่ได้ยังไงกันคะเนี่ย?";
			mes "อ๊ะ สัญลักษณ์นั่น... มาจากกลุ่มของเรานี่นา";
			mes "คุณต้องเป็นนักฝึกหัดแน่นอนเลย";
			next;
			mes "[Callandiva]";
			mes "อ้าว? ไม่ใช่งั้นเหรอ?";
			mes "อ๋อ... เข้าใจแล้ว... เข้าใจแล้วค่ะ...";
			mes "โอเค... เดินทางต่อเถอะค่ะ";
			close;
		}
		mes "[Callandiva]";
		mes "มหัศจรรย์จังเลยนะคะ ทั้งที่เราอยู่ใต้ทะเลแท้ๆ แต่ยังหายใจได้ปกติเลย";
		mes "รู้ไหมคะว่าเป็นเพราะอะไร?";
		next;
		mes "[Callandiva]";
		mes "ดูสิคะ เพราะความชุ่มชื้นแท้ๆ ผิวของฉันเลยนุ่มนิ่มไปหมดเลย";
		mes "แถมปลายนิ้วมือนิ้วเท้าก็ไม่ได้ติดกันเป็นพังผืดด้วย น่าทึ่งจริงๆ";
		next;
		mes "[Callandiva]";
		mes "ตอนแรกที่ Eden Group ส่งฉันมาที่นี่ ฉันตกใจแล้วก็กลัวมากเลยล่ะค่ะ";
		mes "แต่ตอนนี้ฉันชักจะหลงรักสภาพแวดล้อมแบบนี้ซะแล้วสิ";
		mes "มันเงียบสงบจริงๆ นะคะ...";
		close;
	}
	if (para_suv01 == 33) {
		mes "[Callandiva]";
		mes "ลงมาลึกถึงเมืองใต้สมุทรนี่ได้ยังไงกันคะเนี่ย?";
		mes "อ๊ะ สัญลักษณ์นั่น... มาจากกลุ่มของเรานี่นา";
		mes "คุณต้องเป็นนักฝึกหัดแน่นอนเลย";
		next;
		mes "[Callandiva]";
		mes "ดีใจที่ได้เจอนะคะ!";
		mes "เอาล่ะ ลองมองลงไปข้างล่างนั่นสิ";
		mes "สิ่งก่อสร้างโบราณกำลังหลับใหลอยู่ใต้น้ำ จริงๆ แล้วพวกมันถูกสร้างขึ้นใต้ทะเลนี่แหละค่ะ";
		next;
		mes "[Callandiva]";
		mes "อย่างที่คุณเห็น พวกเราไม่มีปัญหาเรื่องการใช้ชีวิตที่นี่เลย";
		mes "แต่ถ้าลองมองไปทางด้านโน้น... ใช่ค่ะ ตรงนั้นแหละ";
		next;
		mes "[Callandiva]";
		mes "เห็นมนุษย์ปลาที่ถือหอกดูน่ากลัวนั่นไหมคะ?";
		mes "เขาถูกเรียกว่า Merman (เมอร์แมน) และเป็นนักรบมืออาชีพเชียวล่ะ";
		next;
		mes "[Callandiva]";
		mes "โอเคค่ะ ลองไปล่า Merman สัก 15 ตัวนะคะ";
		mes "นั่นจะเป็นภารกิจฝึกฝนแรกของคุณที่นี่ค่ะ";
		set para_suv01,34;
		changequest 7156,7157;
		close;
	}
	if (para_suv01 == 34) {
		if (checkquest(7157,HUNTING) == 2) {
			mes "[Callandiva]";
			mes "โอ้ กลับมาแล้วเหรอคะ~!";
			mes "คิดยังไงกับพวก Merman ที่ดูน่ากลัวพวกนั้นบ้างคะ?";
			mes "จริงๆ ฉันแอบคิดว่าคุณจะปอดแหกจนหนีไปซะแล้วนะเนี่ย~";
			next;
			mes "[Callandiva]";
			mes "เก่งมากค่ะ คู่ต่อสู้คนต่อไปของคุณก็คือ...~";
			mes "ใช่ค่ะ เจ้านี่แหละ...";
			mes "มอนสเตอร์ที่ถือสามง่ามอันนั้นไง";
			next;
			mes "[Callandiva]";
			mes "ความสามารถหลักของเขาคือเวทมนตร์ค่ะ";
			mes "มอนสเตอร์ตัวนี้ชื่อว่า Strouf (สโตรฟ)!";
			mes "คราวนี้ ถึงเวลาสู้กับจอมเวทของจริงแล้วค่ะ!";
			next;
			mes "[Callandiva]";
			mes "พยายามเลี่ยงพวก Merman แล้วล่า Strouf ให้ได้ 10 ตัวนะคะ จัดการพวกมันเลยค่ะ";
			set para_suv01,35;
			changequest 7157,7158;
			close;
		}
		mes "[Callandiva]";
		mes "โอ๊ะ เหนื่อยแล้วเหรอคะ?";
		mes "พวก Merman ไม่ใช่คู่ต่อสู้ที่เคี้ยวได้ง่ายๆ เลยล่ะค่ะ";
		mes "เดี๋ยวฉันจะช่วยฟื้นฟูพลังให้เอง ร่าเริงเข้าไว้นะคะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 == 35) {
		if (checkquest(7158,HUNTING) == 2) {
			mes "[Callandiva]";
			mes "จัดการ Strouf หมดแล้วเหรอคะเนี่ย?";
			mes "ฉันไม่ได้คาดหวังเลยนะเนี่ยว่าคุณจะทำสำเร็จ";
			mes "สงสัยฉันจะมีตาหลังล่ะมั้งคะ ฮ่าๆ";
			next;
			mes "[Callandiva]";
			mes "แน่นอนว่าฉันล้อเล่นน่ะค่ะ";
			mes "อย่ามองฉันแบบนั้นสิคะ~ แหม แค่มุกตลกเองนะ~!";
			mes "ตอนนี้คุณคงรู้สึกสบายใจขึ้นแล้วใช่ไหมคะ";
			next;
			mes "[Callandiva]";
			mes "กลับไปที่สำนักงานใหญ่แล้วรายงานผลได้เลยค่ะ~";
			mes "คุณจะได้รับชุดยูนิฟอร์มชุดสุดท้ายจากสำนักงานใหญ่ Eden Group แล้วล่ะ";
			next;
			mes "[Callandiva]";
			mes "ภารกิจฝึกฝนนี้สร้างมาสำหรับมือใหม่";
			mes "ดังนั้นมันอาจจะมีประโยชน์กับคุณบ้างหรือไม่ก็นิดหน่อยล่ะมั้งคะ";
			next;
			mes "[Callandiva]";
			mes "เอาเป็นว่า คุณทำได้เยี่ยมมากเลยค่ะ!";
			set para_suv01,36;
			changequest 7158,7159;
			close;
		}
		mes "[Callandiva]";
		mes "โอ๊ะ เหนื่อยแล้วเหรอคะ?";
		mes "พวก Strouf ไม่ใช่คู่ต่อสู้ที่ง่ายเลยนะ";
		mes "ฉันช่วยให้คุณหายเหนื่อยได้ค่ะ ร่าเริงเข้าไว้นะคะ";
		npcskill "AL_HEAL",11,99,60;
		percentheal 100,100;
		close;
	}
	if (para_suv01 >= 36) {
		mes "[Callandiva]";
		mes "ฉันคงช่วยอะไรคุณไม่ได้มากกว่านี้แล้วล่ะค่ะ";
		mes "กลับไปที่สำนักงานใหญ่แล้วรายงานผลภารกิจฝึกฝนนี้เถอะค่ะ";
		close;
	}
	mes "[Callandiva]";
	mes "มหัศจรรย์จังเลยนะคะ ทั้งที่เราอยู่ใต้ทะเลแท้ๆ แต่ยังหายใจได้ปกติเลย";
	mes "รู้ไหมคะว่าเป็นเพราะอะไร?";
	next;
	mes "[Callandiva]";
	mes "ดูสิคะ เพราะความชุ่มชื้นแท้ๆ ผิวของฉันเลยนุ่มนิ่มไปหมดเลย";
	mes "แถมปลายนิ้วมือนิ้วเท้าก็ไม่ได้ติดกันเป็นพังผืดด้วย น่าทึ่งจริงๆ";
	next;
	mes "[Callandiva]";
	mes "ตอนแรกที่ Eden Group ส่งฉันมาที่นี่ ฉันตกใจแล้วก็กลัวมากเลยล่ะค่ะ";
	mes "แต่ตอนนี้ฉันชักจะหลงรักสภาพแวดล้อมแบบนี้ซะแล้วสิ";
	mes "มันเงียบสงบจริงๆ นะคะ...";
	close;
}

moc_para01,112,96,5	script	Administrator Michael	4_M_RUSMAN1,{
	mes "[Michael]";
	mes "มีธุระอะไรกับผมเหรอครับ?";
	next;
	switch (select("มารับอุปกรณ์สนับสนุน:ที่นี่คือที่ไหน?:อัปเกรดอุปกรณ์")) {
		mes "[Michael]";
	case 1:
		if (para_suv01 == 11) {
			mes "หากคุณผ่านการฝึกขั้นที่ 1 แล้ว";
			mes "เรามีอุปกรณ์สนับสนุนให้ ได้แก่ Eden Group Hat, Uniform, Manteau และ Boots ครับ";
			mes "^4d4dffตรวจสอบช่องเก็บของในตัวให้ว่างก่อนนะครับ^000000";
			next;
			if (select("ขอเช็คช่องเก็บของก่อน:ช่องเก็บของว่างพอแล้ว") == 1) {
				mes "[Michael]";
				mes "กรุณาตรวจสอบให้แน่ใจว่ามีที่ว่างเพียงพอสำหรับอุปกรณ์เหล่านี้นะครับ";
				close;
			}
			mes "[Michael]";
			mes "อุปกรณ์สองอย่างคือ ^4d4dffHat (หมวก) และ Manteau (ผ้าคลุม)^000000";
			mes "จะแจกให้เพียงครั้งเดียวเท่านั้นนะครับ";
			mes "เพราะฉะนั้นรักษาและใช้งานพวกมันให้ดีล่ะ";
			next;
			mes "[Michael]";
			mes "ส่วนรองเท้า (Boots) และชุดยูนิฟอร์ม (Uniform) คุณจะได้รับของที่มีคุณภาพดีขึ้นตามระดับคอร์สที่คุณผ่านครับ";
			next;
			mes "[Michael]";
			mes "ได้รับ Eden Group Hat 1 ชิ้น";
			mes "ได้รับ Eden Group Uniform I 1 ชิ้น";
			mes "ได้รับ Eden Group Boots I 1 คู่";
			mes "ได้รับ Eden Group Manteau 1 ชิ้น";
			mes "รวมทั้งหมด 4 รายการครับ เรียบร้อยแล้ว";
			para_suv01 = 12;
			para_suv02 = 1;
			getitem 5583,1; //Para_Team_Hat1
			getitem 2560,1; //Para_Team_Manteau1
			getitem 2456,1; //Para_Team_Boots1
			getitem 15009,1; //Para_Team_Uniform1
			next;
			mes "[Michael]";
			mes "ถูกต้องไหมครับ?";
			mes "อุปกรณ์เหล่านี้ถูกผลิตมาเพื่อนักผจญภัยมือใหม่ แม้ประสิทธิภาพอาจจะไม่ดีที่สุด แต่ก็ราคาถูกกว่าของตามร้านค้าทั่วไปแน่นอนครับ";
			next;
			mes "[Michael]";
			mes "เราผลิตพวกมันขึ้นมาเพื่อสมาชิกกลุ่ม Eden โดยเฉพาะเลยนะ";
		}
		else if (para_suv01 == 22) {
			mes "หากคุณผ่านการฝึกขั้นที่ 2 แล้ว นอกจากอุปกรณ์พื้นฐาน เราจะมีอาวุธเสริมให้ด้วยครับ";
			next;
			mes "[Michael]";
			mes "เราได้เลือกอาวุธที่เหมาะสมที่สุดสำหรับแต่ละอาชีพไว้ให้แล้ว";
			mes "แต่บางอาชีพที่ยังไม่ได้เข้าร่วมกับเรา เราอาจจะยังไม่มีอาวุธรองรับนะครับ";
			next;
			mes "[Michael]";
			mes "เราคงไม่สามารถผลิตอาวุธทุกชนิดที่มีอยู่ในโลกนี้ได้จริงไหมล่ะ?";
			next;
			mes "[Michael]";
			mes "คุณจะได้รับ อาวุธ 1 ชิ้น, รองเท้า และชุดยูนิฟอร์ม รวมทั้งหมด 3 รายการครับ";
			mes "และเรายังมีอุปกรณ์เสริมพิเศษสำหรับสมาชิกที่ไม่สามารถใช้อุปกรณ์บางอย่างได้ด้วย";
			next;
			mes "[Michael]";
			mes "^4d4dffกรุณาตรวจสอบช่องเก็บของก่อนรับไอเทมนะครับ^000000";
			next;
			if (select("ขอไปจัดการที่ว่างก่อน:ช่องเก็บของว่างพอแล้ว") == 1) {
				mes "[Michael]";
				mes "กรุณาเตรียมที่ว่างให้เพียงพอครับ";
				close;
			}
			mes "[Michael]";
			if (Class == Job_Swordman || Class == Job_Swordman_High || Class == Job_Knight || Class == Job_Crusader) {
				callsub S_Select,"เรามีดาบมือเดียวและดาบสองมือให้เลือกครับ",
					1192,"Eden Slayer I","ดาบสองมือ","พลังโจมตี 162","",//P_Slayer1
					13423,"Eden Saber I","ดาบมือเดียว","พลังโจมตี 147","นี่คือสิ่งที่เราตั้งใจทำครับ";//P_Sabre1
			}
			if (Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class == Job_Rogue || Class == Job_Ninja || Class == Job_Novice || Class == Job_Novice_High) {
				callsub S_Select,"",
					13050,"Eden Dagger I","มีดพก","MATK+60, พลังโจมตี 124","นี่คือสิ่งที่เราตั้งใจทำครับ";//P_Dagger1
			}
			if (Class == Job_Merchant || Class == Job_Merchant_High || Class == Job_Blacksmith || Class == Job_Alchemist) {
				callsub S_Select,"เรามีกระบองและดาบมือเดียวให้เลือกครับ",
					13423,"Eden Saber I","ดาบมือเดียว","พลังโจมตี 147","นี่คือสิ่งที่เราตั้งใจทำครับ",// P_Sabre1
					16004,"Eden Mace I","กระบอง","พลังโจมตี 142","";// P_Mace1
			}
			if (Class == Job_Archer || Class == Job_Archer_High || Class == Job_Hunter || Class == Job_Dancer || Class == Job_Bard) {
				callsub S_Select,"",
					1747,"Eden Bow I","ธนู","พลังโจมตี 82","";//P_Bow1
			}
			if (Class == Job_Acolyte || Class == Job_Acolyte_High || Class == Job_Priest || Class == Job_Monk) {
				callsub S_Select,"เรามีกระบองและไม้เท้าให้เลือกครับ",
					16004,"Eden Mace I","กระบอง","พลังโจมตี 142","", //P_Mace1
					1650,"Eden Staff I","ไม้เท้า","INT+2, MATK+125, พลังโจมตี 60",""; //P_Staff1
			}
			if (Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_Sage) {
				callsub S_Select,"",
					1650,"Eden Staff I","ไม้เท้า","INT+2, MATK+125, พลังโจมตี 60","";//P_Staff1
			}
			if (Class == Job_Gunslinger) {
				callsub S_Select,"",
					13112,"Eden Revolver I","ปืนพก","HIT-5, พลังโจมตี 44","";//P_Revolver1
			}
			if (Class == Job_Summoner) {
				callsub S_Select,"",
					1699,"Eden Foxtail Staff I","ไม้เท้าแมว","MATK+135, พลังโจมตี 120","";//P_Foxtail1
			}
			mes "ไหนดูซิ... คุณจะได้รับ..";
			mes "Eden Group Boots II และ Uniform II ครับ";
			next;
			mes "[Michael]";
			para_suv01 = 23;
			getitem 13050,1; //P_Dagger1
			getitem 2457,1; //Para_Team_Boots2
			getitem 15010,1; //Para_Team_Uniform2
			if (para_suv02 == 0) {
				mes "ผมไม่แน่ใจว่าอาวุธไหนจะเหมาะกับคุณที่สุด ผมเลยขอมอบมีดพกให้ก่อนละกันนะครับ";
				mes "เนื่องจากคุณไม่มีบันทึกว่าเคยได้รับอุปกรณ์สนับสนุนมาก่อนเลย";
				mes "ผมจะมอบ Eden Group Hat และ Manteau ให้ด้วยครับ";
				getitem 5583,1; //Para_Team_Hat
				getitem 2560,1; //Para_Team_Manteau
				para_suv02 = 2;
				next;
				mes "[Michael]";
				mes "ตรวจสอบของที่ได้รับอีกครั้งและดูแลมันให้ดีนะครับ";
				close;
			}
			para_suv02 = 2;
			mes "อาวุธ, ชุดยูนิฟอร์ม และรองเท้า รวมเป็นอุปกรณ์สนับสนุน 3 รายการครับ";
			mes "ผมไม่แน่ใจว่าอาวุธไหนจะเหมาะกับคุณที่สุด เลยมอบมีดพกให้แทนครับ";
			mes "กรุณาตรวจสอบดูอีกรอบนะครับ";
			next;
			mes "[Michael]";
			mes "หวังว่าพวกมันจะเป็นประโยชน์กับคุณนะครับ";
		}
		else if (para_suv01 == 37) {
			mes "คุณผ่านคอร์สฝึกฝนสุดท้ายแล้วสินะครับ";
			mes "ถึงเวลาที่คุณจะได้รับอาวุธชิ้นใหม่แล้วล่ะ";
			next;
			mes "[Michael]";
			mes "เรามีอาวุธ ชุดยูนิฟอร์ม และรองเท้ารุ่นอัปเกรดมามอบให้ครับ";
			mes "อ้อ แต่อาวุธเหล่านี้ผลิตมาเพื่ออาชีพคลาส 1 เท่านั้นนะครับ";
			mes "ดังนั้นผมอาจจะไม่มีอาวุธระดับที่สูงกว่านี้ให้";
			next;
			mes "[Michael]";
			mes "^4d4dffกรุณาตรวจสอบช่องเก็บของก่อนรับไอเทมนะครับ^000000";
			next;
			if (select("เดี๋ยวกลับมาใหม่:ช่องเก็บของว่างพอแล้ว") == 1) {
				mes "[Michael]";
				mes "กรุณาตรวจสอบที่ว่างให้เรียบร้อยครับ";
				close;
			}
			mes "[Michael]";
			if (Class == Job_Swordman || Class == Job_Swordman_High || Class == Job_Knight || Class == Job_Crusader) {
				callsub S_Select,"เรามีดาบมือเดียวและดาบสองมือให้เลือกครับ",
					13424,"Eden Saber II","ดาบมือเดียว","พลังโจมตี 170","",///P_Sabre2
					1193,"Eden Slayer II","ดาบสองมือ","พลังโจมตี 185","";//P_Slayer2
			}
			if (Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class == Job_Rogue || Class == Job_Novice || Class == Job_Novice_High || Class == Job_Super_Novice || Class == Job_Soul_Linker || Class == Job_Ninja) {
				callsub S_Select,"",
					13051,"Eden Dagger II","มีดพก","MATK+70, พลังโจมตี 158","";//P_Dagger2
			}
			if (Class == Job_Acolyte || Class == Job_Acolyte_High || Class == Job_Priest || Class == Job_Monk) {
				callsub S_Select,"เรามีกระบองและไม้เท้าให้เลือกครับ",
					16005,"Eden Mace II","กระบอง","พลังโจมตี 163","", //P_Mace2
					1651,"Eden Staff II","ไม้เท้า","INT+3, MATK+150, พลังโจมตี 60",""; //P_Staff2
			}
			if (Class == Job_Archer || Class == Job_Archer_High || Class == Job_Hunter || Class == Job_Dancer || Class == Job_Bard) {
				callsub S_Select,"",
					1748,"Eden Bow II","ธนู","พลังโจมตี 82","";//P_Bow2
			}
			if (Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_Sage) {
				callsub S_Select,"",
					1651,"Eden Staff II","ไม้เท้า","INT+3, MATK+155, พลังโจมตี 60","";//P_Staff2
			}
			if (Class == Job_Merchant || Class == Job_Merchant_High || Class == Job_Blacksmith || Class == Job_Alchemist) {
				callsub S_Select,"เรามีกระบองและดาบมือเดียวให้เลือกครับ",
					13424,"Eden Saber II","ดาบมือเดียว","พลังโจมตี 170","",//P_Sabre2
					16005,"Eden Mace II","กระบอง","พลังโจมตี 163","";// P_Mace2
			}
			if (Class == Job_Gunslinger) {
				callsub S_Select,"",
					13113,"Eden Revolver II","ปืนพก","HIT-5, พลังโจมตี 60","";//P_Revolver2
			}
			if (Class == Job_Summoner) {
				callsub S_Select,"",
					26100,"Eden Foxtail Staff II","ไม้เท้าแมว","MATK+165, พลังโจมตี 135","";//P_Foxtail1
			}
			mes "ไหนดูซิ... คุณจะได้รับ..";
			mes "Eden Group Boots III และ Uniform III ครับ";
			next;
			para_suv01 = 38;
			getitem 2458,1; //Para_Team_Boots3
			getitem 15011,1; //Para_Team_Uniform3
			mes "[Michael]";
			if (para_suv02 == 0) {
				mes "เนื่องจากคุณไม่มีบันทึกว่าเคยได้รับอุปกรณ์สนับสนุนมาก่อนเลย";
				mes "ผมจะมอบ Eden Group Hat และ Manteau ให้ด้วยครับ";
				para_suv02 = 3;
				getitem 5583,1; //Para_Team_Hat
				getitem 2560,1; //Para_Team_Manteau
				next;
				mes "[Michael]";
				mes "ตรวจสอบของที่ได้รับอีกครั้งและดูแลมันให้ดีนะครับ";
				close;
			}
			para_suv02 = 3;
			mes "ชุดยูนิฟอร์มและรองเท้า รวมทั้งหมด 2 รายการครับ";
			mes "กรุณาตรวจสอบดูอีกทีนะครับ";
			next;
			mes "[Michael]";
			mes "หวังว่าพวกมันจะเป็นประโยชน์กับคุณครับ";
		}
		else {
			mes "เดี๋ยวนะครับ... ขอผมเช็คประวัติก่อน...";
			mes "...";
			mes "...อืมมมมม";
			next;
			mes "[Michael]";
			mes "ขออภัยครับ ผมหาประวัติที่ระบุว่าคุณสามารถรับอุปกรณ์ได้ไม่เจอเลย";
			mes "คุณแน่ใจแล้วใช่ไหมครับ?";
		}
		close;
	case 2:
		mes "ที่นี่เราใช้เก็บอาวุธ ชุดเกราะ และสินค้าอื่นๆ ที่ผลิตโดย Eden Group ครับ";
		mes "เรายังมีของพิเศษอื่นๆ อีกเพียบเลยล่ะ";
		next;
		mes "[Michael]";
		mes "เพื่อเตรียมรับมือเหตุฉุกเฉิน เรามีอุปกรณ์และเสบียงเพียงพอสำหรับกองทัพทั้งกองทัพเลยครับ";
		mes "พูดตามตรง... ตอนนี้เรายังไม่ได้ใช้มันหรอก แต่เผื่อมีเหตุการณ์อะไรเกิดขึ้นเหมือนใน Morocc เราจะได้พร้อมครับ";
		next;
		mes "[Michael]";
		mes "ลองเดินดูรอบๆ ได้ แต่อย่าไปแตะต้องอะไรเข้านะครับ";
		next;
		mes "[Michael]";
		mes "ถ้าผมทำอะไรพลาดขึ้นมา คุณ Reke เอาผมตายแน่";
		close;
	case 3:
		mes "หมายถึงการอัปเกรดอุปกรณ์ใช่ไหมครับ?";
		mes "เราสามารถอัปเกรดได้เฉพาะ Eden Group Hat เท่านั้นนะครับ";
		next;
		mes "[Michael]";
		if (para_suv02 == 3) {
			if (countitem(5583) > 0) {
				disable_items;
				mes "คุณต้องการเพิ่มโบนัสสถานะด้านไหนครับ?";
				next;
				switch (select("อัปเกรด STR:อัปเกรด AGI:อัปเกรด VIT:อัปเกรด INT:อัปเกรด DEX:อัปเกรด LUK:เอาไว้ก่อนละกัน")) {
				case 1: callsub S_Upgrade,"STR",4701;
				case 2: callsub S_Upgrade,"AGI",4731;
				case 3: callsub S_Upgrade,"VIT",4741;
				case 4: callsub S_Upgrade,"INT",4711;
				case 5: callsub S_Upgrade,"DEX",4721;
				case 6: callsub S_Upgrade,"LUK",4751;
				case 7:
					mes "[Michael]";
					mes "ทำไมล่ะครับ? มันจะช่วยให้คุณเก่งขึ้นนะ";
					mes "ยังไงก็ตาม เราอัปเกรดหมวกให้ได้แค่ใบเดียวเท่านั้นนะครับ";
					next;
					mes "[Michael]";
					mes "จะเอาไปทำอะไรต่อก็ขึ้นอยู่กับคุณแล้วล่ะครับ";
					close;
				}
			}
			mes "กรุณานำหมวก (Hat) ที่ต้องการอัปเกรดติดตัวมาด้วยนะครับ";
			mes "และต้องอยู่ในช่องเก็บของปกติ ไม่ใช่ในคลังเก็บของ เข้าใจใช่ไหมครับ?";
			close;
		}
		if (para_suv02 == 4) {
			mes "เอ๋... คุณอัปเกรดไปแล้วไม่ใช่เหรอครับ?";
			mes "ตามบันทึกระบุไว้ว่า";
			mes ""+strcharinfo(0)+": ได้ทำการอัปเกรดหมวกไปแล้ว";
			next;
			mes "[Michael]";
			mes "เราให้บริการอัปเกรดได้แค่ครั้งเดียวเท่านั้นครับ";
			mes "เสียใจด้วยจริงๆ ที่ผมทำให้สองรอบไม่ได้";
			close;
		}
		mes "คุณยังไม่ได้รับอุปกรณ์สนับสนุนจนครบขั้นตอนที่ 3 เลยนะครับ";
		mes "การอัปเกรดหมวกถือเป็นบริการพิเศษสำหรับคนที่ผ่านคอร์สแล้วเท่านั้น";
		next;
		mes "[Michael]";
		mes "ขออภัยครับ ผมช่วยคุณไม่ได้จริงๆ";
		close;
	}
S_Upgrade:
	mes "[Michael]";
	mes "รับทราบครับ";
	mes "ผมจะทำการ ^4d4dffอัปเกรดสถานะ "+ getarg(0) +"^000000 ให้";
	mes "คุณยืนยันนะครับ?";
	next;
	switch (select("ใช่ ยืนยันครับ:เดี๋ยวก่อน ขอคิดดูอีกที")) {
	case 1:
		mes "[Michael]";
		mes "ตกลงครับ ผมจะเริ่มดำเนินการเดี๋ยวนี้";
		next;
		mes "[Michael]";
		mes "เรียบร้อยแล้วครับ นี่ครับหมวกของคุณ";
		para_suv02 = 4;
		delitem 5583,1;
		getitem2 5583,1,1,0,0,0,0,0,getarg(1);
		close;
	case 2:
		mes "[Michael]";
		mes "ไม่อยากอัปเกรดแล้วเหรอครับ?";
		close;
	}

S_Select:
	.@total_arg = getargcount();
	if (.@total_arg < 7)
		mes "เรามีอาวุธที่เหมาะกับคุณอยู่ 1 ชนิดครับ";
	else {
		mes "คุณต้องการอาวุธชนิดไหนครับ?";
		mes getarg(0);
		mes "นี่คือตัวเลือกครับ";
	}
	next;
	mes "[Michael]";
	for ( .@i = 1; .@i < .@total_arg; .@i += 5 ) {
		set .@menu$, .@menu$ + getarg(.@i+1) + ":";
		mes getarg(.@i+1)+": "+ getarg(.@i+2) +". "+ getarg(.@i+3) +".";
	}
	if (.@total_arg < 7)
		mes "มันคืออาวุธ Lv. 2 และเลเวลที่ต้องการคือ "+ (para_suv01 == 22 ? "26" : "40") +" ครับ";
	else
		mes "ทั้งสองอย่างคืออาวุธ Lv. 2 และเลเวลที่ต้องการคือ "+ (para_suv01 == 22 ? "26" : "40") +" ครับ";
	next;
	mes "[Michael]";
	mes "จำไว้ว่าอุปกรณ์เหล่านี้ไม่สามารถแลกเปลี่ยนกับผู้เล่นอื่น หรือนำไปตีบวกได้นะครับ";
	next;
	//			callsub S_Select,"We have one and Two-handed swords",
		//			13424,"Eden Saber II","One-handed sword","Attack 170","",///P_Sabre2
			//		1193,"Eden Slayer II","Two-handed sword","Attack 185","";//P_Slayer2
	if (.@total_arg < 7) {
		.@i = 1;
		mes "[Michael]";
		mes "คุณจะได้รับ "+ getarg(.@i+1) +" ครับ";
		next;
		mes "[Michael]";
	}
	else {
		.@i = (select(.@menu$) -1) *5 +1;
		mes "[Michael]";
		mes "คุณได้เลือก "+ getarg(.@i+1) +" นะครับ";
	}
	if (para_suv01 == 22) {
		mes "นอกจากนี้ คุณยังจะได้รับ Eden Group Boots II และ Uniform II ด้วยครับ";
		next;
		getitem 2457,1;	// Para_Team_Boots2
		getitem 15010,1;// Para_Team_Uniform2
		para_suv01 = 23;
	}
	else {
		mes "นอกจากนี้ คุณยังจะได้รับ Eden Group Boots III และ Uniform III ด้วยครับ";
		next;
		getitem 2458,1;// Para_Team_Boots3
		getitem 15011,1;// Para_Team_Uniform3
		para_suv01 = 38;
	}
	getitem getarg(.@i),1;
	mes "[Michael]";
	if (para_suv02 == 0) {
		mes "เนื่องจากคุณไม่มีบันทึกว่าเคยได้รับอุปกรณ์สนับสนุนมาก่อนเลย";
		mes "ผมจะมอบ Eden Group Hat และ Manteau ให้ด้วยครับ";
		getitem 5583,1;// Para_Team_Hat
		getitem 2560,1;// Para_Team_Manteau
		if (para_suv01 == 22)
			para_suv02 = 2;
		else
			para_suv02 = 3;
		next;
		mes "[Michael]";
		mes "ตรวจสอบของที่ได้รับอีกครั้งและดูแลมันให้ดีนะครับ";
	}
	else {
		if (para_suv01 == 22)
			para_suv02 = 2;
		else
			para_suv02 = 3;
		mes "ได้รับ "+ getarg(.@i+2) +", ชุดยูนิฟอร์ม และรองเท้า รวมเป็นอุปกรณ์สนับสนุน 3 รายการครับ";
		mes "กรุณาตรวจสอบดูอีกทีนะครับ";
		next;
		mes "[Michael]";
		mes "หวังว่าพวกมันจะเป็นประโยชน์กับคุณนะครับ";
		mes getarg(.@i+4);
	}
	close;
}

moc_para01,179,44,3	script	Chef	820,{
	mes "[Chef]";
	mes "ว่าไง?";
	mes "อยากหาอะไรทานหน่อยไหม? หรือมีธุระอย่างอื่นล่ะ?";
	next;
	switch (select("สั่งอาหาร:คุยเล่น")) {
	case 1:
		mes "[Chef]";
		mes "เลือกได้เลยนะ มีคอร์สอาหาร A, B แล้วก็ C";
		mes "อยากให้ฉันอธิบายรายละเอียดแต่ละคอร์สก่อนไหม?";
		next;
		switch (select("อธิบายรายละเอียดให้ฟังหน่อย:สั่งคอร์ส A:สั่งคอร์ส B:สั่งคอร์ส C:จบการสนทนา")) {
		case 1:
			mes "[Chef]";
			mes "หือ? อยากรู้เรื่องไหนล่ะ?";
			next;
			switch (select("เกี่ยวกับคอร์ส A:เกี่ยวกับคอร์ส B:เกี่ยวกับคอร์ส C:จบการสนทนา")) {
			case 1:
				mes "[Chef]";
				mes "คอร์ส A สำหรับคนทั่วไปครับ";
				mes "ประกอบด้วยอาหาร 3 อย่าง และจานหลักก็คือ....";
				mes " ";
				mes "-เชฟพยายามชี้ไปที่รูปอาหารรูปหนึ่ง มีลักษณะเป็นแท่งม้วนสีดำ-";
				next;
				mes "[Chef]";
				mes "มันทำมาจากหญ้าทะเลน่ะ เลยดูแห้งๆ เหมือนกระดาษ";
				mes "มันคือข้าวม้วนที่มีข้าวสุก ผัก แล้วก็เนื้ออยู่ข้างใน";
				mes "ดูเรียบง่ายแต่มันอร่อยและดีต่อสุขภาพมากนะ";
				next;
				mes "[Chef]";
				mes "วัตถุดิบพวกนี้เข้ากันได้ดีกับซอสเผ็ดสูตรพิเศษ";
				mes "แถมยังผสมเนื้อบดที่ทำคล้ายๆ ไส้กรอกลงไปด้วย";
				next;
				mes "[Chef]";
				mes "มันทั้งง่ายและราคาถูก เลยเป็นที่นิยมมากสำหรับทุกคน";
				mes "แค่ 3,000 Zeny เท่านั้น";
				mes "รับรองว่าอิ่มสบายท้องแน่นอนครับ";
				close;
			case 2:
				mes "[Chef]";
				mes "อืม คอร์ส B งั้นเหรอ";
				mes "ฉันทำซอสด้วยผักที่มีกลิ่นหอมและเนื้อเคี่ยวในน้ำซุป";
				next;
				mes "[Chef]";
				mes "เนื้อถูกต้มจนเปื่อยนุ่มชนิดที่ใช้ส้อมสะกิดก็หลุดในน้ำซุปเลยล่ะ";
				mes "พอซุปใกล้ได้ที่ ฉันก็จะใส่เส้นลงไปเป็นการปิดท้าย";
				next;
				mes "[Chef]";
				mes "เป็นเมนูที่เหมาะจะแบ่งกันกินกับเพื่อนๆ มากเลยนะ";
				mes "ราคา 4,000 Zeny";
				mes "เป็นมื้อที่ทานแล้วสนุกมากเลยล่ะ";
				close;
			case 3:
				mes "[Chef]";
				mes "คราวนี้มาถึงคอร์ส C...";
				mes "มันคือผลงานชิ้นเอกแห่งเนื้อ... ตำนานของสายเนื้อเลยล่ะ!";
				next;
				mes "[Chef]";
				mes "มีทั้งเนื้อวัว, เบคอน,";
				mes "เนื้อสันนอก,";
				mes "ริบอาย (Rib Eye)";
				mes "...";
				next;
				mes "[Chef]";
				mes "ต้องบอกอะไรเพิ่มอีกไหม?";
				mes "ไม่ต้องห่วงนะ";
				mes "ฉันใช้ไม้โอ๊คเกรดดีที่สุดในการรมควันเลย";
				mes "อ้อ ราคา 5,000 Zeny เอง ถูกใช่ไหมล่ะ? ฉันใช้แต่เนื้อเกรดพรีเมียมนะ ไม่ต้องกังวล";
				close;
			case 4:
				mes "[Chef]";
				mes "ฉันไม่มีเวลามานั่งคุยเล่นนานหรอกนะ...";
				close;
			}
		case 2:
			mes "[Chef]";
			mes "คอร์ส A เหรอ?";
			mes "อ้อ คิม-ต็อก-ซุน (ข้าวห่อสาหร่าย-ต็อกโบกี-ไส้กรอกเลือด)";
			mes "เป็นอาหารตัวแทนของประชาชนคนธรรมดาเลยล่ะ";
			next;
			if (callfunc("F_HasEdenGroupMark")) {
				if (Zeny > 2699) {
					mes "[Chef]";
					mes "เอ้า นี่ครับ";
					mes "ทานให้อร่อยนะ";
					next;
					mes "- หลังจากทานเข้าไปแล้ว คุณรู้สึกอิ่มขึ้นมานิดหน่อย";
					mes "ค่า HP และ SP ของคุณได้รับการฟื้นฟู -";
					set Zeny, Zeny - 2700;
					percentheal 50,0;
					percentheal 0,50;
					close;
				}
				mes "[Chef]";
				mes "ปกติมื้อนี้ 3,000 Zeny แต่ฉันจะคิดแค่ 2,700 พอ";
				mes "ในฐานะที่คุณเป็นสมาชิก Eden คุณได้ส่วนลด 10% เหลือ 2,700 Zeny ครับ";
				close;
			}
			if (Zeny > 2999) {
				mes "[Chef]";
				mes "เอ้า นี่ครับ";
				mes "ทานให้อร่อยนะ";
				next;
				mes "- หลังจากทานเข้าไปแล้ว คุณรู้สึกอิ่มขึ้นมานิดหน่อย";
				mes "ค่า HP และ SP ของคุณได้รับการฟื้นฟู -";
				set Zeny, Zeny - 3000;
				percentheal 50,0;
				percentheal 0,50;
				close;
			}
			mes "[Chef]";
			mes "มื้อนี้ราคา 3,000 Zeny ครับ";
			mes "ต้องให้บอกอีกกี่รอบเนี่ย?";
			close;
		case 3:
			mes "[Chef]";
			mes "คอร์ส B เหรอ?";
			mes "เมนูที่มีทั้งเนื้อและผักในซุปร้อนๆ นี้มีต้นกำเนิดมาจากชีวิตเร่ร่อนท่ามกลางธรรมชาติที่หนาวเหน็บและแห้งแล้งน่ะ";
			next;
			if (callfunc("F_HasEdenGroupMark")) {
				if (Zeny > 3599) {
					mes "[Chef]";
					mes "เอ้า นี่ครับ";
					mes "ทานให้อร่อยนะ";
					next;
					mes "- หลังจากทานเข้าไปแล้ว คุณรู้สึกอิ่มขึ้นมานิดหน่อย";
					mes "ค่า HP และ SP ของคุณได้รับการฟื้นฟู -";
					set Zeny, Zeny - 3600;
					percentheal 75,0;
					percentheal 0,75;
					close;
				}
				mes "[Chef]";
				mes "มื้อนี้ราคา 4,000 Zeny แต่ฉันลดให้เหลือ 3,600";
				mes "สมาชิก Eden ได้ส่วนลด 10% เหลือ 3,600 Zeny ครับ";
				close;
			}
			if (Zeny > 3999) {
				mes "[Chef]";
				mes "เอ้า นี่ครับ";
				mes "ทานให้อร่อยนะ";
				next;
				mes "- หลังจากทานเข้าไปแล้ว คุณรู้สึกอิ่มขึ้นมานิดหน่อย";
				mes "ค่า HP และ SP ของคุณได้รับการฟื้นฟู -";
				set Zeny, Zeny - 4000;
				percentheal 75,0;
				percentheal 0,75;
				close;
			}
			mes "[Chef]";
			mes "มื้อนี้ราคา 4,000 Zeny ครับ";
			mes "ต้องให้บอกอีกกี่รอบกัน?";
			close;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				if (Zeny > 4499) {
					mes "[Chef]";
					mes "เอ้า นี่ครับ";
					mes "ทานให้อร่อยนะ";
					next;
					mes "- เนื้อริบอายถูกย่างบนฟืนไม้โอ๊คจนหอม";
					mes "- หลังจากทานเข้าไปแล้ว คุณรู้สึกอิ่มขึ้นมา";
					mes "ค่า HP และ SP ของคุณได้รับการฟื้นฟูจนเต็ม -";
					set Zeny, Zeny - 4500;
					percentheal 100,0;
					percentheal 0,100;
					close;
				}
				mes "[Chef]";
				mes "ปกติ 5,000 Zeny แต่สำหรับคุณแค่ 4,500 พอ";
				mes "สมาชิก Eden ลด 10% เหลือ 4,500 Zeny ครับ";
				close;
			}
			if (Zeny > 4999) {
				mes "[Chef]";
				mes "เอ้า นี่ครับ";
				mes "ทานให้อร่อยนะ";
				next;
				mes "- เนื้อริบอายถูกย่างบนฟืนไม้โอ๊คจนหอม";
				mes "- หลังจากทานเข้าไปแล้ว คุณรู้สึกอิ่มขึ้นมา";
				mes "ค่า HP และ SP ของคุณได้รับการฟื้นฟูจนเต็ม -";
				set Zeny, Zeny - 5000;
				percentheal 100,0;
				percentheal 0,100;
				close;
			}
			mes "[Chef]";
			mes "มื้อนี้ราคา 5,000 Zeny ครับ";
			mes "ต้องให้บอกอีกกี่รอบกันเนี่ย?";
			close;
		case 5:
			mes "[Chef]";
			mes "ตามใจคุณละกัน";
			close;
		}
	case 2:
		if (callfunc("F_HasEdenGroupMark")) {
			mes "[Chef]";
			mes "งานส่วนใหญ่นี่ต้องจัดการด้วยตัวเองหมดเลยสินะ มันอาจจะยากหน่อยใช่ไหมล่ะ?";
			mes "ที่จริงมันก็อันตรายนะ พวกเขาถึงได้ส่งคำร้องกันมาไม่หยุดเลย";
			next;
			mes "[Chef]";
			mes "พวกเราทุกคนจะร่วมแรงร่วมใจช่วยกันไม่ได้เลยเหรอ?";
			mes "เอาเถอะ แล้วคุณล่ะ...";
			next;
			if (para_suv01 == 0) {
				if (BaseLevel < 41) {
					mes "[Chef]";
					mes "เป็นยังไงบ้าง?";
					mes "อืม... ตอนนี้คุณดูไม่เหมือนมือใหม่แล้วนะ";
					mes "อยากรู้อะไรดีๆ ไหมล่ะ?";
					next;
					mes "[Chef]";
					mes "กลุ่ม Eden ของเรา... บางทีก็รับงานจ้างนะ";
					mes "แต่พวกเขาก็ผลิตชุดเกราะและอาวุธขึ้นมาเองด้วย";
					next;
					mes "[Chef]";
					mes "ถ้าคุณต้องการ คุณสามารถไปรับชุดยูนิฟอร์มจาก Eden Group ได้นะ";
					next;
					mes "[Chef]";
					mes "การจะได้มันมา คุณต้องทำภารกิจฝึกฝนให้สำเร็จก่อน";
					mes "ถ้าสนใจ ก็ไปหา Instructor Boya ที่เคาน์เตอร์ดูสิ";
					close;
				}
				mes "[Chef]";
				mes "คุณดูเหมือนผู้เชี่ยวชาญเลยนะ...";
				mes "อืมมมมมม.. ";
				mes "อืมมมมมมมมม..";
				next;
				mes "[Chef]";
				mes "ใช่! ฉันเจอสิ่งที่เธอขาดไปแล้ว!";
				mes "เธอยังไม่ได้ชุดยูนิฟอร์มของ Eden Group ใช่ไหม?";
				next;
				mes "[Chef]";
				mes "อืม มันก็ไม่จำเป็นหรอกนะ แต่ถ้าจะอยู่กับ Eden Group ต่อไป การมีชุดมันก็เป็นสัญลักษณ์ที่ดีนะ";
				next;
				mes "[Chef]";
				mes "มันอาจจะได้มาไม่ง่ายนักหรอก แต่...";
				mes "ถ้าสนใจ ลองไปถาม Instructor Boya ที่เคาน์เตอร์ดูนะ";
				close;
			}
			mes "[Chef]";
			mes "ชุดยูนิฟอร์มนั่นดูเหมาะกับคุณดีนะ";
			mes "โอ้ แน่นอนล่ะ";
			mes "เดี๋ยวนี้ท่านผู้นำเป็นคนออกแบบเองเลยนะนั่น";
			next;
			mes "[Chef]";
			mes "อาฮะ.. โดยเฉพาะหมวกนั่นน่ะ";
			mes "สำหรับ.. ฉัน.. หมวกนั่นมัน... อืมมม";
			mes "มันดูเข้ากับ Laime นะ แต่กับ Luke นี่... ฉันไม่แน่ใจแฮะ..";
			next;
			switch (select("หมวกนั่นมันทำไมเหรอ?:Luke คือใคร?")) {
			case 1:
				mes "[Chef]";
				mes "เปล่าหรอก มันก็น่ารักดีนะ";
				mes "ฉันแค่กังวลเรื่องโบว์นั่นน่ะ";
				mes "แต่ก็นะ มันจำเป็นนี่นาที่ฉันต้องใส่หมวกใบนี้..";
				next;
				mes "[Chef]";
				mes "ฮ่าๆๆๆ ฉันไม่สนหรอก";
				mes "ไม่สนเลยสักนิด!";
				mes "ฮ่าๆๆ.. ฮ่าฮ่าฮ่าฮ่าฮ่า... ";
				close;
			case 2:
				mes "[Chef]";
				mes "เขาเป็นผู้เชี่ยวชาญด้านมีดน่ะ";
				mes "หนึ่งในหัวหน้าหน่วยคุ้มกันของเรา";
				mes "และเป็นคนดูแลความปลอดภัยของสำนักงานนี้ด้วย";
				next;
				mes "[Chef]";
				mes "เกือบทุกคนที่เพิ่งถูกจ้างมามักจะสงสัยเรื่องของเขา";
				mes "แต่ Laime กับ Luke นี่ต่างกันลิบลับเลยล่ะ";
				next;
				mes "[Chef]";
				mes "พวกหัวหน้ากลุ่มทำงานร่วมกันมานานมากแล้ว";
				mes "แต่ท่านผู้นำดูเหมือนอยากจะปิดบังอดีตของตัวเองเอาไว้...";
				next;
				mes "[Chef]";
				mes "ที่ฉันเดาไว้ก็คือ... เขาอาจจะเป็นลูกชายจากตระกูลที่ร่ำรวยก็ได้";
				mes "มีข่าวลือว่า Luke เคยเป็นองครักษ์ของตระกูลนั้น";
				mes "ส่วน Laime เคยเป็นคนรับใช้หรือเปล่านะ? ใช่ไหมล่ะ?";
				mes "คุณคิดยังไงกับเรื่องที่ฉันเล่าล่ะ?";
				next;
				select(".....");
				mes "[Chef]";
				mes "............";
				mes "อะฮะ.. เฮ้~ ถึงฉันจะพูดเรื่องไร้สาระไปบ้าง ก็อย่ามองฉันด้วยสายตาแบบนั้นสิ";
				mes "แต่มันเป็นเรื่องจริงนะที่ทั้งสามคนร่วมกันก่อตั้ง Eden Group นี้ขึ้นมา";
				close;
			}
		}
		mes "[Chef]";
		mes "หือ? ภารกิจงั้นเหรอ? มาถูกที่แล้วเหรอเรา?";
		mes "ไหนดูซิ... คุณยังไม่ได้เป็นสมาชิกของเรานี่นา ฉันจะให้งานคุณได้ยังไง?";
		next;
		mes "[Chef]";
		mes "ไปหาอะไรทานในร้านอาหารเถอะ";
		mes "เราเสิร์ฟอาหารให้ทุกคนแหละ";
		mes "ฉันมั่นใจเลยว่ารสชาติมันสุดยอดแน่นอน!";
		close;
	}
}

moc_para01,23,35,5	script	Instructor Ur#2nd01	4_M_KNIGHT_BLACK,{
	mes "[Instructor Ur]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "เฮ้ นายตรงนั้นน่ะ!";
		mes "ดูเหมือนนายจะไม่ได้เป็นเจ้าหน้าที่ของเราสินะ?";
		mes "สนใจอยากจะเข้าร่วมกับเราไหมล่ะ?";
		next;
		mes "[Instructor Ur]";
		mes "สมัครฟรีไม่มีค่าธรรมเนียมนะจะบอกให้!";
		mes "พวกเราเป็นตัวกลางส่งความช่วยเหลือไปยังที่ที่ต้องการ ยิ่งมีนักผจญภัยแบบนายมากเท่าไหร่ กำไรเราก็ยิ่งมากเท่านั้น";
		next;
		mes "[Instructor Ur]";
		mes "ถ้าสนใจก็ไปคุยกับ Raim นะ";
		mes "Raim เป็นคนดีนะจะบอกให้";
		mes "ฮ่าฮ่าฮ่า..";
	}
	else if (para_2nd01 == 0) {
		mes "สวัสดี?";
		mes "ต้องการความช่วยเหลืออะไรไหม?";
		mes "นี่เป็นคำร้องทั่วไปเพื่อที่จะได้รับสิทธิ์รับภารกิจในกระดานขั้นต่อไปน่ะ";
		next;
		if (select( "คุณมาทำอะไรที่นี่เหรอ?", "ไม่มีอะไรครับ/ค่ะ" ) == 2) {
			mes "[Instructor Ur]";
			mes "งั้นฉันก็คงไม่มีธุระอะไรกับนายแล้วล่ะ";
			close;
		}
		mes "[Instructor Ur]";
		mes "ก็นะ ฉันเป็นอาจารย์ฝึกสอนของ Eden Group ยังไงล่ะ";
		next;
		mes "[Instructor Ur]";
		mes "ฉันอยู่ที่นี่เพื่อมอบภารกิจตามระดับเลเวลพื้นฐานของนาย";
		mes "ภารกิจพวกนี้ไม่ได้ยากเกินความสามารถหรอก";
		next;
		mes "[Instructor Ur]";
		mes "ฉันน่ะเป็นอาจารย์อาวุโสของที่นี่เชียวนะ";
		mes "ว่าไงล่ะ สนใจจะรับคำท้าไหม?";
		next;
		if (select( "วันนี้ยังไม่พร้อมครับ/ค่ะ", "ตกลง ผม/ฉันจะลองดู" ) == 1) {
			mes "[Instructor Ur]";
			mes "จริงดิ?";
			mes "น่าเสียดายชะมัด";
			mes "ถ้านายทำภารกิจพวกนี้สำเร็จ ฉันมีของรางวัลดีๆ เตรียมไว้ให้ด้วยนะเนี่ย";
			close;
		}
		mes "[Instructor Ur]";
		mes "เยี่ยม!";
		mes "โอเค ไหนขอเช็คหน่อยซิว่านายควรจะได้รับภารกิจไหนดี?";
		next;
		mes "[Instructor Ur]";
		if (BaseLevel < 60) {
			mes "อืม... ฉันรู้ว่านายมีใจสู้ แต่ตอนนี้ดูเหมือนนายจะยังอ่อนหัดเกินไปหน่อยที่จะช่วยงานนี้ได้";
			next;
			mes "[Instructor Ur]";
			mes "เสียใจด้วยนะ แต่อย่างน้อยเลเวลพื้นฐานของนายต้องถึง 60 ก่อน ถึงจะช่วยภารกิจพวกนี้ได้";
		}
		else if (BaseLevel < 70) {
			mes "โอเค เลเวลของนายถือเป็นระดับขั้นต่ำสุดที่จะทำภารกิจพวกนี้ได้พอดี";
			next;
			mes "[Instructor Ur]";
			mes "ดูจากเลเวลแล้ว ฉันว่าแถว Comodo น่าจะเหมาะกับนายนะ";
			next;
			mes "[Instructor Ur]";
			mes "รู้จักไหมว่า Comodo อยู่ที่ไหน?";
			mes "นายสามารถไปที่นั่นได้โดยผ่านทาง Morocc แล้วเดินทางต่อไปยังประภาคาร Pharos";
			next;
			mes "[Instructor Ur]";
			mes "Comodo เป็นเมืองเล็กๆ ชายฝั่งที่มีถ้ำเยอะแยะเลยล่ะ";
			next;
			mes "[Instructor Ur]";
			mes "ลองไปหาถ้ำทางทิศเหนือดูนะ ที่นั่นจะมีคนชื่อ Romeo อยู่";
			mes "รู้จักใช่ไหม? แบบโรมิโอกับจูเลียตน่ะ ฮ่าๆ";
			next;
			mes "[Instructor Ur]";
			mes "ไปรับภารกิจจากเขาซะ";
			mes "เอาล่ะ แล้วก็กลับมาหาฉันแบบมีชีวิตล่ะ!";
			para_2nd01 = 1;
			setquest 7214;// Advanced Training at Comodo
		}
		else if (BaseLevel < 80) {
			mes "ไหนดูซิ...";
			mes "เลเวลระดับนาย Comodo มันคงง่ายไปแล้วล่ะ";
			mes "ใช่แล้ว ที่นี่แหละเหมาะเลย";
			next;
			mes "[Instructor Ur]";
			mes "รู้จัก Glast Heim ไหม?";
			mes "ถ้าไม่มีอุบัติเหตุโศกนาฏกรรมครั้งนั้น ป่านนี้ที่นั่นคงเป็นเมืองหลวงของอาณาจักร Rune-Midgarts ไปแล้ว";
			next;
			mes "[Instructor Ur]";
			mes "ถ้าที่นั่นไม่เต็มไปด้วยพวกปีศาจ Undead นะ ป่านนี้คงรุ่งเรืองสุดๆ ไปเลย";
			next;
			mes "[Instructor Ur]";
			mes "ฝากด้วยนะ แล้วกลับมาแบบมีชีวิตล่ะ";
			para_2nd01 = 7;
			setquest 7219;// Advanced Training at Glast Heim
		}
		else if (BaseLevel < 90) {
			mes "อา โอเค สำหรับนาย...";
			mes "หืมมม จะส่งไปไหนดีนะ?";
			next;
			mes "[Instructor Ur]";
			mes "มีเมืองอุตสาหกรรมที่ชื่อ Einbroch อยู่";
			mes "รู้จักใช่ไหม? เมืองที่เต็มไปด้วยหมอกควันน่ะ";
			mes "จะว่าไปมันก็เป็นเมืองที่ดูหม่นหมองพอดูเลยล่ะ";
			next;
			mes "[Instructor Ur]";
			mes "เอาเป็นว่า ไปที่ Einbroch แล้วหาอาจารย์ฝึกสอนที่ประจำอยู่ที่นั่นดู เขาจะอยู่แถวๆ ทุ่งหญ้าทางใต้ของตัวเมืองน่ะ";
			setquest 7223;// Advanced Training in Einbroch
			para_2nd01 = 12;
		}
		else
			callsub S_Quest89100;
	}
	else if (para_2nd01 == 5) {
		mes "โอ้ มาแล้วๆ";
		mes "ดูเหมือน Romeo จะกลับมาจากการรายงานสถานการณ์แล้วสินะ";
		next;
		mes "[Instructor Ur]";
		mes "เรามีของรางวัลให้คุณด้วยนะ";
		mes "ฉันแน่ใจว่า Romeo คงให้ของบางอย่างกับนายมาแล้ว แต่เราเตรียมของที่มีประโยชน์กว่านั้นไว้ให้";
		next;
		mes "[Instructor Ur]";
		mes "รู้ใช่ไหมว่าคลังเก็บอาวุธอยู่ตรงไหน?";
		mes "ออกไปทางประตูสีฟ้าบานใหญ่นั่น แล้วเดินไปให้สุดทางเดินเลย";
		mes "ไปถามช่างตีเหล็กที่นั่นนะ แล้วเขาจะมอบอุปกรณ์ใหม่ให้นายเอง";
		next;
		mes "[Instructor Ur]";
		mes "และการฝึกกับภารกิจขั้นที่ 2 จะเริ่มตอนเลเวล 70 เพราะฉะนั้นถึงตอนนั้นค่อยกลับมาใหม่นะ";
		mes "อย่าลืมล่ะ ฉันจะรอนะ";
		para_2nd01 = 6;
		para_2nd02 = 1;
		erasequest 7218;// Report to Instructor Ur
	}
	else if (para_2nd01 == 6) {
		if (BaseLevel > 69) {
			mes "ไง เพื่อน~";
			mes "เป็นยังไงบ้าง?";
			next;
			switch( select( "ฉันอยากเริ่มการฝึกครับ/ค่ะ", "แค่แวะมาทักทายเฉยๆ" ) ) {
				mes "[Instructor Ur]";
			case 1:
				mes "หืม... หน่วยก้านดีนี่นา!";
				mes "ไหนขอเช็คหน่อยซิ....";
				mes "ที่ไหนจะเหมาะกับนายบ้างนะ...?";
				next;
				mes "[Instructor Ur]";
				if (BaseLevel < 80) {
					mes "อา ที่นี่แหละแจ๋วเลย!";
					mes "รู้จัก Glast Heim ไหม?";
					mes "ถ้าไม่ใช่เพราะอุบัติเหตุครั้งใหญ่นั่น ป่านนี้ Glast Heim คงยังเป็น";
					mes "เมืองหลวงของ Rune-Midgarts แทนที่จะเป็น Prontera ไปแล้ว";
					next;
					mes "[Instructor Ur]";
					mes "เอาเถอะ ตอนนี้ปราสาทโบราณนั่นเต็มไปด้วยมอนสเตอร์ Undead ไปหมดแล้ว";
					mes "โดยเฉพาะรอบๆ Abbey (โบสถ์เก่า) ทางใต้น่ะ แย่สุดๆ เลย";
					next;
					mes "[Instructor Ur]";
					mes "พวกมอนสเตอร์ Undead พวกนั้นออกมาจากสุสานใต้ดินน่ะ";
					mes "ก็นะ... ฉันว่านายคงไม่ต้องเข้าไปลึกถึงในสุสานหรอกมั้ง...";
					next;
					mes "[Instructor Ur]";
					mes "พอไปถึงที่ Abbey นายจะเห็นสมาชิกหน่วยของเราประจำการอยู่หน้าทางเข้า";
					mes "ทำตามคำแนะนำของเขาเพื่อช่วยเคลียร์พื้นที่แถวนั้นซะนะ";
					next;
					mes "[Instructor Ur]";
					mes "ขอให้กลับมาแบบปลอดภัยล่ะ";
					para_2nd01 = 7;
					setquest 7219;// Advanced Training at Glast Heim
				}
				else if (BaseLevel < 90)
					callsub S_Quest7990;
				else
					callsub S_Quest89100;
				close;
			case 2:
				mes "งั้นเหรอ? เลเวลระดับนาย ฉันว่านายจัดการภารกิจระดับท็อปได้สบายๆ เลยนะ..";
				mes "ถ้าเปลี่ยนใจเมื่อไหร่ ฉันจะรออยู่ที่นี่นะ";
				close;
			}
		}
		else {
			mes "ภารกิจฝึกฝนขั้นที่ 2 จะเริ่มตอนเลเวล 70 นะ";
			mes "แล้วเจอกันตอนนั้นล่ะ";
			mes "ว่าแต่ไปรับอุปกรณ์ที่จำเป็นจากคลังเก็บของมาหรือยัง?";
			mes "ฮ่าฮ่าฮ่า!";
		}
	}
	else if (para_2nd01 == 10) {
		mes "โอ้! นายมาแล้ว!";
		mes "ฉันเพิ่งได้รับข้อความจาก Johan น่ะ";
		mes "ฉันว่าเขามีอะไรบางอย่างที่ไม่ได้บอกฉันนะ แต่ฉันยังนึกไม่ออกว่ามันคืออะไร";
		next;
		mes "[Instructor Ur]";
		mes "เอาเถอะ ทำได้ดีมาก ดีใจนะที่นายไม่บาดเจ็บ";
		mes "ช่างตีเหล็ก BK บอกว่าจะช่วยเสริมแกร่งอาวุธให้นายด้วยล่ะ";
		next;
		callsub S_Para2;
		mes "[Instructor Ur]";
		mes "รู้ใช่ไหมว่าช่างตีเหล็กอยู่ไหน??";
		mes "ออกไปทางประตูสีฟ้า สุดทางเดินเลยนะ";
		mes "BK เป็นช่างตีเหล็กที่เก่งมาก เขาจะทำให้อาวุธของนายแข็งแกร่งขึ้น";
		mes "รออะไรอยู่ล่ะ?";
		para_2nd01 = 11;// Report to Instructor Ur
		erasequest 7222;
	}
	else if (para_2nd01 == 11) {
		if (BaseLevel > 79) {
			mes "อา พัฒนาขึ้นเรื่อยๆ เลยนะคุณ "+ strcharinfo(0) +"?";
			mes "มีอะไรให้ฉันรับใช้ล่ะวันนี้?";
			next;
			switch( select( "ผม/ฉันมาเพื่อฝึกต่อครับ/ค่ะ", "แค่แวะมาเฉยๆ" ) ) {
				mes "[Instructor Ur]";
			case 1:
				mes "หืม... ดูดีขึ้นเยอะเลยนี่!";
				mes "ไหนขอฉันดูหน่อยซิ....";
				next;
				mes "[Instructor Ur]";
				if (BaseLevel < 90)
					callsub S_Quest7990;
				else
					callsub S_Quest89100;
				close;
			case 2:
				mes "งั้นเหรอ? เลเวลระดับนาย ฉันว่านายจัดการภารกิจระดับท็อปได้สบายๆ เลยนะ..";
				mes "ฉันจะรออยู่ที่นี่ถ้าเปลี่ยนใจนะ";
				close;
			}
		}
		else {
			mes "การฝึกขั้นที่ 3 จะเริ่มตอนเลเวล 80 นะ";
			mes "แล้วเจอกัน";
			mes "อ้อ ถ้ายังไม่ได้ไปเสริมแกร่งอาวุธ ก็ลองไปคุยกับ BK ดูนะ";
		}
	}
	else if (para_2nd01 == 15) {
		mes "อา นายมาแล้ว";
		mes "ฉันเพิ่งได้รับข้อความจาก Kiren น่ะ";
		mes "เป็นไงบ้างล่ะเขา? เขาไม่ได้โวยวายใส่นายใช่ไหมที่ไปปลุกเขาน่ะ?";
		mes "แต่ถ้าเขาโวยวายขึ้นมาฉันก็ไม่แปลกใจหรอกนะ";
		next;
		mes "[Instructor Ur]";
		mes "เอาเถอะ ทำงานได้ดีมาก ดีใจที่นายปลอดภัย";
		mes "ช่างตีเหล็ก BK บอกว่าจะช่วยเสริมแกร่งอาวุธให้นายด้วย";
		next;
		callsub S_Para2;
		mes "[Instructor Ur]";
		mes "รู้ใช่ไหมว่าช่างตีเหล็กอยู่ไหน??";
		mes "ออกไปทางประตูสีฟ้า สุดทางเดินนั่นแหละ";
		mes "BK เป็นช่างฝีมือดี เขาจะทำให้อาวุธนายน่าทึ่งขึ้นเยอะ";
		mes "รออะไรอยู่ล่ะ?";
		erasequest 7228;// Report to Instructor Ur
		para_2nd01 = 16;
	}
	else if (para_2nd01 == 16) {
		if (BaseLevel > 89) {
			mes "ดีใจที่เห็นนายยังแข็งแรงดีอยู่!";
			mes "ลมอะไรหอบมาล่ะวันนี้?";
			next;
			if (select( "อยากฝึกต่อครับ/ค่ะ", "แค่แวะมาเฉยๆ" ) == 2) {
				mes "[Instructor Ur]";
				mes "จริงดิ? เลเวลระดับนายแล้ว ภารกิจระดับท็อปนี่ขนมๆ เลยนะ..";
				mes "ฉันจะรออยู่ที่นี่นะถ้าเปลี่ยนใจ";
				close;
			}
			mes "[Instructor Ur]";
			callsub S_Quest89100;
		}
		else {
			mes "การฝึกขั้นที่ 4 จะเริ่มตอนเลเวล 90 นะ";
			mes "แล้วเจอกัน";
			mes "อ้อ อย่าลืมไปคุยกับ BK เรื่องอาวุธด้วยล่ะ";
		}
	}
	else if (para_2nd01 == 20) {
		mes "ยินดีด้วยที่ผ่านการฝึกครบทุกขั้นตอน!";
		mes "ใช้เวลานานเหมือนกันนะเนี่ย หืม?";
		mes "ช่างตีเหล็ก BK บอกว่าจะเสริมแกร่งอาวุธให้นายด้วยล่ะ";
		next;
		callsub S_Para2;
		mes "[Instructor Ur]";
		mes "รู้ใช่ไหมว่าช่างตีเหล็กอยู่ไหน??";
		mes "ออกไปทางประตูสีฟ้า สุดทางเดินนั่นแหละ";
		mes "BK จะทำให้อาวุธนายไร้เทียมทานไปเลย";
		next;
		mes "[Instructor Ur]";
		mes "ทำได้เยี่ยมมาก!";
		para_2nd01 = 30;
		erasequest 7232;//Report to Instructor Ur
	}
	else if (para_2nd01 == 29) {
		mes "ยินดีด้วยที่ผ่านการฝึกครบทุกขั้นตอน!";
		mes "การสำรวจเป็นยังไงบ้าง? ดูจากสภาพแล้ว ท่าทางจะหนักเอาเรื่องเลยสินะ";
		next;
		mes "[Instructor Ur]";
		mes "เก่งมาก..";
		mes "ช่างตีเหล็ก BK บอกว่าจะเสริมแกร่งอาวุธให้นายครับ";
		next;
		callsub S_Para2;
		mes "[Instructor Ur]";
		mes "รู้ใช่ไหมว่าช่างตีเหล็กอยู่ไหน??";
		mes "ออกไปทางประตูสีฟ้า สุดทางเดินโน่น";
		mes "เขาจะทำให้อาวุธนายน่าทึ่งขึ้นไปอีก";
		next;
		mes "[Instructor Ur]";
		mes "ว่าแต่ เหมือน Michael จะมีอะไรจะคุยกับนายเหมือนกันนะ";
		mes "ลองไปคุยกับเขาดูสิ เขาอยู่ที่คลังอาวุธเหมือนกันนั่นแหละ";
		mes "ไปเถอะ";
		para_2nd01 = 30;
		erasequest 7237;// Report to Instructor Ur
	}
	else if (para_2nd01 == 30) {
		mes "ทำได้ดีมาก";
		mes "อ้อ ถ้ายังไม่ได้เสริมแกร่งอาวุธ ก็รีบไปหา BK ซะล่ะ";
		next;
		mes "[Instructor Ur]";
		mes "แล้วก็ บางที Michael อาจจะต้องการความช่วยเหลือ ถ้าว่างๆ ก็ลองไปช่วยเขาดูนะ";
		mes "ฉันมั่นใจว่าเขาจะมีรางวัลตอบแทนความเหนื่อยให้นายแน่นอน";
	}
	else if (para_2nd01 < 5)
		callsub S_Direction,"Comodo";
	else if (para_2nd01 < 10)
		callsub S_Direction,"ปราสาท Glast Heim";
	else if (para_2nd01 < 15)
		callsub S_Direction,"Einbroch";
	else if (para_2nd01 < 20)
		callsub S_Direction,"Ice Dungeon";
	else if (para_2nd01 < 29) {
		mes "นายต้องเดินทางไปไกลมากเลยนะกว่าจะถึง ^4d4dffMidgard Expedition Camp^000000!";
		mes "ถ้าอยากจะอยู่ที่นั่น ก็ต้องขยันทำภารกิจหน่อยล่ะ เข้าใจไหม?";
	}
	close;
S_Para2:
	if (para_2nd02 == 0) {
		mes "[Instructor Ur]";
		mes "ฉันเพิ่งนึกได้... นี่เป็นภารกิจแรกของนายเลยนี่นา ใช่ไหม?";
		mes "นายคงยังไม่ได้อุปกรณ์พื้นฐานเลยสินะ?";
		mes "ไปรับของที่คลังอาวุธได้เลย";
		mes "ไปถามช่างตีเหล็ก Michael แล้วเขาจะมอบอุปกรณ์ให้นายเอง";
		para_2nd02 = 1;
		next;
	}
	return;
S_Direction:
	mes "แล้ว สรุปว่านายไปที่ ^4d4dff"+ getarg(0) +"^000000 มาหรือยัง?";
	mes "ฉันบอกให้ไปที่นั่นไม่ใช่หรือไง?";
	mes "ไปที่นั่นให้เรียบร้อยก่อนแล้วค่อยกลับมาหาฉัน โอเคไหม?";
	return;

S_Quest7990:
	mes "หืม.. ไม่เลวเลย ...";
	mes "ดูเหมือนนายจะเริ่มจับจุดได้แล้วนะ..";
	mes "งั้นคราวนี้มาลองภารกิจง่ายๆ กันหน่อย";
	next;
	mes "[Instructor Ur]";
	mes "มีเมืองอุตสาหกรรมที่ชื่อ Einbroch อยู่";
	mes "เคยได้ยินไหม? เมืองที่ปกคลุมไปด้วยมลพิษน่ะ";
	mes "พอนายไปที่นั่น นายจะรู้สึกรักอากาศของที่นี่ขึ้นมาทันทีเลยล่ะ...";
	mes "เราควรจะหัดขอบคุณธรรมชาติกันบ้างนะ";
	next;
	mes "[Instructor Ur]";
	mes "เอาเถอะ ออกไปที่ทุ่งหญ้าทางใต้ของ Einbroch แล้วนายจะเจออาจารย์ฝึกสอนที่ถูกส่งไปประจำการ";
	mes "ฉันมั่นใจว่าเขาจะจำนายได้ในทันทีเลยล่ะ";
	mes "อย่าลืมพกเข็มกลัด Eden Group ไปด้วยนะ";
	next;
	mes "[Instructor Ur]";
	mes "มลพิษในเมืองนั่นน่ะทนแทบไม่ได้เลย";
	mes "แต่ทุ่งหญ้ารอบๆ นั้นยังถือว่าสะอาดพอสมควร...";
	mes "งานนี้คงง่ายสำหรับนายล่ะนะ";
	para_2nd01 = 12;
	setquest 7223;// Advanced Training in Einbroch
	return;

S_Quest89100:
	if (BaseLevel < 100) {
		mes "อา-ฮะ....";
		mes "หืม...";
		mes "นายดูเป็นคนใจเย็นดีนะ สนใจจะไปเที่ยวถ้ำน้ำแข็งหน่อยไหมล่ะ?";
		next;
		mes "[Instructor Ur]";
		mes "ทางเหนือของเมือง Rachel จะมีถ้ำที่เต็มไปด้วยน้ำแข็งอยู่";
		mes "ไปหาอาจารย์ฝึกสอนที่ถูกส่งไปที่นั่นซะ";
		para_2nd01 = 17;
		setquest 7229;// Advanced Training in Ice Dungeon
	}
	else {
		mes "แหมๆๆ...";
		mes "ฉันล่ะไม่รู้จะส่งนายไปไหนต่อดีเลยเนี่ย";
		mes "ฉันมั่นใจว่านายคงคุ้นเคยกับเกือบทุกที่แถวนี้หมดแล้ว";
		next;
		mes "[Instructor Ur]";
		mes "งั้น คราวนี้ฉันส่งนายไปที่ไกลๆ กว่าเดิมหน่อยดีไหม?";
		mes "มีเจ้าหน้าที่ Cat Paw (แมวขบวนเรือสินค้า) อยู่ที่นี่ด้วย";
		mes "เห็นเขาอยู่ตรงโน้นไหม?";
		next;
		mes "[Instructor Ur]";
		mes "ผ่านทางเขา นายจะไปที่ Ash Vacuum ได้ค่อนข้างง่ายเลยล่ะ";
		mes "เพราะมีค่ายสำรวจตั้งอยู่ที่นั่น...";
		next;
		mes "[Instructor Ur]";
		mes "ที่นายต้องทำก็คือ หาเจ้าหน้าที่ Eden Group ที่อยู่ที่นั่น";
		mes "บอกเขาว่าฉันส่งมา แล้วเขาจะจัดการที่เหลือเอง";
		mes "นายจะได้สำรวจโลกใหม่เชียวนะ";
		mes "มันเหมือนกับการไปพักร้อนเลยว่าไหมล่ะ?";
		next;
		mes "[Instructor Ur]";
		mes "ฮ่าฮ่าฮ่า!";
		mes ".. บางทีฉันก็น่าจะไปเองแทนที่จะส่งนายไปนะเนี่ย";
		mes "เอาเป็นว่า ชื่อของพวกเขาคือ Ma~ อะไรสักอย่างนี่แหละ ฉันก็จำไม่ได้เหมือนกัน";
		next;
		mes "[Instructor Ur]";
		mes "สรุปก็คือ ภารกิจคือนายต้องไปหาเจ้าหน้าที่ของเราที่ Midgard Expedition Camp";
		mes "นี่มันเป็นภารกิจที่ง่ายที่สุดเท่าที่นายเคยรับมาเลยใช่ไหมล่ะ?";
		mes "อย่าลืมของฝากตอนกลับมาด้วยล่ะ~!";
		para_2nd01 = 22;
		setquest 7233;// Advanced Training at Ash Vacuum
	}
	return;
}

//--60~69
// comodo,173,354,5	script	Dispatched Instructor#2nd02	1_M_JOBTESTER,{
comodo,173,354,5	script	Dispatched Instructor#02	1_M_JOBTESTER,{
	mes "[Romeo]";
	if (para_2nd01 < 1) {
		mes "เป็นยังไงบ้างครับ?";
		mes "Comodo เป็นที่ที่มีสภาพอากาศดีที่สุดในโลกเลยนะ";
		mes "ต่อให้จะเป็นช่วงฤดูหนาว อากาศที่ Comodo ก็ยังยอดเยี่ยมอยู่ดี";
		next;
		mes "[Romeo]";
		mes "รู้ไหมว่าเพราะอะไร?";
		mes "ก็เพราะ Comodo ถูกล้อมรอบไปด้วยมหาสมุทรและถ้ำยังไงล่ะ";
		mes "การเปลี่ยนผ่านของฤดูกาลเลยแทบไม่มีผลต่ออุณหภูมิของที่นี่เลย";
		next;
		mes "[Romeo]";
		mes "ทำตัวตามสบายนะ";
		mes "แถวนี้คือสถานที่สำหรับการพักผ่อนหย่อนใจล่ะ";
	}
	else if (para_2nd01 == 1) {
		mes "สวัสดีครับ!";
		mes "อาจารย์ Ur ส่งนายมาที่นี่ใช่ไหม?";
		mes "มาเพื่อทำเควสสินะครับ?";
		next;
		mes "[Romeo]";
		mes "ผมก็ไม่รู้หรอกนะว่างานนี้จะเหมาะกับนายหรือเปล่า";
		mes "แต่ก็นะ... ลองดูหน่อยก็ไม่เสียหายจริงไหม?";
		next;
		mes "[Romeo]";
		mes "ความพิเศษของถ้ำแห่งนี้ก็คือ มีมอนสเตอร์จำนวนมากที่ก่อตัวขึ้นมาจากหิน";
		mes "ถ้าคุณระมัดระวังตัวให้ดี พวกมันก็ไม่น่ากลัวเท่าไหร่หรอก...";
		next;
		mes "[Romeo]";
		mes "แต่ส่วนใหญ่พวกมันจะโจมตีระยะไกลได้นะ..";
		mes "และบางครั้งก็มีตัวที่แข็งแกร่งมากๆ เดินป้วนเปี้ยนอยู่แถวนี้ด้วย";
		next;
		mes "[Romeo]";
		mes "ก็นะ... นี่คือเหตุผลที่นายมาฝึกฝนใช่ไหมล่ะ?";
		mes "งั้นเรามาลองรับมือกับมอนสเตอร์ตัวใหญ่ๆ กันสักหน่อยดีกว่า";
		next;
		mes "[Romeo]";
		mes "พวกมันถูกเรียกว่า 'Stalactic Golem'";
		mes "ลองไปปราบพวกมันดูสักหน่อยนะ";
		mes "ไม่ยากเกินไปใช่ไหมล่ะ?";
		para_2nd01 = 2;
		erasequest 7214;
		setquest 7215;
	}
	else if (para_2nd01 == 2) {
		if (checkquest(7215,HUNTING) == 2) {
			mes "เป็นไงบ้างครับ ฝีมือการสำรวจพัฒนาไปถึงไหนแล้ว?";
			mes "นายเคยสู้กับพวกมันมาแล้วนี่นา การเคลียร์ถ้ำนี้ก็ไม่น่าจะเป็นปัญหาสำหรับนายนะ";
			next;
			mes "[Romeo]";
			mes "อย่างที่ผมเคยบอกไป เนื่องจาก Comodo เป็นเมืองที่อยู่ในถ้ำ..";
			mes "การเดินทางออกสู่โลกภายนอกส่วนใหญ่จะใช้ทางเข้าทิศตะวันออก แต่ว่า...";
			next;
			mes "[Romeo]";
			mes "เรามาลองออกทางทิศเหนือกันดีกว่า";
			mes "ที่นั่นน่าจะมีอะไรให้นายล่าเยอะเลยล่ะ";
			next;
			mes "[Romeo]";
			mes "และแทนที่จะเดินออกไปเฉยๆ";
			mes "ช่วยจัดการ Stalactic Golem สัก 10 ตัวระหว่างทางออกหน่อยเป็นไง";
			next;
			mes "[Romeo]";
			mes "เพื่อที่นักเดินทางคนอื่นๆ จากทางเหนือจะได้เดินผ่านไปมาได้อย่างปลอดภัย";
			mes "จำไว้นะ 10 ตัว! แล้วไปเจอผมที่ทางออกทางตอนเหนือของถ้ำนะ";
			next;
			mes "[Romeo]";
			mes "ผมจะไปรออยู่ข้างนอกนั่นนะ";
			para_2nd01 = 3;
			getexp 10000,10000;
			erasequest 7215;// Stalactic Golem Hunt
			setquest 7216;// More Golem Hunting
		}
		else {
			mes "เพื่อเป็นการทดสอบระดับความยากของถ้ำนี้";
			mes "ไปจัดการ Stalactic Golem 3 ตัวครับ";
			mes "ระวังมอนสเตอร์ตัวอื่นๆ ด้วยนะ";
			next;
			mes "[Romeo]";
			mes "ให้ผมช่วยฟื้นฟูร่างกายให้หน่อยนะ ถ้าคุณรู้สึกเหนื่อย";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 3) {
		mes "คุณต้องผ่านถ้ำนี้ออกไปข้างนอกให้ได้นะ";
		mes "อย่าลืมจัดการ Stalactic Golem ที่เจอระหว่างทางด้วยล่ะ";
	}
	else {
		mes "ผมมักจะใช้บททดสอบแบบเดิมกับเหล่านักเรียนที่มาที่นี่เสมอ..";
		mes "แต่ผมยังไม่เคยเห็นใครผ่านมันไปได้อย่างสมบูรณ์แบบเท่านายมาก่อนเลยนะเนี่ย";
	}
	close;
}

um_fild01,34,280,5	script	Instructor Romeo#2nd02	1_M_JOBTESTER,{
	mes "[Romeo]";
	if (para_2nd01 < 2) {
		mes "ถ้าคุณถูกส่งตัวมาจาก Eden Group เพื่อรับการฝึก ให้ไปรอผมที่ในเมืองนะ";
		mes "ตอนนี้ผมกำลังรอนักเรียนคนอื่นอยู่ครับ";
	}
	else if (para_2nd01 == 2) {
		mes "ไม่ๆ.. ไม่ใช่ตรงนี้ครับ";
		mes "ตอนนี้ผมกำลังรอนักเรียนคนอื่นอยู่";
		mes "กลับไปก่อนนะ";
	}
	else if (para_2nd01 == 3) {
		if (checkquest(7216,HUNTING) == 2) {
			mes "อา ดูเหมือนคุณจะไม่หลงทางนะ";
			mes "ทักษะการปราบ Stalactic Golem ของคุณน่ะทำได้ดีกว่าที่ผมคาดไว้ซะอีก";
			mes "นั่นหมายความว่าคุณพัฒนาขึ้นมากเลยล่ะ";
			next;
			mes "[Romeo]";
			mes "คราวนี้ลองมองไปรอบๆ สิ";
			mes "ตอนนี้เราอยู่ในป่าทึบกันแล้วนะ";
			mes "ที่นี่มีพวก Dryad แล้วก็ Wootan อยู่เต็มไปหมด...";
			next;
			mes "[Romeo]";
			mes "งานต่อไปคือการกำจัดมอนสเตอร์ในแถบนี้ให้สิ้นซาก";
			mes "ไปนำไอเทมพวกนี้จากมอนสเตอร์มาให้ผมที";
			next;
			mes "[Romeo]";
			mes "Wootan Fighter's Shoulderguard 5 ชิ้น";
			mes "Dryad's Sharp Leaf 7 ชิ้น";
			next;
			mes "[Romeo]";
			mes "นี่คือบททดสอบสุดท้ายแล้วครับ";
			mes "โชคดีนะ";
			para_2nd01 = 4;
			erasequest 7216;// More Golem Hunting
			setquest 7217;// Field drops
			getexp 10000,10000;
		}
		else {
			mes "ดูเหมือนคุณจะไม่หลงทางนะ";
			mes "แต่คุณยังจัดการ Stalactic Golem ไม่ครบตามที่บอกเลย..";
			mes "กลับเข้าไปจัดการพวกมันให้เรียบร้อยก่อนนะครับ";
			next;
			mes "[Romeo]";
			mes "เดี๋ยวผมช่วยฟื้นฟูร่างกายให้ถ้าคุณรู้สึกเหนื่อย";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 4) {
		if (countitem(7196) > 4 && countitem(7100) > 6) {// Shoulder_Protection, Sharp_Leaf
			mes "คุณไปหาของพวกนี้มาได้จริงๆ สินะ?";
			mes "ผมเชื่อคุณครับ! อีกอย่างผมก็อยากออกไปจากที่นี่จะแย่แล้วด้วย";
			next;
			mes "[Romeo]";
			mes "ไม่มีภารกิจฝึกฝนสำหรับที่นี่แล้วล่ะครับ";
			mes "กลับไปรายงานตัวกับอาจารย์ Ur ได้เลย";
			delitem 7196,5;
			delitem 7100,7;
			para_2nd01 = 5;
			erasequest 7217;// Field drops
			setquest 7218;// Report to Instructor Ur
			getexp 10000,10000;
		}
		else {
			mes "ผมบอกงานสุดท้ายไปแล้วไม่ใช่เหรอ?";
			mes "จัดการมอนสเตอร์ในป่านี้ แล้วนำไอเทมที่ผมสั่งมาให้ครบ";
			next;
			mes "[Romeo]";
			mes "Wootan Fighter's Shoulderguard 5 ชิ้น";
			mes "Dryad's Sharp Leaf 7 ชิ้น";
			mes "คุณต้องนำมันมาให้ครบถึงจะผ่านบททดสอบนี้ได้นะ";
			next;
			mes "[Romeo]";
			mes "ผมจะช่วยฟื้นฟูร่างกายให้นะ ถ้าคุณรู้สึกเหนื่อย";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else {
		mes "เฮ้อ.. ที่นี่มันร้อนชะมัดเลย";
		mes "คุณไปได้แล้วล่ะครับ";
	}
	close;
}

//--70~79
// glast_01,195,131,5	script	Dispatched Instructor#2nd03	4_F_SISTER,{
glast_01,195,131,5	script	Dispatched Instructor#03	4_F_SISTER,{
	mes "[Johan]";
	if (para_2nd01 < 7) {
		mes "เฮ้อ~ เบื่อชะมัด";
		mes "นายไม่ใช่คนที่ Ur ส่งมาใช่ไหม?";
		mes "อ้อ~ ไม่เป็นไรหรอก";
		next;
		mes "[Johan]";
		mes "ที่นี่คือ Glast Heim Abbey (โบสถ์ร้าง)";
		mes "ระวังตัวด้วยนะ แถวนี้มีวิญญาณหลงทางเยอะแยะเลยล่ะ";
	}
	else if (para_2nd01 == 7) {
		mes "โอ้ โอ้ว!!";
		mes "ในที่สุดก็มาสักที!";
		mes "ฉันขอให้ Ur ส่งคนมาช่วยฉันที่นี่ตั้งนานแล้ว!";
		next;
		select("อะไรนะ? ผม/ฉันมาเพื่อรับการฝึกนะ..");
		mes "[Johan]";
		mes "ไม่เอาน่า มันก็เรื่องเดียวกันนั่นแหละ!";
		mes "ภารกิจฝึกฝนของนายก็คือช่วยฉันเคลียร์โบสถ์ร้างแห่งนี้ยังไงล่ะ!";
		next;
		mes "[Johan]";
		mes "ฟู่... มันลำบากมากเลยนะ";
		mes "ฉันน่ะบุกเข้าไปคนเดียวไม่ไหวหรอก แถมยังมีพวก Gargoyle คอยไล่จิกฉันอยู่ข้างนอกนี่อีก..";
		mes "ฮ่าฮ่าฮ่า...";
		next;
		mes "[Johan]";
		mes "โบสถ์นี้อยู่ในความรับผิดชอบของเรา";
		mes "ก็เหมือนกับอารามอื่นๆ ที่นั่นมีสุสานใต้ดินด้วย แต่ว่า..";
		next;
		mes "[Johan]";
		mes "อย่างที่รู้กัน ปราสาทแห่งนี้... มันอยู่ในสภาพที่ไม่ปกติ";
		mes "ฉันคิดว่านั่นเป็นสาเหตุที่ทำให้วิญญาณพวกนี้ไม่สามารถไปสู่สุขคติได้";
		next;
		mes "[Johan]";
		mes "เรามากำจัดพวกที่อยู่แถวๆ นี้ก่อนเถอะ เพราะการลงไปใต้ดินตอนนี้มันอาจจะหนักเกินไปสำหรับเราสองคน";
		mes "แล้วฉันจะทดสอบฝีมือของนายไปในตัวด้วยเลยนะ...";
		next;
		mes "[Johan]";
		mes "เป้าหมายแรกคือพวก Wraith";
		mes "นายแยกพวกมันออกได้ง่ายๆ จากเสียงหัวเราะประหลาดๆ ของมันน่ะ";
		mes "รูปร่างมันดูเหมือนปีศาจนะ แต่เราก็ยังไม่แน่ใจนักหรอก...";
		next;
		mes "[Johan]";
		mes "ไปกำจัด Wraith 20 ตัว";
		mes "ฉันจะคอยสนับสนุนนายจากตรงนี้เอง!";
		para_2nd01 = 8;
		erasequest 7219;// Advanced Training at Glast Heim
		setquest 7220;// Wraith Hunt
	}
	else if (para_2nd01 == 8) {
		if (checkquest(7220,HUNTING) == 2) {
			mes "โอ้~ นายนี่แข็งแกร่งไม่เบาเลยนะ!";
			mes "ดีใจจริงๆ ที่มีคนเก่งๆ แบบนายมาช่วย";
			mes "ถ้าฉันอยู่คนเดียวที่นี่ต่อ มีหวังไม่รอดแน่ๆ";
			next;
			mes "[Johan]";
			mes "ว่าแต่ นายเห็นมอนสเตอร์ตัวอื่นนอกจากพวก Wraith ไหม?";
			mes "พวกนั้นคือนักบวชที่จิตใจถูกกัดเซาะจนกลายเป็นวิญญาณร้ายน่ะ";
			mes "พวกเขาเรียกมันว่า 'Evil Druid'";
			next;
			mes "[Johan]";
			mes "คราวนี้เราไปจัดการเจ้า Evil Druid พวกนี้กันเถอะ";
			mes "สัก 10 ตัวนะ";
			mes "นายทำได้อยู่แล้วใช่ไหม?";
			mes "ฉันเชื่อใจนายน้า!";
			para_2nd01 = 9;
			erasequest 7220;// Wraith Hunt
			setquest 7221;// Evil Druid hunt
			getexp 20000,20000;
		}
		else {
			mes "Wraith 20 ตัวนะ!";
			mes "ฉันจะคอยระวังหลังให้เอง!";
			mes "... แะ.. แน่นอนอยู่แล้ว.. ฉันก็จะช่วยสู้ด้วยเหมือนกัน!";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 9) {
		if (checkquest(7221,HUNTING) == 2) {
			mes "...โอ้ กลับมาแล้วเหรอ!";
			mes "(Johan แง้มประตูออกไปเช็คดูข้างนอก)";
			mes "....!";
			mes "โอเค เรียบร้อย!!";
			next;
			mes "[Johan]";
			mes "ขอบคุณมากนะ! ฉันซึ้งใจจริงๆ!";
			mes "คราวนี้ฉันคงเข้าไปข้างในได้แบบไม่ต้องกังวลอะไรแล้วล่ะ!";
			mes "หวังว่าพวกวิญญาณร้ายพวกนั้นจะเลิกยุ่งกับที่นี่สักทีนะ..";
			next;
			mes "[Johan]";
			mes "ฮิฮิ เดี๋ยวฉันจะไปชมให้นายให้ Ur ฟังเยอะๆ เลย!";
			mes "ว่านายน่ะเป็นนักปราบผีที่กล้าหาญและเก่งกาจมาก!";
			next;
			mes "[Johan]";
			mes "นายกลับไปที่ Eden ได้แล้วล่ะ";
			mes "เดี๋ยวฉันจะอยู่เก็บกวาดส่วนที่เหลือเอง ฮิฮิ";
			para_2nd01 = 10;
			erasequest 7221;// Evil Druid hunt
			setquest 7222;// Report to Instructor Ur
			getexp 20000,20000;
		}
		else {
			mes "Evil Druid 10 ตัว!";
			mes "ฉันจะระวังหลังให้เหมือนเดิมนะ";
			mes "... แะ.. แน่นอน.. ฉันก็จะสู้กับพวกมันด้วยเหมือนกัน!";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 10) {
		mes "กลับไปรายงานตัวกับ Ur ได้เลย";
		mes "บอกเขาว่า Glast Heim Abbey เคลียร์เรียบร้อยแล้ว";
		mes "ฉันต้องอยู่ที่นี่ต่อเพื่อหาของบางอย่างน่ะ... ลาก่อนนะ";
	}
	else {
		mes "เฮ้ออ ยังหาของที่ทำหายไปไม่เจอเลย";
		mes "หืม... สงสัยต้องรอให้นักเรียนคนต่อไปมาก่อนล่ะมั้ง ถึงจะเริ่มหาต่อได้... อุ๊ย!";
		next;
		mes "[Johan]";
		mes "เอ่อ... นายไม่ได้ยินอะไรเมื่อกี้ใช่ไหม?";
		mes "ฮ่าฮ่าฮ่าฮ่าฮ่าฮ่า!!!";
		mes "ฮฮ่าฮ่าฮ่าฮ่าฮ่า!!";
	}
	close;
}

//--80~89
// ein_fild08,172,359,3	script	Dispatched Instructor#2nd02	4_M_4THPRIN1,{
ein_fild08,172,359,3	script	Dispatched Instructor#04	4_M_4THPRIN1,{
	mes "[Kiren]";
	if (para_2nd01 < 12) {
		mes "มองอะไร?";
		mes "มีธุระอะไรกับฉันหรือเปล่า?";
		mes "ไปให้พ้นๆ ไป๊ สนใจแต่เรื่องของตัวเองเถอะ..";
	}
	else if (para_2nd01 == 12) {
		mes "อะไรนะ?";
		mes "....";
		mes "หืม Ur ส่งนายมาเหรอ?";
		mes "อ้อ ไอ้ภารกิจฝึกฝนนั่นสินะ";
		next;
		mes "[Kiren]";
		mes "อุตส่าห์เดินทางมาตั้งไกล ไม่ต้องห่วงหรอก ฉันไม่สั่งงานอะไรที่มันหนักเกินไปให้ทำแน่";
		mes "ทุ่งหญ้านอกเมือง Einbroch นี่ก็ไม่ได้แย่เท่าไหร่นะ";
		next;
		mes "[Kiren]";
		mes "มอนสเตอร์แถวนี้ปกติมันก็ไม่ค่อยมายุ่งกับฉันหรอก ฉันเลยนอนกลางวันได้ทั้งวัน";
		mes "แต่มันก็มีบางตัวที่ชอบมาป่วนฉันอยู่เหมือนกัน";
		next;
		mes "[Kiren]";
		mes "รู้จัก Porcellio ไหม?";
		mes "ไอ้แมลงหน้าตาอัปลักษณ์นั่นน่ะ... แถมยังชอบขโมยของด้วยนะ..";
		next;
		mes "[Kiren]";
		mes "ไปจัดการพวกมันเป็นการวอร์มอัพหน่อยสิ";
		mes "แถวนี้มีเพียบเลย สัก 30 ตัวคงไม่ยากเกินไปใช่ไหมล่ะ?";
		next;
		mes "[Kiren]";
		mes "ที่สั่งเนี่ย ไม่ใช่ว่าฉันแค้นที่มันแอบมากินข้าวเที่ยงฉันหรอกนะ";
		mes "ฉันจะพักผ่อนรออยู่ตรงนี้จนกว่านายจะเสร็จละกัน";
		para_2nd01 = 13;
		erasequest 7223;// Advanced Training in Einbroch
		setquest 7224;// Porcellio hunting
	}
	else if (para_2nd01 == 13) {
		if (checkquest(7224,HUNTING) == 2) {// Porcellio hunting
			mes "อะไรกัน เสร็จแล้วเหรอ?";
			mes "ฉันกำลังจะเคลิ้มหลับพอดีเชียว... เฮ้อ ให้ตายสิ..";
			mes "ช่วยไม่ได้แฮะ งั้นย้ายไปจุดอื่นกันเถอะ..";
			next;
			mes "[Kiren]";
			mes "เปล่าๆ นายพูดเรื่องอะไรน่ะ?";
			mes "ให้ฉัน... ไปกับนายเหรอ?";
			mes "นายไปคนเดียวได้อยู่แล้วจริงไหมล่ะ?";
			next;
			mes "[Kiren]";
			mes "โอเค งานต่อไป!";
			mes "ชาวเมือง Einbroch บอกว่ามลพิษทางทิศเหนือน่ะแย่มาก";
			mes "พวกเขากลัวว่ามลพิษมันจะลอยเข้ามาในเมือง";
			next;
			mes "[Kiren]";
			mes "ก็น่าจะเป็นเพราะพวกโรงงานแถวนั้นแหละ...";
			mes "ฉันว่าฉันควรจะตรวจสอบเรื่องนี้หน่อยนะ";
			mes "ต้นตอของมลพิษน่ะ... ไปหาให้เจอแล้วทำลายมันซะ";
			next;
			mes "[Kiren]";
			mes "Venomous 30 ตัว กับ Teddy Bear 5 ตัว";
			mes "แค่นี้น่าจะพอแล้วล่ะ";
			next;
			mes "[Kiren]";
			mes "ฉันมั่นใจว่านายทำได้โดยไม่ต้องให้ฉันคอยดูหรอก";
			mes "เพื่อเป็นการสนับสนุน ฉันจะให้ EXP นายหน่อย แล้วก็รักษาแผลให้ละกัน";
			next;
			mes "[Kiren]";
			mes "มอนสเตอร์พวกนั้นอยู่ทุ่งหญ้าทางเหนือของ Einbroch นะ";
			mes "โอเค งั้นขอให้ปลอดภัยนะ";
			mes "ฉันจะไปงีบละ";
			para_2nd01 = 14;
			erasequest 7224;// Porcellio hunting
			setquest 7226;// Venomous hunting
			setquest 7227;// Teddy Bear hunting
			getexp 30000,30000;
		}
		else {
			mes "สรุปว่าได้ไปสั่งสอนพวก Porcellio หรือยัง?";
			mes "ฉันว่านายยังทำไม่เสร็จนะ?";
			next;
			mes "[Kiren]";
			mes "ถ้าเจ็บมา เดี๋ยวฉันจะรักษาให้หน่อยละกัน";
		}
		npcskill "AL_HEAL",10,99,60;
		percentheal 0,100;
	}
	else if (para_2nd01 == 14) {
		if (checkquest(7226,HUNTING) == 2 && checkquest(7227,HUNTING) == 2) {
			mes "อืม...";
			mes "กำลังทำอยู่สินะ";
			mes "หืม...";
			mes "ฉันยังเพลียๆ อยู่เลย...";
			next;
			mes "[Kiren]";
			mes "ดีมาก มอนสเตอร์พวกนั้นน่ะกวนใจคนงานใน Einbroch มานานแล้ว";
			mes "ยอดเยี่ยมไปเลย..";
			next;
			mes "[Kiren]";
			mes "ฉันมีเรื่องบางอย่างต้องรายงานให้อาจารย์ Ur ทราบ";
			mes "ฉันแอบดูนายอยู่นะ (จริงๆ คือแอบหลับ) แล้วนายก็ทำได้ดีมาก กลับไปหา Ur แล้วส่งรายงานนี้ให้เขาซะ";
			para_2nd01 = 15;
			erasequest 7226;// Venomous hunting
			erasequest 7227;// Teddy Bear hunting
			setquest 7228;// Report to Instructor Ur
			getexp 30000,30000;
		}
		else {
			mes "มอนสเตอร์พวกนั้นอยู่ทุ่งหญ้าทางเหนือของ Einbroch นะ";
			mes "ไปตามหา Venomous...";
			mes "อย่ามาโกหกฉันนะว่าจัดการพวกมันเสร็จแล้ว";
			next;
			mes "[Kiren]";
			mes "เอ้อ แบบนี้โอเคไหม?";
			mes "เฮ้อ งานนี้มันลำบากจริงๆ";
			mes "ฉันจะช่วยให้นายรู้สึกดีขึ้นละกัน";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 15) {
		mes "พูดจริงดิ?";
		mes "นายส่งรายงานให้อาจารย์ Ur หรือยัง?";
		mes "Einbroch มีงานจ้างมาเรื่อยๆ นั่นหมายความว่านายต้องฝึกเพิ่มขึ้นนะ";
	}
	else {
		mes "ฉันไม่ใช่โจรนะ...";
		mes "ชีวิตมันลำบากจริงๆ";
		mes "แถมไม่ได้มีความทะเยอทะยานอะไรด้วย";
		next;
		mes "[Kiren]";
		mes "นักเรียนเหรอ?";
		mes "ไปสร้างผลงานซะสิ";
		mes "สบายจริงนะเนี่ย...";
		mes "หาววว.. ขอหลับยาวๆ สักงีบเถอะ..";
	}
	close;
}

//--90~99
// ice_dun01,154,13,5	script	Dispatched Instructor#2nd02	4_F_JOB_BLACKSMITH,{
ice_dun01,154,13,5	script	Dispatched Instructor#05	4_F_JOB_BLACKSMITH,{
	mes "[Naomi]";
	if (para_2nd01 <= 17) {
		mes "มันยิ่งกว่านั้นอีกนะ~";
		mes "ให้ตายสิ เวลาหายไปไหนหมดเนี่ย~";
		mes "ฉันจำได้ว่าเธอบอกว่าจะส่งคนมาหาฉันนี่นา~!";
		next;
		mes "[Naomi]";
		if (para_2nd01 < 17) {
			mes "มีอะไรเหรอ? จะมาช่วยฉันงั้นเหรอ? ใช่ไหม?";
			mes "ไม่ล่ะ ไม่ต้องลำบากหรอก";
			mes "ฉันกำลังรอคนที่มีกำหนดการจะมาช่วยฉันอยู่จ้ะ";
			close;
		}
		mes "แล้วเธอคือ?";
		mes "อ้อ Ur ส่งเธอมาที่นี่เหรอ?";
		mes "ไม่รู้เลยนะเนี่ยว่าฉันต้องยืนรอท่ามกลางหิมะนี่นานแค่ไหนแล้ว";
		next;
		mes "[Naomi]";
		mes "ที่นี่คือถ้ำน้ำแข็งแห่งที่ราบตอนเหนือจ้ะ";
		mes "ไม่มีที่ไหนที่จะหนาวเหน็บไปกว่าถ้ำที่เย็นยะเยือกตลอดทั้งปีแบบนี้อีกแล้ว";
		next;
		mes "[Naomi]";
		mes "ที่นี่แหละที่ชาวเมือง Rachel มาขนน้ำแข็งไปใช้กัน";
		mes "แต่ก็นะ แถวนี้เต็มไปด้วยมอนสเตอร์อันตรายยั้วเยี้ยไปหมดเลย";
		next;
		mes "[Naomi]";
		mes "หนึ่งในมอนสเตอร์พวกนั้นมีชื่อว่า Siroma";
		mes "พวกมันน่าจะเป็นบททดสอบที่ท้าทายสำหรับเธอนะ";
		next;
		mes "[Naomi]";
		mes "เอาล่ะ เข้าไปในถ้ำแล้วตามหาพวก Siroma ซะ";
		mes "ข้างในนั้นมันหนาวหน่อยนะ แต่งานก็ต้องเป็นงานล่ะ";
		next;
		mes "[Naomi]";
		mes "โอ้ แล้วก็บางครั้ง...";
		mes "ระวังตัวให้ดีด้วยล่ะ";
		mes "เอาล่ะ โชคดีนะจ๊ะ~!";
		para_2nd01 = 18;
		erasequest 7229;// Advanced Training in Ice Dungeon
		setquest 7230;// Siroma hunting
	}
	else if (para_2nd01 == 18) {
		if (checkquest(7230,HUNTING) == 2) {
			mes "อา... จบแล้วสินะ...";
			mes "ดีมาก!";
			mes "ฝีมือของเธอได้รับการพิสูจน์แล้ว";
			next;
			mes "[Naomi]";
			mes "ทีนี้ ฉันมีอะไรให้ทำต่ออีกนิดหน่อย";
			mes "ดูเหมือนพวก Siroma จะขยายพันธุ์เร็วขึ้นนะ";
			mes "เหมือนเดิมเลย ไปจัดการ Siroma เพิ่มอีก 30 ตัว แล้วนำวัตถุดิบบางอย่างมาให้ฉันด้วย";
			next;
			mes "[Naomi]";
			mes "แล้วก็นี่... เอ่อ...";
			mes "ฟังให้ดีๆ นะจ๊ะ";
			mes "Ice Cubic 30 อัน";
			mes "Sweet Sauce 1 อัน";
			mes "Milk 1 อัน";
			next;
			mes "[Naomi]";
			mes "ฉันไม่ได้สั่งงานนี้เพื่อตัวเองหรอกนะ รู้ใช่ไหม?";
			mes "ฉันทำเพื่อทุกคนต่างหากล่ะ";
			next;
			mes "[Naomi]";
			mes "นั่นคืออย่างสุดท้ายแล้วล่ะ";
			mes "ไปจัดการ Siroma แล้วหาของพวกนั้นมาซะ";
			mes "เราเข้าใจตรงกันนะ?";
			next;
			select("แล้วคุณจะทำอะไรล่ะ?");
			mes "[Naomi]";
			mes "ฉันเหรอ? ก็แหงอยู่แล้ว... ฉันก็จะยืนเฝ้ายามอยู่ตรงนี้ไง";
			mes "ฉัน... ฉันเป็นคนงานยุ่งนะ!";
			mes "อีกอย่างนี่มันคือการฝึกฝนของเธอนะ เธอมีสิทธิ์อะไรมาถามฉันเนี่ยว่าฉันจะทำอะไร?";
			next;
			mes "[Naomi]";
			mes "ไปได้แล้ว!";
			mes "พวก Siroma นั่นจะยิ่งอาละวาดหนักขึ้นทุกนาทีนะ";
			para_2nd01 = 19;
			erasequest 7230;// Siroma hunting
			setquest 7231;// Hunting and Gathering
			getexp 40000,40000;
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
		else {
			mes "อุปสรรคที่ใหญ่ที่สุดก็คือการรับมือกับพวก Siroma นี่แหละ";
			mes "จัดการไปครบ 30 ตัวหรือยังจ๊ะ?";
			next;
			mes "[Naomi]";
			mes "โอ้ อยากให้ฉันช่วยฟื้นฟู HP/SP งั้นเหรอ?";
			mes "ไม่มีทางเสียหรอกจ้ะ~!";
			mes "มันจะไปเป็นการท้าทายได้ยังไงถ้าฉันช่วยเธอง่ายๆ แบบนั้น?";
		}
	}
	else if (para_2nd01 == 19) {
		if (checkquest(7231,HUNTING) == 2 && countitem(7066) > 29 && countitem(7453) > 0 && countitem(519) > 0) {// Ice_Piece, Sweet_Sauce, Milk
			mes "โอ้!";
			mes "เธอหามาได้ครบทุกอย่างเลย";
			mes "โอเค งั้นขอแค่อย่างสุดท้าย...!";
			next;
			select("...... (จ้องหน้า)");
			mes "[Naomi]";
			mes "จ้ะๆ มีอะไรเหรอ? ทำไมล่ะ?";
			mes "...";
			mes "อ้อ ใช่แล้ว! ฉันต้องให้อะไรบางอย่างกับเธอสินะ";
			mes "เธอทำมามากพอแล้วล่ะ ฉันรู้";
			next;
			mes "[Naomi]";
			mes "เดี๋ยวฉันจะแจ้งให้อาจารย์ Ur ทราบนะ";
			mes "เหลืออีกไม่กี่ขั้นตอนแล้ว ไม่ต้องห่วง!";
			next;
			mes "[Naomi]";
			mes "โอ้ แล้วก็ยินดีด้วยนะ!";
			mes "นี่คือขั้นตอนการฝึกฝนด่านสุดท้ายที่เตรียมไว้แล้วล่ะ!";
			next;
			mes "[Naomi]";
			mes "สงสัยฉันต้องเริ่มทำบางส่วนด้วยตัวเองบ้างแล้วล่ะสิ~";
			mes "มัวฮ่าฮ่า~";
			delitem 7066,30;
			delitem 7453,1;
			delitem 519,1;
			para_2nd01 = 20;
			erasequest 7231;// Hunting and Gathering
			setquest 7232;// Report to Instructor Ur
			getexp 40000,40000;
		}
		else {
			mes "จัดการ Siroma ครบ 30 ตัวหรือยัง?";
			mes "เธอเคยทำมาแล้วครั้งนึง ครั้งนี้ก็ไม่น่าจะยากนะ...";
			next;
			mes "[Naomi]";
			mes "แล้วก็อย่าลืม Ice Cubic 30 อัน, Sweet Sauce 1 อัน และ Milk 1 อัน ด้วยล่ะ";
		}
	}
	else if (para_2nd01 == 20) {
		mes "ยังอยู่ตรงนี้อีกเหรอ?";
		mes "คนอื่นๆ เขามาแล้วก็ไปกันตั้งเยอะแยะแล้วนะตั้งแต่เธอมาครั้งล่าสุดน่ะ";
		mes "ตอนนี้ฉันก็ไม่ต้องไปหาวัตถุดิบพวกนั้นเองแล้วล่ะ";
		next;
		mes "[Naomi]";
		mes "กลับไปรายงานตัวกับอาจารย์ Ur ได้แล้วจ้ะ บอกเขาว่างานเรียบร้อยแล้ว";
		mes "อย่ามัวแต่โอ้เอ้ล่ะ";
	}
	else
		mes "เฮ้อออ จะมีที่ไหนที่หิมะทับถมกันหนาไปกว่าที่นี่อีกไหมนะ?";
	close;
}

// mid_camp,212,229,3	script	Dispatched Instructor#2nd02	4_F_HUWOMAN,{
mid_camp,212,229,3	script	Dispatched Instructor#06	4_F_HUWOMAN,{
	mes "[Margaret]";
	if (para_2nd01 < 22) {
		mes "อุ๊ย ขอโทษทีจ้ะ";
		mes "ฉันไม่ค่อยรู้เรื่องเกี่ยวกับการใช้ชีวิตของนักผจญภัยเท่าไหร่หรอก";
		mes "ฉันก็แค่สมาชิกคนหนึ่งของ Eden Group น่ะ...";
	}
	else if (para_2nd01 == 22) {
		mes "หลักฐานที่นายถืออยู่นั่น...";
		mes "นายมาถึงสรวงสวรรค์แห่งนี้แล้วสินะ";
		mes "อาจารย์ Ur ส่งนายมาใช่ไหม?";
		next;
		if (select( "ใช่ครับ/ค่ะ", "ไม่ใช่" ) == 2) {
			mes "[Margaret]";
			mes "อ้าว ฉันเข้าใจผิดไปเองสินะ";
			close;
		}
		mes "[Margaret]";
		mes "สวัสดีจ้ะ";
		mes "อ้อ นายมาเพื่อรับการฝึกจากอาจารย์ Ur สินะ";
		next;
		mes "[Margaret]";
		mes "โอเค ตอนนี้ฉันกำลังเจอปัญหาอยู่นิดหน่อยน่ะ ก็เลยขอให้อาจารย์ Ur ใช้ภารกิจฝึกฝนของเขามาช่วยงานทางนี้หน่อย";
		next;
		select("ปัญหาอะไรเหรอครับ/คะ?");
		mes "[Margaret]";
		mes "ใช่แล้วล่ะ";
		mes "ไปหา Mine แล้วทำตามที่เขาสั่งซะนะ";
		next;
		mes "[Margaret]";
		mes "พวกเราส่งเขาออกไปทำภารกิจบางอย่าง แต่เขายังทำไม่เสร็จสักที ฉันเลยเดาว่าเขาไม่ปอดแหกก็คงทำพลาดไปแล้วล่ะ";
		next;
		mes "[Margaret]";
		mes "ช่วยไปหาเขาที่ทุ่งหญ้า Splendide Field หน่อยได้ไหม?";
		next;
		mes "[Margaret]";
		mes "ไปหาเขาให้เจอ แล้วช่วยดูทีว่าเขาทำบ้าอะไรอยู่ที่นั่นกันแน่";
		para_2nd01 = 23;
		erasequest 7233;//  Advanced Training at Ash Vacuum
		setquest 7234;// Margaret's favor
	}
	else if (para_2nd01 < 27) {
		mes "นายหาพี่น้องสองคนนั่นเจอหรือยัง?";
		mes "ไปดูทีว่าพวกเขาเป็นยังไงกันบ้าง";
	}
	else if (para_2nd01 == 27) {
		mes "สรุปว่ามันเกิดอะไรขึ้นล่ะ?!";
		next;
		mes "- คุณเล่าเรื่องของ Meeru และ Mine ให้เธอฟัง -";
		next;
		mes "[Margaret]";
		mes "จริงเหรอ? อะไรนะ?";
		mes "มันไม่ค่อยจะ...";
		mes "โอเค เดี๋ยวฉันจะไปจัดการพวกเขาเอง";
		next;
		mes "[Margaret]";
		mes "เอาเถอะ ยังไงซะหลักฐานการปราบ Nepenthes และ Pinguicula ก็รวบรวมมาครบแล้ว";
		next;
		mes "[Margaret]";
		mes "การฝึกของนายเสร็จสิ้นแล้วล่ะ กลับไปคุยกับอาจารย์ Ur ได้เลย";
		para_2nd01 = 29;
		erasequest 7234;// Margaret's favor
		setquest 7237;// Report to Instructor Ur
	}
	else if (para_2nd01 == 29) {
		mes "ยังไงซะหลักฐานการปราบ Nepenthes และ Pinguicula ก็รวบรวมมาครบแล้วล่ะนะ";
		next;
		mes "[Margaret]";
		mes "การฝึกของนายเสร็จสิ้นแล้ว กลับไปหาอาจารย์ Ur ได้เลยจ้ะ";
	}
	else
		mes "........";
	close;
}

// man_fild01,43,234,3	script	Dispatched Instructor#2nd02	4_DST_SOLDIER,{
man_fild01,43,234,3	script	Dispatched Instructor#07	4_DST_SOLDIER,{
	mes "[Meeru]";
	if (para_2nd01 < 23) {
		mes "คิดว่าฉันจะแพ้งั้นเหรอ!";
		mes "...?";
		mes "มากันเพียบเลย... นายเป็นใครกันน่ะ?!";
		mes "Mine ส่งนายมาเป็นลูกสมุนเหรอ?";
		mes "เขาส่งนายมาจับตาดูฉันงั้นสิ!?";
		next;
		mes "[Meeru]";
		mes "หืม? ไม่ใช่หรอกเหรอ?";
		mes "... ...";
		mes "Mine ไม่ได้ส่งนายมาสินะ?";
	}
	else if (para_2nd01 == 23) {
		mes "คิดว่าฉันจะแพ้งั้นเหรอ!";
		mes "...?";
		mes "มากันเพียบเลย... นายเป็นใครกันน่ะ?!";
		mes "Mine ส่งนายมาเป็นลูกสมุนเหรอ?";
		mes "เขาส่งนายมาจับตาดูฉันงั้นสิ!?";
		next;
		mes "[Meeru]";
		mes "หืม? ไม่ใช่หรอกเหรอ?";
		mes "... ...";
		mes "Mine ไม่ได้ส่งนายมาสินะ?";
		mes "แล้วนายมาทำอะไรที่นี่ล่ะ?";
		next;
		select("มาร์กาเร็ตส่งผม/ฉันมา");
		mes "[Meeru]";
		mes "อา มาร์กาเร็ตนั่นเอง!";
		mes "Mine กับมาร์กาเร็ตชอบหาเรื่องป่วนฉันตลอดเลย ก็แค่เพราะฉันชนะ-!";
		mes "โธ่เอ๊ย...";
		next;
		select( "เกิดอะไรขึ้นเหรอ?", "ชนะอะไรเหรอ?" );
		mes "[Meeru]";
		mes "มันเริ่มจากการที่พวกนั้นทะเลาะกันน่ะ";
		mes "มาร์กาเร็ตถูกขอให้มาช่วยสอดแนมให้เราทั้งคู่ เหมือนที่นายกำลังทำอยู่นี่แหละ";
		next;
		mes "[Meeru]";
		mes "Mine บอกว่าเขาแข็งแกร่งพอที่จะปราบมอนสเตอร์ตัวไหนก็ได้";
		mes "ฉันก็เลยคะยั้นคะยอให้เขาไปพิสูจน์ให้ดูที่ทางตะวันออกหรือไม่ก็ทางตะวันตกของค่ายนี่ไง";
		next;
		mes "[Meeru]";
		mes "Mine ได้แต่ขู่ฟ่อๆ ทำเป็นพ่นลมฮึดฮัด แต่สุดท้ายก็ไม่ทำอะไรเลย";
		next;
		mes "[Meeru]";
		mes "พูดง่ายๆ ก็คือ มาร์กาเร็ตไม่เชื่อน้ำหน้าเขา ก็เลยท้าให้เขาไปฆ่า Nepenthes สัก 1 ตัว";
		next;
		mes "[Meeru]";
		mes "สรุปคือตอนนี้ฉันเลยต้องรับหน้าที่สั่งให้คนไปช่วยล่า Nepenthes 1 ตัว ก็เพราะไอ้เรื่องทะเลาะเบาะแว้งนั่นแหละ";
		next;
		mes "[Meeru]";
		mes "การล่าแค่ตัวเดียวมันไม่ยากหรอก แค่ไปที่ทุ่งหญ้าทางตะวันออกของที่นี่แล้วจัดการมันซะตัวนึงนะ";
		para_2nd01 = 24;
		setquest 7235;// Nepenthes Hunt
	}
	else if (para_2nd01 == 24) {
		if (checkquest(7235,HUNTING) == 2) {
			mes "หืมม ใช่ ฉันเห็นแล้ว";
			mes "รู้อย่างนี้ฉันน่าจะสั่งให้นายล่ามากกว่า 1 ตัวนะเนี่ย";
			next;
			mes "[Meeru]";
			mes "ยังไงซะ นายก็พิสูจน์แล้วว่านายแข็งแกร่งกว่าที่ Mine พยายามโม้ไว้เยอะเลย";
			next;
			mes "[Meeru]";
			mes "เอาล่ะ นี่เป็นรางวัลเล็กๆ น้อยๆ นะ";
			next;
			mes "[Meeru]";
			mes "ฉันจะอยู่ตรงนี้คอยดูให้แน่ใจว่าคนอื่นๆ ไปล่า Nepenthes กัน ใครจะไปรู้ ฉันอาจจะสั่งให้พวกเขาล่ามากกว่า 1 ตัวก็ได้นะ ฮ่าๆ";
			para_2nd01 = 25;
			erasequest 7235;// Nepenthes Hunt
			getexp 50000,50000;
		}
		else {
			mes "ล่า Nepenthes ตัวเดียวมันหนักเกินไปสำหรับนายเหรอ?";
			mes "หน้าดูแดงๆ นะ";
			mes "ก็ได้ เดี๋ยวฉันรักษาให้!";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 25)
		mes "ฉันจะอยู่ตรงนี้คอยดูให้แน่ใจว่าคนอื่นๆ ไปล่า Nepenthes กัน ใครจะไปรู้ ฉันอาจจะสั่งให้พวกเขาล่ามากกว่า 1 ตัวก็ได้นะ";
	else {
		mes "มีอะไรผิดปกติหรือเปล่า?";
		mes "หรือว่านายมีปัญหากับเจ้า Nepenthes นั่น...";
		mes ".........ฮึก ฮึก";
	}
	close;
}

// spl_fild02,377,149,3	script	Dispatched Instructor#2nd02	4_M_DST_MASTER,{
spl_fild02,377,149,3	script	Dispatched Instructor#08	4_M_DST_MASTER,{
	mes "[Mine]";
	if (para_2nd01 < 25) {
		mes "โลกนี้มันช่างโหดร้าย...";
		mes "...";
		mes "เดินผ่านไปเถอะครับ";
		mes "ดูสิว่าเรื่องมันกลายเป็นความล้มเหลวขนาดไหนแล้ว";
	}
	else if (para_2nd01 == 25) {
		mes "โลกที่แสนโหดร้าย...";
		mes "...";
		mes "คุณเป็นใครน่ะ?";
		next;
		if (select( "มาร์กาเร็ตส่งผม/ฉันมา", "ใช่ ฉันมาที่นี่เพื่อ..." ) == 1) {
			mes "[Mine]";
			mes "เอ๊ะ? ใครนะ!";
			mes "อ้อ เข้าใจแล้ว ถึงเวลาแล้วสินะ?";
			mes "งานนี้มันยากจริงๆ นะคุณ";
			mes "ตอนนี้ผมกำลังตกที่นั่งลำบากสุดๆ เลย!";
		}
		else {
			mes "[Mine]";
			mes "อ้อ สรุปว่าคุณไม่ได้มาที่นี่ด้วยความสมัครใจสินะ?";
			mes "แล้วจะมาทำไมให้เสียเวลาล่ะ?";
			next;
			mes "[Mine]";
			mes "คุณไม่อยากมาที่นี่ใช่ไหมล่ะ?";
			mes "ผมพนันได้เลยว่ามาร์กาเร็ตส่งคุณมาแทนคนอื่นแน่ๆ";
			mes "เอาเถอะ ยังไงตอนนี้ผมก็กำลังแย่อยู่ดี!";
		}
		next;
		mes "[Mine]";
		mes "โอเค ผมไม่อยากจะยอมรับหรอกนะ แต่คุณต้องช่วยผมหน่อยแล้วล่ะ";
		next;
		mes "[Mine]";
		mes "คือแค่ผมบ่นนิดบ่นหน่อย ไม่ได้หมายความว่าพวกเขาต้องส่งผมมาที่นี่นี่นา!";
		next;
		mes "[Mine]";
		mes "ผมหมายถึง พวกเขาเรียกร้องมากเกินไปจากคนธรรมดาๆ อย่างผมนะ";
		next;
		mes "[Mine]";
		mes "เอาละ ข้อตกลงคือแบบนี้";
		mes "จริงๆ ผมต้องไปล่าเจ้า Pinguicula สัก 5 ตัว";
		mes "แต่คุณจะช่วยทำแทนผมหน่อยใช่ไหมล่ะ?";
		next;
		mes "[Mine]";
		mes "ผมสัญญาว่าจะให้ของดีๆ เป็นการตอบแทน";
		mes "ขอบคุณมากนะ";
		para_2nd01 = 26;
		setquest 7236;// Pinguicula Hunt
	}
	else if (para_2nd01 == 26) {
		if (checkquest(7236,HUNTING) == 2) {
			mes "ยอดเยี่ยม!";
			mes "ทีนี้ผมก็กลับไปที่ค่ายได้แล้ว และ...";
			next;
			mes "[Mine]";
			mes "เอ่อ ผมหมายถึง คุณช่วยไปบอกมาร์กาเร็ตทีได้ไหมว่าทุกอย่างเรียบร้อยแล้ว?";
			next;
			mes "[Mine]";
			mes "โอ้ ไม่ต้องห่วงหรอก เธอไม่ว่าอะไรหรอก เดี๋ยวผมจะกลับไปทำงานที่ค้างไว้ต่อ นี่รางวัลของคุณครับ";
			para_2nd01 = 27;
			erasequest 7236;// Pinguicula Hunt
			getexp 50000,50000;
		}
		else {
			mes "ไม่เอาน่า แค่ Pinguicula 5 ตัวเอง";
			mes "ถามจริง คุณมาถึง New World ได้ยังไงเนี่ย?";
			next;
			mes "[Mine]";
			mes "คุณไม่ได้หลอกผมเรื่องที่เป็นนักผจญภัยจริงๆ ใช่ไหม?";
			npcskill "AL_HEAL",10,99,60;
			percentheal 0,100;
		}
	}
	else if (para_2nd01 == 27) {
		mes "มาร์กาเร็ตน่ะได้ทุกอย่างที่เธอต้องการจากค่ายอยู่แล้วล่ะ";
		mes "ไม่ต้องห่วงนะ เดี๋ยวผมก็ตามกลับไปเร็วๆ นี้แหละ";
	}
	else
		mes ".........";
	close;
}

moc_para01,111,83,3	script	Blacksmith Thorn#2nd10	2_M_OLDBLSMITH,{
	if (checkweight(2571,5) == 0) {
		mes "- ช่องเก็บของของคุณเต็มเกินไป ไม่สามารถดำเนินการต่อได้ -";
		close;
	}
	mes "[Thorn]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "หืม คุณเป็นสมาชิก Eden Group หรือเปล่า?";
		mes "คุณต้องเป็นสมาชิกของเราก่อนถึงจะใช้บริการของฉันได้นะ";
		close;
	}
	mes "มีอะไรให้ช่วยล่ะ?";
	next;
	switch( select( "เสริมพลังอุปกรณ์", "คำขอส่วนตัว", "ขอรับอุปกรณ์ใหม่" ) ) {
		mes "[Thorn]";
	case 1:
		if (para_2nd01 < 6) {
			mes "หืม เสริมพลังอุปกรณ์งั้นเรอะ";
			mes "ฉันไม่ได้ทำให้ใครสุ่มสี่สุ่มห้านะ";
			mes "ขั้นแรกคุณต้องผ่านการฝึกระดับสูงมาบ้าง คุณผ่านการฝึกของ Eden Group หรือยังล่ะ?";
			next;
			mes "[Thorn]";
			mes "พลังในตัวคุณยังไม่ตื่นขึ้นเลย";
			mes "ไปทำภารกิจที่อาจารย์ Ur มอบให้เสร็จก่อน แล้วฉันจะช่วยเสริมพลังให้";
			next;
			mes "[Thorn]";
			mes "ฉันจะไม่ช่วยจนกว่าคุณจะทำเรื่องนั้นให้เสร็จก่อน!";
		}
		else if (para_2nd04 == 3) {
			mes "จะเสริมพลังอุปกรณ์สินะ?";
			mes "ฉันจะเสริมพลังให้อุปกรณ์ Eden Group ชิ้นใหม่ โดยการเพิ่มค่าสถานะแบบสุ่มให้";
			next;
			mes "[Thorn]";
			mes "แล้วคุณอยากจะเสริมพลังชิ้นไหนล่ะ?";
			next;
			switch( select( "Eden Group Manteau II", "Eden Group Armor IV", "Eden Group Boots IV" ) ) {
				case 1: callsub S_Enchant,2571,"Manteau";// Para_Team_Manteau2
				case 2: callsub S_Enchant,15031,"Armor";// Para_Team_Armor
				case 3: callsub S_Enchant,2473,"Boots";// Para_Team_Boots4
			}
		}
		else {
			mes "ฉันจะเสริมพลังให้ หลังจากที่คุณช่วยทำคำขอส่วนตัวของฉันเสร็จแล้ว";
			next;
			mes "[Thorn]";
			mes "ฉันแค่ต้องการให้แน่ใจว่าคุณคู่ควรกับบริการของฉันน่ะ";
		}
		close;
	case 2:
		if (para_2nd02 < 2) {
			mes "คุณเริ่มคำขอของอาจารย์ Ur หรือยัง?";
			mes "ฉันจะให้บริการเฉพาะคนที่คู่ควรเท่านั้น";
			next;
			mes "[Thorn]";
			mes "อีกอย่าง คุณจะยังไม่มีอุปกรณ์ที่ฉันสามารถเสริมพลังให้ได้ จนกว่าคุณจะทำภารกิจพวกนั้นเสร็จ";
		}
		else if (para_2nd04 == 0) {
			mes "ยินดีที่ได้เจอ";
			mes "ในฐานะช่างตีเหล็ก ฉันต้องการวัตถุดิบจำพวกเหล็กอยู่ตลอดเวลา";
			next;
			mes "[Thorn]";
			mes "แน่นอนว่าต้องมีรางวัลตอบแทนอยู่แล้ว...";
			mes "เพราะฉะนั้น...";
			next;
			mes "[Thorn]";
			if (BaseLevel < 80) {
				mes "ไปหาวัตถุดิบมาให้ฉันหน่อย";
				mes "ฉันต้องการ 20 Iron Ore และ 10 Iron";
			}
			else {
				mes "ไปหาวัตถุดิบมาให้ฉันหน่อย";
				mes "เอา Used Iron Plate มา 20 อัน";
				.@add = 1;
			}
			mes "ของพวกนี้หาไม่ยากใช่ไหมล่ะ ช่วยหามาให้ตอนนี้เลยได้ไหม?";
			next;
			switch( select( "ได้เลยครับ/ค่ะ", "ไม่เอาด้วยหรอก" ) ) {
				mes "[Thorn]";
			case 1:
				mes "ขอบใจมาก";
				mes "ตามสบายนะ ฉันจะรออยู่ตรงนี้";
				para_2nd04 = 1 + .@add;
				setquest (7238 + .@add);// Errands for Thorn (easy/hard)
				close;
			case 2:
				mes "เอ๊ะ...?";
				mes "สงสัยคุณคงจะยุ่งเกินกว่าจะช่วยฉันสินะ";
				close;
			}
		}
		else if (para_2nd04 == 1 || para_2nd04 == 2) {
			// (ส่วนเช็คไอเทม 20 Iron Ore / 10 Iron หรือ 20 Used Iron Plate)
			if (para_2nd04 == 1) {
				setarray .@item[0],1002,998;// Iron_Ore, Iron
				setarray .@amount[0],20,10;
				setarray .@countitem[0],countitem(1002),countitem(998);
				.@string$ = "ฉันขอให้คุณไปหาแร่ Iron Ore 20 ชิ้น และ Iron 10 ชิ้น";
			}
			else {
				.@item[0] = 7319;
				.@amount[0] = 20;
				.@countitem[0] = countitem(7319);
				.@add = 1;
				.@string$ = "ฉันขอให้คุณหา Used Iron Plate 20 ชิ้น";
			}
			if (.@countitem[0] >= .@amount[0] && .@countitem[1] >= .@amount[1]) {
				mes "ให้ฉันเดานะ...";
				mes "คุณนำวัตถุดิบคุณภาพดีที่สุดมาให้ฉันสินะ";
				mes "ดีมาก";
				next;
				mes "[Thorn]";
				mes "เหล็กพวกนี้มีประโยชน์กับฉันเสมอ";
				mes "แทนที่จะให้รางวัลทั่วไป ฉันจะมอบอุปกรณ์ระดับสูงให้คุณแทน";
				next;
				mes "[Thorn]";
				mes "ถ้าเมื่อไหร่ที่คุณรู้สึกว่าต้องเสริมพลังอุปกรณ์ ก็แวะมาหาฉันอีกนะ";
				delitem .@item[0],.@amount[0];
				if (.@countitem[1]) delitem .@item[1],.@amount[1];
				para_2nd04 = 3;
				erasequest (7238 + .@add);
				setquest 7240;
			}
			else {
				mes .@string$;
				mes "ฉันว่ามันไม่ได้ยากเกินความสามารถของคุณหรอกนะ";
			}
		}
		else if (para_2nd04 == 3) {
			mes "เอาละ พอแค่นี้ก่อน";
			mes "สำหรับวันนี้เท่านี้ก็เกินพอแล้ว";
			mes "ตอนนี้ ถ้าอยากให้ข้าตีบวกเพิ่มพลังให้อุปกรณ์ชิ้นไหน ก็บอกมาได้เลย";
			next;
			mes "[Thorn]";
			mes "ถ้าเจ้ามีของพวกนี้อยู่ละก็ เอามาให้ข้าได้เลย";
			mes "Eden Group Manteau II, Eden Group Armor IV, หรือ Eden Group Boots IV";
		}
		else if (para_2nd04 == 4) {
			if (checkquest(7241,PLAYTIME) == 0) {
				mes "ค่อยกลับมาใหม่ทีหลังนะ";
				mes "เจ้าต้องรอให้เวลาผ่านไปสักพักก่อน";
			}
			else {
				mes "อาฮะ...";
				mes "เราจะทำอะไรต่อไปดีนะ... โอ๊ะ ไม่ต้องกังวลไป";
				mes "ไว้ค่อยมาหาข้าใหม่วันหลังนะ";
				erasequest 7241;// Errands for Thorn timer
				para_2nd04 = 0;
			}
		}
		close;
	case 3:
		if (para_2nd02 < 1) {
			mes "อาจารย์ Ur ยังไม่อนุญาตให้ฉันมอบสิ่งนี้ให้คุณ";
			mes "อย่ามาล้อเล่นกับฉันนะ";
		}
		else if (para_2nd02 == 1) {
			mes "โอเค ฉันจะมอบอุปกรณ์ให้";
			mes "มันเป็นเกราะและอุปกรณ์ชั้นดีที่รับรองว่าคุณต้องภูมิใจ";
			next;
			mes "[Thorn]";
			mes "ทีนี้ ^4d4dffตรวจสอบให้แน่ใจนะว่าคุณมีช่องว่างในกระเป๋าเพียงพอ^000000";
			next;
			if (select( "ขอเวลาจัดของสักครู่", "ฉันมีที่ว่างพอ" ) == 1) {
				mes "[Thorn]";
				mes "ไปจัดการซะ";
				mes "เคลียร์ที่ว่างให้เรียบร้อย จะได้เอาอาวุธ รองเท้า และอุปกรณ์อื่นๆ ไปได้";
				close;
			}
			mes "[Thorn]";
			if (BaseJob == Job_Knight || BaseJob == Job_Crusader) {
				callsub L_Select,"เจ้าต้องการ ดาบมือเดียว, ดาบสองมือ หรือ หอก ดีล่ะ?",
					1197,"Eden Group Slayer III","ดาบสองมือ (Two-handed Sword), พลังโจมตี 200",
					13434,"Eden Group Saber III","ดาบมือเดียว (One-handed Sword), พลังโจมตี 185",
					1434,"Eden Group Spear I","หอกมือเดียว (One-handed Spear), พลังโจมตี 165";
			}
			else if (BaseJob == Job_Assassin) {
				callsub L_Select,"เจ้าสามารถเลือกได้ระหว่าง มีด หรือ กาตาร์",
					13066,"Eden Group Dagger III","มีด (Dagger), MATK +80, ATK 165",
					1289,"Eden Group Katar I","กาตาร์ (Katar), ATK 155";
			}
			else if (BaseJob == Job_Rogue || BaseClass == Job_Novice)
				getitem 13066,1;// P.Dagger III
			else if (BaseJob == Job_Alchemist || BaseJob == Job_Blacksmith) {
				callsub L_Select,"เจ้าเลือกได้ระหว่าง กระบอง, เซเบอร์ หรือ ขวาน",
					16014,"Eden Group Mace III","กระบอง (Mace), พลังโจมตี 172",// P_Mace3
					13434,"Eden Group Saber III","ดาบ (Sword), พลังโจมตี 185",//P.Saber III
					1391,"Eden Group Axe I","ขวาน (Axe), พลังโจมตี 195"; //P.Two-Handed Axe I
			}
			else if (BaseJob == Job_Wizard)
				getitem 1658,1;// P.Staff III
			else if (BaseJob == Job_Sage) {
				callsub L_Select,"",
					1658,"Eden Group Staff III","คทา (Staff), MATK + 170, INT +4, พลังโจมตี 60", //P.Staff III
					1583,"Eden Group Dictionary I","หนังสือ (Book), MATK +100, พลังโจมตี 135"; //P.Book I
			}
			else if (BaseJob == Job_Priest) {
				callsub L_Select,"",
					1658,"Eden Group Staff III","คทา (Staff), MATK + 170, INT +4, พลังโจมตี 60", //P.Staff III
					1583,"Eden Group Dictionary I","หนังสือ (Book), MATK +100, พลังโจมตี 135", //P.Book I
					16014,"Eden Group Mace III","กระบอง (Mace), พลังโจมตี 172"; //P.Mace III
			}
			else if (BaseJob == Job_Monk) {
				callsub L_Select,"",
					1831,"Eden Group Knuckles I","สนับมือ (Knuckle), พลังโจมตี 120", //P.Knuckle I
					16014,"Eden Group Mace III","กระบอง (Mace), พลังโจมตี 172"; //P.Mace III
			}
			else if (BaseJob == Job_Hunter)
				getitem 18106,1;// P.Bow III
			else if (BaseJob == Job_Bard) {
				callsub L_Select,"",
					18106,"Eden Group Bow III","ธนู (Bow), พลังโจมตี 140", //P.Bow III
					1931,"Eden Group Guitar I","เครื่องดนตรี (Guitar), พลังโจมตี 125"; //P.Guitar I
			}
			else if (BaseJob == Job_Dancer) {
				callsub L_Select,"",
					18106,"Eden Group Bow III","ธนู (Bow), พลังโจมตี 140", //P.Bow III
					1986,"Eden Group Whip I","แส้ (Whip), พลังโจมตี 125"; //P.Tail I
			}
			else if (Class == Job_Ninja) {
				callsub L_Select,"",
					13066,"Eden Group Dagger III","มีด (Dagger), MATK +80, ATK 165", //P.Dagger III
					13310,"Eden Group Huuma Shuriken I","ดาวกระจายยักษ์ (Huuma Shuriken), MATK +50, ATK 170"; //P.Huuma Suriken I
			}
			else if (Class == Job_Star_Gladiator)
				getitem 1583,1; //P.Book I
			else if (Class == Job_Soul_Linker) {
				callsub L_Select,"",
					13066,"Eden Group Dagger III","มีด (Dagger), MATK +80, ATK 165", //P.Dagger III
					1658,"Eden Group Staff III","คทา (Staff), MATK +170, INT +4, ATK 60"; //P.Staff III
			}
			else if (Class == Job_Gunslinger)
				getitem 13114,1; //P.Revolver III
			else if (Class == Job_Summoner)
				getitem 26101,1; //P.Foxtail III
			else {
				mes "หืมม ดูเหมือนเจ้าจะมีสิทธิ์รับอุปกรณ์นะ";
				mes "เรื่องนั้นข้าเข้าใจแล้วละ";
				next;
				mes "[Thorn]";
				mes "แต่เหมือนจะมีบางอย่างผิดพลาด... ข้าเองก็ไม่แน่ใจว่าเพราะอะไร";
				close;
			}
			para_2nd02 = 2;
			getitem 18514,1;// Paradise Hat II
			getitem 2571,1;// Paradise Mantle II
			getitem 2473,1;// Paradise Boots IV
			getitem 15031,1;// Paradise Uniform IV
			mes "นี่คืออุปกรณ์ของคุณ";
			mes "ฉันจะมอบเครื่องป้องกันอื่นๆ ที่เข้าเซ็ตกันให้ไปด้วยนะ";
		}
		else if (para_2nd02 == 2) {
			mes "เจ้าสามารถเสริมพลังให้อุปกรณ์ที่ข้าเคยให้ไปได้นะ";
			mes "ผู้เชี่ยวชาญด้านอาวุธ BK ที่สำนักงานใหญ่ Eden Group จะเป็นคนจัดการเรื่องนั้นให้";
			next;
			mes "[Thorn]";
			mes "ลองไปหาเขาดูสิ เขาจะช่วยให้อุปกรณ์ของเจ้าแข็งแกร่งขึ้นได้";
		}
		else {
			mes "เจ้าได้รับอุปกรณ์ไปหรือยัง?";
			mes "ข้าสามารถมอบอุปกรณ์ให้เจ้าได้ แต่ข้าไม่สามารถเสริมพลัง (Enchant) ให้เจ้าได้หรอกนะ";
			next;
			mes "[Thorn]";
			mes "ชุดเกราะของเจ้าน่ะ ต้องให้คนอื่นเป็นคนเสริมพลังให้";
			mes "ข้าก็อยากจะช่วยหรอกนะ แต่ข้าทำไม่ได้จริงๆ";
		}
	}
	close;

L_Select:
	mes "เลือกอาวุธที่ต้องการ";
	mes getarg(0);
	mes "เลือกจากรายการต่อไปนี้";
	for ( .@i = 1; .@i < getargcount(); .@i += 3 ) {
		set .@menu$, .@menu$ + getarg(.@i+1) + ":";
		mes getarg(.@i+1)+" : "+ getarg(.@i+2);
	}
	next;
	.@i = (select(.@menu$) -1) *3 +1;
	mes "[Toren]";
	getitem getarg(.@i),1;
	return;

S_Enchant:
	mes "[Thorn]";
	if (countitem(getarg(0)) > 0) {
		// .@1st = rand(1,12);
		// .@2nd = rand(1,12);

		// if (.@1st == 1) .@1st = 4751;// Luck2
		// else if (.@1st == 2) .@1st = 4787;// Mdef4
		// else if (.@1st == 3) .@1st = 4791;// Def3
		// else if (.@1st == 4) .@1st = 4701;// Strength2
		// else if (.@1st == 5) .@1st = 4788;// Mdef6
		// else if (.@1st == 6) .@1st = 4792;// Def6
		// else if (.@1st == 7) .@1st = 4731;// Agility2
		// else if (.@1st == 8) .@1st = 4789;// Mdef8
		// else if (.@1st == 9) .@1st = 4793;// Def9
		// else if (.@1st == 10) .@1st = 4721;// Dexterity2
		// else if (.@1st == 11) .@1st = 4711;// Inteligence2
		// else if (.@1st == 12) .@1st = 4741;// Vitality2

		// if (.@2nd == 1) .@2nd = 4701;// Strength2
		// else if (.@2nd == 2) .@2nd = 4787;// Mdef4
		// else if (.@2nd == 3) .@2nd = 4741;// Vitality2
		// else if (.@2nd == 4) .@2nd = 4791;// Def3
		// else if (.@2nd == 5) .@2nd = 4788;// Mdef6
		// else if (.@2nd == 6) .@2nd = 4792;// Def6
		// else if (.@2nd == 7) .@2nd = 4721;// Dexterity2
		// else if (.@2nd == 8) .@2nd = 4793;// Def9
		// else if (.@2nd == 9) .@2nd = 4751;// Luck2
		// else if (.@2nd == 10) .@2nd = 4789;// Mdef8
		// else if (.@2nd == 11) .@2nd = 4731;// Agility2
		// else if (.@2nd == 12) .@2nd = 4711;// Inteligence2
		setarray .@bonus[1],
			4751,// Luck2
			4787,// Mdef4
			4791,// Def3
			4701,// Strength2
			4788,// Mdef6
			4792,// Def6
			4731,// Agility2
			4789,// Mdef8
			4793,// Def9
			4721,// Dexterity2
			4711,// Inteligence2
			4741;// Vitality2
		.@1st = .@bonus[ rand(1,12) ];
		.@2nd = .@bonus[ rand(1,12) ];
		mes "Eden Group "+ getarg(1) +" สินะ?";
		mes "ตกลง ข้าเข้าใจแล้ว";
		mes "จำไว้ว่าค่าสถานะที่เพิ่มขึ้นมานั้นจะเป็นการสุ่มทั้งหมดนะ";
		mes "เจ้าแน่ใจแล้วใช่ไหมว่าต้องการจะเสริมพลังให้มัน?";
		next;
		switch( select( "เปลี่ยนใจแล้ว", "ใช่ เสริมพลังเลย" ) ) {
			mes "[Thorn]";
		case 1:
			mes "แน่ใจนะว่าไม่อยากเสริมพลังมันแล้ว?";
			mes "ก็แล้วแต่เจ้านะ";
			close;
		case 2:
			mes "รับทราบ";
			mes "งั้นเรามาเริ่มการเสริมพลังกันเลย!";
			next;
			specialeffect EF_REPAIRWEAPON;
			progressbar "ffff00",3;
			delitem getarg(0),1;
			para_2nd04 = 4;
			// erasequest 7240;// Equipment Enhancement
			// setquest 7241;// Errands for Thorn timer
			changequest 7240,7241;
			getitem2 getarg(0),1,1,0,0,0,0,.@1st,.@2nd;						
			mes "[Thorn]";
			mes "เรียบร้อย ดูเหมือนการเสริมพลังจะผ่านไปได้ด้วยดีนะ";
			mes "ถ้าเจ้าไม่พอใจกับผลลัพธ์ พรุ่งนี้ค่อยกลับมาลองใหม่ได้";
			close;
		}
	}
	mes "หืม... เจ้าไม่ได้พก "+ getarg(1) +" มาด้วยนี่?";
	mes "ข้าว่าเจ้าไม่ได้เอาอุปกรณ์ชิ้นนั้นติดตัวมานะ";
	close;
}

moc_para01,112,79,3	script	Weapons Expert BK#2nd11	4_M_REPAIR,{
	setarray .@wplist[0],
		1197,	// P_Slayer3
		13434,	// P_Saber3
		13066,	// P_Dagger3
		1289,	// P_Katar1
		1391,	// P_Two_Handed_Axe1
		1434,	// P_Spear I
		1658,	// P_Staff3
		16014,	// P_Mace3
		18106,	// P_Bow3
		1583,	// P.Book I
		1931,	// P_Guitar I
		1986,	// P_Tail1
		13114,	// P_Revolver3
		1831,	// P_Knuckle1
		13310,	// P_Huuma_Shuriken1
		26101;	// P_Foxtail III
	.@wplist_size = getarraysize(.@wplist);

	.@menu$ = "Eden Group Slayer III:" +
		"Eden Group Saber III:" +
		"Eden Group Dagger III:" +
		"Eden Group Katar I:" +
		"Eden Group Axe I:" +
		"Eden Group Spear I:" +
		"Eden Group Staff III:" +
		"Eden Group Mace III:" +
		"Eden Group Bow III:" +
		"Eden Group Dictionary I:" +
		"Eden Group Guitar I:" +
		"Eden Group Whip I:" +
		"Eden Group Revolver III:" +
		"Eden Group Knuckles I:" +
		"Eden Group Huuma Shuriken I:" +
		"Eden Group Foxtail III";
	mes "[BK]";
	mes "มีอะไรเกิดขึ้นงั้นเหรอ?";
	mes "เป็นไปไม่ได้น่า?!";
	next;
	if (select( "มาเสริมพลังอาวุธ", "อะไรนะ?" ) == 2) {
		mes "[BK]";
		mes "หืม?";
		mes "นี่กะจะมาลองภูมิสิ่งที่ฉันรู้หรือยังไง?";
		next;
		select("...??");
		mes "[BK]";
		mes ".....????";
		close;
	}
	mes "[BK]";
	mes "อา คุณมาเพื่อเสริมพลังให้อุปกรณ์สินะ";
	mes "โอเค มาเริ่มกันเลยไหมล่ะ?";
	next;
	mes "[BK]";
	if (para_2nd02 < 2) {
		mes "คุณต้องมีอุปกรณ์มาก่อนนะ ฉันถึงจะทำอะไรให้ได้";
		mes "นี่ยังไม่ได้อะไรจากเจ้า Thorn มาเลยใช่ไหมล่ะ?";
	}
	else if (para_2nd02 == 2) {
		if (para_2nd01 < 11) {
			mes "อืม ยังก่อนนะ";
			mes "ฉันยังไม่ได้รับอนุญาตจากอาจารย์ Ur เลย";
			mes "คุณต้องไปฝึกฝนให้เสร็จก่อน";
			next;
			mes "[BK]";
			mes "เมื่อคุณทำเสร็จแล้ว ฉันถึงจะช่วยได้";
			mes "จนกว่าจะถึงตอนนั้น ฉันจะไม่คุยกับคุณเรื่องนี้หรอกนะ";
		}
		else {
			mes "โอเค อยากจะเสริมพลังให้อาวุธชิ้นไหนล่ะ?";
			next;
			.@s = select(.@menu$) -1;
			mes "[BK]";
			if (countitem(.@wplist[.@s]) > 0) {
				mes "โอเค สำหรับการเสริมพลังขั้นแรก";
				mes "จะเอา ATK + 3% หรือ MATK + 3% ดีล่ะ?";
				mes "ฉันจะให้คุณเลือกออฟชั่นที่จะใส่ลงไปในอาวุธชิ้นนั้น";
				next;
				mes "[BK]";
				mes "เลือกแล้วเปลี่ยนใจไม่ได้นะ เพราะฉะนั้นตัดสินใจให้ดีล่ะ";
				next;
				if (select( "ATK +3%","MATK +3%" ) == 1) {
					.@string$ = "โอเค คุณเลือกที่จะเพิ่มพลังโจมตีกายภาพสินะ";
					.@card = 4767;
				}
				else {
					.@string$ = "โอเค คุณเลือกที่จะเพิ่มพลังโจมตีเวทมนตร์สินะ";
					.@card = 4806;
					.@add = 1;
				}
				mes "[BK]";
				mes .@string$;
				mes "ดูนั่นสิ! อาวุธของคุณแข็งแกร่งขึ้นแล้ว";
				para_2nd03 = 1 + .@add;
				next;
				specialeffect EF_REPAIRWEAPON;
				progressbar "ffff00",3;
				para_2nd02 = 3;
				delitem .@wplist[.@s],1;
				getitem2 .@wplist[.@s],1,1,0,0,0,0,0,.@card;
				mes "[BK]";
				mes "มันไม่ดูวิเศษไปเลยเหรอ?";
				mes "ฉันรู้ๆ ฉันน่ะมันสุดยอดอยู่แล้ว!";
			}
			else {
				mes "ล้อกันเล่นหรือเปล่า?";
				mes "ดูเหมือนคุณจะไม่มีอาวุธชิ้นนั้นอยู่กับตัวนะ";
			}
		}
	}
	else if (para_2nd02 == 3) {
		if (para_2nd01 < 16) {
			mes "หืม ฉันว่าคุณยังไม่พร้อมนะ";
			mes "คุณต้องทำภารกิจทั้ง 3 ขั้นของอาจารย์ Ur ให้เสร็จก่อน";
			next;
			mes "[BK]";
			mes "เข้าใจที่ฉันพูดใช่ไหม?";
			mes "ไปทำภารกิจที่สามของอาจารย์ Ur ให้เสร็จซะ";
		}
		else {
			mes "ดีมาก คราวก่อนคุณได้รับ Atk หรือ Matk เพิ่มไปแล้วสินะ";
			mes "สำหรับออฟชั่นนี้ คุณสามารถเลือกประเภทสัตว์, พืช, ปลา, มังกร หรือเพิ่มแรงฮีลก็ได้";
			next;
			mes "[BK]";
			mes "เมื่อเลือกแล้วจะเปลี่ยนใจไม่ได้เหมือนเดิมนะ";
			mes "อาวุธของคุณจะโจมตีแรงขึ้นตามประเภทที่คุณเลือกเสริมพลังเข้าไป";
			next;
			mes "[BK]";
			mes "อธิบายง่ายๆ คือฉันจะใส่คุณสมบัติของการ์ดมอนสเตอร์ตามประเภทที่คุณเลือกให้นั่นแหละ";
			mes "โชคดีนะ!";
			next;
			switch( select( "เผ่าสัตว์", "เผ่าพืช", "เผ่าแมลง", "เผ่าปลา", "เผ่ามังกร", "เพิ่มแรงฮีล" ) ) {
				case 1:
					setarray .@arg[0],4060,4472,111;// Goblin_Card, Bradium_Goram_Card
					.@type$ = "Brute";
					break;
				case 2:
					setarray .@arg[0],4068,4470,121;// Scorpion_Card, Nepenthes_Card
					.@type$ = "Plant";
					break;
				case 3:
					setarray .@arg[0],4063,4476,131;// Caramel_Card, Pinguicula_Card
					.@type$ = "Insect";
					break;
				case 4:
					setarray .@arg[0],4080,4469,141;// Flora_Card, Naga_Card
					.@type$ = "Fish";
					break;
				case 5:
					setarray .@arg[0],4118,4471,151;// Petit_Card, Egg_Of_Draco_Card
					.@type$ = "Dragon";
					break;
				case 6:
					setarray .@arg[0],4805,4805,161;// Heal_Amount2
			}
			mes "[BK]";
			for ( .@i = 0; .@i < .@wplist_size; .@i++ ) {
				if (countitem(.@wplist[.@i]) > 0) {
					if (.@arg[0] == 4805)
						mes "ดีมาก อาวุธของคุณจะเพิ่มประสิทธิภาพการฮีลแล้วนะ";
					else
						mes "ดีมาก อาวุธของคุณจะโจมตีมอนสเตอร์ประเภท " + .@type$ + " ได้แรงขึ้น";
					next;
					specialeffect EF_REPAIRWEAPON;
					progressbar "ffff00",3;
					delitem .@wplist[.@i],1;
					para_2nd02 = 4;
					if (para_2nd03 == 1) {
						para_2nd03 = .@arg[2];
						getitem2 .@wplist[.@i],1,1,0,0,0,0,.@arg[0],4767;
					}
					else {
						para_2nd03 = .@arg[2] + 100;
						getitem2 .@wplist[.@i],1,1,0,0,0,0,.@arg[1],4806;
					}
					mes "[BK]";
					mes "มันไม่ดูวิเศษไปเลยเหรอ?";
					mes "ฉันรู้ๆ ฉันน่ะมันสุดยอดอยู่แล้ว!";
					close;
				}
			}
			mes "เสียใจด้วยนะ ดูเหมือนคุณจะไม่มีอาวุธที่สามารถเสริมพลังได้อยู่กับตัว";
		}
	}
	else if (para_2nd02 == 4) {
		if (para_2nd01 < 30) {
			mes "อาจารย์ Ur มีภารกิจให้ทำเยอะเลยใช่ไหมล่ะ?";
			mes "เหลืออีกแค่ขั้นเดียวเท่านั้น";
			mes "ไปให้ถึงการฝึกขั้นสุดท้ายซะก่อนนะ";
		}
		else {
			mes "โอเค นี่คือขั้นตอนสุดท้ายแล้ว";
			mes "ฉันจะทำให้อาวุธของคุณทรงพลังยิ่งขึ้นไปอีก";
			next;
			mes "[BK]";
			mes "พลังของการ์ดที่ใส่ไปในขั้นตอนก่อนหน้าจะถูกเพิ่มเป็นสองเท่า!";
			mes "และถ้าคุณมีอาวุธสายฮีล มันก็จะฮีลแรงขึ้นไปอีก เข้าใจนะ?";
			next;
			mes "[BK]";
			mes "เอาละ ตอนนี้คุณถืออาวุธชิ้นไหนอยู่?";
			next;
			.@s = select(.@menu$) -1;
			mes "[BK]";
			if (countitem(.@wplist[.@s]) > 0) {
				mes "กำลังเพิ่มพลังโบนัสสถานะให้เป็นสองเท่า...";
				next;
				specialeffect EF_REPAIRWEAPON;
				progressbar "ffff00",3;
				delitem .@wplist[.@s],1;
				para_2nd02 = 5;
				if (para_2nd03 == 111)
					getitem2 .@wplist[.@s],1,1,0,0,0,4060,4060,4767;// Goblin_Card, Goblin_Card, Atk3
				else if (para_2nd03 == 121)
					getitem2 .@wplist[.@s],1,1,0,0,0,4068,4068,4767;// Scorpion_Card, Scorpion_Card, Atk3
				else if (para_2nd03 == 131)
					getitem2 .@wplist[.@s],1,1,0,0,0,4063,4063,4767;// Caramel_Card, Caramel_Card, Atk3
				else if (para_2nd03 == 141)
					getitem2 .@wplist[.@s],1,1,0,0,0,4080,4080,4767;// Flora_Card, Flora_Card, Atk3
				else if (para_2nd03 == 151)
					getitem2 .@wplist[.@s],1,1,0,0,0,4118,4118,4767;// Petit_Card, Petit_Card, Atk3
				else if (para_2nd03 == 161)
					getitem2 .@wplist[.@s],1,1,0,0,0,4805,4805,4767;// Heal_Amount2, Heal_Amount2, Atk3
				else if (para_2nd03 == 211)
					getitem2 .@wplist[.@s],1,1,0,0,0,4472,4472,4806;// Bradium_Goram_Card, Bradium_Goram_Card, Matk3
				else if (para_2nd03 == 221)
					getitem2 .@wplist[.@s],1,1,0,0,0,4470,4470,4806;// Nepenthes_Card, Nepenthes_Card, Matk3
				else if (para_2nd03 == 231)
					getitem2 .@wplist[.@s],1,1,0,0,0,4476,4476,4806;// Pinguicula_Card, Pinguicula_Card, Matk3
				else if (para_2nd03 == 241)
					getitem2 .@wplist[.@s],1,1,0,0,0,4469,4469,4806;// Naga_Card, Naga_Card, Matk3
				else if (para_2nd03 == 251)
					getitem2 .@wplist[.@s],1,1,0,0,0,4471,4471,4806;// Egg_Of_Draco_Card, Egg_Of_Draco_Card, Matk3
				else if (para_2nd03 == 261)
					getitem2 .@wplist[.@s],1,1,0,0,0,4805,4805,4806;// Heal_Amount2, Heal_Amount2, Matk3
				mes "[BK]";
				mes "มันไม่ดูวิเศษไปเลยเหรอ?";
				mes "ฉันรู้ๆ ฉันน่ะมันสุดยอดอยู่แล้ว!";
			}
			else {
				mes "นี่ คิดว่าฉันพูดเล่นเหรอ?";
				mes "สรุปว่ามีอาวุธมาให้หรือเปล่า?";
			}
		}
	}
	else if (para_2nd02 > 4) {
		mes "เป็นยังไงบ้างล่ะ อาวุธที่เสริมพลังแล้วถูกใจไหม?";
		mes "หวังว่ามันจะช่วยคุณได้นะ";
		next;
		mes "[BK]";
		mes "ฉันมอบพลังระดับสูงสุดให้กับอาวุธของคุณไปแล้ว";
		mes "แต่อาวุธไม่ใช่สิ่งเดียวที่ต้องแข็งแกร่งหรอกนะ คุณว่าไหมล่ะ?";
		next;
		mes "[BK]";
		mes "เอาน่า ยอมรับมาเถอะ";
		mes "Thorn ให้แรงบันดาลใจ ส่วนฉันก็มีหน้าที่มอบอุปกรณ์ที่แข็งแกร่งให้ นั่นแหละคือวิถีชีวิตล่ะ";
		mes "ฮ่าๆๆๆ!";
	}
	close;
}

sec_in02,25,33,4	script	Assistant#para_suvquest	422,{
	callfunc "F_GM_NPC";
	mes "รหัสผ่าน";
	next;
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "กรุณาเลือกตัวแปรที่ต้องการแก้ไข";
		next;
		set .@var, select("para_suv01:para_suv02");
		mes "กรอกค่าที่ต้องการเปลี่ยน";
		next;
		input .@input,0,9999;
		mes "ค่าของ para_suv0"+.@var+" ถูกเปลี่ยนเป็น "+.@input+" เรียบร้อยแล้ว";
		setd "para_suv0"+.@var,.@input;
		close;
	} else {
		mes "......เมี้ยววว";
		close;
	}
}

