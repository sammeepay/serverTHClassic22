//===== rAthena Script =======================================
//= Eden Group Quests - Mission [121-130]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 121 - 130.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,81,3	script	Melody-Jack#1	4_M_MELODY,8,8,{
	mes "[Melody-Jack]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "แกเป็นใครกัน? ไสหัวไปซะ!";
		mes "ข้าจะคุยกับนักผจญภัยเลเวล ^FF0000121-130^000000 ที่มีตราประทับ Eden Group เท่านั้น";
	}
	else if (para_120lv01 == 0)
		mes "อะไร? อย่ามาเกะกะ ข้ากำลังยุ่ง";
	else if (para_120lv01 == 1) {
		mes "เจ้าได้รับภารกิจมาจากยัยเด็กนั่นเหรอ? คนที่ตัวเล็กๆ พูดมาก แล้วก็แต่งตัวกึ่งเปลือยตรงนั้นน่ะนะ?";
		next;
		select("ตอบว่าใช่", "เลี่ยงที่จะไม่ตอบ");
		mes "[Melody-Jack]";
		mes "ช่างเถอะ ข้าไม่สนหรอก ขอแค่เจ้าไม่ได้หลงทางเข้ามาในห้องนี้ก็นับว่าใช้ได้แล้ว ข้าต้องการใครสักคนที่ไม่ใช่พวกปัญญาอ่อนแบบยัยนั่น";
		next;
		mes "[Melody-Jack]";
		mes "ฟังให้ดี ข้ามีข้อเสนอที่น่าสนใจจะมอบให้เจ้า";
		next;
		mes "[Melody-Jack]";
		mes "ข้าคือ ^ff0000Melody - Jack!^000000 ผู้ที่ใช้มีดสั้นได้เก่งกาจที่สุดในปฐพี!";
		next;
		mes "Melody-Jack กวัดแกว่งมีดสั้นเฉียดหน้าคุณไปอย่างรวดเร็ว";
		next;
		mes "[Melody-Jack]";
		mes "ข้าคือผู้ได้รับมอบหมายให้ทำภารกิจลับ และตอนนี้ถือเป็นโชคดีของเจ้า";
		next;
		mes "[Melody-Jack]";
		mes "พอดีข้ามีภารกิจหนึ่งที่ต้องสละสิทธิ์ ทั้งข้อตกลงและคำสัญญา... ข้าไม่สามารถไปจัดการมันตอนนี้ได้";
		next;
		mes "[Melody-Jack]";
		mes "จงรับภารกิจของข้าไป และไปสะสางคำขอของ Aigu ซะ แล้วข้าจะมอบรางวัลตอบแทนให้อย่างงาม";
		next;
		mes "[Melody-Jack]";
		mes "เรื่องที่เราคุยกันต้องเป็นความลับจาก Aigu เท่านั้น ถ้าเจ้าไม่ได้มี 9 ชีวิตเหมือนแมวล่ะก็นะ... เอาล่ะ มาเริ่มกันเลย";
		para_120lv01 = 2;
	}
	else if (para_120lv01 > 1) {
		mes "......";
		next;
		if (select( "ขอดูรายการภารกิจหน่อย!", "ชวนคุยเรื่องทั่วไป" ) == 2) {
			mes "[Melody-Jack]";
			mes "อะไร? นี่เจ้าสนใจในตัวข้าอย่างงั้นรึ?";
			next;
			switch( select( "ข้าสนใจในตัวท่าน", "เกี่ยวกับภารกิจ", "เกี่ยวกับข้อตกลง", "คำสัญญาคืออะไร?" ) ) {
				mes "[Melody-Jack]";
			case 1:
				mes "งั้นก็ไสหัวไปซะ ข้าไม่ได้มีความสนใจในตัวเจ้าแม้แต่นิดเดียว";
				close;
			case 2:
				mes "ข้าถอนตัวไม่ได้เพราะประวัติเสียๆ ของข้าเอง... มันน่าหงุดหงิดชะมัด";
				next;
				mes "[Melody-Jack]";
				mes "ยังดีที่ข้าได้ทำสัญญาสองทางกับนักผจญภัยอย่างเจ้า พวกนั้นไม่ได้รวมอยู่ในสัญญาของเราหรอกนะ";
				close;
			case 3:
				mes "บอกตามตรง ข้าไม่คิดว่าจะมีใครที่ควรค่าแก่การไว้วางใจหรอก ความเชื่อใจหรือคำสัญญามันก็แค่เรื่องไร้สาระ";
				next;
				mes "[Melody-Jack]";
				mes "ข้าเชื่อใจแค่ตัวเองและ 'ข้อตกลง' เท่านั้น ความเชื่อใจหรือคำสัญญามันจะว่างเปล่าทันทีที่ถูกทำลาย แต่ถ้าข้อตกลงถูกละเมิด... สิ่งที่จะตามมาคือการชดใช้ หรือไม่ก็การแก้แค้น";
				close;
			case 4:
				mes "ไอ้เด็กพวกนี้มันใครกันหมดเนี่ย?";
				next;
				mes "[Melody-Jack]";
				mes "ข้าออกมาโดยไม่ได้อะไรติดมือเลย แต่ข้าก็ต้องทำหน้าที่ของข้าให้สำเร็จ ข้าไม่ได้ติดค้างอะไรใครทั้งนั้น";
				close;
			}
		}
		mes "[Melody-Jack]";
		if (BaseLevel < 121)
			mes "โอ้ ข้าดูคนผิดไปสินะ เจ้ายังไม่ใช่ระดับที่ข้ากำลังมองหาอยู่ ข้าต้องการนักผจญภัยที่มีเลเวลระหว่าง 121 ถึง 130 เท่านั้น";
		else if (BaseLevel < 131) {
			mes "ข้าไม่ค่อยชอบใจนักหรอก แต่ข้อตกลงก็ต้องเป็นข้อตกลง จงกลับมาหาข้าหลังจากที่เจ้ากำจัดพวกมอนสเตอร์ในสถานที่ที่ Aigu สั่งให้ไปสำรวจเสร็จสิ้นแล้ว";
			next;
			switch( select( "ภารกิจเกี่ยวกับ Thanatos Tower", "ภารกิจเกี่ยวกับ Rachel Shrine", "ภารกิจเกี่ยวกับ Nameless Island", "ภารกิจเกี่ยวกับ Abyss Lake" ) ) {
				mes "[Melody-Jack]";
			case 1: // หมวด Thanatos Tower
				mes "หอคอย Thanatos เป็นสถานที่ที่น่ารำคาญที่สุด แต่ไม่ต้องห่วงเรื่องค่าตอบแทนหรอกนะ ข้าเป็นคนรักษาคำพูดเสมอ";
				next;
				switch( select( "Baroness of Retribution", "Lady Solace", "Mistress of Shelter", "Dame of Sentinel" ) ) {
					mes "[Melody-Jack]";
				case 1: // ภารกิจ Baroness of Retribution (??????)
					if (checkquest(13120,PLAYTIME) == 0)
						mes "ข้าว่าข้าจบการแสดงของวันนี้ไปแล้วนะ แต่จะประมาทไม่ได้... ใครจะรู้ว่าพวกมอนสเตอร์จะโผล่มาเมื่อไหร่";
					else if (checkquest(13119,HUNTING) == 1) {
						cutin "retribution_card",3;
						mes "ข้าไม่ชอบไอ้ตัวพรรค์นั้นเลย ไม่ใช่เพราะ Aigu ไม่ชอบพวกมันหรอกนะ... แต่เจ้าพวกนั้นถือดียังไง? ใครกันแน่ที่จะลงทัณฑ์ใคร?";
						next;
						mes "[Melody-Jack]";
						mes "พวกมันเป็นหนึ่งในสิ่งมีชีวิตที่อันตรายที่สุดในโลก และคนอย่างพวกมันไม่สมควรได้รับความยุติธรรมใดๆ ทั้งสิ้น";
						next;
						cutin "",255;
						mes "[Melody-Jack]";
						mes "แน่นอนว่าเจ้าเชื่อใจข้าได้ เพราะข้าศรัทธาในข้อตกลงที่แท้จริง ไปกวาดล้างพวกมันให้สิ้นซากซะ";
					}
					else if (checkquest(13119,HUNTING) == 2) {
						mes "จัดการพวกมันเรียบร้อยแล้วสินะ? เท่านี้เจ้าเด็กนั่นก็คงจะไม่โดนกวนใจไปได้สักพัก ทำได้ดีมาก";
						callsub S_Quest,13119,13120,1;
					}
					else {
						cutin "retribution_card",3;
						mes "อย่างที่เจ้ารู้ หอคอย Thanatos เป็นที่ที่ Aigu ให้ความสำคัญมากที่สุด และในบางแง่มันก็เป็นสถานที่ที่อันตรายที่สุดด้วย";
						next;
						mes "[Melody-Jack]";
						mes "ช่วยกำจัดมอนสเตอร์พวกนั้นเพื่อช่วยงาน Aigu ทีเถอะ อย่าปล่อยให้พวกมือสมัครเล่นทำเรื่องมันยุ่งยากเลย";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "คงจะมีพวกมันอยู่เต็มไปหมด อย่าลืมรักษาเรื่องนี้เป็นความลับด้วยล่ะ"; 
							callsub S_Quest,13120,13119;
						}
					}
					close;
				case 2: // ภารกิจ Lady Solace (?????)
					if (checkquest(13122,PLAYTIME) == 0)
						mes "ข้าว่าข้าจบการแสดงของวันนี้ไปแล้วนะ แต่ข้ายังผ่อนคลายไม่ได้... ถ้าพวกมันโผล่มาตอนนี้จะทำยังไง?";
					else if (checkquest(13121,HUNTING) == 1) {
						cutin "solace_card",3;
						mes "สถาปัตยกรรมที่นั่นดูเหมือนไม่ได้ถูกออกแบบโดยมนุษย์ แถมยังมีท่วงทำนองประหลาดๆ บรรเลงอยู่ตลอดเวลา";
						next;
						mes "[Melody-Jack]";
						mes "อย่าได้หลงระเริงไปกับเสียงเพลงนั่นเชียวล่ะ เพราะมันอาจจะเป็นเพลงสุดท้ายที่เจ้าจะได้ยินในชีวิต";
						next;
						cutin "",255;
						mes "[Melody-Jack]";
						mes "จำไว้ว่าในโลกนี้มีดนตรีบางประเภทที่คมกริบยิ่งกว่ามีดสั้น และมันสามารถฆ่าคนได้";
					}
					else if (checkquest(13121,HUNTING) == 2) {
						mes "เจ้าเคยได้ยินท่วงทำนองที่พวกมันบรรเลงบ้างไหม? เจ้าจะมีโอกาสได้ฟังมันอีกแน่ ถ้าเจ้าเด็กนั่นเริ่มการแสดงอีกครั้ง";
						callsub S_Quest,13121,13122,1;
					}
					else {
						cutin "solace_card",3;
						mes "ท่วงทำนองที่งดงาม... บางทีมันอาจจะไพเราะเพียงเพราะว่ามี 'สิ่งนั้น' เป็นผู้บรรเลง";
						next;
						mes "[Melody-Jack]";
						mes "อย่างไรก็ตาม นั่นก็เป็นแค่อุปสรรคอีกอย่างหนึ่ง ข้อตกลงต้องมาก่อนความชอบส่วนตัวเสมอ เพราะฉะนั้นไปกำจัด Lady Solace ซะ";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "พวกเราก็แค่ทำตามข้อตกลงให้ลุล่วง ไปกำจัดพวกมันซะเดี๋ยวนี้";
							callsub S_Quest,13122,13121;
						}
					}
					close;
				case 3: // ภารกิจ Mistress of Shelter (??????)
					if (checkquest(13124,PLAYTIME) == 0)
						mes "ข้าว่าข้าจบการแสดงของวันนี้ไปแล้วนะ แต่ข้ายังผ่อนคลายไม่ได้... ถ้าพวกมอนสเตอร์โผล่มาล่ะ?";
					else if (checkquest(13123,HUNTING) == 1) {
						cutin "shelter_card",3;
						mes "เจ้าน่ะ... เคยเห็นเทวทูตที่กลายร่างเป็นมอนสเตอร์ไหม? ข้าก็ไม่รู้หรอกนะว่ามันเคยเป็นเทวทูตจริงหรือเปล่า แต่รูปลักษณ์ของมันน่ะใช่เลย";
						next;
						mes "[Melody-Jack]";
						mes "อย่าได้ตื่นตระหนกไปกับรูปลักษณ์ที่งดงาม ไม่ว่ามันจะเป็นเทวทูตหรืออสูรกายก็ตาม เจ้าแค่ต้องทำหน้าที่ของตัวเองให้สำเร็จ";
						next;
						mes "[Melody-Jack]";
						mes "ทุกสิ่งทุกอย่างมีเวลาของมัน และที่เจ้าได้รับคำขอนี้มา ก็อาจเป็นเพราะเวลานั้นมาถึงแล้ว";
						cutin "",255;
					}
					else if (checkquest(13123,HUNTING) == 2) {
						mes "ดีมาก... ถ้ามีใครมาตามหาข้า ก็บอกให้ข้ารู้ด้วยล่ะ เพราะข้าอาจจะต้องเปลี่ยนรายละเอียดของข้อตกลงใหม่";
						callsub S_Quest,13123,13124,1;
					}
					else {
						cutin "shelter_card",3;
						mes "เจ้าน่ะ... เคยเห็นเทวทูตที่กลายร่างเป็นมอนสเตอร์ไหม? ข้าก็ไม่รู้หรอกนะว่ามันเคยเป็นเทวทูตจริงหรือเปล่า แต่รูปลักษณ์ของมันน่ะใช่เลย";
						next;
						mes "[Melody-Jack]";
						mes "ข้าไม่สนหรอกว่ามันจะเป็นเทวทูตจริงๆ หรือแค่อสูรกาย ไปกำจัด Mistress of Shelter ซะ";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "ดอกไม้ที่เหี่ยวเฉาย่อมร่วงโรยไปตามแรงลมและสายฝน นั่นคือสิ่งที่เจ้าต้องทำ";
							emotion ET_BEST;
							callsub S_Quest,13124,13123;
						}
					}
					close;
				case 4: // ภารกิจ Dame of Sentinel (?????)
					if (checkquest(13126,PLAYTIME) == 0)
						mes "ข้าจบการแสดงสำหรับวันนี้แล้ว แต่จะประมาทไม่ได้ เผื่อพวกมอนสเตอร์มันโผล่มาแบบไม่ให้ตั้งตัว";
					else if (checkquest(13125,HUNTING) == 1) {
						cutin "observation_card",3;
						mes "ทันทีที่เจ้าก้าวเข้าไปในพื้นที่ส่วนบนของหอคอย Thanatos เจ้าจะสัมผัสได้ทันทีว่ากำลังถูกพวกมันจับตาดูอยู่";
						next;
						mes "[Melody-Jack]";
						mes "Dame of Sentinel ไม่ใช่คู่ต่อสู้ที่เคี้ยวได้ง่ายๆ เพราะฉะนั้นเตรียมตัวให้พร้อมสำหรับการต่อสู้";
						next;
						mes "[Melody-Jack]";
						mes "วันนี้เป็นวันที่เหมาะแก่การตายจริงๆ... ไม่เจ้าก็พวกมันนั่นแหละที่ต้องไป พยายามเข้าล่ะ";
						cutin "",255;
					}
					else if (checkquest(13125,HUNTING) == 2) {
						mes "ดูท่าทางยังสบายดีนี่? เจ้าอาจจะเก่งกว่าที่ข้าคิดไว้ก็ได้นะ ถึงแม้ข้าจะไม่ได้เห็นด้วยตาตัวเองก็เถอะ";
						callsub S_Quest,13125,13126,1;
					}
					else {
						cutin "observation_card",3;
						mes "เจ้าจะได้เผชิญหน้ากับพวกมันแน่ ถ้า Aigu ต้องการ... พวก Dame of Sentinels";
						next;
						mes "[Melody-Jack]";
						mes "ข้าหวังว่าพวกมันจะถูกกำจัดไปให้หมด เพื่อยัยนั่นจะได้เลิกส่งคำขอให้มาทำภารกิจที่นี่สักที";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "คำขอซ้อนคำขอ... จะอะไรก็ช่างเถอะ ไปทำหน้าที่ซะ";
							callsub S_Quest,13126,13125;
						}
					}
					close;
				}
			case 2: // หมวด Rachel Shrine
				mes "การผจญภัยของ Aigu นำไปสู่ Rachel Shrine งั้นเหรอ... เอาเถอะ มันไม่แปลกหรอกถ้าจะเจอ ^FF0000คนคนนั้น^000000 ที่ Aigu กำลังตามหาอยู่ไม่ว่าที่ไหนก็ตาม";
				next;
				switch( select( "Vanberk และ Isilla", "Hodremlin", "Agav และ Echio" ) ) {
					mes "[Melody-Jack]";
				case 1: // ภารกิจ Vanberk และ Isilla
					if (checkquest(13128,PLAYTIME) == 0)
						mes "เจ้าไปพักสักวันเถอะ จัดการไปมากพอแล้ว แต่พวกคลั่งศาสนากลับเพิ่มจำนวนขึ้นเรื่อยๆ";
					else if (checkquest(13127,HUNTING) == 1) {
						cutin "isilla_card",3;
						mes "คนที่เชื่อว่าตัวเองถูกต้องเสมอคือคนที่อันตรายที่สุด... โลกนี้มันเต็มไปด้วยพวกคนโง่";
						next;
						mes "[Melody-Jack]";
						mes "สุดท้ายพวกนั้นก็แค่บังคับให้คนอื่นเชื่อในสิ่งที่ตัวเองเชื่อ ไปจัดการ Vanberk และ Isilla ซะ";
						next;
						mes "[Melody-Jack]";
						mes "จริงๆ มันไม่ใช่ธุระอะไรของข้าหรอกนะ แต่ครั้งนี้มันต่างออกไป เจ้าเด็กนั่นอาจจะกำลังตกอยู่ในอันตราย เพราะงั้นรีบไปจัดการมอนสเตอร์พวกนี้ซะ";
						cutin "",255;
					}
					else if (checkquest(13127,HUNTING) == 2) {
						mes "แค่ประวิงเวลาไว้ได้สักวัน... ก็ถือว่าใช้ได้แล้ว";
						callsub S_Quest,13127,13128,1;
					}
					else {
						cutin "vanberk_card",3;
						mes "วิหารศักดิ์สิทธิ์ใน Rachel ห้ามคนธรรมดาเข้าไป เจ้าต้องรู้เรื่องนั้นไว้ด้วย";
						next;
						mes "[Melody-Jack]";
						mes "พวกนั้นคิดว่าจะหาเบาะแสได้ที่นั่น แต่มันไม่ใช่ที่ที่ปลอดภัยหรอก เจ้าควรจะไปกำจัด Vanberk และ Isilla ไว้ล่วงหน้า";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "จัดการพวกมันให้เร็วที่สุดเท่าที่เจ้าจะทำได้";
							emotion ET_SPARK;
							callsub S_Quest,13128,13127;
						}
					}
					close;
				case 2: // ภารกิจ Hodremlin
					if (checkquest(13130,PLAYTIME) == 0)
						mes "มอนสเตอร์ที่ไม่รู้จักมักจะอันตราย เพราะงั้นไปกำจัดพวกมันก่อน ถ้ามีอะไรเกิดขึ้นข้าจะแจ้งเจ้าเอง";
					else if (checkquest(13129,HUNTING) == 1) {
						cutin "hodremlin_card",3;
						mes "เขี้ยวที่เย็นยะเยือกอาจจะกำลังคืบคลานเข้ามา... คมเขี้ยวของมันเร็วกว่ามือของเจ้าเสียอีก";
						next;
						mes "[Melody-Jack]";
						mes "พวกมันกระจายอยู่เต็มวิหาร Rachel เจ้าต้องระวังเจ้าตัวนี้ไว้ให้ดี";
						next;
						mes "[Melody-Jack]";
						mes "เลิกคิดฟุ้งซ่านแล้วจดจ่ออยู่กับงานตรงหน้าก็พอ";
						cutin "",255;
					}
					else if (checkquest(13129,HUNTING) == 2) {
						mes "ข้าก็ไม่ได้ห่วงอะไรหรอกนะ แต่เจ้าปลอดภัยดีใช่ไหม? ไม่ได้โดนมันกัดมาหรอกนะ?";
						callsub S_Quest,13129,13130,1;
					}
					else {
						cutin "hodremlin_card",3;
						mes "พวกคนคลั่งน่ะคือปัญหา... แต่ปัญหาที่หนักกว่าคือการที่พวกมันโผล่มาแบบไม่ให้ตั้งตัว";
						next;
						mes "[Melody-Jack]";
						mes "กำจัด ^FF0000Hodremlin^000000 ซะโดยไม่ต้องลังเล เพราะพวกมันอันตรายมาก";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "พวกมันต้องการการตอบสนองที่เร็วกว่าการใช้สมองคิด ข้าไม่ห่วงเจ้าหรอกนะ แต่ระวังตัวด้วยล่ะ";
							callsub S_Quest,13130,13129;
						}
					}
					close;
				case 3: // ภารกิจ Agav และ Echio
					if (checkquest(13132,PLAYTIME) == 0)
						mes "ถ้าเจ้ายังมีแรงเหลือมาคุยได้... ตอนนี้ยังไม่มีภารกิจอะไรเพิ่มหรอก ไปพักผ่อนเสียเถอะ";
					else if (checkquest(13131,HUNTING) == 1) {
						cutin "echio_card",3;
						mes "ที่นี่เต็มไปด้วยความบ้าคลั่ง... ข้ารู้สึกขยะแขยงชะมัด";
						next;
						mes "[Melody-Jack]";
						mes "นั่นคือสิ่งที่ข้ารู้สึกนะ แต่เจ้าอาจจะชอบมันก็ได้ แต่ที่แน่ๆ ^FF0000Agav^000000 และ ^FF0000Echio^000000 คงจะไม่ชอบขี้หน้าเจ้าแน่";
						next;
						mes "[Melody-Jack]";
						mes "พวกมันจะพุ่งเข้าใส่เจ้า อย่าได้ลังเลและตั้งสติกับงานให้ดี";
						cutin "",255;
					}
					else if (checkquest(13131,HUNTING) == 2) {
						mes "เจอใครบ้างไหม? ข้าไม่กังวลหรอก... คำสัญญาก็คือคำสัญญา และข้อตกลงก็คือข้อตกลง";
						callsub S_Quest,13131,13132,1;
					}
					else {
						cutin "agav_card",3;
						mes "มันเป็นเรื่องจริงที่ภายในวิหาร Rachel เริ่มจะอันตรายขึ้นเรื่อยๆ แต่ Aigu คงไม่สนใจเรื่องที่เรายังไม่มั่นใจหรอก";
						next;
						mes "[Melody-Jack]";
						mes "งานที่ผ่านมาก็ไม่ได้แย่นัก... แต่ดูเหมือนจะมีใครบางคนต้องการความช่วยเหลือนะ? ไปจัดการ ^FF0000Agav^000000 และ ^FF0000Echio^000000 ในวิหาร Rachel ซะ";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "ข้าปลีกตัวไปไม่ได้... ขอให้โชคดี";
							callsub S_Quest,13132,13131;
						}
					}
					close;
				}
			case 3: // หมวด Nameless Island
				mes "เกาะนิรนาม (Nameless Island) ไม่ค่อยเป็นที่รู้จักในหมู่นักผจญภัยทั่วไปนักหรอก แต่ Aigu เองก็ดูจะสนใจที่นั่นอยู่เหมือนกัน";
				next;
				switch( select( "ล่า Ragged Zombie", "ล่า Zombie Slaughter", "ล่า Banshee" ) ) {
					mes "[Melody-Jack]";
				case 1: // ภารกิจ Ragged Zombie
					if (checkquest(13134,PLAYTIME) == 0)
						mes "จำนวน Ragged Zombie ลดลงไปบ้างแล้ว เป็นจังหวะดีที่จะพักเรื่องคำขอของ Aigu ไว้สักครู่";
					else if (checkquest(13133,HUNTING) == 1) {
						cutin "ragged_zombie_card",3;
						mes "บนเกาะนิรนามนั่นเจ้าจะเจอแต่พวกหน้าเดิมๆ เต็มไปหมด น่าเบื่อชะมัด";
						next;
						mes "[Melody-Jack]";
						mes "ไปกำจัดพวกมันซะ ถือว่าช่วยงาน Aigu และเหล่านักผจญภัยคนอื่นๆ ด้วย";
						cutin "",255;
					}
					else if (checkquest(13133,HUNTING) == 2) {
						mes "นี่เจ้าไปอาบน้ำมาหรือยัง? กลิ่นเหม็นโชยมาถึงนี่เลยนะ... แต่เอาเถอะ ข้าพอจะเดาออกว่าเจ้าคงลำบากกับกลิ่นเน่าๆ พวกนั้นมาไม่น้อย";
						callsub S_Quest,13133,13134,1;
					}
					else {
						cutin "ragged_zombie_card",3;
						mes "พวกคนตายจะฟื้นกลับมาในตอนกลางคืน... เหอะ พูดคำว่า 'ฟื้น' มันดูแปลกไปหน่อยนะ เลิกพูดเรื่องไร้สาระดีกว่า";
						next;
						mes "[Melody-Jack]";
						mes "ข้าอยากให้เจ้าไปกำจัด ^FF0000Ragged Zombie^000000 งานนี้ไม่ยากหรอก เพราะพวกมันโผล่มาให้เห็นทุกที่นั่นแหละ";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "เสร็จงานแล้วก็อย่าลืมไป ^FF0000อาบน้ำ^000000 ซะล่ะ กลิ่นจะได้หายไปเสียที";
							callsub S_Quest,13134,13133;
						}
					}
					close;
				case 2: // ภารกิจ Zombie Slaughter
					if (checkquest(13136,PLAYTIME) == 0)
						mes "ข้ารู้ว่าภารกิจที่เกาะนิรนามมันไม่ง่าย แต่ถ้าเจ้าจะกลับมาช่วยงานอีกครั้งข้าคงจะยินดีมาก";
					else if (checkquest(13135,HUNTING) == 1) {
						cutin "zombie_slaughter_card",3;
						mes "แม้แต่สำหรับข้าเอง การย่างกรายเข้าไปบนเกาะนั่นก็ไม่ใช่เรื่องง่ายเลย ข้าไม่ใช่คนที่มีความอดทนสูงขนาดนั้นด้วย";
						next;
						mes "[Melody-Jack]";
						mes "แต่นักผจญภัยควรจะลองท้าทายดูสักครั้งนะ ^FF0000Zombie Slaughter^000000 น่ะเป็นเป้าหมายที่คุ้มค่าแก่การกำจัด";
						cutin "",255;
					}
					else if (checkquest(13135,HUNTING) == 2) {
						mes "เจ้าเก่งไม่เบานี่! จัดการพวกมันได้หมดเลยรึ คงต้องใช้เวลาอีกสักพักกว่าพวกมันจะฟื้นกลับมาล่ะนะ";
						callsub S_Quest,13135,13136,1;
					}
					else {
						cutin "zombie_slaughter_card",3;
						mes "พวกมันอันตรายกว่า Ragged Zombie มาก เพราะพวกมันรู้จักกวัดแกว่งของมีคม";
						next;
						mes "[Melody-Jack]";
						mes "ไปกำจัด ^FF0000Zombie Slaughter^000000 ซะ ระวังอย่าให้โดนคมมีดของพวกมันเข้าล่ะ";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "เจ้าคงมีเรื่องให้ต้องจัดการอีกเยอะ ข้าจะไม่พูดมากก็แล้วกัน ไปจัดการซะ";
							callsub S_Quest,13136,13135;
						}
					}
					close;
				case 3: // ภารกิจ Banshee
					if (checkquest(13138,PLAYTIME) == 0)
						mes "จัดการ Banshee เรียบร้อยแล้วสินะ ภารกิจลุล่วงไปได้ก็ดี แต่ยังไม่จบแค่นี้หรอก อีกไม่นานเจ้าก็ต้องไปจัดการพวกมันอีกแน่";
					else if (checkquest(13137,HUNTING) == 1) {
						cutin "banshee_card",3;
						mes "ในการผจญภัย บางครั้งเจ้าจะได้พบกับสิ่งที่ไม่น่าอภิรมย์เอาเสียเลย ไม่ว่าจะเป็นตัวมอนสเตอร์หรือคนร่วมทางก็ตาม";
						next;
						mes "[Melody-Jack]";
						mes "ภารกิจนี้สำหรับข้าก็เป็นแบบนั้นแหละ ข้าแค่ไม่ชอบพวกอสูรกาย และไม่ชอบเหตุผลที่ต้องมอบภารกิจนี้ให้ด้วย";
						cutin "",255;
					}
					else if (checkquest(13137,HUNTING) == 2) {
						mes "เจ้าคงเหนื่อยแย่เลยนะ ที่ต้องไปในที่ที่อันตรายเกินความสามารถแบบนั้น... แต่อย่าเพิ่งโกรธข้าล่ะ ข้าจะให้รางวัลเจ้าอย่างงามเพื่อตอบแทน";
						callsub S_Quest,13137,13138,1;
					}
					else {
						cutin "banshee_card",3;
						mes "ไปกำจัด ^FF0000Banshee^000000 บนเกาะนิรนามทีเถอะ อย่าปฏิเสธเลย เจ้าก็รู้ว่านี่ก็เพื่อ Aigu เหมือนกัน";
						next;
						mes "[Melody-Jack]";
						mes "ทั้ง Banshee ทั้ง Aigu ต่างก็น่ารำคาญพอกัน เป็นข้าข้าคงไม่รับงานนี้แน่ แต่เจ้าคงต่างออกไป... ใช่ไหมล่ะ?";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "เห็นไหมล่ะ? ยังไงก็ต้องมีใครสักคนรับงานนี้อยู่ดี ไปกำจัด ^FF0000Banshee^000000 บนเกาะนิรนามซะ";
							callsub S_Quest,13138,13137;
						}
					}
					close;
				}
			case 4: // หมวด Abyss Lake
				mes "ภารกิจที่ Abyss Lake นี่มาจากยัยหนูนั่นน่ะ ขยันขันแข็งจริงๆ เลยนะนั่น";
				next;
				if (select( "จัดการ Ferus และ Deleter เรียบร้อย", "จัดการ Gold และ Blue Acidus เรียบร้อย" ) == 1) {
					mes "[Melody-Jack]";
					if (checkquest(13140,PLAYTIME) == 0)
						mes "เจ้ามังกรเขียวกับมังกรแดงพวกนั้นน่ะ เห็นแล้วทำให้ข้านึกถึงแตงโมชะมัด";
					else if (checkquest(13139,HUNTING) == 1) {
						cutin "red_ferus_card",3;
						mes "Abyss Lake... เจ้าพอจะรู้วิธีเข้าไปข้างในนั่นไหม? หรือเจ้าไม่ใช่นักผจญภัยประเภทที่ชอบขุดคุ้ยข้อมูลกันล่ะ?";
						next;
						mes "[Melody-Jack]";
						mes "เอาเป็นว่าข้าจะใบ้ให้หน่อยละกัน ว่าทั้งกุญแจและทางเข้าน่ะ สามารถหาได้จากแถวๆ นั้นแหละ";
						cutin "",255;
					}
					else if (checkquest(13139,HUNTING) == 2) {
						mes "จัดการพวกมังกรนั่นเรียบร้อยแล้วงั้นรึ? ไปพักผ่อนสักวันเถอะ";
						callsub S_Quest,13139,13140,1;
					}
					else {
						cutin "green_ferus_card",3;
						mes "ที่ Abyss Lake น่ะเต็มไปด้วยมังกรทั้งข้างนอกข้างใน มังกรพวกนี้ไม่ได้พิเศษอะไรหรอก แต่มันมีเยอะจนน่ารำคาญ";
						next;
						mes "[Melody-Jack]";
						mes "ระหว่าง Ferus กับ Deleter ตัวไหนอันตรายกว่ากันงั้นเหรอ? ข้าเองก็ไม่รู้เหมือนกัน ไปจัดการมันทั้งคู่เลยละกัน";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "ไม่ว่าจะสีเขียวหรือสีแดง ต่างก็ขวางหูขวางตาพอกันนั่นแหละ เอาเป็นว่าจัดการให้หมดแล้วค่อยกลับมาหาข้าละกัน";
							callsub S_Quest,13140,13139;
						}
					}
				}
				else { // ส่วนของ Acidus
					mes "[Melody-Jack]";
					if (checkquest(13142,PLAYTIME) == 0)
						mes "ไปพักซะ เดี๋ยวเราค่อยไปปราบเจ้าพวกมังกร Acidus กันใหม่อีกรอบ";
					else if (checkquest(13141,HUNTING) == 1) {
						cutin "blue_acidus_card",3;
						mes "Abyss Lake มันไม่ได้อยู่ใกล้ๆ เลยนะนั่น แต่ก็คุ้มค่าที่จะถ่อไปที่นั่นอยู่หรอก";
						next;
						mes "[Melody-Jack]";
						mes "จริงๆ แล้วงานนี้มันก็เหมือนการฝึกหัดนั่นแหละ แต่เจ้าแค่แกล้งทำเป็นว่ากำลังช่วยใครบางคนอยู่ละกัน แล้วเดี๋ยวข้าจะมีรางวัลให้";
						cutin "",255;
					}
					else if (checkquest(13141,HUNTING) == 2) {
						mes "เจ้ามีความสามารถมากกว่าที่ข้าคิดไว้นะเนี่ย ถ้าเจ้าจับทางพวกมันได้แล้ว ครั้งหน้าก็คงจะง่ายขึ้น การเรียนรู้พวกนี้มันขึ้นอยู่กับตัวเจ้าเองนะ";
						callsub S_Quest,13141,13142,1;
					}
					else {
						cutin "gold_acidus_card",3;
						mes "มันจะมีถ้ำที่พวกมังกรอาศัยอยู่ใน Abyss Lake น่ะ พวกมันมีเยอะมาก แต่พวกตัวที่บินได้เนี่ยสิที่น่ารำคาญที่สุด";
						next;
						mes "[Melody-Jack]";
						mes "ไปสยบพวกมันให้สุดความสามารถของเจ้าเถอะ ไม่ต้องไปสนใจสีของพวกมันหรอก เพราะยังไงเจ้าก็ต้องกวาดล้างพวกมันให้เกลี้ยงอยู่ดี";
						next;
						cutin "",255;
						if (select( "รับภารกิจ", "ยกเลิก" ) == 1) {
							mes "[Melody-Jack]";
							mes "ไปปราบ Gold Acidus และ Blue Acidus ที่ชั้นสุดท้ายของ Abyss Lake ซะ";
							callsub S_Quest,13142,13141;
						}
					}
				}
				close;
			}
		}
		else {
			mes "เจ้าช่วยงาน Aigu เสร็จแล้วงั้นรึ? หรือว่าจะล้มเลิกกลางคันล่ะนั่น? จะอย่างไรก็ตาม ข้าไม่มีธุระอะไรกับเจ้าแล้วล่ะ";
			next;
			// ระบบทำการลบรายการเควสต์ที่ไม่สำเร็จในกลุ่มเลเวล 121-130
			callsub S_Erasequest,
				13119,"Baroness of Retribution",
				13121,"Lady Solace",
				13123,"Mistress of Shelter",
				13125,"Dame of Sentinel",
				13127,"Vanberk และ Isilla",
				13129,"Hodremlin",
				13131,"Agav และ Echio",
				13133,"Ragged Zombie",
				13135,"Zombie Slaughter",
				13137,"Banshee",
				13139,"Ferus",
				13141,"Acidus";
			mes "[Melody-Jack]";
			mes "ข้าไม่รู้หรอกนะว่าเจ้าได้พบสิ่งที่กำลังตามหาอยู่หรือเปล่า... แต่ข้าคงยุ่งเกี่ยวกับเจ้าไปมากกว่านี้ไม่ได้แล้วล่ะ ขอบใจมาก";
		}
	}
	close;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp 900000,200000;
	return;

S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		.@quest_id = getarg(.@i);
		switch( checkquest(.@quest_id,HUNTING) ) {
		case -1:
			if (checkquest(.@quest_id+1) > -1)
				erasequest (.@quest_id+1);
			continue;
		case 0:
		case 1: // กรณีที่ยังล่าไม่เสร็จแต่ระบบสั่งให้ลบ
			mes "[Melody-Jack]";
			mes "รายการวิจัย "+ getarg(.@i+1) +" ถูกยกเลิกแล้ว ไม่จำเป็นต้องรับคำขอเพิ่มอีก";
			break;
		case 2: // กรณีที่ล่าเสร็จเรียบร้อยแล้ว
			mes "[Melody-Jack]";
			mes "จัดการ "+ getarg(.@i+1) +" เรียบร้อย ข้อตกลงก็คือข้อตกลง... นี่คือค่าตอบแทนของเจ้า";
			getexp 900000,200000;
			break;
		}
		erasequest .@quest_id;
		next;
	}
	return;

OnTouch_: // เหตุการณ์เมื่อเดินเข้าใกล้ NPC
	if (para_120lv01 == 1) {
		emotion ET_SURPRISE;
		mes "[Melody-Jack]";
		mes "เฮ้ เจ้าน่ะ! ... เดี๋ยวสิ?";
		close;
	}
	end;
}

moc_para01,17,77,5	script	Aigu#1	4_F_IU,{
	mes "[Aigu]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "เอ๋ ทำไมคุณถึงยังไม่มีตราประทับของ Eden Group ล่ะคะ?";
		mes "ฉันจะคุยกับนักผจญภัยที่มีเลเวลระหว่าง ^FF0000121-130^000000 เท่านั้นค่ะ";
	}
	else if (para_120lv01 == 0) {
		cutin "igu01",2;
		mes "สวัสดีค่ะ? วันนี้อากาศดีจังเลยนะคะ ฉันขอรบกวนอะไรคุณหน่อยได้ไหม? ได้ยินมาว่านักผจญภัยใน Eden Group ใจดีกันทุกคนเลย";
		next;
		if (select( "ฉันไม่ใช่พวกใจดีหรอกนะ", "ได้สิ มีอะไรให้ช่วยล่ะ" ) == 1) {
			cutin "igu05",2;
			mes "[Aigu]";
			mes "โธ่... ใจร้ายจัง";
		}
		else {
			cutin "igu03",2;
			mes "[Aigu]";
			mes "จริงเหรอคะ! งั้นฉันขอแนะนำตัวก่อนนะ! ฉันชื่อ ^ff0000Aigu หัวหน้าวงดนตรี Dandelion ค่ะ^000000";
			next;
			mes "[Aigu]";
			mes "ฉันเพิ่งกลับมาจากการแสดงน่ะค่ะ จริงๆ มีคนที่ฉันอยากเจออยู่ แต่ตอนนี้เขาหายตัวไป ฉันก็เลยมาที่ Eden Group เพื่อขอความช่วยเหลือค่ะ";
			next;
			mes "[Aigu]";
			mes "เห็นว่าที่นี่ไม่มีคนคอยดูแลเรื่องนี้แถมทุกคนก็ยุ่งกันหมด ฉันเลยตัดสินใจจัดการเรื่องคำขอด้วยตัวเองค่ะ ขอบคุณที่ช่วยนะคะ!";
			para_120lv01 = 1;
		}
	}
	else if (para_120lv01 > 0) {
		cutin "igu01",2;
		mes "การได้กลับมาที่ Midgard นี่มันดีจริงๆ! ตื่นเต้นเหมือนวันแรกที่มาเลยล่ะค่ะ";
		next;
		if (select( "ภารกิจของคุณคืออะไร?", "ชวนคุยเรื่องอื่น" ) == 2) {
			cutin "igu01",2;
			mes "[Aigu]";
			mes "วง Dandelion ของเราเดินทางไปทั่วโลกเพื่อมอบดนตรีที่ช่วยเยียวยาจิตใจให้กับผู้คนค่ะ";
			next;
			cutin "igu02",2;
			mes "[Aigu]";
			mes "จริงๆ เพลงของพวกเราไม่ได้มีพลังเวทมนตร์อะไรหรอกนะคะ แต่ฉันมั่นใจว่ามันช่วยให้ผู้คนรู้สึกดีขึ้นได้แน่นอน เพราะฉันตั้งใจร้องสุดความสามารถเลยยังไงล่ะ!";
		}
		else if (BaseLevel < 121) {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "ฉันว่าภารกิจนี้อาจจะยากเกินไปสำหรับคุณนะคะ? ขืนส่งไปตอนนี้อาจจะทำให้คนหลงทางเพิ่มขึ้นก็ได้ ลองออกเดินทางเก็บเกี่ยวประสบการณ์ให้มากกว่านี้ก่อนดีไหมคะ?";
		}
		else if (BaseLevel < 131) {
			mes "[Aigu]";
			mes "ฉันได้สร้างที่พักชั่วคราวไว้สำหรับคนที่ฉันกำลังตามหาและนักผจญภัยที่หลงทางน่ะค่ะ ฉันเลยอยากรู้ว่ามีใครเคยแวะเวียนไปที่นั่นบ้างไหม";
			next;
			switch( select( "สำรวจหอคอย Thanatos", "สำรวจวิหาร Rachel", "สำรวจเกาะนิรนาม", "สำรวจทะเลสาบ Abyss Lake" ) ) {
				mes "[Aigu]";
			case 1: // Thanatos Tower
				if (checkquest(13115,PLAYTIME) == 0)
					mes "ฉันพบร่องรอยของคนคนนั้นแล้วล่ะค่ะ มีความหวังขึ้นมานิดนึงแล้ว ไว้รออีกสักพักค่อยมาช่วยฉันสำรวจอีกรอบนะ";
				else if (isbegin_quest(13111) == 1) {
					cutin "igu04",2;
					mes "คุณบอกว่ามีคนไปที่นั่น แต่ไม่รู้ว่าเป็นใครเหรอคะ? ขอบคุณมากค่ะ แต่พรุ่งนี้ช่วยไปดูให้อีกรอบได้ไหม เผื่อเราจะรู้ว่าเป็นใครกันแน่";
					callsub S_Quest,13111,13115,1250000,300000;
				}
				else if (isbegin_quest(13107) == 1)
					mes "คุณอาจจะพบที่พักชั่วคราวบนยอดหอคอยนะคะ ถึงมันจะมีแค่พวหน้ำกับกองไฟก็เถอะ";
				else {
					mes "ที่หอคอย Thanatos ทั้งสูงและมีมอนสเตอร์อันตราย นักผจญภัยเลยหลงทางกันง่ายมาก แต่สำหรับคุณคงไม่ยากเกินไปใช่ไหมคะ รบกวนไปตรวจสอบให้ทีค่ะ";
					next;
					if (select( "เริ่มการสำรวจ", "ยกเลิก" ) == 1) {
						mes "[Aigu]";
						mes "หอคอย Thanatos อยู่ทางเหนือของสาธารณรัฐ Schwarzwald ค่ะ! อยู่ไม่ไกลจาก Juno หรือ Hugel เท่าไหร่";
						callsub S_Quest,13115,13107;
					}
				}
				break;
			case 2: // Rachel Sanctuary
				if (checkquest(13116,PLAYTIME) == 0)
					mes "การสำรวจที่ Rachel พักไว้แค่นี้ก่อนค่ะ ไว้พรุ่งนี้ค่อยมาช่วยฉันใหม่นะคะ";
				else if (isbegin_quest(13112) == 1) {
					mes "การเดินทางเป็นยังไงบ้างคะ? ไม่มีข่าวคราวพิเศษเลยเหรอ... แต่ภารกิจยังไม่จบแค่นี้นะ พรุ่งนี้ฉันคงต้องรบกวนคุณอีก";
					callsub S_Quest,13112,13116,1000000,300000;
				}
				else if (isbegin_quest(13108) == 1)
					mes "วิหารนั่นไม่ได้เข้าไปได้ง่ายๆ ด้วยวิธีปกติหรอกนะคะ มันยากหน่อยแต่ฉันว่าเราน่าจะหาเบาะแสใน Rachel ได้";
				else {
					cutin "igu03",2;
					mes "มีวิหารศักดิ์สิทธิ์อยู่ภายใน Rachel Shrine ค่ะ ถ้าหลงเข้าไปแล้วอาจจะติดอยู่ในนั้นได้ง่ายๆ เลย";
					next;
					mes "[Aigu]";
					mes "รบกวนคุณช่วยไปดูร่องรอยของนักผจญภัยที่หลงทาง บริเวณทางตะวันออกเฉียงใต้ของชั้น 2 ในโซนกลางให้หน่อยนะคะ";
					next;
					if (select( "เริ่มการสำรวจ", "ยกเลิก" ) == 1) {
						cutin "igu05",2;
						mes "[Aigu]";
						mes "ขอให้คุณปลอดภัยในการเดินทางนะคะ";
						callsub S_Quest,13116,13108;
					}
				}
				break;
			case 3: // Nameless Island
				if (checkquest(13117,PLAYTIME) == 0)
					mes "การสำรวจเกาะนิรนามเดี๋ยวค่อยมาต่อกันคราวหน้าค่ะ ตอนนี้คุณไปพักผ่อนก่อนดีกว่าไหมคะ?";
				else if (isbegin_quest(13113) == 1) {
					mes "ขอบคุณมากค่ะ! มีนักผจญภัยพเนจรคนอื่นๆ ช่วยเหลือด้วยเหมือนกันแฮะ ยังไงเราก็ต้องสำรวจกันต่อ ไว้คราวหน้ามาช่วยฉันใหม่นะ";
					callsub S_Quest,13113,13117,900000,300000;
				}
				else if (isbegin_quest(13109) == 1) {
					cutin "igu04",2;
					mes "บางที... เขาอาจจะกำลังนั่งดื่มสังสรรค์อยู่กับพวกคนตายก็ได้นะ (ล้อเล่นค่ะ!) ยังไงอย่าลืมกลับมาส่งข่าวฉันด้วยล่ะ";
				}
				else {
					cutin "igu03",2;
					mes "ใช่ค่ะ บางทีพวกเขาอาจจะกลับมาไม่ได้เพราะโดนพวกคนตายจับตัวไว้! เกาะนิรนามนั่นต้องมีอะไรแน่ๆ รบกวนไปสำรวจที่นั่นทีค่ะ";
					next;
					if (select( "เริ่มการสำรวจ", "ยกเลิก" ) == 1) {
						cutin "igu01",2;
						mes "[Aigu]";
						mes "ฉันอยากให้คุณไปสำรวจชั้นใต้ดินชั้นที่ 1 ของวิหารบนเกาะนิรนามค่ะ ฉันไม่อยากให้คุณเสี่ยงเลยถ้าคุณไม่เต็มใจ";
						callsub S_Quest,13117,13109;
					}
				}
				break;
			case 4: // Abyss Lake
				if (checkquest(13118,PLAYTIME) == 0)
					mes "การสำรวจที่ Abyss Lake ไว้ค่อยมาเริ่มกันใหม่คราวหน้านะคะ";
				else if (isbegin_quest(13114) == 1) {
					cutin "igu05",2;
					mes "กลับมาอย่างปลอดภัยก็ดีแล้วค่ะ ถึงดูจากสีหน้าแล้วจะยังไม่มีข่าวดีก็เถอะ แต่ฉันจะไม่ยอมแพ้หรอก ขอบคุณมากนะคะ!";
					callsub S_Quest,13114,13118,900000,200000;
				}
				else if (isbegin_quest(13110) == 1) {
					cutin "igu05",2;
					mes "หรือว่า... เขาจะหมดหวังจนกระโดดลงไปใต้ก้นทะเลสาบที่มีมอนสเตอร์เต็มไปหมดแล้วนะ...";
				}
				else {
					mes "ฉันอยากให้คุณไปสำรวจที่ Abyss Lake ค่ะ เจาะจงไปที่ถ้ำใจกลางทะเลสาบนั่นเลย";
					next;
					mes "[Aigu]";
					mes "ใช่ค่ะ! การจบการผจญภัยที่ยอดเยี่ยมมันต้องคู่กับมังกร! ได้ยินมาว่ามีมอนสเตอร์น่ากลัวๆ อยู่ที่ก้นทะเลสาบด้วยนะคะ";
					next;
					if (select( "เริ่มการสำรวจ", "ยกเลิก" ) == 1) {
						mes "[Aigu]";
						mes "Abyss Lake เป็นทางลัดที่เร็วที่สุดที่จะไป Hugel ค่ะ รบกวนไปตรวจสอบที่ถ้ำใต้ดินชั้นที่ 3 ให้ทีนะคะ!";
						callsub S_Quest,13118,13110;
					}
				}
			}
		}
		else {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "ตอนนี้ฉันยังไม่มีภารกิจสำรวจเพิ่มให้คุณแล้วค่ะ! คุณควรไปท้าทายกับสิ่งที่ยากกว่านี้ให้สมกับเลเวลของคุณดีกว่านะ";
			callsub S_Erasequest,
				13107,"หอคอย Thanatos",
				13108,"วิหาร Rachel",
				13109,"เกาะนิรนาม",
				13110,"ทะเลสาบ Abyss Lake";
			callsub S_Erasequest2,
				13111,"หอคอย Thanatos",1250000,300000,
				13112,"วิหาร Rachel",1000000,300000,
				13113,"เกาะนิรนาม",900000,300000,
				13114,"ทะเลสาบ Abyss Lake",900000,200000;
			for ( .@i = 13115; .@i <= 13118; .@i++ )
				if (isbegin_quest(.@i)) erasequest .@i;
		}
	}
	close2;
	cutin "",255;
	end;

S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp getarg(2),getarg(3);
	return;

S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "โอ้ คุณกำลังทำภารกิจสำรวจที่ "+ getarg(.@i+1) +" อยู่สินะคะ เดี๋ยวฉันจะล้างบันทึกให้ คุณจะได้ไปทำภารกิจอื่นที่เหมาะสมกว่าค่ะ";
			erasequest getarg(.@i);
		}
	}
	return;

S_Erasequest2:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 4 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "โอ้ คุณทำภารกิจสำรวจที่ "+ getarg(.@i+1) +" เสร็จพอดีเลย ขอบคุณมากนะคะที่ช่วยฉันจัดการเรื่องนี้!";
			erasequest getarg(.@i);
			getexp getarg(.@i+2),getarg(.@i+3);
		}
	}
	return;
}

-	script	ร่องรอยของนักผจญภัย	-1,{
	.@quest = 13106 + atoi(strnpcinfo(2));
	.@delay = 13110 + atoi(strnpcinfo(2));
	if (isbegin_quest(.@quest) == 1) {
		mes "พบร่องรอยว่าเคยมีคนอยู่ที่นี่ แต่ไม่พบเบาะแสอื่นเพิ่มเติมเลย... กลับไปรายงานให้ Aigu ทราบดีกว่า";
		erasequest .@quest;
		setquest .@delay;
	}
	else
		mes "มีร่องรอยว่าเคยมีใครบางคนอยู่ที่นี่";
	close;
}
thana_step,167,292,5	duplicate(ร่องรอยของนักผจญภัย)	ร่องรอยของนักผจญภัย#1	4_TRACE
ra_san05,265,69,5	duplicate(ร่องรอยของนักผจญภัย)	ร่องรอยของนักผจญภัย#2	4_TRACE
abbey02,155,290,5	duplicate(ร่องรอยของนักผจญภัย)	ร่องรอยของนักผจญภัย#3	4_TRACE
abyss_03,102,140,5	duplicate(ร่องรอยของนักผจญภัย)	ร่องรอยของนักผจญภัย#4	4_TRACE

tha_t06,126,128,3	script	Suspicious Monster#1	4_DEVIRUCHI,{
	mes "[นักผจญภัยที่ปลอมตัวมา]";
	mes "ข้าต้องมาทำงานในที่ที่อันตรายแบบนี้แลกกับเงินแค่ 5,000 Zeny เองนะเนี่ย... เจ้าคือนักผจญภัยที่ได้รับคำขอมาจาก Eden Group ใช่ไหม?";
	next;
	switch( select( "ใช่", "ไม่ใช่" ) ) {
		mes "[นักผจญภัยที่ปลอมตัวมา]";
	case 1:
		if (BaseLevel < 121 || BaseLevel > 130) {
			mes "ข้ามองปราดเดียวก็รู้แล้วว่าไม่ใช่เจ้า!";
			close;
		}
		mes "นักผจญภัยที่กำลังทำภารกิจให้ Aigu จะได้รับสิทธิ์ให้วาร์ปขึ้นไปยังชั้นบนแทนตัวเธอเอง";
		next;
		if (select( "ขึ้นไปข้างบน", "ยังไม่ขึ้นไป" ) == 1) {
			mes "[นักผจญภัยที่ปลอมตัวมา]";
			if (isbegin_quest(13107) != 1)
				mes "ดูเหมือนว่าตอนนี้เจ้าจะไม่ได้ทำภารกิจสำรวจหอคอย Thanatos อยู่นะ ข้าเสียใจด้วยแต่ข้ามาทำงานนี้ให้ Aigu ไม่ใช่เจ้า";
			else {
				mes "ระวังตัวด้วยล่ะ! ถ้าขึ้นไปแล้วเจ้าจะลงมาไม่ได้นะ... เพื่อ Aigu!";
				close2;
				warp "thana_step",69,369;
				end;
			}
		}
		close;
	case 2:
		mes "งั้นก็ช่วยทำเป็นไม่รู้จักข้าทีเถอะ! ถ้าความลับแตกข้าจะตกอยู่ในอันตรายนะ!";
		close;
	}
}
