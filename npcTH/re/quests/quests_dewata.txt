//===== rAthena Script =======================================
//= Dewata Quest NPCs
//===== By: ==================================================
//= Gennosuke Kouga, Muad_Dib
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Quest NPCs related to Dewata:
//== "Dewata Legend"
//== "Help Out the Old Man"
//== "Traditional Weapon"
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Standardized. [Euphy]
//= 1.2 Updated to match the official script. [Lemongrass/Euphy]
//============================================================

// Dewata Legend Quest :: dewata_legend
//============================================================
dew_fild01,57,274,0	script	#hideout_legend	111,6,6,{
OnTouch:
	if (BaseLevel > 59 && dew_legend < 1) {
		mes "- คุณได้ยินเสียงการสนทนาแผ่วเบาจากด้านใน -";
		next;
		mes "[Tribal Chief Paiko]";
		mes "อีกครั้งแล้วที่ข้าล้มเหลวในการ";
		mes "สร้าง ^8B4513Jaty Crown^000000...";
		mes "ข้ามั่นใจว่าครั้งนี้ข้าจะทำมันได้แท้ๆ";
		mes "ทำไมมันถึงไม่ได้ผลกันนะ?";
		next;
		mes "[Sage Kasyapa]";
		mes "^006400Jaty Crown^000000 เป็นที่รู้จักว่ามีพลังอันน่าอัศจรรย์มาตั้งแต่สมัยโบราณ";
		mes "แต่ว่ากันว่ามีเพียงผู้ที่ได้รับเกียรติแห่งเผ่าเท่านั้น";
		mes "ที่จะสามารถใช้งานมันได้";
		next;
		mes "[Tribal Chief Paiko]";
		mes "^0000FFKasyapa^000000!!";
		mes "เจ้ากำลังจะบอกว่าแม้ข้าจะเป็นผู้นำแห่งเผ่า แต่ข้ายังไม่ได้รับเกียรตินั้นงั้นรึ?";
		next;
		mes "[Sage Kasyapa]";
		mes "ใจเย็นก่อน ^0000FFChief Paiko^000000...";
		mes "ในฐานะหัวหน้าเผ่าผู้ยิ่งใหญ่และทรงพลัง ท่านควรจะมีคุณสมบัติมากเกินพออยู่แล้ว";
		next;
		mes "[Sage Kasyapa]";
		mes "ในสมัยที่ข้ายังหนุ่ม ข้าคงแก้ปัญหานี้ได้ง่ายๆ แต่จิตใจของชายแก่คนนี้คิดไม่ออกจริงๆ ว่าอะไรที่ขาดหายไป";
		next;
		mes "เสียงสนทนาค่อยๆ เงียบหายไปและคุณไม่ได้ยินอะไรอีก";
		set dew_legend,1;
		close;
	}
	end;
}

dew_in01,15,33,0	script	#hidein_legend	111,5,5,{
OnTouch:
	if (dew_legend == 3) {
		mes "Kasyapa และ Chief Paiko เริ่มคุยกันในขณะที่คุณกำลังจะเดินจากไป...";
		next;
		mes "[Sage Kasyapa]";
		mes "ข้าไม่แน่ใจว่าอะไรที่ขาดหายไป แต่ผู้เดินทางคนนั้นอาจเป็นตัวช่วยสำคัญในการสร้าง ^006400Jaty Crown^000000";
		mes "เรื่องนี้ข้ามั่นใจ";
		mes "เหตุผลก็คือ...";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เหตุผลคืออะไรล่ะ?";
		next;
		mes "[Sage Kasyapa]";
		mes "^8B4513Cendrawasih Feather^000000 นี้เคยเป็นของผู้นำเผ่าคนแรกเมื่อตอนที่มงกุฎถูกสร้างขึ้นครั้งแรก";
		mes "มันถูกเก็บรักษาไว้ในฐานะมรดกตกทอดอันล้ำค่าของเผ่าเรา";
		next;
		mes "[Sage Kasyapa]";
		mes "แต่เมื่อผู้เดินทางคนนั้นเข้ามาใกล้ ขนนกนี้ก็เริ่มส่องแสง และข้าคิดว่านี่ไม่ใช่แค่เรื่องบังเอิญ";
		mes "บุคคลผู้นี้จะเป็นกุญแจสำคัญในการช่วยพวกเราสร้างมงกุฎ";
		next;
		mes "[Tribal Chief Paiko]";
		mes "มันจะเกิดขึ้นจริงแน่งั้นรึ?";
		mes "ถ้าเป็นเช่นนั้น ข้าจะมอบทุกสิ่ง";
		mes "เพื่อสนับสนุนพวกเขา...";
		set dew_legend,4;
		close;
	}
	end;
}

dew_in01,22,48,3	script	Sage Kasyapa#dew	541,{
	if (dew_legend == 0) {
		mes "[Sage Kasyapa]";
		mes "อา คนแปลกหน้าสำหรับดินแดนของเรา";
		mes "เจ้าหลงทางงั้นรึ?";
		mes "ข้าหวังว่าเจ้าจะไม่พบอันตรายมากมายนักระหว่างการเดินทางมาที่นี่นะ";
		close;
	} else if (dew_legend == 1) {
		mes "[Sage Kasyapa]";
		mes "สีหน้าอันมุ่งมั่นของเจ้าบอกข้าว่า";
		mes "เจ้ามีบางอย่างจะพูด";
		mes "เจ้าบอกข้ามาเลยดีกว่าว่ามันคืออะไร";
		next;
		if(select("ข้าบังเอิญได้ยินเรื่องเกี่ยวกับมงกุฎน่ะ?:แค่มาหาร้านขายของฝากครับ/ค่ะ") == 2) {
			mes "[Tribal Chief Paiko]";
			mes "จริงรึ?";
			mes "ดูเหมือนว่าดวงตาของเจ้า";
			mes "อยากจะพูดบางอย่างนะ";
			close;
		}
		mes "[Sage Kasyapa]";
		mes "โอ้ เจ้าได้ยินเรื่องมงกุฎงั้นรึ?";
		mes "อา ชายแก่คนนี้ปากสว่างจริงๆ";
		mes "เราควรจะระมัดระวังให้มากกว่านี้สักหน่อย...";
		next;
		select("มีอะไรที่ข้าพอจะช่วยได้ไหม?");
		mes "[Sage Kasyapa]";
		mes "ข้าเห็นในดวงตาของเจ้าว่า";
		mes "เจ้าเป็นคนที่ไว้วางใจได้... แต่...";
		next;
		mes "มีแสงระยิบระยับเกิดขึ้นชั่วขณะ ^0000FFKasyapa^000000 ดูเหมือนจะตกใจขึ้นมาทันที";
		specialeffect2 EF_PNEUMA;
		next;
		mes "[Sage Kasyapa]";
		mes "อา ขนนกนั่น......!";
		mes "แทนที่จะคุยกับข้า ทำไมเจ้าไม่ลองไปคุยกับผู้นำเผ่าของเราดู ล่ะ ^0000FFPaiko^000000";
		set dew_legend,2;
		close;
	} else if (dew_legend == 2) {
		mes "[Sage Kasyapa]";
		mes "แทนที่จะคุยกับข้า ทำไมเจ้าไม่ลองไปคุยกับผู้นำเผ่าของเราดู ล่ะ ^0000FFChief Paiko^000000";
		close;
	} else if (dew_legend == 3) {
		mes "[Sage Kasyapa]";
		mes "ถ้าเป็นความต้องการของ Chief ^0000FFPaiko^000000";
		mes "ข้าก็ไม่มีอะไรจะพูดอีก";
		mes "อืม....";
		mes "แต่...... นี่มัน....";
		next;
		mes "[Sage Kasyapa]";
		mes "ช่างมันเถอะ ขอให้เดินทางโดยสวัสดิภาพนะ";
		close;
	} else if (dew_legend == 4) {
		mes "[Sage Kasyapa]";
		mes "ข้ารอเจ้าอยู่เลยผู้เดินทาง";
		mes "ผู้นำเผ่าของเรามีเรื่องไหว้วาน";
		mes "อยากจะขอให้เจ้าช่วย";
		mes "โปรดไปคุยกับ Chief ^0000FFPaiko^000000 เถอะ";
		close;
	} else if (dew_legend == 5) {
		mes "[Sage Kasyapa]";
		mes "ข้าได้ยินว่าเจ้าสามารถช่วยสร้างมงกุฎ";
		mes "ให้แก่ผู้นำเผ่าของเราได้";
		mes "เจ้าจะเป็น";
		mes "ความช่วยเหลือที่ยิ่งใหญ่สำหรับพวกเราอย่างแน่นอน...";
		mes "ข้าเชื่อสายตาตัวเอง!";
		mes "และแสงที่ส่องประกายด้วย...";
		next;
		mes "[Sage Kasyapa]";
		mes "สัญลักษณ์ของเผ่า Dayak";
		mes "และสัตว์ลึกลับผู้โด่งดัง";
		mes "จงหา ^006400Cendrawasih Feather^000000 จำนวน 15 อัน";
		mes "แล้วนำไปให้ Chief ^0000FFPaiko^000000";
		close;
	} else if (dew_legend == 6) {
		mes "[Sage Kasyapa]";
		mes "ด้วยขนนกที่เจ้านำมา การสร้าง ^006400Jaty Crown^000000 ดำเนินไปอย่างราบรื่นขึ้นมาก";
		mes "ไปหา Chief ^0000FFPaiko^000000 เถอะ";
		close;
	} else if (dew_legend == 7) {
		mes "[Sage Kasyapa]";
		mes "มีบางอย่าง";
		mes "ที่ข้าอยากจะมอบให้เจ้า";
		mes "เพื่อเป็นรางวัลสำหรับความช่วยเหลือ";
		mes "ได้โปรด รับมันไปเถอะ ถือเป็นเกียรติสำหรับพวกเรา";
		next;
		if (checkweight(6406,1) == 0) {
			if (Weight + getiteminfo(6406, ITEMINFO_WEIGHT) > MaxWeight) {
				mes "[Sage Kasyapa]";
				mes "เจ้าน้ำหนักเกินขีดจำกัดที่จะแบกรับได้";
				mes "ข้าไม่สามารถมอบรางวัลให้เจ้าได้ เว้นแต่เจ้าจะลดน้ำหนักของไอเทมที่เจ้าพกมา";
				close;
			} else {
				mes "[Sage Kasyapa]";
				mes "ช่องเก็บของของเจ้าเต็มแล้ว";
				mes "ข้าจะมอบรางวัลให้เมื่อเจ้าไปเคลียร์ของออกแล้วกลับมาใหม่";
				close;
			}
		}
		set dew_legend,8;
		getitem 6406,1; //Cendrawasih_SF
		completequest 9157;
		erasequest 9157;
		mes "[Sage Kasyapa]";
		mes "เหตุผลที่ข้าขอให้เจ้าเริ่มภารกิจสำคัญนี้ก็เพราะขนนกชิ้นนี้";
		mes "ต้นกำเนิดในตำนานของมงกุฎที่สืบทอดกันมารุ่นต่อรุ่นคือขนนกนี้...";
		specialeffect2 EF_PNEUMA;
		next;
		mes "[Sage Kasyapa]";
		mes "ข้าเห็นขนนกนี้เริ่มส่องแสงเมื่อเจ้ามาที่นี่";
		mes "ข้ารู้ว่าเจ้าเป็นคนที่จะ";
		mes "พยายามช่วยเหลือพวกเรา";
		mes "ในนามของคนในเผ่าของข้า";
		mes "ข้าขอกล่าวคำขอบคุณ";
		next;
		mes "[Sage Kasyapa]";
		mes "นอกจากนี้ ขนนกที่เจ้าได้รับไม่ได้มีค่าเพียงแค่ในตัวมันเองเท่านั้น แต่มันยังเป็นการยอมรับจากเผ่าของเราด้วย";
		next;
		mes "[Sage Kasyapa]";
		mes "อย่างเช่นภูเขาไฟ ^8B4513Krakatau^000000 เมื่อเจ้าผจญภัยในพื้นที่อันตรายเช่นนั้น หากเจ้าแสดงขนนกนี้ เจ้าจะได้รับความช่วยเหลือจากคนในเผ่า";
		mes "อย่างไรก็ตาม ข้าขอบคุณเจ้าจริงๆ";
		close;
	} else {
		mes "[Sage Kasyapa]";
		mes "นอกจากนี้ ขนนกที่เจ้าได้รับไม่ได้มีค่าเพียงแค่ในตัวมันเองเท่านั้น แต่มันยังเป็นการยอมรับจากเผ่าของเราด้วย";
		next;
		mes "[Sage Kasyapa]";
		mes "อย่างเช่นภูเขาไฟ ^8B4513Krakatau^000000 เมื่อเจ้าผจญภัยในพื้นที่อันตรายเช่นนั้น หากเจ้าแสดงขนนกนี้ เจ้าจะได้รับความช่วยเหลือจากคนในเผ่า";
		mes "อย่างไรก็ตาม ข้าขอบคุณเจ้าจริงๆ";
		close;
	}
}

dew_in01,15,49,4	script	Tribal Chief Paiko#dew	540,{
	if (dew_legend < 2) {
		mes "[Tribal Chief Paiko]";
		mes "ข้าคือผู้นำของเผ่านี้";
		mes "^0000FFPaiko^000000";
		close;
	} else if (dew_legend == 2) {
		mes "[Tribal Chief Paiko]";
		mes "ข้าเห็นว่าเจ้าเป็นนักเดินทาง";
		mes "มาจากต่างแดน";
		mes "อะไรนำพาเจ้ามาที่นี่ล่ะ?";
		next;
		if(select("ข้าได้ยินเรื่องราวมาโดยบังเอิญครับ/ค่ะ:ข้าไม่มีอะไรจะพูด") == 2) {
			mes "[Tribal Chief Paiko]";
			mes "เจ้ากำลังเสียเวลาอยู่ที่นี่เปล่าๆ";
			mes "มีสถานที่น่าสนใจอื่นๆ อีกมากมายรอบหมู่บ้านของเรา";
			close;
		}
		mes "[Tribal Chief Paiko]";
		mes "เจ้าหมายความว่ายังไง?";
		mes "เจ้าได้ยินเรื่องราวเกี่ยวกับมงกุฎงั้นรึ?";
		mes "เจ้าคงฟังผิดไปแล้วล่ะ";
		mes "ข้าไม่มีอะไรจะพูดกับเจ้า";
		mes "เกี่ยวกับเรื่องพวกนั้น";
		next;
		select("Kasyapa กำลังพูดเกี่ยวกับมงกุฎน่ะครับ/ค่ะ...");
		mes "[Tribal Chief Paiko]";
		mes "ดูเหมือนว่า ^0000FFKasyapa^000000 จะเล่านิทานหลอกเด็กให้เจ้าฟังเสียแล้ว แต่ข้าก็ขอบคุณในความเป็นห่วงต่อธุระของเรานะ";
		next;
		mes "[Tribal Chief Paiko]";
		mes "มันไม่ใช่สิ่งที่คนแปลกหน้าอย่างเจ้าจะเข้าใจได้ ดังนั้นไปหาที่อื่นทำธุระของเจ้าซะเถอะ";
		set dew_legend,3;
		close;
	} else if (dew_legend == 3) {
		mes "[Tribal Chief Paiko]";
		mes "มีสถานที่น่าสนใจอื่นๆ อีกมากมายรอบหมู่บ้านของเรา";
		next;
		mes "[Tribal Chief Paiko]";
		mes "ถ้าเป็นเรื่องมงกุฎ ข้าอยากให้เจ้าเลิกถามถึงมันได้แล้ว";
		mes "โปรดออกไปซะ";
		close;
	} else if (dew_legend == 4) {
		mes "[Tribal Chief Paiko]";
		mes "ข้ารอเจ้าอยู่เลย ผู้เดินทาง";
		mes "ตามที่นักปราชญ์ ^0000FFKasyapa^000000 บอก";
		mes "เจ้าเต็มใจที่จะช่วยเราสร้างมงกุฎสำหรับเผ่าของเรา";
		select("มงกุฎอะไรนะครับ/คะ?");
		mes "[Tribal Chief Paiko]";
		mes "ผู้นำเผ่ามีหน้าที่ในการสร้างและปกป้อง ^006400Jaty Crown^000000 เพื่อรักษาความภาคภูมิใจและเกียรติยศของเผ่า";
		next;
		mes "[Tribal Chief Paiko]";
		mes "แต่ตั้งแต่ข้าขึ้นเป็นหัวหน้าเผ่า ก็มีอุปสรรคในการสร้างมันมาตลอด";
		next;
		mes "[Tribal Chief Paiko]";
		mes "ตามที่นักปราชญ์ ^0000FFKasyapa^000000 บอก เจ้าสามารถช่วยพวกเราสร้าง ^006400Jaty Crown^000000 ได้";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เจ้าคิดว่าจะช่วยเราได้ไหม?";
		mes "ความพยายามของเจ้าจะไม่เสียเปล่าแน่นอน";
		next;
		if(select("ตกลงครับ ข้าจะช่วย:ขอโทษด้วยครับ ข้ายังช่วยตอนนี้ไม่ได้") == 2) {
			mes "[Tribal Chief Paiko]";
			mes "นั่นน่าเสียดายจริงๆ";
			mes "ถ้าหากเจ้าเปลี่ยนใจ";
			mes "โปรดกลับมาหาข้าอีกครั้ง";
			mes "ข้าจะรอเจ้า";
			close;
		}
		mes "[Tribal Chief Paiko]";
		mes "ขอบคุณมากนะ คนแปลกหน้า";
		mes "ถ้า Kasyapa เชื่อใจเจ้า ข้าก็รู้ว่าเจ้าจะช่วยพวกเราสร้าง";
		mes "^006400Jaty Crown^000000 ได้";
		mes "สิ่งที่เจ้าสามารถช่วยเราได้";
		mes "มันง่ายมาก";
		next;
		mes "[Tribal Chief Paiko]";
		mes "สัญลักษณ์ของเผ่า Jaty ยังเป็นสัตว์ลึกลับที่มีชื่อเสียงอีกด้วย";
		mes "จงหา ^006400Cendrawasih Feather^000000 15 อันแล้วนำมาให้ข้า";
		mes "ความหวังของเราอยู่ที่เจ้าแล้ว คนแปลกหน้า";
		set dew_legend,5;
		setquest 9155;
		close;
	} else if (dew_legend == 5) {
		if (countitem(6405) < 15) {
			mes "[Tribal Chief Paiko]";
			mes "ดูเหมือนว่าเจ้าจะยังหาวัตถุดิบในการสร้าง ^006400Jaty Crown^000000 ไม่ครบนะ";
			next;
			mes "[Tribal Chief Paiko]";
			mes "สัญลักษณ์ของเผ่า Dayak ยังเป็นสัตว์ลึกลับที่มีชื่อเสียงอีกด้วย";
			mes "จงหา ^006400Cendrawasih Feather^000000 15 อันแล้วนำมาให้ข้า";
			close;
		}
		set dew_legend,6;
		delitem 6405,15; //Cendrawasih_F
		changequest 9155,9156;
		mes "[Tribal Chief Paiko]";
		mes "โอ้! เจ้าหามาครบหมดแล้ว ดูสิ! ขนนกที่เจ้าหามามันกำลังส่องประกายอยู่ ใช่ไหมล่ะ?";
		mes "เอาล่ะ ให้ข้าเริ่มสร้าง ^006400Jaty Crown^000000 เลย ข้ารู้สึกว่าข้าพร้อมแล้ว";
		next;
		mes "ทันใดนั้น เสียงดังสนั่นก็ดังขึ้นพร้อมกับที่คุณและ ^0000FFPaiko^000000 ถูกล้อมรอบด้วยพลังงานประกายแสงอันอบอุ่นและสว่างไสวเหมือนหิ่งห้อยนับพันตัว";
		specialeffect2 EF_BASH3D2;
		close;
	} else if (dew_legend == 6) {
		mes "[Tribal Chief Paiko]";
		mes "ด้วยความช่วยเหลือของเจ้า ^006400Jaty Crown^000000 กำลังดำเนินไปได้ด้วยดี";
		mes "มันต้องใช้เวลาสักพักจนกว่างานจะเสร็จสมบูรณ์ โปรดรอสักครู่";
		next;
		mes "[Tribal Chief Paiko]";
		mes "หลังจากที่ ^006400Jaty Crown^000000 เสร็จสมบูรณ์ มีของขวัญที่ข้าอยากจะมอบให้เจ้า";
		next;
		mes "[Tribal Chief Paiko]";
		mes "อา นอกจากนี้เจ้าควรไปหานักปราชญ์ ^0000FFKasyapa^000000 ผู้ที่อยากจะมอบรางวัลบางอย่างให้เจ้าด้วยนะ";
		set dew_legend,7;
		changequest 9156,9157;
		close;
	} else if (dew_legend == 7) {
		mes "[Tribal Chief Paiko]";
		mes "อา เจ้าควรไปคุยกับนักปราชญ์ ^0000FFKasyapa^000000 ผู้ที่อยากจะมอบรางวัลบางอย่างให้เจ้าด้วยนะ";
		close;
	} else if (dew_legend == 8) {
		mes "[Tribal Chief Paiko]";
		mes "ด้วยการสนับสนุนของเจ้า ข้าไม่กังวลเกี่ยวกับการสร้าง ^006400Jaty Crown^000000 เลย";
		mes "ข้าซาบซึ้งใจจริงๆ";
		next;
		mes "[Tribal Chief Paiko]";
		mes "วันที่น่ายินดีเช่นวันนี้ควรค่าแก่การเฉลิมฉลองนะ เจ้าไม่คิดอย่างนั้นรึ?";
		mes "ดังนั้น ข้าจึงได้เตรียมตัวอย่างอาหารพิเศษของเผ่าเราที่เรียกว่า ^006400Satay^000000 ไว้";
		next;
		mes "[Tribal Chief Paiko]";
		mes "แต่ว่า ^006400Satay^000000 นี้มันอร่อยเกินกว่าที่เราสองคนจะเก็บไว้กินกันเอง เราเลยอยากจะแบ่งปันให้กับผู้คนในหมู่บ้านด้วย";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เจ้าคิดว่ายังไงล่ะ?";
		mes "เพื่อเป็นการประกาศข่าวว่า";
		mes "^006400Jaty Crown^000000 ถูกสร้างขึ้นอย่างสำเร็จผล และเพื่อแบ่งปัน ^006400Satay^000000 อันแสนอร่อยกับเพื่อนพ้องของเรา ข้าต้องการความช่วยเหลือจากเจ้า";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เจ้าพร้อมสำหรับงานจัดส่งสั้นๆ ไหม?";
		next;
		if(select("แน่นอนครับ ข้าช่วยได้:ขอโทษครับ ข้ายุ่งเกินกว่าจะช่วยได้") == 2) {
			mes "[Tribal Chief Paiko]";
			mes "ข้าเชื่อว่าเจ้าจะต้องช่วยข้าแน่ๆ...";
			mes "มันน่าผิดหวังเล็กน้อยนะ";
			mes "ถ้าหากเจ้าเปลี่ยนใจ";
			mes "ก็กลับมาคุยกับข้าอีกครั้งล่ะ";
			close;
		}
		mes "[Tribal Chief Paiko]";
		mes "โอ้! ข้ารู้ว่าเจ้าจะช่วย";
		mes "นักรบของเผ่าเราคือกลุ่มแรกที่ข้านึกถึงก่อนที่จะแบ่งปันให้คนอื่น";
		mes "โปรดนำ Satay ไปแบ่งปันให้เหล่านักรบในหมู่บ้านที";
		next;
		mes "[Tribal Chief Paiko]";
		mes "สำหรับตอนนี้ ข้าจะให้ ^006400Satay^000000 แก่เจ้า 2 ไม้";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เจ้าอาจจะถูกยั่วยวนด้วยกลิ่นหอมของ ^006400Satay^000000 และเจ้าอาจจะแอบกินสักไม้หนึ่งก็ได้ แต่ถ้าเจ้ากินมากกว่านั้น เจ้าจะไม่เหลือพอไปแบ่งปัน ^006400Satay^000000";
		mes "ให้กับเพื่อนๆ ในหมู่บ้านนะ";
		set dew_legend,9;
		getitem 11533,2; //Satay
		setquest 9158;
		close;
	} else if (dew_legend == 9) {
		callsub L_Delivery,0;
		mes "[Tribal Chief Paiko]";
		mes "นักรบของเผ่าเราคือกลุ่มแรกที่ข้านึกถึงเพื่อนนำอาหารไปส่งและแบ่งปัน Satay กับเหล่านักรบในหมู่บ้าน";
		close;
	} else if (dew_legend == 10) {
		mes "[Tribal Chief Paiko]";
		mes "อา คนต่อไปที่ข้าอยากให้ส่ง ^006400Satay^000000 ไปให้คือคนที่อยู่ฝั่งตรงข้ามของแม่น้ำ ท่าน ^0000FFTribe Manager^000000";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เพื่อการนี้ ข้าจะมอบ ^006400Satay^000000 ให้เจ้าอีก 2 ชุด";
		next;
		mes "[Tribal Chief Paiko]";
		mes "ถ้าเจ้าถูกดึงดูดด้วยกลิ่นหอมของ ^006400Satay^000000 เจ้าจะกินสักไม้หนึ่งก็ได้นะ แต่ถ้ากินมากกว่านั้นเจ้าจะไม่เหลือ ^006400Satay^000000 ไปแบ่งปันให้เพื่อนของเรา";
		set dew_legend,11;
		getitem 11533,2; //Satay
		changequest 9159,9160;
		close;
	} else if (dew_legend == 11) {
		callsub L_Delivery,0;
		mes "[Tribal Chief Paiko]";
		mes "คนชุดที่สองที่จะต้องไปส่ง ^006400Satay^000000 คือคนที่อยู่ฝั่งตรงข้ามแม่น้ำ";
		next;
		mes "นั่นคือ ^0000FFTribe Manager^000000";
		mes "โปรดนำไปส่งให้เขาก่อนที่มันจะเย็นชืดนะ";
		close;
	} else if (dew_legend == 12) {
		mes "[Tribal Chief Paiko]";
		mes "โอ้ เจ้าส่งเรียบร้อยแล้วรึ?";
		mes "คนชุดที่สามที่จะต้องส่ง ^006400Satay^000000 ให้คือ ^0000FFGatekeeper of Krakatau Volcano^000000";
		next;
		mes "[Tribal Chief Paiko]";
		mes "ข้าจะให้ ^006400Satay^000000 อีก 2 ชุดสำหรับการจัดส่งครั้งนี้";
		next;
		mes "[Tribal Chief Paiko]";
		mes "และเหมือนเดิม อย่าพ่ายแพ้ต่อกลิ่นหอมและพยายามอย่ากิน ^006400Satay^000000 เข้าไปล่ะ";
		set dew_legend,13;
		getitem 11533,2; //Satay
		changequest 9161,9162;
		close;
	} else if (dew_legend == 13) {
		callsub L_Delivery,1;
		mes "[Tribal Chief Paiko]";
		mes "คนชุดที่สามที่จะต้องส่ง ^006400Satay^000000 ให้คือ ^0000FFGatekeeper of Krakatau Volcano^000000";
		mes "โปรดนำไปส่งก่อนที่มันจะเย็นชืดนะ";
		close;
	} else if (dew_legend == 14) {
		mes "[Tribal Chief Paiko]";
		mes "โอ้ เจ้าส่งเรียบร้อยแล้ว";
		mes "คนสุดท้ายที่ต้องส่ง ^006400Satay^000000 ให้คือ ^0000FFBorobudur Temple Manager^000000 ที่อยู่บนเกาะ ^8B4513Dewata Island^000000";
		next;
		mes "[Tribal Chief Paiko]";
		mes "ข้าจะให้ ^006400Satay^000000 อีก 2 ไม้";
		next;
		mes "[Tribal Chief Paiko]";
		mes "และเหมือนเดิม อย่าพ่ายแพ้ต่อกลิ่นหอมและพยายามอย่ากิน ^006400Satay^000000 เข้าไปล่ะ";
		set dew_legend,15;
		getitem 11533,2; //Satay
		changequest 9163,9164;
		close;
	} else if (dew_legend == 15) {
		callsub L_Delivery,1;
		mes "[Tribal Chief Paiko]";
		mes "คนสุดท้ายที่จะต้องส่ง ^006400Satay^000000 ให้คือ";
		mes "^0000FFBorobudur Temple Manager^000000 ซึ่งอยู่ที่";
		mes "^8B4513Dewata Island^000000";
		mes "โปรดนำไปส่งก่อนที่มันจะเย็นชืดนะ";
		close;
	} else if (dew_legend == 16) {
		mes "[Tribal Chief Paiko]";
		mes "โอ้ เจ้าส่งเรียบร้อยแล้ว";
		mes "เจ้าทำได้ดีมาก";
		mes "มันเป็นช่วงเวลาที่ดีสำหรับเราทุกคนที่ได้แบ่งปันและเพลิดเพลินกับโอกาสแห่งความสุขนี้ด้วยกัน ขอบใจมากนะ";
		next;
		mes "[Tribal Chief Paiko]";
		mes "ในระหว่างที่เจ้ากำลังเดินทางกลับจากการส่ง ^006400Satay^000000";
		mes "Jaty Crown ก็ได้ถูกสร้างจนเสร็จสมบูรณ์ บรรยากาศและรัศมีของมันทำให้มันเป็นมงกุฎที่ไร้ที่ติอย่างแท้จริง";
		next;
		mes "[Tribal Chief Paiko]";
		mes "นอกจากนี้ เจ้ายังได้นำวัตถุดิบมามากเกินพอ ดังนั้นหลังจากทำ ^006400Jaty Crown^000000 เสร็จแล้ว พวกเราเลยสามารถสร้างชิ้นที่คล้ายกันให้เจ้าเก็บไว้ได้ด้วยนะ";
		next;
		mes "[Tribal Chief Paiko]";
		mes "มันอาจจะไม่เหมือนกับของดั้งเดิม แต่มันมีความพิเศษในแบบของมันเอง มันคือรางวัลแทนคำขอบคุณจากข้าสำหรับสิ่งที่เจ้าได้ทำลงไป";
		next;
		if (checkweight(18520,1) == 0) {
			mes "[Tribal Chief Paiko]";
			mes "เจ้าน้ำหนักเกินกว่าจะรับรางวัลได้นะ";
			mes "โปรดลดน้ำหนักลงแล้วกลับมาใหม่อีกครั้ง";
			close;
		}
		mes "[Tribal Chief Paiko]";
		mes "ที่ทำด้วยสูตรลับการหมักของเผ่าเรา ลองกิน ^006400Satay^000000 ดูสิ";
		mes "มันถูกทำขึ้นมาด้วยหัวใจทั้งหมดของเรา โดยเฉพาะสำหรับเจ้า แต่ก็อย่าลืมแบ่งปันให้เพื่อนๆ ด้วยล่ะ";
		set dew_legend,17;
		getitem 18520,1; //Jaty_C
		getitem 11533,5; //Satay
		completequest 9165;
		erasequest 9165;
		next;
	}
	mes "[Tribal Chief Paiko]";
	mes "เจ้าทำได้ดีมาก";
	mes "ไม่ว่าเจ้าจะทำอะไรในอนาคต ข้าขอให้เจ้ามีชีวิตที่เต็มไปด้วยโชคลาภและความสุข";
	mes "ในฐานะหัวหน้าเผ่า ^8B4513Jaty Tribe^000000 ข้าขอให้เจ้าโชคดีที่สุด!!";
	close;
L_Delivery:
	mes "[Tribal Chief Paiko]";
	if (getarg(0) == 0) {
		mes "ดูเหมือนว่าเจ้าจะยังไม่ได้ส่ง ^006400Satay^000000 เลยนะ";
		mes "เจ้าเจอปัญหาอะไรหรือเปล่า?";
	} else {
		mes "เจ้ายังไม่ได้ส่ง ^006400Satay^000000 อีกรึ?";
		mes "มีอะไรผิดปกติไหม?";
	}
	next;
	switch(select("ข้ากินมันหมดเกลี้ยงเลยครับ/ค่ะ:ให้ส่งไปที่ใครนะครับ/คะ:ไม่มีอะไรครับ")) {
	case 1:
		mes "[Tribal Chief Paiko]";
		mes "หืม... สรุปคือเจ้าอดใจไม่ไหวล่ะสิ ฮึ?";
		mes "อย่าโทษตัวเองมากไปเลย มันยากที่จะต้านทานกลิ่นหอมที่น่าลุ่มหลงของ ^006400Satay^000000 จริงๆ นั่นแหละ";
		next;
		mes "[Tribal Chief Paiko]";
		mes "เผ่าของเราขาดแคลนเสบียงอาหาร ดังนั้นเราจึงไม่สามารถให้ ^006400Satay^000000 เพิ่มได้อีก แต่มีข่าวลือว่าเจ้าของร้านอาหารบนเกาะ Dewata กำลังทำและขาย ^006400Satay^000000 ของพวกเขาเองอยู่";
		next;
		mes "[Tribal Chief Paiko]";
		mes "มันอาจจะไม่อร่อยเท่าสูตรของเผ่าเรา แต่เนื่องจากนี่เป็นงานเร่งด่วน แค่ไปหามันมาแล้วนำไปส่งก็พอ";
		close;
	case 2:
		return;
	case 3:
		mes "[Tribal Chief Paiko]";
		mes "เอาล่ะ! ไปส่งเดี๋ยวนี้เลย!";
		close;
	}
}

dew_fild01,78,288,6	script	Jaty Tribe Warrior#dew	541,{
	mes "[Jaty Tribe Warrior]";
	mes "พวกเราเชิดชูความแข็งแกร่งและความกล้าหาญ";
	mes "พวกเราคือเผ่า ^FF0000Jaty^000000";
	if (dew_legend != 9 || !countitem(11533))
		close;
	next;
	mes "[Jaty Tribe Warrior]";
	mes "ดวงตาที่ผ่านการฝึกฝนของข้าเห็นว่า";
	mes "เจ้าดูเหมือนกำลังจะพูดบางอย่าง";
	mes "จงพูดมา และบอกธุระของเจ้าซะ";
	next;
	select("^0000FFTribal Chief Paiko^000000");
	mes "[Jaty Tribe Warrior]";
	mes "ข้าได้ยินเรื่องการกระทำที่เปี่ยมไปด้วยความช่วยเหลือของเจ้าแล้ว...";
	mes "ในฐานะตัวแทนของเผ่า";
	mes "ข้าขอกล่าวคำขอบคุณต่อเจ้า";
	next;
	mes "[Jaty Tribe Warrior]";
	mes "นอกจากนี้ ขอบคุณสำหรับ ^006400Satay^000000 นะ";
	mes "น้ำลายข้าสอแล้วล่ะ ขอบใจมาก";
	mes "มีบางอย่างที่ข้าอยากจะแบ่งปันให้เจ้าเกี่ยวกับเผ่าของเรา";
	delitem 11533,1; //Satay
	set dew_legend,10;
	changequest 9158,9159;
	next;
	mes "[Jaty Tribe Warrior]";
	mes "ไม่เหมือนในสมัยโบราณที่เหล่านักรบของเผ่าเราต้องต่อสู้และหลั่งเลือด โลกทุกวันนี้สงบสุขมากขึ้น แต่ดินแดนของเผ่าเราก็หดตัวลงอย่างมาก";
	next;
	mes "[Jaty Tribe Warrior]";
	mes "อย่างไรก็ตาม พวกเราจะยังคงรักษาประเพณีนักรบของเผ่าเราให้คงอยู่ต่อไป และเราจะถนอมความภาคภูมิใจที่เราได้รับผ่านการเสียสละของนักรบในอดีตเหล่านั้น";
	next;
	mes "[Jaty Tribe Warrior]";
	mes "ข้าหวังว่าความพากเพียรและความภาคภูมิใจของเจ้าจะคงอยู่กับเจ้าตลอดการเดินทาง";
	close;
}

dew_fild01,185,300,6	script	Tribe Manager#dew	541,{
	mes "[Tribe Manager]";
	mes "การรักษาวัฒนธรรมและประเพณีของเราเป็นสิ่งที่สำคัญมาก";
	if (dew_legend != 11 || !countitem(11533)) {
		if (dew_legend < 11)
			mes "ถึงแม้มันจะน่าเบื่อไปสักหน่อยก็เถอะ...";
		close;
	}
	next;
	mes "[Tribe Manager]";
	mes "ดูเหมือนว่าเจ้าจะมีบางอย่าง";
	mes "จะพูดกับข้าใช่ไหม?";
	next;
	select("^0000FFTribal Chief Paiko^000000");
	mes "[Tribe Manager]";
	mes "ข้าไม่เคยรู้เลยว่ามีการทุ่มเทแรงกายแรงใจเช่นนี้เกิดขึ้น";
	mes "และเจ้าก็มีบทบาทสำคัญในความพยายามนี้ด้วย...";
	mes "เจ้าคือฮีโร่ของ ^8B4513Jaty Tribe^000000 เลยนะ";
	next;
	mes "[Tribe Manager]";
	mes "ว้าว~ กลิ่นหอมฟุ้งของ";
	mes "^006400Satay^000000 โฮมเมดแสนอร่อย!!";
	mes "ขอบคุณสำหรับอาหารนะ";
	mes "ได้โปรดฝากความคิดถึงของข้า";
	mes "ไปถึงผู้นำเผ่าของเราด้วย";
	delitem 11533,1; //Satay
	set dew_legend,12;
	changequest 9160,9161;
	next;
	mes "[Tribe Manager]";
	mes "มีการติดต่อสื่อสารกันเพียงเล็กน้อยระหว่าง";
	mes "^8B4513Dewata Island^000000 และ ^8B4513Jaty tribe^000000";
	mes "พวกเราเคยมีสายสัมพันธ์ที่แน่นแฟ้นกว่านี้";
	mes "ในสมัยก่อน";
	next;
	mes "[Tribe Manager]";
	mes "แต่เมื่อมีวัฒนธรรมใหม่ๆ และชาวต่างชาติไหลเข้ามา";
	mes "^8B4513Dewata Island^000000 ก็กลายเป็นสถานที่ท่องเที่ยว และเราก็ได้ขาดการติดต่อกับพวกเขาไป";
	next;
	mes "[Tribe Manager]";
	mes "สถานที่ท่องเที่ยวขยายตัวอย่างต่อเนื่อง แต่เหตุผลที่มันยังไม่ส่งผลกระทบต่อเราที่นี่ ก็เป็นเพราะวัดเล็กๆ แห่งนี้ที่ข้ากำลังดูแลอยู่";
	next;
	mes "[Tribe Manager]";
	mes "ข้าไม่ยอมปล่อยให้ประเพณีของเราเลือนหายไปง่ายๆ แบบนั้นหรอก";
	mes "ข้าหวังว่าการรักษาประเพณีและวัฒนธรรมของเราให้คงอยู่ จะทำให้ผู้คนได้รับรู้ว่าสิ่งเหล่านี้สำคัญต่อเราเพียงใด";
	next;
	mes "[Tribe Manager]";
	mes "นับจากนี้ไป ข้าหวังว่าผู้คนจาก ^8B4513Dewata Island^000000 และ ^8B4513Jaty Tribe^000000 จะสามารถเข้ากันได้และมีปฏิสัมพันธ์กันบ่อยขึ้น";
	close;
}

//Original name: Gatekeeper of Krakatau Volcano
dewata,235,56,4	script	Gatekeeper of Krakatau#1	534,{
	mes "[Gatekeeper of Krakatau Volcano]";
	mes "^8B4513Krakatau Volcano^000000 เป็น";
	mes "พื้นที่ที่อันตรายมาก";
	mes "ข้าไม่สามารถปล่อยให้ใครเข้าไปในนั้นได้";
	next;
	if (dew_legend == 13 && countitem(11533))
		set .@menu$, ":^0000FFTribal Chief Paiko^000000";
	switch(select("ขอให้ข้าผ่านไปเถอะ:ข้าไม่อยากเข้าไปหรอก"+.@menu$)) {
	case 1:
		if (dew_legend < 8) {
			mes "[Gatekeeper of Krakatau Volcano]";
			mes "^8B4513Krakatau Volcano^000000 เป็น";
			mes "พื้นที่ที่อันตรายมาก";
			mes "ดังนั้นข้าอนุญาตให้เจ้าเข้าไม่ได้";
			mes "เว้นแต่เจ้าจะมีคุณสมบัติเพียงพอ";
			close;
		}
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "เจ้าคือนักเดินทางที่ได้รับการยอมรับจากผู้นำเผ่าในฐานะเพื่อนของเผ่า และได้รับ ^006400Shining Cendrawasih Feather^000000";
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "เอาล่ะ ข้าจะให้เจ้าผ่านไปได้ แต่ ^8B4513Krakatau Volcano^000000 นั้นอันตรายมาก ดังนั้นโปรดระวังตัวด้วย";
		close2;
		warp "dew_dun01",289,160;
		end;
	case 2:
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "เจ้าเลือกได้ฉลาดแล้ว";
		mes "^8B4513Krakatau Volcano^000000 เป็น";
		mes "พื้นที่ที่อันตรายสุดๆ";
		close;
	case 3:
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "โอ้! ข้าไม่เคยรู้เรื่องนั้นเลย!";
		mes "นี่เป็นข่าวดีที่สุดชิ้นหนึ่ง";
		mes "ที่ข้าได้ยินมาเมื่อเร็วๆ นี้เลยนะ";
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "อืม กลิ่นที่ยอดเยี่ยมนี้คืออะไรกัน?";
		mes "แน่นอน มันคือ ^006400Satay^000000 ของเผ่าเรานี่เอง!";
		mes "ข้ากำลังหิวนิดๆ อยู่พอดี";
		mes "ดังนั้นสิ่งนี้จะช่วยให้ข้าอิ่มท้องได้";
		delitem 11533,1; //Satay
		set dew_legend,14;
		changequest 9162,9163;
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "ในเมื่อเจ้าอุตส่าห์มาถึงที่นี่แล้ว";
		mes "ข้าจะเล่าเรื่องที่น่าสนใจให้เจ้าฟัง";
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "มันกลายเป็นประเพณีที่สูญหายไปในช่วงไม่กี่ปีมานี้ แต่เมื่อประมาณ 10 ปีก่อนหรือมากกว่านั้น ในการที่จะกลายเป็นนักรบแห่ง ^8B4513Jaty Tribe^000000 เจ้าจะต้องปีนภูเขาไฟ Krakatau เพื่อพิสูจน์ความอดทนของเจ้า";
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "แววตาของนักรบเหล่านั้นมีความกังวลแต่ก็มุ่งมั่นก่อนที่จะขึ้นไปบน ^8B4513Krakatau Volcano^000000";
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "ข้ายังคงลืมแววตาเหล่านั้นไม่ได้ และข้าสัมผัสได้ถึงอารมณ์ที่คล้ายกันจากตัวเจ้า นักผจญภัย";
		next;
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "ข้าอยากให้เจ้ามีชีวิตที่ยืนยาวและรุ่งเรือง";
		mes "ขอให้มีสุขภาพดีและปลอดภัยนะผู้เดินทาง เพื่อที่เจ้าจะได้มีชีวิตอยู่บอกเล่าเรื่องราวการผจญภัยของเจ้าต่อไป";
		close;
	}
}

//Original name: Gatekeeper of Krakatau Volcano
dew_dun01,292,164,4	script	Gatekeeper of Krakatau#2	541,{
	mes "[Gatekeeper of Krakatau Volcano]";
	mes "ต้องการกลับไปยัง";
	mes "Dewata ไหม?";
	next;
	switch(select("ใช่:ไม่")) {
	case 1:
		close2;
		warp "dewata",232,53;
		end;
	case 2:
		mes "[Gatekeeper of Krakatau Volcano]";
		mes "^8B4513Krakatau Volcano^000000 เป็น";
		mes "สถานที่ที่อันตราย";
		mes "เจ้าควรจะกลับไปเดี๋ยวนี้เลยนะ";
		close;
	}
}

dewata,78,192,6	script	Borobudur Temple Manager	536,{
	mes "[Borobudur Temple Manager]";
	mes "อากาศวันนี้ช่างสดใสเหลือเกิน";
	if (dew_legend != 15 || !countitem(11533))
		close;
	next;
	mes "[Borobudur Temple Manager]";
	mes "ว่าแต่ คุณมีอะไร...";
	mes "จะบอกฉันหรือเปล่า?";
	next;
	select("^0000FFTribal Chief Paiko^000000");
	mes "[Borobudur Temple Manager]";
	mes "ฉันไม่เคยรู้เลยว่ามีเรื่องแบบนั้นเกิดขึ้นด้วย";
	mes "ถ้าคุณบอกฉันล่วงหน้า ฉันคงจะช่วยอะไรได้บ้างไม่มากก็น้อย";
	mes "แต่ถึงอย่างนั้น ทุกอย่างก็ผ่านพ้นไปด้วยดีเพราะความช่วยเหลือของคุณ";
	next;
	mes "[Borobudur Temple Manager]";
	mes "อ๊ะ นี่มัน ^006400Satay^00000 ที่ฉันเคยชิมตอนที่คนในเผ่าเชิญฉันไปเมื่อคราวก่อนนี่นา";
	next;
	mes "[Borobudur Temple Manager]";
	mes "ดีจัง~~ นี่คือรสชาติเดียวกับที่ฉันจำได้เลย";
	mes "เพื่อเป็นการขอบคุณ ฉันจะเล่าเรื่องที่น่าสนใจให้คุณฟังแล้วกันนะ";
	delitem 11533,1; //Satay
	set dew_legend,16;
	changequest 9164,9165;
	next;
	mes "[Borobudur Temple Manager]";
	mes "ตำนานเล่าถึงนกประหลาดที่เรียกว่า ^FF0000Garuda^000000";
	mes "มันมีลำตัว แขน และขาเหมือนมนุษย์ แต่มีหัวและจะงอยปากเหมือนนกอินทรี";
	next;
	mes "[Borobudur Temple Manager]";
	mes "เรื่องราวของนกตัวนี้กล่าวว่ามันช่วยเหล่าทัพเทพเจ้ากำจัดปิศาจและอสรพิษที่ชั่วร้าย";
	next;
	mes "[Borobudur Temple Manager]";
	mes "เรื่องเล่านี้ถูกส่งต่อกันมาแบบรุ่นสู่รุ่น";
	mes "น่าสนใจมากเลยใช่ไหมล่ะ?";
	next;
	mes "[Borobudur Temple Manager]";
	mes "ฉันแน่ใจว่าในหมู่บ้านของ ^8B4513Jaty Tribe^000000 จะต้องมีเรื่องเล่าในตำนานอีกมากมายแน่ๆ";
	mes "เมื่อฉันได้พบกับหัวหน้าเผ่า ฉันจะต้องได้ฟังเรื่องราวแบบนั้นอีกเยอะแน่นอน";
	next;
	mes "[Borobudur Temple Manager]";
	mes "ฉันไม่แน่ใจว่าเราจะได้พบกันอีกเมื่อไหร่ แต่หวังว่าสักวันเราจะได้เจอกันอีกนะ";
	mes "ขอให้เป็นการเดินทางที่รื่นรมย์นะ นักเดินทางรุ่นเยาว์";
	close;
}

// Help Out the Old Man :: dewata_drink
//============================================================
dewata,109,262,4	script	Memo#dew_drink	534,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 5000) {
		if (dew_drink) {
			mes "[Memo]";
			mes "ทำไมคุณถึงแบกของเยอะขนาดนั้น? ไม่ปวดหลังบ้างเหรอ?";
			close;
		} else {
			mes "[Memo]";
			mes "เดี๋ยวนี้วัยรุ่นเขานิยมแบกของหนักๆ กันเหรอ?";
			close;
		}
	}
	set .@playtime, checkquest(5044,PLAYTIME);
	if (.@playtime == 0) {
		mes "[Memo]";
		mes "พรุ่งนี้จะมาช่วยฉันอีกไหม?";
		mes "Poby ยังปวดหลังอยู่เลย";
		mes "อาการปวดหลังน่ะไม่ได้หายไปภายในวันเดียวหรอกนะ";
		close;
	} else if (.@playtime == 1) {
		erasequest 5044;
	} else if (.@playtime == 2) {
		completequest 5044;
		erasequest 5044;
	}
	if (BaseLevel < 60) {
		mes "[Memo]";
		mes "Dewata มักจะพลุกพล่านเสมอ แต่วันนี้เงียบเหงาจัง";
		close;
	}
	if (dew_drink == 0) {
		if (rand(3)) {
			mes "[Memo]";
			mes "พับผ่าสิ! เรายังไม่ได้รับข้อความจากเผ่า Jaty เลย";
			emotion ET_PROFUSELY_SWEAT;
			next;
			mes "[Memo]";
			mes "พวกเขาไม่เคยผิดสัญญาเลยตลอด 10 ปี ฉันไม่รู้สาเหตุเลยว่าเกิดอะไรขึ้น?";
			next;
			mes "[Memo]";
			mes "ของเหลวทั้งหมดอาจจะเสียได้ถ้ามันยังเป็นแบบนี้ต่อไป";
			emotion ET_CRY;
			close;
		}
		mes "[Memo]";
		mes "ฉันควรทำยังไงดี? ยังไม่มีข่าวคราวจากเผ่า Jaty เลย";
		emotion ET_PROFUSELY_SWEAT;
		next;
		mes "[Memo]";
		mes "โอ้ ไม่นะ~ ของเหลวพวกนี้กำลังจะเสียแล้ว...";
		next;
		select("เกิดอะไรขึ้นเหรอ?");
		mes "[Memo]";
		mes "คุณเป็นใครน่ะ?";
		emotion ET_QUESTION;
		next;
		mes "[Memo]";
		mes "คุณเป็นนักผจญภัยใช่ไหม?";
		mes "อย่างที่คุณเห็นจากไหน้ำรอบตัวฉัน ฉันทำ ^0000FFเหล้าพื้นเมือง^000000 เลี้ยงชีพน่ะ";
		next;
		mes "[Memo]";
		mes "ใครๆ ก็รู้เรื่องนี้ดี ดังนั้น ^0000FFเกาะ Dewata^000000 เลยดึงดูดนักท่องเที่ยวได้มากมาย";
		next;
		mes "[Memo]";
		mes "โดยเฉพาะเหล้าพื้นเมืองสูตรนี้ที่สืบทอดมาถึงสี่รุ่น! แต่ตอนนี้ ^FF0000Ark^00000 ขาดแคลนวัตถุดิบ";
		mes "Ark จำเป็นต้องใช้ ^0000FFPalm Oils^000000 ซึ่งทำมาจากลูกปาล์ม (Oil Palm Fruit)";
		next;
		mes "[Memo]";
		mes "โชคร้ายที่ช่วงนี้เราไม่ได้รับ ^0000FFPalm Oil^000000 จากหมู่บ้านเผ่า Jaty เลย";
		mes "ส่วนผสมพวกนี้อาจจะไร้ค่าไปเลยถ้ามันส่งมาสายเกินไป";
		next;
		select("ฟังดูเป็นปัญหาใหญ่เลยนะนั่น");
		mes "[Memo]";
		mes "มันเป็นปัญหามากกว่าที่คุณคิดซะอีก การขายของเราเป็นระบบเครดิต แต่ถ้าวันนี้เราทำส่งไม่ทัน เราจะเสียทั้งเครดิต เสียทั้งวัตถุดิบ และนักท่องเที่ยวก็จะไม่ได้กิน Ark กันพอดี ซึ่งมันเสียหายมาก";
		next;
		mes "[Memo]";
		mes "... ...";
		emotion ET_THINK;
		next;
		emotion ET_SURPRISE;
		mes "[Memo]";
		mes "อ๊ะ จริงด้วย! ตอนนี้คุณยุ่งอยู่หรือเปล่า?";
		next;
		if(select("ขอโทษที ฉันยุ่งอยู่น่ะ:ไม่หรอก ฉันไม่ยุ่ง") == 1) {
			mes "[Memo]";
			mes "อืม ฉันไม่มีทางเลือกสินะ";
			mes "คุณไม่สนเลยว่าคนอื่นจะลำบาก โลกใบนี้มันกลายเป็นแบบนี้ไปตั้งแต่เมื่อไหร่กัน?";
			emotion ET_HNG;
			close;
		}
		mes "[Memo]";
		mes "ดีจังที่ได้ยินแบบนั้น";
		mes "ในเมื่อคุณก็เดินเที่ยวเตร่อยู่แถวนี้...";
		next;
		mes "[Memo]";
		mes "คุณจะรังเกียจไหมถ้าฉันจะขอให้ช่วยอะไรหน่อย~? จริงๆ ฉันต้องไปที่หมู่บ้านเผ่า Jaty แต่เจ้า ^FF0000Ark^00000 ที่ยังบ่มไม่ได้ที่พวกนี้ทำให้ฉันทิ้งไปไหนไม่ได้เลย";
		next;
		mes "[Memo]";
		mes "คุณช่วยไปที่หมู่บ้าน Jaty แล้วถาม ^0000FFLero^00000 หน่อยได้ไหมว่าเกิดอะไรขึ้น?";
		next;
		mes "[Memo]";
		mes "หมอนั่นไม่เคยผิดสัญญาเลยตลอด 10 ปี เขาต้องมีเหตุผลจำเป็นจริงๆ แน่ที่มาไม่ได้";
		set dew_drink,1;
		setquest 5035;
		next;
		mes "[Memo]";
		mes "ตามหาปรมาจารย์ด้านน้ำมัน";
		mes "ชื่อของเขาคือ ^0000FFLero^00000";
		close;
	} else if (dew_drink < 5) {
		mes "[Memo]";
		mes "สรุปว่า Lero ว่ายังไงบ้าง?";
		next;
		mes "[Memo]";
		mes "... ... ... ...";
		emotion ET_THINK;
		next;
		mes "[Memo]";
		mes "คุณยังหาไม่เจออีกเหรอว่าเกิดอะไรขึ้น?";
		next;
		mes "[Memo]";
		mes "ไปหาปรมาจารย์ด้านน้ำมัน ชื่อของเขาคือ ^0000FFLero^00000 นะ";
		close;
	} else if (dew_drink == 5) {
		mes "[Memo]";
		mes "สรุปว่า Lero ว่ายังไงบ้าง?";
		next;
		select("ต้นปาล์ม... กลายเป็น");
		mes "- คุณอธิบายสถานการณ์ให้คุณ Memo ฟัง -";
		next;
		mes "[Memo]";
		mes "Lero ฝากคำขอโทษมางั้นเหรอ?";
		mes "อืม ไม่เป็นไรหรอก";
		next;
		mes "[Memo]";
		mes "นั่นคงจะเป็น ^0000FFPoby^00000 สินะ ถ้าเขาบาดเจ็บ นั่นแหละคือเหตุผลที่ไม่มีข่าวส่งมาเลย Lero น่ะเป็นห่วงเขามากจริงๆ...";
		next;
		mes "[Memo]";
		mes "มันอาจจะยุ่งยากหน่อยนะ แต่ช่วยกลับไปที่หมู่บ้าน Jaty อีกทีเถอะ Lero ต้องทำน้ำมันให้เสร็จนะ";
		next;
		mes "[Memo]";
		mes "ถ้ายังไม่เสร็จ ก็ให้รอที่นั่นแล้วนำ ^FF0000น้ำมัน 30 ขวด^00000 มาให้ฉัน";
		set dew_drink,6;
		changequest 5040,5041;
		close;
	} else if (dew_drink == 6) {
		mes "[Memo]";
		mes "มันอาจจะยุ่งยากหน่อยนะ แต่ช่วยกลับไปที่หมู่บ้าน Jaty อีกทีเถอะ Lero ต้องทำน้ำมันให้เสร็จนะ";
		next;
		mes "[Memo]";
		mes "ถ้ายังไม่เสร็จ ก็ให้รอที่นั่นแล้วนำ ^FF0000น้ำมัน 30 ขวด^00000 มาให้ฉัน";
		close;
	} else if (dew_drink == 7) {
		if (countitem(6401) < 30) {
			mes "[Memo]";
			mes "อ้าว ฉันไม่ได้บอกให้คุณเอาน้ำมันมา 30 ขวดหรอกเหรอ? ลืมไปแล้วเหรอ?";
			next;
			mes "[Memo]";
			mes "กลับไปหา Lero แล้วเอามาเพิ่มซะ ถ้ามันเป็นความผิดพลาดของคุณ เขาก็คงจะให้เพิ่มแหละ";
			close;
		}
		mes "[Memo]";
		mes "คุณเอาน้ำมันมาแล้ว เยี่ยมมาก ขอบใจนะ";
		next;
		mes "[Memo]";
		mes "ขอโทษทีนะ พอดีฉันมีเรื่องด่วนต้องไปจัดการ คุณช่วยรอสักพักแล้วค่อยมาคุยกับฉันตอนฉันเสร็จธุระได้ไหม?";
		delitem 6401,30; //Palm_O
		changequest 5042,5043;
		set dew_drink,8;
		close;
	} else if (dew_drink == 8) {
		if (checkquest(5043,PLAYTIME) == 0) {
			mes "[Memo]";
			mes "... ... ...";
			emotion ET_PROFUSELY_SWEAT;
			next;
			mes "- Memo กำลังวุ่นอยู่กับการทำ Ark -";
			close;
		} else if (checkquest(5043,PLAYTIME) == -1) {
			mes "- มีข้อผิดพลาดในเควส -";
			close;
		}
		mes "[Memo]";
		mes "ฉันทำให้คุณรอนานไปหรือเปล่า?";
		next;
		mes "[Memo]";
		mes "ครอบครัวของฉันทำ Ark มาสี่รุ่นแล้ว";
		next;
		mes "[Memo]";
		mes "คุณปู่ของฉันเคยอยู่อย่างยากลำบากเพราะตอนนั้นยอดขายไม่ค่อยดีนัก";
		next;
		mes "[Memo]";
		mes "คนบนเกาะ Dewata ไม่ค่อยดื่มของพวกนี้ในสภาพอากาศร้อนๆ หรอก";
		next;
		mes "[Memo]";
		mes "ตอนฉันยังเด็ก Dewata ถูกพัฒนาให้เป็นแหล่งท่องเที่ยว และนั่นคือตอนที่ ^FF00FFแพ็กเกจทัวร์^000000 Ark เริ่มขายดีเป็นเทน้ำเทท่า";
		next;
		mes "[Memo]";
		mes "เหล้าที่ดีต้องใช้น้ำมันที่สดใหม่ ฉันทำเหล้ามาสิบปีแล้วและส่งของได้ตรงเวลาเสมอ";
		next;
		mes "[Memo]";
		mes "เพราะน้ำมันของวันนี้มาไม่ตรงเวลา ฉันเลยคิดว่าฉันอาจจะเจองานหนักเข้าให้แล้ว";
		next;
		mes "[Memo]";
		mes "แต่คุณก็ได้ช่วยให้ฉันทำเหล้าออกมาได้อย่างถูกต้อง";
		next;
		mes "[Memo]";
		mes "มันมีสุภาษิตที่ว่า ^0000FF'Mengatakan mudah, mengerjakan susah.'^000000 ซึ่งหมายความว่า พูดน่ะมันง่าย แต่ทำน่ะมันยาก";
		next;
		mes "[Memo]";
		mes "พูดอีกอย่างก็คือ มันง่ายที่จะพูดว่าจะช่วยคนอื่น แต่มันยากที่จะลงมือทำจริงๆ";
		next;
		emotion ET_THANKS;
		mes "[Memo]";
		mes "คุณทำได้ดีมากจริงๆ ฉันชอบคุณนะ เพราะคุณแตกต่างจากพวกวัยรุ่นสมัยนี้";
		changequest 5043,5044;
		set dew_drink,10;
		getexp 50000,10000;
		getitem 617,1; //Old_Violet_Box
		next;
		mes "[Memo]";
		mes "คุณจะช่วยงานฉันเป็นครั้งคราวได้ไหมล่ะ?";
		mes "ฉันจะจ่ายค่าตอบแทนสำหรับสิ่งที่คุณทำให้ฉันแน่นอน";
		close;
	} else if (dew_drink == 10) {
		for(set .@i,0; .@i<7; set .@i,.@i+1)
			if (checkquest(5045+.@i) > -1)
				set .@j, .@j | (1<<.@i);
		if (.@j == 0) {
			mes "[Memo]";
			mes strcharinfo(0)+", ลมอะไรหอบคุณมาที่นี่ล่ะ?";
			next;
			switch(select("แค่แวะมาทักทายน่ะ:ฉันจะมาช่วยงานครับ/ค่ะ")) {
			case 1:
				mes "[Memo]";
				mes "ฉันเดาว่าคุณคงยุ่ง";
				mes "ถ้าเป็นไปได้ ช่วยไปเอาน้ำมันจาก Lero ให้หน่อยจะได้ไหม? ฉันจะขอบคุณมาก...";
				close;
			case 2:
				mes "[Memo]";
				mes "ขอบใจนะ ฉันรู้ว่าคุณยุ่งและการช่วยงานฉันมันก็น่าเบื่อ";
				next;
				mes "[Memo]";
				mes "คุณช่วยไปช่วย Lero แล้วเอาน้ำมันปาล์มกลับมาให้หน่อยได้ไหม...?";
				setquest 5045;
				close;
			}
		} else if ((.@j & 1) || (.@j & 2) || (.@j & 8) || (.@j & 32)) {
			mes "[Memo]";
			mes "คุณช่วยไปช่วย Lero แล้วเอาน้ำมันปาล์มกลับมาให้หน่อยได้ไหม...?";
			close;
		} else if ((.@j & 4) || (.@j & 16) || (.@j & 64)) {

			// .@i[]: <required amount>,<quest ID>,<reward amount>
			if (.@j & 4)
				setarray .@i[0],10,5047,1;
			else if (.@j & 16)
				setarray .@i[0],15,5049,2;
			else
				setarray .@i[0],20,5051,3;

			if (countitem(6401) < .@i[0]) {
				mes "[Memo]";
				mes "คุณเอาน้ำมันมาไม่พอหรอกนะ";
				next;
				mes "... ... ... ...";
				next;
				mes "[Memo]";
				mes "Poby ติดค้างฉันอยู่สองสามอย่าง เขาจะยอมให้คุณเองแหละ";
				close;
			}
			mes "[Memo]";
			mes "โอ้ คุณเอาน้ำมันมาแล้ว ทำดีมาก";
			next;
			mes "[Memo]";
			mes "คุณคงจะลำบากแย่ที่ต้องแบกน้ำมันมา เอ้านี่ รับเครื่องดื่มนี่ไปสิ";
			delitem 6401,.@i[0]; //Palm_O
			erasequest .@i[1];
			setquest 5044;
			getitem 11534,.@i[2]; //Coco_Juice
			next;
			switch(rand(4)) {
			case 0:
				mes "[Memo]";
				mes "Ark น่ะดังในหมู่นักท่องเที่ยวมาก มีผู้ชายคนหนึ่งซื้อไปตั้งหลายขวดด้วยตัวคนเดียวเลยล่ะ";
				next;
				mes "[Memo]";
				mes "กังวลว่าเขาจะดื่มคนเดียวงั้นเหรอ? การดื่มมากเกินไปมันก็กลายเป็นยาพิษได้เสมอแหละ";
				break;
			case 1:
				mes "[Memo]";
				mes "พระอาทิตย์ขึ้นทางทิศตะวันออกเสมอ ความขี้เกียจที่เป็นที่เลื่องลือก็มักจะตื่นแต่เช้าตรู่";
				next;
				mes "[Memo]";
				mes "เขาตื่นมาพบความจริงหรือยังนะ? เขาเป็นคนดีนะ แต่เสียอย่างเดียวคือขี้เกียจ";
				break;
			case 2:
				mes "[Memo]";
				mes "การเริ่มต้นที่ดีคือชัยชนะไปกว่าครึ่ง";
				next;
				mes "[Memo]";
				mes "ฉันคิดว่าคุณถูกกำหนดมาให้ทำสิ่งที่ยิ่งใหญ่นะ";
				break;
			case 3:
				mes "[Memo]";
				mes "มีคนกล่าวว่าพูดไปสองไพเบี้ย ดูเหมือนว่าคำพูดนั้นจะง่ายแต่ความจริงมันไม่ใช่เลย";
				next;
				mes "[Memo]";
				mes "ฉันเชื่อว่าคุณจะรักษาคำพูดของคุณนะ";
				break;
			}
			next;
			mes "[Memo]";
			mes "ฉันอยากให้คุณช่วยงานฉันในครั้งนี้";
			next;
			mes "[Memo]";
			mes "Poby ยังปวดหลังอยู่ และคงต้องใช้เวลาอีกหลายวันกว่าเขาจะฟื้นตัว";
			close;
		}
	} else { // Should never happen.
		mes "[Memo]";
		mes "เกาะ Dewata มักจะพลุกพล่านเสมอ แต่วันนี้เงียบเหงาจัง";
		close;
	}
}

dew_fild01,114,275,4	script	Lero#dew_drink	536,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 5000) {
		mes "[Lero]";
		mes "คุณแบกของเยอะเกินไปแล้ว...";
		close;
	}
	if (dew_drink == 1) {
		mes "[Lero]";
		mes "โอ้ ไม่นะ! ฉันมีออเดอร์เยอะมาก แต่วัตถุดิบกำลังจะหมดแล้ว";
		mes "ยังไม่มาอีกเหรอ? เกิดอะไรขึ้นเนี่ย Poby? เขาหายไปไหนของเขานะ?";
		emotion ET_FRET;
		next;
		select("ขอประทานโทษครับ/ค่ะ ท่านคือ Lero ใช่ไหม?");
		mes "[Lero]";
		mes "ถ้า Poby กลับมานะ ฉันจะสั่งสอนเขาให้ลืมไม่ลงเลยคอยดู!";
		emotion ET_ANGER;
		next;
		mes "- Lero ดูท่าทางจะโกรธใครบางคนมากและเมินคุณ -";
		next;
		mes "[Lero]";
		mes "อุ๊ย คุณเป็นใครน่ะ?";
		emotion ET_QUESTION;
		next;
		select("คุณ Memo ส่งฉันมาที่นี่ครับ/ค่ะ");
		mes "[Lero]";
		mes "อ๊ะ จริงด้วย! ฉันลืมเรื่องของ Memo ไปเลย เพราะคนงานที่เก็บถั่ว (ลูกปาล์ม) ยังไม่กลับมาเลย ขอโทษทีนะ";
		next;
		mes "[Lero]";
		mes "การจะทำน้ำมันปาล์มให้สดใหม่ ฉันต้องใช้ผลปาล์มที่สดๆ แล้วฉันก็จะสกัดน้ำมันสดๆ ออกมาจากผลไม้ที่สดใหม่เหล่านั้นได้";
		next;
		mes "[Lero]";
		mes "บอกตามตรงนะ คุณ Memo น่ะจู้จี้มาก เขาจะไม่ยอมใช้น้ำมันที่ไม่สดเด็ดขาด";
		next;
		mes "[Lero]";
		mes "เพื่อให้น้ำมันสดใหม่ เขาเลยส่งคนงานไปที่ป่าต้นปาล์มเสมอ แต่... พวกที่ส่งไปตั้งแต่เช้ามืดยังไม่กลับมากันเลย";
		next;
		mes "[Lero]";
		mes "ตอนแรกฉันก็นึกว่าเขาขี้เกียจ ซึ่งมันทำให้ฉันหงุดหงิดมาก";
		mes "คุณช่วยไปตามหาคนงานในป่าต้นปาล์มหน่อยได้ไหม?";
		next;
		select("ได้ครับ/ค่ะ ฉันจะไปตามหาพวกเขาเอง");
		mes "[Lero]";
		mes "ขอบคุณมากนะ";
		mes "ไปหา Poby ซะ";
		next;
		mes "[Lero]";
		mes "Poby น่าจะอยู่ที่ ^0000FFPalm Forest^000000 ผลผลิตที่ดีที่สุดจะอยู่ที่ทางตอนใต้ของป่านะ";
		set dew_drink,2;
		changequest 5035,5036;
		close;
	} else if (dew_drink == 2) {
		mes "[Lero]";
		mes "คุณเจอ Poby หรือยัง?";
		emotion ET_QUESTION;
		next;
		mes "[Lero]";
		mes "... ... ...";
		next;
		mes "[Lero]";
		mes "โอ้ คุณยังไม่ได้ไปทางตะวันออกของ ^0000FFPalm Forest^000000 เลยนี่นา ฉันเริ่มกังวลแล้วนะ";
		close;
	} else if (dew_drink == 3) {
		mes "[Lero]";
		mes "คุณกลับมาแล้ว... แล้ว... Poby ล่ะ...";
		mes "Poby เป็นยังไงบ้าง?";
		emotion ET_QUESTION;
		next;
		select("คือว่า Poby.....");
		mes "[Lero]";
		mes "อ๊ะ! ฉันไม่รู้เรื่องนั้นเลย ฉันคอยบอก Poby เสมอว่า ^0000FF'Kemarau setahun rusak sehari.'^000000";
		next;
		mes "[Lero]";
		mes "เขาปีนต้นไม้โดยไม่มีอุปกรณ์ความปลอดภัยเลย เขาอาจจะตกลงมาบาดเจ็บได้";
		next;
		mes "[Lero]";
		mes "อืม... ขอโทษทีนะ ตอนนี้ฉันยังทำน้ำมันปาล์มให้ไม่ได้หรอก";
		next;
		mes "[Lero]";
		mes "ช่วยไปบอกคุณ Memo หน่อยได้ไหมว่ามันเป็นไปไม่ได้ที่จะทำน้ำมันตอนนี้เพราะวัตถุดิบขาดแคลน?";
		mes "ช่างฝีมือที่ไหนก็สร้างอะไรไม่ได้หรอกถ้าไม่มีวัตถุดิบ";
		next;
		select("ให้ฉันไปหาวัตถุดิบให้ไหมล่ะ?");
		mes "[Lero]";
		mes "อ๊ะ นั่นอาจจะเป็นวิธีที่ดีนะ แต่การเก็บถั่วน่ะมันยากสำหรับมือใหม่นะ";
		next;
		mes "[Lero]";
		mes "หืม!!!";
		mes "ฉันมีความคิดดีๆ แล้ว";
		next;
		mes "[Lero]";
		mes "คุณลองไปถาม Poby เกี่ยวกับวิธีเก็บถั่วที่ดีที่สุดตรงจุดที่คุณเจอเขาในป่าดูสิ เขาควรจะสอนเทคนิคให้คุณได้นะ";
		set dew_drink,4;
		changequest 5037,5038;
		next;
		mes "[Lero]";
		mes "ถึงเขาจะขี้เกียจและค่อนข้างสะเพร่า แต่เขาก็เป็นคนงานที่เก่งที่สุดเลยนะ";
		close;
	} else if (dew_drink == 4) {
		if (checkquest(5038) > -1) {
			mes "[Lero]";
			mes "คุณลองไปถาม Poby เกี่ยวกับวิธีเก็บถั่วที่ดีที่สุดตรงจุดที่คุณเจอเขาในป่าดูสิ เขาควรจะสอนเทคนิคให้คุณได้นะ";
			next;
			mes "[Lero]";
			mes "ถึงเขาจะขี้เกียจและค่อนข้างสะเพร่า แต่เขาก็เป็นคนงานที่เก่งที่สุดเลยนะ";
			close;
		} else if (checkquest(5039) > -1) {
			if (countitem(6402) < 60) {
				mes "[Lero]";
				mes "คุณเก็บถั่วมาครบแล้วเหรอ?";
				next;
				mes "[Lero]";
				mes "หนึ่ง, สอง...";
				next;
				mes "[Lero]";
				mes "นี่มันไม่ครบตามจำนวนที่ฉันสั่ง Poby ไว้นี่นา";
				next;
				mes "[Lero]";
				mes "มันจะลำบากเอานะถ้ามันดีเลย์ไปมากกว่านี้ รีบไปเอามาให้ครบ 60 ชิ้นเร็วเข้า";
				close;
			}
			mes "[Lero]";
			mes "หนึ่ง, สอง, สาม, สี่, . . . .";
			next;
			mes "[Lero]";
			mes "ครบ 60 ชิ้นพอดีเป๊ะ ฉันจะสกัดน้ำมันให้เดี๋ยวนี้แหละ";
			next;
			emotion ET_SORRY;
			mes "[Lero]";
			mes "มันต้องใช้เวลาสักหน่อยนะ ดังนั้นคุณกลับไปหาคุณ Memo แล้วบอกสถานการณ์ให้เขาทราบที แล้วบอกเขาด้วยว่าฉันขอโทษที่ส่งของล่าช้า";
			delitem 6402,60; //Oil_Palm_F
			set dew_drink,5;
			changequest 5039,5040;
			close;
		}
	} else if (dew_drink == 5) {
		mes "[Lero]";
		mes "มันต้องใช้เวลาสักหน่อยนะ ดังนั้นคุณกลับไปหาคุณ Memo แล้วบอกสถานการณ์ให้เขาทราบที แล้วบอกเขาด้วยว่าฉันขอโทษที่ส่งของล่าช้า";
		close;
	} else if (dew_drink == 6) {
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 15000) {
			mes "[Lero]";
			mes "ตอนนี้คุณแบกของหนักเกินกว่าจะรับอะไรเพิ่มได้แล้วนะ";
			close;
		}
		mes "[Lero]";
		mes "คุณมาได้ถูกเวลาพอดีเลย ฉันเพิ่งทำน้ำมันต้นปาล์มเสร็จเมื่อกี้เอง";
		next;
		mes "[Lero]";
		mes "มันหนักกว่าที่คุณคิดนะ ระวังตอนแบกไปด้วยล่ะ";
		next;
		emotion ET_SORRY;
		mes "[Lero]";
		mes "อ้อ ฝากบอกคุณ Memo ด้วยว่าฉันขอโทษที่ส่งของสาย และอธิบายเขาด้วยว่ามันไม่ใช่ความผิดของฉันนะ";
		changequest 5041,5042;
		set dew_drink,7;
		getitem 6401,30; //Palm_O
		close;
	} else if (dew_drink == 7) {
		if (checkquest(5042) > -1 && countitem(6401) > 29) {
			mes "[Lero]";
			mes "มันหนักกว่าที่คุณคิดนะ ระวังตอนแบกไปด้วยล่ะ";
			next;
			emotion ET_SORRY;
			mes "[Lero]";
			mes "อ้อ ฝากบอกคุณ Memo ด้วยว่าฉันขอโทษที่ส่งของสาย และอธิบายเขาด้วยว่ามันไม่ใช่ความผิดของฉันนะ";
			close;
		} else {
			set .@num, 30 - countitem(6401);
			if (checkweight(6401,.@num) == 0) {
				mes "[Lero]";
				mes "ตอนนี้คุณแบกของหนักเกินกว่าจะรับอะไรเพิ่มได้แล้วนะ";
				close;
			}
			mes "[Lero]";
			mes "ฉันจำได้แม่นว่าให้พวงน้ำมันปาล์มคุณไป 30 ขวดนะ นี่คุณทำหายหมดเลยเหรอ?";
			next;
			select("ขอโทษครับ/ค่ะ มันเป็นความผิดของฉันเอง...");
			emotion ET_FRET;
			mes "[Lero]";
			mes "นี่คือน้ำมันปาล์มที่กำลังขาดแคลนอยู่นะ ระวังหน่อยสิ กว่าจะได้มามันยากลำบากมากเลยนะ...";
			getitem 6401,.@num; //Palm_O
			close;
		}
	} else if (dew_drink == 8) {
		mes "[Lero]";
		mes "คุณ Memo ว่ายังไงบ้าง?";
		emotion ET_QUESTION;
		next;
		mes "["+strcharinfo(0)+"]";
		emotion ET_BLABLA, playerattached();
		mes "... ... ... ...";
		next;
		mes "[Lero]";
		mes "อ้อ... ฉันเข้าใจแล้ว...";
		emotion ET_OK;
		close;
	} else if (dew_drink == 10) {
		for(set .@i,0; .@i<7; set .@i,.@i+1)
			if (checkquest(5045+.@i) > -1)
				set .@j, .@j | (1<<.@i);
		if (.@j == 0) {
			mes "[Lero]";
			mes "เกิดอะไรขึ้นเหรอ? ฉันไม่เห็นได้รับโน้ตจากคุณ Memo เลยว่าเขาจะส่งใครมาที่นี่";
			close;
		} else if (.@j & 1) {
			mes "[Lero]";
			mes "เฮ้ ฉันรอคุณอยู่เลย ฉันไม่คิดเลยว่าอาการบาดเจ็บของ Poby จะทำให้ชีวิตฉันลำบากขนาดนี้";
			next;
			set .@quest,rand(3);
			mes "[Lero]";
			mes "ฉันก็ละอายใจนะที่ต้องขอให้คุณช่วย แต่คุณช่วยนำ ^0000FFผลปาล์ม (oil palm nuts) "+(20+(.@quest*10))+" ชิ้น^000000 มาให้หน่อยได้ไหม?";
			changequest 5045,5046+(.@quest*2);
			next;
			mes "[Lero]";
			mes "เมื่อคุณนำผลปาล์มมาแล้ว คุณ Memo จะแลกเปลี่ยนพวกมันเป็นน้ำมันให้เอง";
			close;
		} else if ((.@j & 2) || (.@j & 8) || (.@j & 32)) {
			
			// .@i[]: <required amount>,<quest ID>,<reward amount>,<free weight>
			if (.@j & 2)
				setarray .@i[0],20,5046,10,5000;
			else if (.@j & 8)
				setarray .@i[0],30,5048,15,7500;
			else
				setarray .@i[0],40,5050,20,10000;

			if (countitem(6402) < .@i[0]) {
				mes "[Lero]";
				mes "เมื่อคุณนำผลปาล์มมาครบ "+.@i[0]+" ชิ้นแล้ว คุณ Memo จะแลกเปลี่ยนพวกมันเป็นน้ำมันให้เอง";
				close;
			}
			if (checkweight(1201,1) == 0 || MaxWeight - Weight < .@i[3]) {
				mes "[Lero]";
				mes "ตอนนี้คุณแบกของหนักเกินกว่าจะรับอะไรเพิ่มได้แล้วนะ";
				close;
			}
			mes "[Lero]";
			mes "หนึ่ง, สอง, สาม, สี่, . . . .";
			next;
			mes "[Lero]";
			mes "ครบ "+.@i[0]+" ชิ้นพอดีเป๊ะ มาแลกเปลี่ยนเป็นน้ำมันกันเถอะ";
			changequest .@i[1], .@i[1]+1;
			delitem 6402,.@i[0]; //Oil_Palm_F
			getitem 6401,.@i[2]; //Palm_O
			next;
			mes "[Lero]";
			mes "อย่างที่ฉันบอกไปก่อนหน้านี้ มันหนักกว่าที่คุณคิดนะ ระวังตอนแบกไปด้วยล่ะ";
			close;
		} else if ((.@j & 4) || (.@j & 16) || (.@j & 64)) {
			mes "[Lero]";
			mes "อย่างที่ฉันบอกไปก่อนหน้านี้ มันหนักกว่าที่คุณคิดนะ ระวังตอนแบกไปด้วยล่ะ";
			close;
		}
	}
	mes "[Lero]";
	mes "เกิดอะไรขึ้นกับ Poby กันนะ...? ทำไมเขายังไม่กลับมาอีก...?";
	next;
	mes "- ดูเหมือนว่าเขากำลังรอใครบางคนอยู่ -";
	close;
}

dew_fild01,259,48,4	script	Poby#dew_drink	538,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 500) {
		if (dew_drink > 0) {
			mes "[Poby]";
			mes "ทำไมคุณถึงแบกของเยอะขนาดนั้นล่ะ?";
			close;
		} else {
			mes "[Poby]";
			mes "คุณจะเสียใจนะถ้าแบกของเยอะขนาดนั้นน่ะ";
			close;
		}
	}
	if (dew_drink == 2) {
		mes "[Poby]";
		mes "โอ๊ยย~ ปวดหลังจัง...";
		emotion ET_PROFUSELY_SWEAT;
		next;
		select("คุณคือ Poby ใช่ไหม?");
		mes "[Poby]";
		mes "คุณเป็นใครน่ะ?";
		next;
		mes "[Poby]";
		mes "ฉันไม่รู้จักคุณหรอกนะ แต่ว่า";
		mes "คุณเป็นแฟนคลับของฉันเหรอ?";
		mes "ฉันน่ะ Poby นักล่าผลไม้ที่เก่งที่สุดในเผ่า Jaty เลยนะ";
		next;
		mes "[Poby]";
		mes "คุณมาขอลายเซ็นฉันงั้นเหรอ?";
		emotion ET_QUESTION;
		next;
		select("Lero ให้ฉันมาหาคุณน่ะ");
		mes "[Poby]";
		mes "อ๊ะ! ท่าน Lero ให้คุณมาหานี่เอง ฉันขยับเขยื้อนไม่ได้เลยล่ะ เพราะฉันทำหลังเดาะตอนปีนต้นไม้น่ะสิ";
		next;
		mes "[Poby]";
		mes "!#@$*&%!@$&!$%!$#*&!@#$";
		emotion ET_CRY;
		next;
		mes "[Poby]";
		mes "แถมฉันยังไม่เคยมีแฟนเลยสักคนด้วย...";
		next;
		mes "[Poby]";
		mes "!#@$*&%!@$&!$%!$#*&!@#$";
		emotion ET_CRY;
		next;
		mes "[Poby]";
		mes "อ๊ะ! ขอโทษที ฝากบอกท่าน Lero ด้วยว่าฉันจะกลับไปถ้าฉันหาวิธีขยับตัวได้แล้ว";
		changequest 5036,5037;
		set dew_drink,3;
		next;
		mes "[Poby]";
		mes "ฉันขยับไม่ได้เลยจริงๆ เพราะไอ้ความปวดหลังนี่ล่ะ...";
		close;
	} else if (dew_drink == 3) {
		mes "[Poby]";
		mes "อ๊ะ! ขอโทษที ฝากบอกท่าน Lero ด้วยว่าฉันจะกลับไปถ้าฉันหาวิธีขยับตัวได้แล้ว";
		next;
		mes "[Poby]";
		mes "ฉันขยับไม่ได้เลยจริงๆ เพราะไอ้ความปวดหลังนี่ล่ะ...";
		emotion ET_PROFUSELY_SWEAT;
		close;
	} else if (dew_drink == 4) {
		if (checkquest(5039) > -1) {
			if (countitem(6402) > 59) { // Oil_Palm_F
				mes "[Poby]";
				mes "คุณเก็บผลไม้ได้ครบแล้วนี่ เอามันไปให้ท่าน Lero สิ";
				close;
			} else {
				mes "[Poby]";
				mes "มันง่ายที่สุดสำหรับมือใหม่อย่างคุณที่จะใช้วิธีเขย่าหรือเตะต้นไม้น่ะ";
				next;
				mes "[Poby]";
				mes "มันอาจจะอันตรายนะ ดังนั้นอย่าลืมสวม ^0000FFSafety Helmet^000000 ด้วยล่ะ";
				next;
				mes "[Poby]";
				mes "เก็บ ^FF000060 ^0000FFPalm Oil Fruit^000000 แล้วนำไปให้ท่าน Lero นะ";
				close;
			}
		}
		mes "[Poby]";
		mes "โอ๊ยย~ ปวดหลังจัง...";
		emotion ET_PROFUSELY_SWEAT;
		next;
		mes "[Poby]";
		mes "โอ้!! คุณคือ-- ให้ฉันทายนะ...";
		next;
		mes "[Poby]";
		mes "... ... ... ...";
		emotion ET_THINK;
		next;
		mes "[Poby]";
		mes "...คุณกลับมาแล้ว! คุณกะจะมาเก็บ ^0000FFPalm Oil Fruit^000000 แทนฉันใช่ไหมล่ะ?";
		next;
		mes "[Poby]";
		mes "มันยากนะสำหรับมือใหม่ที่จะเก็บผลไม้ได้เหมือนฉันน่ะ ไม่สิ มันเป็นไปไม่ได้เลยต่างหาก";
		next;
		mes "[Poby]";
		mes "พวกระดับปรมาจารย์ปกติจะปีนต้นไม้ขึ้นไปเก็บ แต่คุณมันแค่มือใหม่...";
		next;
		mes "[Poby]";
		mes "วิธีที่ง่ายที่สุดสำหรับมือใหม่คือการเขย่าหรือเตะต้นไม้นะ มันอาจจะอันตราย ดังนั้นจำไว้ว่าต้องสวม ^0000FFSafety Helmet^000000 ตลอดเวลา";
		changequest 5038,5039;
		next;
		mes "[Poby]";
		mes "เอาล่ะ! เก็บ ^0000FFPalm Oil Fruit^000000 มาให้ครบ ^FF000060^0000FF ชิ้น แล้วนำไปให้ท่าน Lero นะ";
		emotion ET_SURPRISE;
		close;
	} else if (dew_drink == 10) {
		mes "[Poby]";
		mes "โอ้! "+strcharinfo(0)+", เราเจอกันอีกแล้วนะ มีอะไรล่ะ?";

		if (checkquest(5047) > -1)
			set .@count,10;
		else if (checkquest(5049) > -1)
			set .@count,15;
		else if (checkquest(5051) > -1)
			set .@count,20;
		if (.@count) {
			if (countitem(6401) >= .@count) {
				// Custom translation
				mes "[Poby]";
				mes "ทำได้ดีมาก! ระวังตัวตอนไปส่งของด้วยล่ะ~";
				close;
			}
			set .@new_count, .@count-countitem(6401);
			mes "- คุณอธิบายสถานการณ์ให้เขาฟัง -";
			next;
			mes "[Poby]";
			mes "จริงด้วย... ฉันติดค้างคุณปู่ของ Memo อยู่ รอแป๊บนะ ฉันซ่อนน้ำมันบางส่วนไว้แถวๆ นี้แหละ";
			next;
			if (checkweight(6401,.@new_count) == 0) {
				mes "[Poby]";
				mes "ฉันพอมีเหลืออยู่นะ แต่ดูเหมือนคุณจะพกของมาเยอะเกินไปแล้ว";
				close;
			}
			mes "- กำลังค้นหา... กำลังค้นหา... -";
			next;
			mes "[Poby]";
			mes "อ๊ะ! นี่ไง เจออีกขวดแล้ว เอาไปสิ ระวังอย่าทำหายอีกนะ แล้วก็เหยียบไว้เป็นความลับจากท่าน Lero ด้วยล่ะ";
			getitem 6401,.@new_count; //Palm_O
			next;
			mes "[Poby]";
			mes "ขอร้องล่ะนะ~ "+strcharinfo(0)+".";
			close;
		}
		next;
		switch(select("มาทำอะไรที่นี่เหรอ?:ฉันอยากรู้วิธีเก็บผลไม้น่ะ")) {
		case 1:
			mes "[Poby]";
			mes "ฉันมาเดินเล่นน่ะ เพราะคิดว่ามันจะช่วยให้หายปวดหลังเร็วขึ้น";
			close;
		case 2:
			mes "[Poby]";
			mes "วิธีที่ง่ายที่สุดสำหรับมือใหม่คือการเขย่าหรือเตะต้นไม้นะ";
			next;
			mes "[Poby]";
			mes "มันอาจจะอันตรายนะ ดังนั้นสวม ^0000FFSafety Helmet^000000 ไว้เสมอด้วยล่ะ";
			close;
		}
	}
	mes "[Poby]";
	mes "โอ๊ยยย หลังฉันจะหักอยู่แล้วเนี่ย";
	emotion ET_CRY;
	close;
}

//	สคริปต์	::DewataPalm	-
-	script	::DewataPalm	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 5000) {
		mes "น้ำหนักตัวของคุณมากเกินไป";
		close;
	}

	if (checkquest(5039) > -1) set .@count,60;
	else if (checkquest(5046) > -1) set .@count,20;
	else if (checkquest(5048) > -1) set .@count,30;
	else if (checkquest(5050) > -1) set .@count,40;
	if (.@count == 0) {
		mes "มีต้นปาล์มต้นหนึ่ง";
		mes "กำลังยืนรับลมเย็นๆ";
		close;
	} else if (countitem(6402) >= .@count) {
		mes "ต้นปาล์มปฏิเสธ";
		mes "มืออันละโมบของคุณ";
		close;
	}

	if (isequipped(5009)) { // Safety_Helmet
		mes "มีต้นปาล์มที่มีผลและดอกแขวนอยู่ตามกิ่งก้านของมัน";
		mes "อย่าลืมสวม Safety Helmet ในขณะที่พยายามเก็บผลไม้ด้วยนะ";
	} else {
		mes "มีต้นปาล์มต้นหนึ่ง";
		mes "คุณสามารถเตะหรือเขย่ามันก็ได้";
		mes "คุณต้องการจะทำอะไร?";
	}
	next;
	switch(select("เขย่าต้นไม้:เตะต้นไม้")) {
	case 1:
		specialeffect EF_HITLINE2;
		set .@message$, "ในขณะที่คุณเขย่าต้นปาล์ม...";
		mes "คุณเขย่าต้นปาล์ม";
		break;
	case 2:
		specialeffect EF_BASH;
		set .@message$, "ในขณะที่คุณเตะเข้าที่หน้าแข้งของต้นปาล์มอย่างรวดเร็ว...";
		mes "คุณเตะต้นปาล์มอย่างรวดเร็วด้วยแรงทั้งหมดที่มี";
		break;
	}
	next;
	mes "... ... ...";
	next;
	mes .@message$;
	switch(rand(1,20)) {
	case 1:
	case 2:
	case 3:
		mes "มี ^FF0000stone^000000 หล่นลงมา";
		getitem 7049,1; //Stone
		break;
	case 6:
	case 7:
		mes "มี ^FF0000Great Leaf^000000 หล่นลงมา";
		getitem 7198,1; //Great_Leaf
		break;
	case 8:
	case 9:
		mes "มี ^FF0000Worm Peeling^000000 หล่นลงมา";
		getitem 955,1; //Worm_Peelings
		break;
	case 4:
	case 5:
	case 11:
	case 12:
	case 13:
		mes "มี ^0000FFOil Palm Fruit^000000 หล่นลงมา";
		getitem 6402,1; //Oil_Palm_F
		break;
	case 14:
	case 16:
	case 20:
		specialeffect2 EF_BASH;
		if (isequipped(5009)) { //Safety_Helmet
			mes "มีบางอย่างหล่นใส่หัวของคุณ แต่เนื่องจากคุณสวม Safety Helmet คุณจึงปลอดภัยดี";
			emotion ET_BEST, playerattached();
			break;
		} else {
			mes "มีบางอย่างกระแทกลงบนหัวของคุณ ตรงจุดที่บอบบางพอดี $#*@! นี่มันโอกาสหนึ่งในล้านเลยนะเนี่ย";
			percentheal -30,0;
			close2;
			donpcevent strnpcinfo(0)+"::OnDisable";
			warp "dew_fild01",371,211;
			end;
		}
	case 10:
	case 17:
		if (countitem(6402) <= .@count-5) {
			mes "^0000FFOil Palm Fruit^000000 จำนวนมากร่วงหล่นลงมาจากต้นไม้";
			getitem 6402,1; //Oil_Palm_F
			getitem 6402,1; //Oil_Palm_F
			getitem 6402,1; //Oil_Palm_F
			getitem 6402,1; //Oil_Palm_F
			getitem 6402,1; //Oil_Palm_F
		} else {
			mes "มี ^0000FFOil Palm Fruit^000000 หล่นลงมา";
			getitem 6402,1; //Oil_Palm_F
		}
		break;
	default:
		mes "แต่ไม่มีอะไรเกิดขึ้น";
		break;
	}
	close2;
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnEnable:
	enablenpc();
	stopnpctimer;
	end;
OnDisable:
	disablenpc();
	initnpctimer;
	end;
OnTimer60000:
OnTimer180000:
OnTimer120000:
OnTimer240000:
	if(rand(2))
		donpcevent strnpcinfo(0)+"::OnEnable";
	end;
OnTimer300000:
	donpcevent strnpcinfo(0)+"::OnEnable";
	end;
}
dew_fild01,247,62,0	duplicate(DewataPalm)	Palm Tree#drink01	844
dew_fild01,276,73,0	duplicate(DewataPalm)	Palm Tree#drink02	844
dew_fild01,278,63,0	duplicate(DewataPalm)	Palm Tree#drink03	844
dew_fild01,301,88,0	duplicate(DewataPalm)	Palm Tree#drink04	844
dew_fild01,314,72,0	duplicate(DewataPalm)	Palm Tree#drink05	844
dew_fild01,320,68,0	duplicate(DewataPalm)	Palm Tree#drink06	844
dew_fild01,334,107,0	duplicate(DewataPalm)	Palm Tree#drink07	844
dew_fild01,345,88,0	duplicate(DewataPalm)	Palm Tree#drink08	844
dew_fild01,344,71,0	duplicate(DewataPalm)	Palm Tree#drink09	844
dew_fild01,358,93,0	duplicate(DewataPalm)	Palm Tree#drink10	844
dew_fild01,338,121,0	duplicate(DewataPalm)	Palm Tree#drink11	844
dew_fild01,358,136,0	duplicate(DewataPalm)	Palm Tree#drink12	844
dew_fild01,326,147,0	duplicate(DewataPalm)	Palm Tree#drink13	844
dew_fild01,307,146,0	duplicate(DewataPalm)	Palm Tree#drink14	844
dew_fild01,325,159,0	duplicate(DewataPalm)	Palm Tree#drink15	844
dew_fild01,339,163,0	duplicate(DewataPalm)	Palm Tree#drink16	844
dew_fild01,346,193,0	duplicate(DewataPalm)	Palm Tree#drink17	844
dew_fild01,355,193,0	duplicate(DewataPalm)	Palm Tree#drink18	844
dew_fild01,363,219,0	duplicate(DewataPalm)	Palm Tree#drink19	844
dew_fild01,341,261,0	duplicate(DewataPalm)	Palm Tree#drink20	844
dew_fild01,289,302,0	duplicate(DewataPalm)	Palm Tree#drink21	844
dew_fild01,70,196,0	duplicate(DewataPalm)	Palm Tree#drink22	844
dew_fild01,97,145,0	duplicate(DewataPalm)	Palm Tree#drink23	844
dew_fild01,119,42,0	duplicate(DewataPalm)	Palm Tree#drink24	844
dew_fild01,188,72,0	duplicate(DewataPalm)	Palm Tree#drink25	844
dew_fild01,238,80,0	duplicate(DewataPalm)	Palm Tree#drink26	844
dew_fild01,252,97,0	duplicate(DewataPalm)	Palm Tree#drink27	844
dew_fild01,256,98,0	duplicate(DewataPalm)	Palm Tree#drink28	844
dew_fild01,279,93,0	duplicate(DewataPalm)	Palm Tree#drink29	844
dew_fild01,299,107,0	duplicate(DewataPalm)	Palm Tree#drink30	844
dew_fild01,355,113,0	duplicate(DewataPalm)	Palm Tree#drink31	844
dew_fild01,278,136,0	duplicate(DewataPalm)	Palm Tree#drink32	844
dew_fild01,258,128,0	duplicate(DewataPalm)	Palm Tree#drink33	844
dew_fild01,240,125,0	duplicate(DewataPalm)	Palm Tree#drink34	844
dew_fild01,225,117,0	duplicate(DewataPalm)	Palm Tree#drink35	844
dew_fild01,219,121,0	duplicate(DewataPalm)	Palm Tree#drink36	844
dew_fild01,226,99,0	duplicate(DewataPalm)	Palm Tree#drink37	844
dew_fild01,209,137,0	duplicate(DewataPalm)	Palm Tree#drink38	844
dew_fild01,206,156,0	duplicate(DewataPalm)	Palm Tree#drink39	844
dew_fild01,209,201,0	duplicate(DewataPalm)	Palm Tree#drink40	844

// Traditional Weapon Quest :: dewata_weapon
//============================================================
dew_fild01,127,240,4	script	Gatti#weapon	813,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 10000) {
		mes "- หยุดอยู่ตรงนั้นก่อน!! -";
		mes "- คุณถือไอเทมมากเกินไปและมีพื้นที่ในช่องเก็บของไม่เพียงพอที่จะรับรางวัล -";
		mes "- โปรดทำให้มีพื้นที่ว่างในช่องเก็บของมากขึ้นแล้วกลับมารับความท้าทายนี้ใหม่ -";
		close;
	}
	if (BaseLevel < 60) {
		mes "[Gatti]";
		mes "นี่ฉันอยู่ที่ไหนกันแน่? ทุกคนที่นี่ต่างพากันหลบหน้าฉันกันหมด";
		close;
	}
	if (dew_weapon == 0) {
		mes "[Gatti]";
		mes "เฮ้ พ่อหนุ่ม "+((Sex == SEX_MALE)?"คนนั้น":"แม่หนูคนนั้น")+"~ เธอเป็นคนที่แถวนี้หรือเปล่า?";
		next;
		select("ไม่ใช่ครับ/ค่ะ");
		mes "[Gatti]";
		mes "ฉันอยากจะถาม";
		mes "เธอสักหน่อย";
		mes "ที่นี่คือที่ไหนกัน?";
		next;
		mes "- ดูเหมือนเขาจะถามชื่อเกาะ -";
		next;
		input .@island_name$;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ที่นี่เรียกว่า "+ .@island_name$ +" ครับ/ค่ะ มีอะไรหรือเปล่า?";
		next;
		if (.@island_name$ == "Dewata Island") {
			mes "[Gatti]";
			mes "ที่นี่คือเกาะ Dewata อย่างนั้นเหรอ? เธอแน่ใจนะว่าที่นี่คือเกาะ Dewata?";
			next;
			mes "[Gatti]";
			mes "ฮ่าๆๆๆๆๆๆๆๆๆๆๆๆ...!!!";
			next;
			mes "- ชายชราเริ่มหัวเราะเหมือนคนบ้าหลังจากได้ยินชื่อเกาะ คุณตัดสินใจว่าจะถามเขาอีกครั้งในภายหลัง -";
			set dew_weapon,1;
			close;
		} else {
			mes "[Gatti]";
			mes "งั้นเหรอ... "+ .@island_name$ +"? "+ .@island_name$ +"... "+ .@island_name$ +"... "+ .@island_name$ +"...";
			next;
			mes "- ชายชราถอนหายใจยาวอย่างท้อแท้และพูดต่อ -";
			next;
			mes "[Gatti]";
			mes "บ้าจริง... แผนของฉันพังหมดแล้ว ฉันลงทุนไปจนหมดตัวเพื่อมาที่นั่น แต่ฉันกลับล้มเหลว ตอนนี้ฉันทำอะไรไม่ได้แล้ว เรือของฉันก็อับปางไปแล้วด้วย";
			next;
			mes "- ชายชราเริ่มร้องไห้ -";
			mes "- คุณรู้สึกอึดอัด จึงรีบจากไปอย่างรวดเร็ว -";
			close;
		}
	} else if (dew_weapon == 1) {
		select("เอ่อ... คุณลุงครับ/ค่ะ...");
		mes "[Gatti]";
		mes "แค็ก... แค็ก...";
		next;
		mes "[Gatti]";
		mes "โอ้... ขอโทษทีๆ ฉันดีใจมากที่ในที่สุดก็ได้มาถึงเกาะ Dewata เสียที";
		next;
		mes "[Gatti]";
		mes "เรื่องนี้มันยาวน่ะ โอ้! ฉันต้องแนะนำตัวก่อน ฉันชื่อ Mastersmith Gatti และเป็นนักผจญภัยด้วย";
		next;
		mes "[Gatti]";
		mes "ฉันไปพบพิมพ์เขียวอาวุธปริศนาอันหนึ่งเข้า แต่ฉันจำไม่ได้แน่ชัดว่าได้มันมาตอนไหน";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "พิมพ์เขียวอาวุธเหรอ?";
		next;
		mes "[Gatti]";
		mes "ใช่! ฉันเดินทางไปทั่วเพื่อตามหาวัสดุสำหรับสร้างอาวุธชิ้นนี้ แต่ว่า...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "แต่ว่า?";
		next;
		mes "[Gatti]";
		mes "ฉันหาวัสดุชิ้นสุดท้ายไม่เจอจากที่ไหนเลย";
		next;
		mes "[Gatti]";
		mes "ฉันเกือบจะถอดใจแล้ว แต่มีตัวอักษรเล็กๆ บนพิมพ์เขียวบอกว่าสามารถหาได้ที่เกาะ Dewata";
		next;
		mes "[Gatti]";
		mes "ฉันไม่เข้าใจว่าทำไมใครบางคนถึงเขียนเรื่องสำคัญแบบนี้ไว้ด้วยตัวอักษรเล็กๆ แบบนั้น อย่างไรก็ตาม หลังจากได้อ่าน ฉันก็ลงทุนไปจนหมดตัวเพื่อตามหาเกาะนี้";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "โอ้... อย่างนั้นเหรอครับ/ค่ะ?";
		next;
		mes "[Gatti]";
		mes "เกาะนี้มันแปลก ฉันมาถึงที่นี่เมื่อสัปดาห์ก่อน และทุกคนที่นี่ก็เอาแต่หลบหน้าฉัน";
		next;
		mes "[Gatti]";
		mes "สัปดาห์ที่แล้วมันลำบากมาก แต่เธอช่วยฉันไว้ด้วยการบอกว่าที่นี่คือเกาะ Dewata";
		mes "ขอบใจที่บอกฉันนะ และขอบใจที่ผ่านมาที่นี่ได้จังหวะพอดี";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เอ่อ... ไม่เป็นไรครับ/ค่ะ";
		next;
		mes "[Gatti]";
		mes "ก็นะ... มีปัญหาเรื่องเรืออับปางนิดหน่อย แต่นั่นไม่ใช่เรื่องสำคัญอะไรหรอก";
		next;
		mes "[Gatti]";
		mes "ตอนนี้ฉันรู้แล้วว่าที่นี่คือเกาะ Dewata ฉันควรจะไปหาวัสดุสำหรับอาวุธที่ยอดเยี่ยมชิ้นนี้เสียที";
		next;
		mes "[Gatti]";
		mes "... ... ... ... ...";
		next;
		if (dew_legend < 9) {
			mes "[Gatti]";
			mes "อ้อ! วัสดุที่ฉันกำลังตามหาอยู่คือ Comodo Leather";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Comodo Leather เหรอ?";
			next;
			mes "[Gatti]";
			mes "ใช่ ในพิมพ์เขียวระบุว่ามีเพียงชาวพื้นเมือง Dewata เพียงไม่กี่คนที่ได้รับการยอมรับเท่านั้นที่คู่ควรจะต่อสู้กับ Comodo Dragon";
			next;
			mes "[Gatti]";
			mes "ในการที่จะต่อสู้กับ Comodo Dragon ได้ เธอต้องได้รับการยอมรับจากชาวพื้นเมืองก่อน ฉันเดาว่ามันคงยากน่าดูที่จะได้รับการยอมรับนั่น";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "แน่นอนครับ/ค่ะ มันต้องเป็นแบบนั้นอยู่แล้ว...";
			next;
			mes "[Gatti]";
			mes "ฉันขอร้องอะไรเธออย่างสิ ถ้าเธอได้รับการยอมรับก่อนล่ะก็ อย่าลืมกลับมาหาฉันนะ";
			close2;
			setquest 5052;
			set dew_weapon,2;
			end;
		}
		mes "[Gatti]";
		if(Sex == SEX_FEMALE)
			mes "แม่หนู เธอมีพลังที่แข็งแกร่งนะเนี่ย";
		else
			mes "เธอมีพลังที่แข็งแกร่งนะเนี่ย";
		mes "เธอเคยเห็นมอนสเตอร์ที่เรียกว่า Comodo Dragon บนเกาะนี้บ้างไหม?";
		next;
		select("ครับ/ค่ะ เคยเห็น");
		mes "[Gatti]";
		mes "โอ้! จริงเหรอ?";
		mes "ฉันโชคดีจริงๆ ที่ได้พบเธอ ทุกอย่างกำลังไปได้สวยเลย";
		next;
		mes "[Gatti]";
		mes "เธอพอจะช่วยฉันหน่อยได้ไหม?";
		next;
		mes "[Gatti]";
		mes "วัสดุอาวุธที่ฉันตามหาอยู่คือ ^0000FFComodo Leather^000000 ไปหามาให้ฉันสัก 100 ชิ้น แล้วฉันจะสร้างอาวุธที่สวยงามให้เธอเอง";
		setquest 5053;
		set dew_weapon,3;
		next;
		mes "[Gatti]";
		mes "ฉันได้ยินมาว่า Comodo Dragon เป็นมอนสเตอร์ที่ทรงพลังมาก โปรดระวังตัวด้วยนะ";
		close;
	} else if (dew_weapon == 2) {
		if (dew_legend < 9) {
			mes "[Gatti]";
			mes "เธอได้รับการยอมรับจากชาวพื้นเมืองหรือยัง?";
			next;
			mes "[Gatti]";
			mes "... .... ...";
			next;
			mes "[Gatti]";
			mes "ฉันเดาว่ายังสินะ ฉันลองถามไปทั่วแล้ว แต่ก็ยังหาวิธีที่จะได้รับการยอมรับจากชาวพื้นเมืองไม่ได้เลย ฉันไม่รู้เลยว่าจะสร้างอาวุธนี้เสร็จเมื่อไหร่";
			next;
			mes "[Gatti]";
			mes "ถ้าเธอได้รับการยอมรับก่อนฉันล่ะก็ อย่าลืมกลับมาหาฉันล่ะ";
			close;
		}
		mes "[Gatti]";
		mes "สรุปว่า เธอได้รับการยอมรับจากชาวพื้นเมืองหรือยัง?";
		next;
		mes "[Gatti]";
		mes "... .... ...";
		next;
		mes "[Gatti]";
		mes "โอ้ ใช่! ในที่สุด";
		if (Sex == SEX_FEMALE)
			mes "แม่หนู เธอเองก็มีพลังที่แข็งแกร่งเหมือนกันนะ";
		else
			mes "เธอเองก็มีพลังที่แข็งแกร่งเหมือนกันนะ";
		mes "ในที่สุดเธอก็ได้รับการยอมรับจากชาวพื้นเมืองแล้วใช่ไหม?";
		next;
		select("ใช่ครับ/ค่ะ");
		mes "[Gatti]";
		mes "โอ้! เป็นอย่างที่ฉันคาดไว้เลย!";
		mes "ฉันโชคดีมากที่ได้พบเธอ ทุกอย่างกำลังไปได้สวย";
		next;
		mes "[Gatti]";
		mes "ฉันรู้ว่าฉันไม่มีสิทธิ์ขอร้องเธอแบบนี้ แต่การได้พบกันแบบนี้มันคือโชคชะตา...";
		next;
		mes "- Gatti พึมพำ -";
		next;
		mes "[Gatti]";
		mes "วัสดุอาวุธที่ฉันโหยหามานานคือ ^0000FFComodo Leather^000000 ได้โปรดหามาให้ฉัน 100 ชิ้นเถอะ";
		next;
		mes "[Gatti]";
		mes "ฉันกระหายที่จะสร้างอาวุธตามพิมพ์เขียวนี้เหลือเกิน";
		changequest 5052,5053;
		set dew_weapon,3;
		next;
		mes "[Gatti]";
		mes "ได้โปรด ก่อนที่ไฟในตัวฉันจะมอดดับไป";
		next;
		mes "[Gatti]";
		mes "โอ้! และ Comodo Dragon เป็นมอนสเตอร์ที่ทรงพลังมาก ดังนั้นโปรดระวังตัวด้วย";
		close;
	} else if (dew_weapon == 3) {
		if (countitem(6403) > 99) {
			mes "[Gatti]";
			mes "ดูจากท่าทางการเดินของเธอแล้ว เธอคงเอา Comodo Leather มาให้ฉันครบ 100 ชิ้นแล้วสินะ ส่งมาให้ฉันเลย";
			delitem 6403,100;
			erasequest 5053;
			set dew_weapon,4;
			next;
			mes "- Gatti รับ Comodo Leather ไปจากคุณ -";
			close;
		} else if (countitem(6403)) {
			mes "[Gatti]";
			mes "นั่นแหละ Comodo Leather แต่ยังไม่พอ";
			next;
			mes "[Gatti]";
			mes "ฉันรู้ว่าฉันกำลังขอร้องเธออยู่ แต่ฉันต้องการ 100 ชิ้นนะ";
			close;
		} else {
			mes "[Gatti]";
			mes "โปรดระวังตัวด้วยเพราะ Comodo Dragon เป็นมอนสเตอร์ที่ทรงพลังมาก และอย่าลืมนำ Comodo Leather 100 ชิ้นมาให้ฉันตอนขากลับด้วยล่ะ";
			close;
		}
	} else if (dew_weapon == 4) {
		mes "[Gatti]";
		mes "โอ้~ ดูนี่สิ ความยืดหยุ่นนี่... ช่างนุ่มนวลเหลือเกิน";
		next;
		mes "[Gatti]";
		mes "Comodo Leather คือวัสดุที่ยอดเยี่ยมที่สุดเท่าที่เคยมีมาเลย";
		next;
		mes "[Gatti]";
		mes "ใช่ ฉันควรจะแปรรูปมันยังไงดีนะ?";
		next;
		mes "[Gatti]";
		mes "อืม... แบบนี้ดีไหม?";
		next;
		mes "[Gatti]";
		mes "หรือแบบนั้นดี? ใช่เลย! ต้องทางนี้แหละ!";
		next;
		mes "- Mastersmith Gatti กำลังสนุกกับการแปรรูปหนังที่คุณนำมาเพื่อสร้างอาวุธ -";
		mes "- คุณควรรอสักครู่ -";
		set dew_weapon,5;
		close;
	} else if (dew_weapon == 5) {
		mes "["+strcharinfo(0)+"]";
		mes "เอ่อ... Gatti ครับ/ค่ะ?";
		next;
		mes "[Gatti]";
		mes "โอ้! ขอโทษทีๆ! หนังนี่มันน่าสนใจมากจริงๆ";
		next;
		mes "[Gatti]";
		mes "ฉันสร้างด้ามจับ (Hilt) จากหนังที่เธอนำมาให้เสร็จแล้ว";
		next;
		mes "[Gatti]";
		mes ".... .... .... ....";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ด้ามจับเหรอ?";
		next;
		mes "[Gatti]";
		mes "เธอไม่รู้เหรอว่าด้ามจับคืออะไร? มันก็คือส่วนที่ใช้มือจับของอาวุธยังไงล่ะ";
		next;
		mes "- Gatti แสดงสิ่งที่มีลักษณะเหมือนด้ามจับอาวุธให้ดู -";
		next;
		mes "[Gatti]";
		mes "ฉันพยายามสร้างมันตามที่เขียนไว้ในพิมพ์เขียว แต่ฉันต้องการวัสดุเพิ่ม";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "จริงเหรอครับ/ค่ะ?";
		next;
		mes "[Gatti]";
		mes "... ... ... ...";
		next;
		mes "[Gatti]";
		mes "ใช่... บอกตามตรงนะ ฉันทำพังไปเยอะเลยจากการลองผิดลองถูกหลายๆ แบบ";
		next;
		mes "[Gatti]";
		mes "ฉันรู้ว่ามันมากเกินไปที่จะขอ แต่เธอช่วยหา Comodo Leather มาให้อีก 100 ชิ้น, Steel 20 ชิ้น และ Oridecon 10 ชิ้นได้ไหม?";
		next;
		mes "[Gatti]";
		mes "ฉันจะสร้างอาวุธที่ยอดเยี่ยมที่สุดเท่าที่เคยมีมาให้เธอเลย";
		setquest 5054;
		set dew_weapon,6;
		getitem 6404,1; //Caress_H
		next;
		mes "[Gatti]";
		mes "โอ้! เกือบลืมไปเลย อย่าลืมเอาด้ามจับที่ฉันเพิ่งทำให้ติดตัวมาด้วยล่ะ";
		next;
		mes "[Gatti]";
		mes "อาวุธมันต้องมีด้ามจับนะ";
		close;
	} else if (dew_weapon == 6) {
		disable_items;
		mes "[Gatti]";
		mes "เธอนำวัสดุทั้งหมดมาครบแล้วเหรอ?";
		mes "เธอเองก็คงอยากเห็นอาวุธตอนเสร็จแล้วสินะ? ฮ่าๆ";
		next;
		mes "[Gatti]";
		mes ".... .... ....";
		next;
		if (countitem(6403) < 100 || countitem(999) < 20 || countitem(984) < 10 || countitem(6404) < 1) {
			mes "[Gatti]";
			mes "อืม... ฉันต้องการพวกนั้นเพิ่มอีก ถ้าเรือของฉันไม่พังล่ะก็ ฉันคงหามาทำเองได้ แต่ว่านะ...";
			next;
			mes "- Gatti พึมพำกับตัวเอง -";
			next;
			mes "[Gatti]";
			mes "สิ่งที่ฉันต้องการคือ Comodo Leather 100 ชิ้น, Steel 20 ชิ้น และ Oridecon 10 ชิ้น";
			next;
			mes "[Gatti]";
			mes "ถ้าเธอนำมาให้ครบ ฉันจะสร้างอาวุธที่ยอดเยี่ยมที่สุดให้เลย";
			close;
		}
		mes "[Gatti]";
		mes "เยี่ยม! เรามีครบแล้ว รอประเดี๋ยว";
		next;
		specialeffect2 EF_REPAIRWEAPON;
		next;
		emotion ET_PROFUSELY_SWEAT;
		next;
		mes "[Gatti]";
		mes "ฉันสร้างอาวุธเสร็จแล้ว ในพิมพ์เขียวเรียกสิ่งนี้ว่า Caress";
		next;
		mes "[Gatti]";
		mes "ฉันมักจะพบสิ่งใหม่ๆ เสมอเมื่อได้มองดูพิมพ์เขียวนี้";
		next;
		mes "[Gatti]";
		mes "ตอนนี้ฉันเรียนรู้วิธีสร้างอาวุธนี้แล้ว มาถามฉันได้ทุกเมื่อถ้าเธอต้องการมัน";
		erasequest 5054;
		set dew_weapon,7;
		delitem 6403,100; //Comodo_L
		delitem 999,20; //Steel
		delitem 984,10; //Oridecon
		delitem 6404,1; //Caress_H
		getitem 13067,1; //Caress
		close;
	} else if (dew_weapon >= 7) {
		mes "[Gatti]";
		mes "เธอคือ "+strcharinfo(0)+",";
		mes "เป็นยังไงบ้าง Caress ที่ฉันทำให้ถูกใจไหม?";
		switch(select("มันยอดมากเลย!:ฉันต้องการ Caress อีกอัน:ฉันสามารถทำให้มันแข็งแกร่งขึ้นได้ไหม?")) {
		case 1:
			mes "[Gatti]";
			mes "ใช่ๆ แน่นอนว่ามันต้องยอดเยี่ยมอยู่แล้ว";
			mes "ฉันเป็นคนสร้างมันเองกับมือ มันต้องดีแน่นอน";
			close;
		case 2:
			disable_items;
			if (countitem(6404)) {
				if (countitem(6403) < 70 || countitem(984) < 10 || countitem(999) < 20 || countitem(6404) < 1) {
					mes "[Gatti]";
					mes "เธอมีแค่ด้ามจับ (Hilt) เองนะ เธอต้องนำ Oridecon 10 ชิ้น, Steel 20 ชิ้น และ Comodo Leather 70 ชิ้นมาให้ฉัน";
					next;
					mes "[Gatti]";
					mes "ถ้าเธอนำมันมา ฉันจะสร้าง Caress อันใหม่ให้";
					close;
				}
				mes "[Gatti]";
				mes "เธอมีวัสดุครบแล้ว รอสักครู่นะ";
				next;
				specialeffect2 EF_REPAIRWEAPON;
				next;
				emotion ET_PROFUSELY_SWEAT;
				next;
				mes "[Gatti]";
				mes "นี่คือ Caress! มาหาฉันได้ทุกเมื่อล่ะ";
				delitem 6403,70; //Comodo_L
				delitem 999,20; //Steel
				delitem 984,10; //Oridecon
				delitem 6404,1; //Caress_H
				getitem 13067,1; //Caress
				close;
			} else {
				if (countitem(6403) < 50 || countitem(7201) < 1) {
					mes "[Gatti]";
					mes "เธออยากให้ฉันสร้าง Caress เพิ่มให้อีกอันงั้นเหรอ? ในกรณีนั้นฉันต้องการด้ามจับ (Hilt) ซึ่งต้องใช้ Comodo Leather 50 ชิ้น และ Log 1 อัน นั่นคือสิ่งที่ฉันต้องการเพื่อสร้างด้ามจับ";
					close;
				}
				mes "[Gatti]";
				mes "เธอนำวัสดุสำหรับทำด้ามจับมาแล้ว ฉันจะทำให้เดี๋ยวนี้แหละ";
				next;
				mes "[Gatti]";
				mes "วางตรงนี้ แล้วก็แปรรูปแบบนี้...";
				next;
				mes "[Gatti]";
				mes "นี่ไงล่ะ ฉันจะสร้าง Caress ให้ได้ถ้าเธอนำ Oridecon 10 ชิ้น, Steel 20 ชิ้น และ Comodo Leather 70 ชิ้นมาให้ฉัน";
				delitem 6403,50; //Comodo_L
				delitem 7201,1; //Log
				getitem 6404,1; //Caress_H
				close;
			}
		case 3:
			mes "[Gatti]";
			mes "งั้นเธออยากจะเสริมพลัง (Reinforce) อาวุธของเธอสินะ? อาวุธอาจจะพังได้เป็นบางครั้งในระหว่างกระบวนการ แต่มันก็ไม่ได้เกิดขึ้นบ่อยหรอก";
			if(select("โปรดเสริมพลังอาวุธให้ฉันด้วย:เอาไว้คราวหน้าละกัน") == 2) {
				mes "[Gatti]";
				mes "เมื่อไหร่ก็ตามที่เธอต้องการเสริมพลัง Caress แค่นำ Caress และเงิน 100,000 zeny มาให้ฉัน";
				close;
			}
			mes "[Gatti]";
			mes "ถ้าเธออยากจะทำ ฉันต้องบอกข้อควรระวังบางอย่างก่อนที่จะทำการเสริมพลัง Caress";
			next;
			mes "[Gatti]";
			mes "^FF0000อย่างแรก!^009977 ฉันต้องการ Caress 1 อัน และเงิน 100,000 zeny เพื่อเสริมพลัง ก็อย่างที่รู้ ตอนนี้ฉันไม่มีเงินเลยเพราะเรือของฉันอับปาง^000000";
			next;
			mes "[Gatti]";
			mes "^FF0000อย่างที่สอง!^009977 ถ้าเธอมี Caress ที่ตีบวกแล้วหรือใส่การ์ดไว้ในกระเป๋าซึ่งเธอไม่อยากจะเสริมพลังมัน มันอาจจะถูกทำลายหรือระดับการตีบวกอาจจะเปลี่ยนไปในระหว่างกระบวนการเสริมพลังได้";
			next;
			mes "[Gatti]";
			mes "ถ้าเธอไม่ต้องการให้เป็นแบบนั้น ให้เอา Caress อันที่ตีบวกแล้วไปเก็บไว้ที่อื่นก่อนที่จะทำการเสริมพลัง^000000";
			next;
			mes "[Gatti]";
			mes "^FF0000อย่างที่สาม!^009977 บางครั้งการเสริมพลังก็ล้มเหลว อาวุธไม่ได้ถูกทำลายทุกครั้งที่ล้มเหลว อย่างไรก็ตาม เธอควรจะรู้ไว้ว่าอาวุธจะถูกทำลายได้ในบางครั้งถ้าล้มเหลว^000000";
			next;
			if(select("โปรดเสริมพลังอาวุธให้ฉันด้วย:เอาไว้คราวหน้าดีกว่า") == 2) {
				mes "[Gatti]";
				mes "เมื่อไหร่ก็ตามที่เธอต้องการเสริมพลัง Caress แค่นำ Caress และเงิน 100,000 zeny มาให้ฉัน";
				close;
			}
			break;
		}

		if (countitem(13067) < 1 || Zeny < 100000) {
			mes "[Gatti]";
			mes "เธอมีวัสดุไม่ครบ มันเป็นไปไม่ได้ที่จะเสริมพลังถ้าเธอมีไม่ครบทุกอย่าง ถ้าเธอต้องการเสริมพลัง ให้นำเงิน 100,000 zeny และ Caress 1 อันมา";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		progressbar "0xFFFF00",3;
		set Zeny, Zeny - 100000;

		// Socket 3 (ช่องที่ 3)
		set .@i, rand(4300);
		  if (.@i < 150) set .@s3,4700; // STR + 1
		else if (.@i < 300) set .@s3,4730; // AGI + 1
		else if (.@i < 450) set .@s3,4740; // VIT + 1
		else if (.@i < 600) set .@s3,4710; // INT + 1
		else if (.@i < 750) set .@s3,4720; // DEX + 1
		else if (.@i < 900) set .@s3,4750; // LUK + 1
		else if (.@i < 1050) set .@s3,4761; // MATK + 2%
		else if (.@i < 1200) set .@s3,4764; // CRIT + 5
		else if (.@i < 1350) set .@s3,4786; // MDEF + 2
		else if (.@i < 1500) set .@s3,4791; // DEF + 3
		else if (.@i < 1650) set .@s3,4795; // HP + 100
		else if (.@i < 1750) set .@s3,4800; // SP + 50
		else if (.@i < 1850) set .@s3,4797; // HP + 300
		else if (.@i < 1950) set .@s3,4793; // DEF + 9
		else if (.@i < 2050) set .@s3,4788; // MDEF + 6
		else if (.@i < 2150) set .@s3,4752; // LUK + 3
		else if (.@i < 2250) set .@s3,4722; // DEX + 3
		else if (.@i < 2350) set .@s3,4712; // INT + 3
		else if (.@i < 2450) set .@s3,4742; // VIT + 3
		else if (.@i < 2550) set .@s3,4732; // AGI + 3
		else if (.@i < 2650) set .@s3,4702; // STR + 3
		else if (.@i < 2700) set .@s3,4703; // STR + 4
		else if (.@i < 2750) set .@s3,4733; // AGI + 4
		else if (.@i < 2800) set .@s3,4743; // VIT + 4
		else if (.@i < 2850) set .@s3,4713; // INT + 4
		else if (.@i < 2900) set .@s3,4723; // DEX + 4
		else if (.@i < 2950) set .@s3,4753; // LUK + 4
		else if (.@i < 3000) set .@s3,4789; // MDEF + 8
		else if (.@i < 3001) set .@s3,4704; // STR + 5
		else if (.@i < 3002) set .@s3,4734; // AGI + 5
		else if (.@i < 3003) set .@s3,4744; // VIT + 5
		else if (.@i < 3004) set .@s3,4714; // INT + 5
		else if (.@i < 3005) set .@s3,4724; // DEX + 5
		else if (.@i < 3006) set .@s3,4754; // LUK + 5
		else if (.@i < 3007) set .@s3,4806; // MATK + 3%
		else if (.@i < 3008) set .@s3,4765; // CRIT + 7
		else if (.@i < 3009) set .@s3,4766; // ATK + 2%
		else if (.@i < 3010) set .@s3,4801; // SP + 100
		else if (.@i < 3140) set .@s3,4796; // HP + 200
		else if (.@i < 3270) set .@s3,4792; // DEF + 6
		else if (.@i < 3400) set .@s3,4787; // MDEF + 4
		else if (.@i < 3530) set .@s3,4751; // LUK + 2
		else if (.@i < 3660) set .@s3,4721; // DEX + 2
		else if (.@i < 3790) set .@s3,4711; // INT + 1
		else if (.@i < 3920) set .@s3,4741; // VIT + 2
		else if (.@i < 4050) set .@s3,4731; // AGI + 2
		else if (.@i < 4180) set .@s3,4701; // STR + 2
		else set .@s3,0;

		// Socket 4 (ช่องที่ 4)
		set .@j, rand(4100);
		  if (.@j < 130) set .@s4,4700; // STR + 1
		else if (.@j < 260) set .@s4,4730; // AGI + 1
		else if (.@j < 390) set .@s4,4740; // VIT + 1
		else if (.@j < 520) set .@s4,4710; // INT + 1
		else if (.@j < 650) set .@s4,4720; // DEX + 1
		else if (.@j < 780) set .@s4,4750; // LUK + 1
		else if (.@j < 910) set .@s4,4760; // MATK + 1%
		else if (.@j < 1040) set .@s4,4764; // CRIT + 5
		else if (.@j < 1170) set .@s4,4766; // ATK + 2%
		else if (.@j < 1300) set .@s4,4786; // MDEF + 2
		else if (.@j < 1430) set .@s4,4791; // DEF + 3
		else if (.@j < 1560) set .@s4,4795; // HP + 100
		else if (.@j < 1690) set .@s4,4800; // SP + 50
		else if (.@j < 1790) set .@s4,4796; // HP + 200
		else if (.@j < 1890) set .@s4,4792; // DEF + 6
		else if (.@j < 1990) set .@s4,4787; // MDEF + 4
		else if (.@j < 2090) set .@s4,4767; // ATK + 3%
		else if (.@j < 2190) set .@s4,4765; // CRIT + 7
		else if (.@j < 2290) set .@s4,4751; // LUK + 2
		else if (.@j < 2390) set .@s4,4721; // DEX + 2
		else if (.@j < 2490) set .@s4,4711; // INT + 2
		else if (.@j < 2590) set .@s4,4741; // VIT + 2
		else if (.@j < 2690) set .@s4,4731; // AGI + 2
		else if (.@j < 2790) set .@s4,4701; // STR + 2
		else if (.@j < 2860) set .@s4,4702; // STR + 3
		else if (.@j < 2930) set .@s4,4732; // AGI + 3
		else if (.@j < 3000) set .@s4,4742; // VIT + 3
		else if (.@j < 3140) set .@s4,4712; // INT + 3
		else if (.@j < 3210) set .@s4,4722; // DEX + 3
		else if (.@j < 3280) set .@s4,4788; // MDEF + 8
		else if (.@j < 3350) set .@s4,4793; // DEF + 9
		else if (.@j < 3420) set .@s4,4797; // HP + 300
		else if (.@j < 3490) set .@s4,4801; // SP + 100
		else if (.@j < 3540) set .@s4,4798; // HP + 400
		else if (.@j < 3590) set .@s4,4789; // MDEF + 8
		else if (.@j < 3640) set .@s4,4753; // LUK + 4
		else if (.@j < 3690) set .@s4,4723; // DEX + 4
		else if (.@j < 3740) set .@s4,4713; // INT + 4
		else if (.@j < 3790) set .@s4,4743; // VIT + 4
		else if (.@j < 3840) set .@s4,4733; // AGI + 4
		else if (.@j < 3890) set .@s4,4703; // STR + 4
		else if (.@j < 3900) set .@s4,4704; // STR + 5
		else if (.@j < 3910) set .@s4,4734; // AGI + 5
		else if (.@j < 3920) set .@s4,4744; // VIT + 5
		else if (.@j < 3930) set .@s4,4714; // INT + 5
		else if (.@j < 3940) set .@s4,4724; // DEX + 5
		else if (.@j < 3950) set .@s4,4754; // LUK + 5
		else set .@s4,0;

		delitem 13067,1; //Caress
		if (.@s3 == 0 && .@s4 == 0) {
			mes "[Gatti]";
			mes "โธ่เอ๋ย~~ เกือบจะสำเร็จแล้วเชียว... แต่ล้มเหลวเสียได้ ขอโทษทีนะ ขอโทษที...";
			emotion ET_HUK;
			close;
		}
		getitem2 13067,1,1,0,0,0,0,.@s3,.@s4;
		mes "[Gatti]";
		mes "การเสริมพลัง Caress ประสบความสำเร็จแล้ว แล้วเจอกันใหม่ล่ะ";
		close;
	}
}
