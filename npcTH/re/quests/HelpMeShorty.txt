//===== rAthena Script =======================================
//= Dynamic NPC: HelpMeShorty.
//===== Description: =========================================
//- [Walkthrough conversion]
//= Short quest to allow the player to summon, via an item, a dynamic NPC that gives basic game information.
//= After level 30, the player can get a costume in exchange for the item.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Starter
izlude,173,210,3	script	Black Shadow#fgtg01	4_CENERE,5,5,{
	if (isbegin_quest(14688) == 0 && BaseLevel < 31) {
		mes "[Black Shadow]";
		mes "เอาตัวข้าไปที! เจ้าต้องรับผิดชอบข้าด้วยที่มาสบตากันแบบนี้!";
		next;
		mes "[Black Shadow]";
		mes "ถ้าเจ้าพาข้าไปด้วย ^0000cdข้าจะช่วยเจ้าเอง^000000 ข้าจะช่วยเจ้าได้หลายอย่างเลยล่ะ! ข้าจะสอน ^0000cdหลายๆ อย่าง^000000 ให้เจ้าเอง!!";
		next;
		mes "[Shorty Notice]";
		mes "^EE0000หากคุณเลือกที่จะร่วมทางกับ Shorty ไอเทม ^0000cd<Help Me Shorty>^EE0000 จะถูกใส่ลงในช่องเก็บของของคุณโดยอัตโนมัติ";
		next;
		mes "[Shorty Notice]";
		mes "หากคุณใช้ไอเทม <Help Me Shorty> ใน ^EE0000ช่องเก็บของ คุณจะสามารถอัญเชิญ Shorty ออกมาเพื่อขอ ^0000cdความช่วยเหลือและคำแนะนำ^EE0000 จากมันได้";
		next;
		mes "[Shorty Notice]";
		mes "^EE0000เมื่อคุณมีเลเวล 31 หรือสูงกว่า คุณจะได้รับเควสต์ที่ต้องไปส่ง Shorty ซึ่งการทำเควสต์นี้สำเร็จจะทำให้ Shorty จากไปและคุณจะได้รับรางวัลในเวลาเดียวกัน";
		next;
		mes "[Shorty Notice]";
		mes "^EE0000ก่อนที่จะถึงเลเวล 31 คุณสามารถส่ง Shorty ไปได้ทุกเมื่อผ่านเมนู ^0000cd<Let's Break Up>^EE0000 แต่หลังจากนั้นคุณจะไม่สามารถทำให้ Shorty มาเป็นพวกได้อีกเลย";
		next;
		if (select( "ตกลง ไปด้วยกันเถอะ", "ฉันไม่อยากไปกับนาย" ) == 2) {
			mes "[Black Shadow]";
			mes "เจ้าจะทำเรื่องพวกนี้โดยไม่มีข้าช่วยจริงๆ เหรอ? เจ้าเดินไปไม่ถึงสิบก้าวก็คงจะคิดถึงข้าแล้วล่ะ ชีวิตคนเรามันคือการช่วยเหลือกันไม่ใช่เหรอ? มันไม่ควรเป็นแบบนี้สิ";
			close;
		}
		mes "[Black Shadow]";
		mes "ฟู่ว ค่อยยังชั่ว ตอนนี้พวกเราเป็นเพื่อนกันแล้วใช่ไหม?";
		next;
		mes "[Black Shadow]";
		mes "ว่าแต่ สายตาแบบนั้นมันอะไรกัน...? เจ้ากลัวข้าเหรอ? ข้าแค่ทำแบบนี้เพื่อให้ดูโดดเด่นน่ะ รอเดี๋ยวนะ...";
		next;
		cloakonnpcself();
		cloakoffnpcself("Mysterious Creature#fgtg01" );
		mes "[Black Shadow]";
		mes "แค่นี้พอไหม?";
		next;
		mes "[Shorty]";
		mes "ข้าชื่อ Shorty!!";
		next;
		mes "[Shorty]";
		mes "เจ้าชื่ออะไรล่ะ?";
		next;
		select( "ฉันชื่อ " + strcharinfo(0) + "." );
		mes "[Shorty]";
		mes "เป็นชื่อที่เรียกง่ายดีนะ เจ้ามีกระเป๋าไหม? ใบที่เจ้าสะพายไว้ที่เอวน่ะ มันขนาดพอดีที่ข้าจะเข้าไปอยู่เลยล่ะ";
		next;
		select( "นายอยากเข้าไปในกระเป๋างั้นเหรอ?" );
		mes "[Shorty]";
		mes "ข้าเข้าไปอยู่ในกระเป๋าเจ้าได้ เจ้าจะตกใจอะไรขนาดนั้น? อยู่ไม่นานหรอก เดี๋ยวข้าก็ไปแล้ว";
		next;
		mes "[Shorty]";
		mes "กด ^0000cdALT+E^000000 เพื่อดูของในกระเป๋า กดที่แถบไอเทมใช้ไป (Consumption) แล้วข้าจะอยู่ตรงนั้นแหละ";
		next;
		mes "[Shorty]";
		mes "เวลาที่เจ้าอยากเรียกข้า ให้กด ^0000cd<Help Me Shorty>^000000 สองครั้งเร็วๆ แล้วข้าจะออกมาจากกระเป๋าเพื่อช่วยเจ้า เป็นไงล่ะ? ยอดไปเลยใช่ไหม?";
		next;
		mes "[Shorty]";
		mes "ถ้าเจ้าไม่อยากให้ข้าอยู่ในกระเป๋าแล้ว ก็พาข้าไปหา ^0000cdSalty^000000 ที นั่นคือจุดหมายของข้า เมื่อถึงเวลานั้น ข้าจะไปจาก ^0000cdกระเป๋าของเจ้า^000000 เอง";
		next;
		select( "คนคนนั้นอยู่ที่ไหน?" );
		mes "[Shorty]";
		mes "เดี๋ยวข้าจะบอกทีหลัง ตอนนี้ข้าอยากพักผ่อนก่อน";
		next;
		mes "[Shorty]";
		mes "โชคดีนะในวันข้างหน้า!";
		setquest 14688;
		completequest 14688;
		getitem 23278,1;	// HelpmeShorty
		cloakonnpcself("Mysterious Creature#fgtg01" );
		close;
	}
	end;

OnTouch:
	if (isbegin_quest(14688) == 0 && BaseLevel < 31)
		npctalk "ช่วยข้าด้วย! กอดข้าเร็วเข้า! ช่วยข้าที!", "", bc_self;
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(14688) == 0 && BaseLevel < 31" );
	cloakonnpc();
	end;
}

//izlude,173,210,3	script	Mysterious Creature#fgtg01	4_GALAPAGO,5,5,{	// unknown OnTouch effect
izlude,173,210,3	duplicate(dummy_cloaked_npc)	Mysterious Creature#fgtg01	4_GALAPAGO

izlude,161,199,0	script	#fgtg01	HIDDEN_WARP_NPC,3,5,{
	end;
OnTouch:
	if (isbegin_quest(14688) == 0 && BaseLevel < 31)
		cloakoffnpcself( "Black Shadow#fgtg01" );
	end;
}
izlude,194,209,0	duplicate(#fgtg01)	#fgtg02	HIDDEN_WARP_NPC,3,5
izlude,173,210,0	duplicate(#fgtg01)	#fgtg03	HIDDEN_WARP_NPC,3,5


// Exchange npc. Step 1 of 2 (NPC แลกเปลี่ยน ขั้นตอนที่ 1 จาก 2)
payon_in01,193,23,3	script	Chanmo#fgtg01	1_F_ORIENT_01,{
	if (isbegin_quest(14693) == 1) {
		cloakoffnpcself( "Shorty#fgtg004" );
		mes "[Shorty]";
		mes "Salty! Salty~ ข้ามาแล้ว~ เจ้ารออยู่หรือเปล่า? เจ้ารอนานไหม?";
		next;
		mes "[In the inn]";
		mes "......";
		next;
		mes "[Shorty]";
		mes "Salty, Salty? Salty? เจ้าไปอยู่ที่ไหนมา Salty?";
		next;
		mes "[Chanmo]";
		mes "พวกเจ้ากำลังพูดถึงใครกัน?";
		next;
		mes "[Shorty]";
		mes "ข้ามาหา Salty.. ข้ามาแล้ว แต่ไม่เจอ Salty เลย";
		next;
		mes "[Chanmo]";
		mes "Salty งั้นเหรอ? อ้อ เจ้าหมายถึงผู้หญิงที่พักอยู่ในห้องสินะ ข้าเพิ่งส่งนางไปทำธุระน่ะ";
		next;
		mes "[Shorty]";
		mes "ธุระเหรอ? ทำไมล่ะ?";
		next;
		mes "[Chanmo]";
		mes "ข้าเบื่อๆ น่ะ เลยส่งนางไปหาเยลลี่กับลูกโอ๊กมาให้";
		next;
		mes "[Shorty]";
		mes "เฮ้! ท่านมาจากไหนกัน? พวกเรามาเพื่อพบ Salty นะ ทำไมท่านถึงส่ง Salty ไปที่อื่นล่ะ~";
		next;
		mes "[Chanmo]";
		mes "ข้าไม่ได้บังคับใครให้มานี่ และข้าก็จัดการเจ้านั่นจนมันกลิ้งออกไปแล้ว ดังนั้นข้าเลยส่งนางออกไป";
		next;
		mes "[Shorty]";
		mes "ถ้าอย่างนั้นข้าขอรอที่นี่ได้ไหม?";
		next;
		mes "[Chanmo]";
		mes "ถ้าเจ้ารีบนัก เจ้าก็ไปช่วยนางสิ เจ้าสามารถไปหา";
		mes "<NAVI>[Coco's nest]<INFO>prt_fild10,318,191,0,101,0</INFO></NAVI> ทางทิศตะวันตกเฉียงใต้ของ Prontera เจ้าจะได้รับลูกโอ๊กพวงใหญ่จาก Coco";
		next;
		mes "[Shorty]";
		mes "อืม.. ก็ได้ ท่านอาจารย์ พวกเราไปกันเถอะ ไปที่รังของ Coco";
		next;
		select("นายอยู่ที่นี่เหรอ?");
		mes "[Shorty]";
		mes "ข้าคิดถึง Salty ไปหา Salty กันเถอะ";
		next;
		select("ตกลง ไปกันเถอะ");
		mes "[Shorty]";
		mes "เอาล่ะ ไปกันเลย!!!";
		cloakonnpcself("Shorty#fgtg004");
		completequest 14693;
		setquest 14694;
		close;
	}
	mes "[Chanmo]";
	mes "วันนี้ข้าต้องเก็บลูกโอ๊กกับเยลลี่สักหน่อย";
	close;
}

payon_in01,190,24,5	duplicate(dummy_cloaked_npc)	Shorty#fgtg004	4_GALAPAGO


// Exchange npc. Step 2 of 2 (NPC แลกเปลี่ยน ขั้นตอนที่ 2 จาก 2)
prt_fild10,318,191,0	script	#salt01	HIDDEN_WARP_NPC,3,5,{
	end;
OnTouch:
	if (isbegin_quest(14694) == 1)
		cloakoffnpcself( "Salty#fgtg01" );
	end;
}
prt_fild10,262,39,0	duplicate(#salt01)	#salt02	HIDDEN_WARP_NPC,3,5
prt_fild10,332,128,0	duplicate(#salt01)	#salt03	HIDDEN_WARP_NPC,3,5
prt_fild10,222,293,0	duplicate(#salt01)	#salt04	HIDDEN_WARP_NPC,3,5
// 5?
prt_fild10,26,196,0	duplicate(#salt01)	#salt06	HIDDEN_WARP_NPC,3,5
prt_fild10,311,167,0	duplicate(#salt01)	#salt07	HIDDEN_WARP_NPC,3,5
prt_fild10,298,212,0	duplicate(#salt01)	#salt08	HIDDEN_WARP_NPC,3,5


prt_fild10,318,191,3	script	Salty#fgtg01	4_F_ALCHE_A,{
	if (isbegin_quest(14694) == 1) {
		mes "[Salty]";
		mes "โอ้! ลูกโอ๊ก! มุกมุก, เยลลี่ลูกโอ๊ก! เจ้าจะทำอะไรกับลูกโอ๊กงั้นเหรอ? ก็ต้องกินเยลลี่ลูกโอ๊กสิ~";
		next;
		cloakoffnpcself( "Shorty#fgtg005" );
		mes "[Shorty]";
		mes "เจ้าดูตื่นเต้นนะ Salty เจ้ามีความสุขดีเหรอตอนไม่มีข้า?";
		next;
		mes "[Salty]";
		mes "เอ๊ะ? อ๊ะ-หือ??? Shorty!!!!!!!!!!";
		next;
		mes "[Shorty]";
		mes "เจ้าสนุกมากไหมตอนไม่มีข้า? สนุกมากไหมล่ะ...";
		next;
		mes "[Salty]";
		mes "Shorty ทำไมเจ้ามาอยู่ที่นี่ล่ะ? วันหนึ่งข้าได้รับจดหมายบอกว่าเจ้ากำลังจะจากไป!!!";
		next;
		mes "[Shorty]";
		mes "มันมีเหตุการณ์หลายอย่างน่ะ สถานการณ์นี้ท่านอาจารย์ของข้าที่นี่จะเป็นคนอธิบายเอง";
		next;
		mes "[Salty]";
		mes "อะไรนะ? เจ้าลักพาตัว Shorty ของพวกเรามาเหรอ??";
		next;
		select( "เปล่า ไม่ใช่อย่างนั้นนะ..." );
		mes "[Salty]";
		mes "อ้อ งั้นเหรอ? แต่ Shorty! เกิดอะไรขึ้นที่นี่กันแน่?";
		next;
		mes "[Shorty]";
		mes "ข้าไปที่ Payon มา และที่มาที่นี่เพราะคุณยายที่ทำอาหารบอกว่าเจ้ามาที่นี่น่ะ";
		next;
		mes "[Salty]";
		mes "โอเค! ข้าเองก็กำลังจะไปเหมือนกัน แต่เดี๋ยวแวะที่ Prontera สักพักแล้วค่อยไป Payon กันเถอะ ไปเก็บข้าวของแล้วออกเดินทางไปทั่วทวีปกัน!";
		next;
		mes "[Shorty]";
		mes "เย้! ท่านอาจารย์ การผจญภัยที่พวกเรามีร่วมกันมันสนุกมากเลย!";
		next;
		mes "[Salty]";
		mes "ข้าต้องขอบคุณท่านด้วย ดูเหมือนท่านจะแบก Shorty ไว้ในกระเป๋าใบเล็กๆ นั่นสินะ";
		next;
		mes "[Shorty]";
		mes "ท่านอาจารย์ ขอบคุณมากจริงๆ นะ";
		next;
		select( "นายจะไปแล้วเหรอ?" );
		mes "[Shorty]";
		mes "ความสัมพันธ์ของพวกเราจะไม่จบลงตรงนี้ จำไว้นะว่าเจ้าเคยพบข้า Salty มีอะไรอีกไหม? สิ่งที่จะเอาไว้เตือนความจำน่ะ";
		next;
		mes "[Salty]";
		mes "อือฮึ มีของแบบนั้นอยู่เหมือนกันนะ";
		next;
		mes "[Shorty]";
		mes "มีแค่นี้เหรอ? เพนกวิน? เพนกวิน? เจ้าชอบเพนกวินมากกว่าข้าเหรอ?";
		next;
		mes "[Salty]";
		mes "เปล่านะ ไม่ใช่... เดิมทีข้าซื้อมาจาก Paros เพื่อเอาไว้ระลึกถึงเจ้า แต่ตอนนี้มาคิดดูแล้ว เจ้าไม่ใช่เพนกวินนี่นา ข้าเลยเก็บมันไว้เฉยๆ ไม่น่ารักเหรอ?";
		next;
		mes "[Shorty]";
		mes "ก็น่ารักดีนะ";
		next;
		mes "[Salty]";
		mes "ข้าจะมอบสิ่งนี้ให้ท่าน ขอบคุณที่ช่วยดูแล Shorty นะ! Shorty ไปกันเถอะ!";
		next;
		specialeffect EF_TELEPORTATION2;
		cloakonnpcself();
		mes "[Shorty]";
		mes "ท่านอาจารย์ ต่อให้เวลาจะผ่านไปนานแค่ไหน ก็อย่าลืมข้านะ และจงเป็นนักผจญภัยที่ยิ่งใหญ่ที่มีชื่อเสียงไปทั่วทวีปเลยล่ะ!";
		specialeffect EF_TELEPORTATION2, AREA, "Shorty#fgtg005";
		cloakonnpcself( "Shorty#fgtg005" );
		erasequest 14694;
		if (isbegin_quest(14695) > 0)
			erasequest 14695;
		delitem 23278,1;	// HelpmeShorty
		getitem 20267,1;	// C_Penguin_Cap
		getexp 4000,1000;
		close;
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(14694) == 1" );
	cloakonnpc();
	end;
}

prt_fild10,315,189,7	duplicate(dummy_cloaked_npc)	Shorty#fgtg005	4_GALAPAGO


// Base for the dynamic npc (พื้นฐานสำหรับ NPC แบบไดนามิก)
izlude,1,1,0	script	Shorty#fgtg01	4_GALAPAGO,{
	if (isbegin_quest(14689) == 0) {
		mes "[Shorty]";
		mes "เก่งมาก ต่อไปถ้าเจ้าต้องการความช่วยเหลือจากข้า เจ้าเรียกข้าแบบนี้ได้เลยนะ";
		next;
		select( "นายช่วยอะไรฉันได้บ้าง?" );
		mes "[Shorty]";
		mes "ตอนมาที่นี่ครั้งแรกเจ้าไม่รู้อะไรเลยใช่ไหมล่ะ? ข้าจะสอนทุกอย่างที่ข้ารู้ให้เอง ^0000cdถามข้าได้ทุกเรื่องเลยนะ!^000000";
		next;
		mes "[Shorty]";
		mes "และข้าจะคอยให้บริการ ^0000cd(Services)^000000 ดีๆ หลายอย่างเพื่อช่วยเจ้าในการล่าด้วย";
		next;
		mes "[Shorty]";
		mes "สิ่งที่ข้าทำได้ก็คือ ^0000cdเพิ่มความคล่องตัว (Increase Agility)^000000 เพื่อให้เคลื่อนที่เร็วขึ้น และ ^0000cdการอวยพร (Blessing)^000000 เพื่อเพิ่มความแข็งแกร่ง ความแม่นยำ และสติปัญญา แต่มันช่วยได้แน่นอนใช่ไหมล่ะ?";
		next;
		mes "[Shorty]";
		mes "ใช่แล้ว! ถ้าเจ้าเรียกข้าออกมาแล้วไม่พูดอะไรเลยเป็นเวลาหนึ่งนาที ข้าจะกลับเข้าไปในกระเป๋าเองนะ";
		next;
		mes "[Shorty]";
		mes "และเมื่อธุระเสร็จแล้ว ก็แค่ปล่อยข้าไว้ ข้าจะสำรวจรอบๆ ตัวเจ้าแล้วกลับเข้าไปในกระเป๋าเอง ข้าตามเก่งมากนะจะบอกให้";
		next;
		select( "เมื่อไหร่นายจะไปหาคนที่ชื่อ Salty ล่ะ?" );
		mes "[Shorty]";
		mes "ยังไม่ใช่ตอนนี้หรอกท่านอาจารย์ Shorty ผู้มาจาก Galapagos คนนี้รู้จักบุญคุณคน เมื่อท่านสามารถถอดชุดผู้เริ่มต้นและเริ่มการผจญภัยด้วยตัวเองได้ เมื่อนั้น Shorty จะจากไป";
		next;
		mes "[Shorty]";
		mes "ในตอนนั้น ต่อให้ท่านจะดึงข้าไว้แล้วร้องไห้ ข้าก็จะต้องไป ก่อนหน้านั้น ท่านต้องเพิ่มเลเวลให้ได้ประมาณ ^0000cdเลเวล 31^000000 จนกว่าจะถึงตอนนั้น พวกเรามาพยายามกันให้เต็มที่เถอะ";
		setquest 14689;
		completequest 14689;
		next;
	}
	.@state = isbegin_quest(14693);
	.@state = ((.@state == 2 && isbegin_quest(14694) == 1) ? 1 : .@state);

	switch( .@state ) {
	case 0:
		if (BaseLevel < 31)
			break;
		mes "[Shorty]";
		mes "ท่านเติบโตขึ้นอย่างน่าอัศจรรย์เลยล่ะ ตอนนี้ข้าอยากไปพบ Salty แล้ว นางคงจะเป็นห่วงข้าแย่";
		next;
		select( "เพิ่งจะมาคิดได้ตอนนี้เหรอ?" );
		mes "[Shorty]";
		mes "จดหมายฉบับล่าสุดจาก Salty บอกว่าส่งมาจาก ^0000cdโรงเตี๊ยมของ Payon (Payon's Inn)^000000";
		next;
		mes "[Shorty]";
		mes "ไปกันเถอะ";
		mes "ไปหา <NAVI>[Inn]<INFO>payon_in01,193,23,0,101,0</INFO></NAVI> ใน Payon กัน";
		setquest 14693;
		close;
	case 1:
		break;
	case 2:
		mes "[Shorty]";
		mes "เจ้าเป็นใครน่ะ? เจ้ากำลังพูดกับใครอยู่? ข้าไม่ชอบให้คนที่ไม่รู้จักมาพูดด้วยหรือมาแตะต้องตัวข้านะ";
		close;
	}
	if (BaseLevel < 31) {
		switch( rand(4) ) {
		case 0:
			mes "[Shorty]";
			mes "เปิดหน้าต่างไอเทมแล้วเจ้าจะเห็น ^0000cdสี่แถบ (Tabs)^000000 เจ้าเห็นมันไหม?";
			next;
			mes "[Shorty]";
			mes "ถ้าเจ้าลากไอเทมที่ไม่ค่อยได้ใช้ หรือไอเทมที่ไม่ควรขายให้ NPC ไปไว้ใน ^0000cd<แถบส่วนตัว (Personal tab)> และกดปุ่มล็อคการซื้อขาย^000000 ที่ด้านล่าง เจ้าก็จะไม่เผลอขายไอเทมพวกนั้นทิ้งแน่นอน";
			break;
		case 1:
			mes "[Shorty]";
			mes "ท่านอาจารย์ ท่านเคยกดปุ่ม ^0000cdF12^000000 ไหม? จะมีช่องสี่เหลี่ยมสีขาวๆ ลอยขึ้นมา";
			next;
			mes "[Shorty]";
			mes "ลากไอคอนสกิลหรือไอเทมไปวางตรงนั้น ถ้าเจ้าปล่อยมือ เจ้าจะสามารถใช้สกิลและไอเทมได้อย่างรวดเร็วและง่ายดายเพียงแค่กดปุ่ม ^0000cdลัด (Shortcut)^000000 มันสะดวกมากเลยนะ";
			break;
		case 2:
			mes "[Shorty]";
			mes "เขาบอกว่าถ้าเจ้าไม่ต้องการหมวกหรือเสื้อผ้าที่ได้จากสถาบันแล้ว เจ้าสามารถนำไปแลกเป็นของที่มีประโยชน์ได้ที่ ^0000cdตู้ขายของอัตโนมัติที่สำนักงานเปลี่ยนอาชีพของแต่ละอาชีพ^000000 นะ";
			next;
			mes "[Shorty]";
			mes "Salty ก็ใช้มันบ่อยมากเลย ข้าอยากได้พวกหอยกาบถ้าเป็นไปได้นะ";
			break;
		case 3:
			mes "[Shorty]";
			mes "ก่อนจะไปลองเช็คดูหน่อยว่าที่ที่เราล่าไปเมื่อกี้มีอะไรบ้าง อาจจะมีกาดมอนสเตอร์ตกอยู่ก็ได้นะ";
			next;
			mes "[Shorty]";
			mes "นี่เป็นเคล็ดลับล้ำค่าที่ Salty สอนข้ามาเลยนะ~ เจ้ามีกาดมอนสเตอร์บ้างไหมล่ะ?";
			break;
		}
		setarray .@menu$[0], "ขอกำลังให้ฉันหน่อย!", "ฉันควรไปที่ไหนดี?", "มีอะไรดีๆ บ้างไหม?", "ฉันจะอัพสกิลและสเตตัสยังไง?", "บอกวิธีควบคุมหน่อย" ;
	}
	else {
		switch( rand(4) ) {
		case 0:
			mes "[Shorty]";
			mes "ท่านอาจารย์ ท่านรู้ไหมว่านอกจากทวีป Midgard ที่เรารู้จักแล้ว ท่านยังสามารถไปที่ใหม่ๆ ที่ลึกลับได้โดยการนั่งเรือด้วยนะ? ส่วนใหญ่จะชื่อ อัล... อัล... อัลเลโกร?";
			next;
			mes "[Shorty]";
			mes "^0000cdอ้อ! ท่านสามารถนั่งเรือไปได้จาก Alberta^000000! ไว้เราค่อยไปกันนะ! ท่านอาจารย์! มันคือการผจญภัยครั้งใหม่ล่ะ!";
			break;
		case 1:
			mes "[Shorty]";
			mes "ข้าได้ยินมาว่ามีดันเจี้ยนหลายแห่งที่แม้แต่นักผจญภัยอายุน้อยแบบท่านก็ไปได้เหมือนกันนะ";
			next;
			select( "ทำไมฉันถึงโง่แบบนี้เนี่ย!" );
			mes "[Shorty]";
			mes "ท่านอาจารย์ ท่านพูดอะไรน่ะ? ยังไงก็ตาม มีทั้งท่อระบายน้ำใต้ดิน, ถ้ำ Payon, ดันเจี้ยนมด และเรืออับปาง ท่านไม่อยากเป็นเจ้าแห่งดันเจี้ยนเหรอท่านอาจารย์?";
			next;
			select( "ถ้านายช่วยฉันสู้ล่ะก็นะ" );
			mes "[Shorty]";
			mes "ท่านอาจารย์ ท่านควรดูแลร่างกายตัวเองให้ดีนะ เพราะท่านมีแค่ชีวิตเดียว";
			break;
		case 2:
			mes "[Shorty]";
			mes "ท่านอาจารย์พร้อมหรือยัง? ข้าอยากไปจริงๆ นะ!";
			next;
			select( "มันคืออะไรเหรอ? อยู่ที่ไหนล่ะ?" );
			mes "[Shorty]";
			mes "ท่านไม่รู้จัก Ohiso, Boiso, Saiso, ^0000cdParamarket^000000 งั้นเหรอ? ท่านอาจารย์จะทำยังไงถ้าไม่มีข้าเนี่ย จริงๆ เลย";
			next;
			mes "[Shorty]";
			mes "Paramarket เป็นสถานที่ที่ขายไอเทมที่หาได้ยากในตลาดหรือแผงลอยริมถนน โดยมีจำนวนจำกัดและขายในเวลาจำกัด เป็นที่ที่ดีมากในการทำธุรกิจเลยล่ะ";
			next;
			select( "ฉันจะไปที่นั่นได้ยังไง?" );
			mes "[Shorty]";
			mes "คุยกับ Paramarket Windman ใน ^0000cdParadise Corridor^000000 แล้วเขาจะส่งท่านไปที่นั่นเอง";
			break;
		case 3:
			mes "[Shorty]";
			mes "ท่านอาจารย์ บางครั้งเวลาท่านอยากซื้อของจากแผงลอยริมถนน ท่านอาจจะสงสัยว่าตอนนี้มีใครขายอยู่บ้างและใครเป็นคนขาย";
			next;
			select( "จริงด้วย ฉันดูแผงลอยทั้งหมดไม่ไหวหรอก" );
			mes "[Shorty]";
			mes "ในกรณีนั้น ท่านสามารถใช้ ^0000cdแคตตาล็อก (Catalog)^000000 ได้ เห็นเขาบอกว่าตอนนี้มี ^0000cdตู้ขายของอัตโนมัติใน Paradise หรือพ่อค้าใน Alberta^000000 กำลังขายมันอยู่";
			next;
			mes "[Shorty]";
			mes "วันนี้ข้าก็อยากไปช้อปปิ้งเหมือนกันนะ";
			break;
		}
		setarray .@menu$[5], "มีอะไรดีๆ บ้าง?";
	}
	next;
	mes "[Shorty]";
	mes "เจ้าต้องการความช่วยเหลือเพิ่มเติมจากข้าไหม?";
	next;
	switch( select( .@menu$[0], .@menu$[1], .@menu$[2], .@menu$[3], .@menu$[4], .@menu$[5 ], "คู่มือข้อมูล", "ฉันสงสัยเกี่ยวกับสิ่งที่นายพูด", "แยกทางกันตอนนี้เลย" ) ) {
	case 1:
		switch( checkquest(14695,PLAYTIME) ) {
		case -1:
			mes "[Shorty]";
			mes "ท่านอาจารย์ ข้าจะให้ความช่วยเหลือตามที่ข้าเคยสัญญาไว้ เพิ่มความคล่องตัวและการอวยพร";
			next;
			mes "[Shorty]";
			mes "แต่ข้าแอบเรียนเรื่องนี้มาน่ะ และพอข้าทำทีหนึ่ง วิญญาณข้าก็จะเหนื่อยมาก เลยให้บ่อยๆ ไม่ได้ ข้าต้องใช้เวลาพักฟื้นประมาณครึ่งชั่วโมงนะ";
			next;
			mes "[Shorty]";
			mes "ถ้าท่านอยากรู้ว่าเมื่อไหร่จะขอรับได้อีก ให้เปิดหน้าต่างเควสต์ด้วย ALT+U แล้วดูเวลาที่แสดงอยู่ด้านล่างนะ";
			next;
			mes "[Shorty]";
			mes "ไปเลย! ท่านอาจารย์!!";
			npcskill "AL_INCAGI",10,99,60;
			npcskill "AL_BLESSING",10,99,60;
			setquest 14695;
			close;
		case 0:
		case 1:
			mes "[Shorty]";
			mes "ท่านอาจารย์ มันยังไม่ถึง 30 นาทีเลยนะ ข้ายังเหนื่อยอยู่เลย ข้าจะให้กำลังท่านเพิ่มถ้าข้าไหวแล้ว เพราะงั้นรอหน่อยนะ";
			close;
		case 2:
			mes "[Shorty]";
			mes "ไปเลย! ท่านอาจารย์!!";
			npcskill "AL_INCAGI",10,99,60;
			npcskill "AL_BLESSING",10,99,60;
			erasequest 14695;
			setquest 14695;
			close;
		}
		end;
	case 2:
		mes "[Shorty]";
		mes "ท่านอาจารย์อยากเรียนรู้เกี่ยวกับเรื่องอะไรล่ะ?";
		next;
		switch( select( "เส้นทางของนักผจญภัยมือใหม่!", "ฉันอยากช่วยผู้คนรอบข้าง", "ฉันไม่ชอบเส้นทางที่ถูกกำหนดไว้", "ไม่มีที่ไหนที่ฉันอยากไปเป็นพิเศษ" ) ) {
		case 1:
			mes "[Shorty]";
			mes "ข้าจำได้ว่าเห็นอาจารย์ ^0000cd<Thinking Archer>^000000 อยู่แถวทางเข้า Izlude นะ แต่ดูเหมือนเขาจะมีความกังวลเยอะเลย ทำไมท่านไม่ลองไปถามดูล่ะว่าเกิดอะไรขึ้น? ไปกันเถอะ ไปกันเถอะ~";
			close;
		case 2:
			mes "[Shorty]";
			mes "ทำไมไม่ไปเจอ Paul และ Anne ที่ด้านนอก ^0000cdประตูทิศตะวันตกของ Prontera (West Gate)^000000 ล่ะ? ตอนข้าเดินผ่าน ข้าได้ยินว่าพวกเขาต้องการความช่วยเหลือนะ";
			next;
			mes "[Shorty]";
			mes "หรือลองไปฟังเรื่องจากชายวัยกลางคนที่ดูวิตกกังวลใน ^0000cdPayon^000000 ดูไหม?";
			close;
		case 3:
			mes "[Shorty]";
			mes "การกด ^0000cdCtrl+`^000000 จะเป็นการเปิดแผนที่นะ";
			next;
			mes "[Shorty]";
			mes "ท่านเห็นช่องสำหรับเลือกภูมิภาคที่มุมบนซ้ายไหม?";
			next;
			mes "[Shorty]";
			mes "หลังจากเลือกพื้นที่ที่ท่านอยากดูแล้ว ให้เช็คที่ ^0000cdแถบมอนสเตอร์ (Monster tab)^000000 ที่มุมบนขวา หรือกดปุ่ม ^0000cdTab^000000 ท่านจะเห็นชื่อและเลเวลของมอนสเตอร์ที่กระจายตัวอยู่ในพื้นที่นั้นมากที่สุด";
			next;
			mes "[Shorty]";
			mes "มันจะดีกว่าถ้าท่านไปผจญภัยในที่ที่เลเวลเหมาะกับท่านนะ ถ้า ^0000cdเลเวลต่างกับมอนสเตอร์มากเกินไป^000000 ท่านจะไม่ได้รับประสบการณ์ 100% เต็ม ท่านต้องคำนวณส่วนต่างเลเวลด้วยใช่ไหมล่ะ?";
			next;
			mes "[Shorty]";
			mes "ถ้าท่านกดไปที่เมืองที่ท่านอยากไป เมนูนำทางจะปรากฏขึ้น กดปุ่ม ^0000cd<Guide to the target> ที่ด้านล่าง^000000 แล้วออกจากแผนที่เพื่อดูการนำทางได้เลย";
			close;
		case 4:
			mes "[Shorty]";
			mes "ท่านอาจารย์ ท่านเบื่อเหรอ? ถ้าอย่างนั้นท่านก็คุยกับคนรอบข้างดูสิ";
			next;
			mes "[Shorty]";
			mes "นอกเหนือจากนั้น ลองทำ ^0000cdเควสต์ของ Paradise^000000 ดูก็ดีนะ ท่านอยากไป Paradise ไหม? เขาบอกว่าจะมอบอาวุธให้ด้วยถ้าท่านไปที่นั่น";
			next;
			mes "[Shorty]";
			mes "ท่านอยากไปที่ไหนล่ะ? พิมพ์ ^0000cd/place^000000 ในช่องแชทและเปรียบเทียบตำแหน่งปัจจุบันกับแผนที่ที่ปรากฏขึ้นจากการกดปุ่ม ^0000cdCtrl+`^000000 ดูสิ";
			next;
			switch( select( "Prontera", "Geffen", "Payon", "Morroc", "Alberta", "เสร็จสิ้น" ) ) {
			case 1:
				mes "[Shorty]";
				mes "คลิกที่ <NAVI>[Prontera]<INFO>prontera,124,76,0,101,0</INFO></NAVI> เพื่อค้นหา";
				break;
			case 2:
				mes "[Shorty]";
				mes "คลิกที่ <NAVI>[Geffen]<INFO>geffen,132,66,0,101,0</INFO></NAVI> เพื่อค้นหา";
				break;
			case 3:
				mes "[Shorty]";
				mes "คลิกที่ <NAVI>[Payon]<INFO>payon,177,111,0,101,0</INFO></NAVI> เพื่อค้นหา";
				break;
			case 4:
				mes "[Shorty]";
				mes "คลิกที่ <NAVI>[Morroc: Paradise Space Traveler]<INFO>morocc,161,97,0,101,0</INFO></NAVI> เพื่อค้นหา";
				next;
				mes "[Shorty]";
				mes "Morroc คือที่ที่ Paradise ตั้งอยู่ เดินเล่นแถวนั้นก็น่าจะโอเคนะ?";
				next;
				mes "[Shorty]";
				mes "ไปที่ <NAVI>[Morroc Paradise]<INFO>morocc,206,285,0,101,0</INFO></NAVI>";
				break;
			case 5:
				mes "[Shorty]";
				mes "คลิกที่ <NAVI>[Alberta]<INFO>alberta,124,67,0,101,0</INFO></NAVI> เพื่อค้นหา";
				break;
			case 6:
				mes "[Shorty]";
				mes "ตกลงไหม? งั้นเดี๋ยวข้าขอสำรวจรอบๆ แป๊บหนึ่งแล้วจะกลับเข้าไปในกระเป๋านะ";
				close;
			}
			next;
			mes "[Shorty]";
			mes "ไปลงทะเบียนกับ Lime Ivenor ก่อน แล้วค่อยเช็คบอร์ดข่าวสารข้างๆ นะ";
			next;
			mes "[Shorty]";
			mes "มันจะดีมากถ้าได้เดินทางไปตามเมืองและทุ่งหญ้าเพื่อช่วยเหลือผู้คน คนที่ต้องการความช่วยเหลือจะหาได้ง่ายขึ้นเพราะมีเครื่องหมาย <!> อยู่เหนือหัวน่ะ";
			close;
		}
		end;
	case 3:
		mes "[Shorty]";
		mes "ท่านยังไม่ได้เข้าร่วม Paradise Club เหรอ?";
		next;
		mes "[Shorty]";
		mes "ท่านอยากไปที่ ^0000cdParadise^000000 ไหม? เขาบอกว่าถ้าไปที่นั่น เขาจะให้พวกอาวุธด้วยนะ";
		next;
		mes "[Shorty]";
		mes "ท่านอยากไปที่ไหนล่ะ? พิมพ์ /place ในช่องแชทแล้วเปรียบเทียบตำแหน่งปัจจุบันกับแผนที่ที่ขึ้นมาตอนกด ^0000cdCtrl+`^^000000 ที่ไหนใกล้ที่สุดล่ะ?";
		next;
		switch( select( "Prontera", "Geffen", "Payon", "Morroc", "Alberta", "เสร็จสิ้น" ) ) {
		case 1:
			mes "[Shorty]";
			mes "คลิกที่ <NAVI>[Prontera]<INFO>prontera,124,76,0,101,0</INFO></NAVI> เพื่อค้นหา";
			break;
		case 2:
			mes "[Shorty]";
			mes "คลิกที่ <NAVI>[Geffen]<INFO>geffen,132,66,0,101,0</INFO></NAVI> เพื่อค้นหา";
			break;
		case 3:
			mes "[Shorty]";
			mes "คลิกที่ <NAVI>[Payon]<INFO>payon,177,111,0,101,0</INFO></NAVI> เพื่อค้นหา";
			break;
		case 4:
			mes "[Shorty]";
			mes "คลิกที่ <NAVI>[Morroc: Paradise Space Traveler]<INFO>morocc,161,97,0,101,0</INFO></NAVI> เพื่อค้นหา";
			next;
			mes "[Shorty]";
			mes "Morroc คือที่ที่ Paradise ตั้งอยู่ เดินดูแถวนั้นก็น่าจะโอเคนะ?";
			next;
			mes "[Shorty]";
			mes "ไปที่ <NAVI>[Morroc Paradise]<INFO>morocc,206,285,0,101,0</INFO></NAVI>";
			break;
		case 5:
			mes "[Shorty]";
			mes "คลิกที่ <NAVI>[Alberta]<INFO>alberta,124,67,0,101,0</INFO></NAVI> เพื่อค้นหา";
			break;
		case 6:
			mes "[Shorty]";
			mes "ตกลงไหม? งั้นเดี๋ยวข้าขอสำรวจรอบๆ แป๊บหนึ่งแล้วจะกลับเข้าไปในกระเป๋านะ";
			close;
		}
		next;
		mes "[Shorty]";
		mes "^0000cdไปลงทะเบียนกับ Lime Ivenor ก่อนนะ^000000 แล้วค่อยคุยกับ Boyana Ur และเจ้าหน้าที่ Paradise Logistics ที่อยู่ทั้งสองฝั่ง";
		next;
		mes "[Shorty]";
		mes "หลังจากทำเควสต์และฝึกฝนบางอย่าง พวกเขาจะให้อุปกรณ์แก่ข้าด้วย";
		close;
	case 4:
		mes "[Shorty]";
		mes "เจ้าสงสัยอะไรเกี่ยวกับสเตตัสและสกิลล่ะ?";
		next;
		switch( select( "ฉันสงสัยเกี่ยวกับสกิล", "ฉันสงสัยเกี่ยวกับสเตตัส", "ฉันไม่สนใจแล้ว" ) ) {
		case 1:
			mes "[Shorty]";
			mes "กดปุ่ม ^0000cdAlt+S^000000 เพื่อเปิดหน้าต่างรายการสกิลขึ้นมา";
			next;
			mes "[Shorty]";
			mes "ถ้าเจ้าติ๊กถูกที่ช่อง ^0000cd<View skill description> ที่มุมขวาบน^000000 เจ้าจะดูคำอธิบายสกิลได้เมื่อเอาเมาส์ไปชี้ที่ไอคอนสกิลนะ";
			next;
			mes "[Shorty]";
			mes "เจ้าเรียนรู้สกิลได้โดยการใช้ ^0000cd<แต้มสกิล (Skill Points)>^000000 ซึ่งจะได้มาทุกครั้งที่เลเวลอาชีพ (Job Level) เพิ่มขึ้น เจ้าเช็คแต้มสกิลที่มีได้ที่มุมล่างซ้ายของหน้าต่างนี้นะ";
			next;
			mes "[Shorty]";
			mes "ตอนนี้ท่านอาจารย์มี " + SkillPoint + " แต้มสกิล " + (SkillPoint > 1 ? "แต้ม" : "แต้ม") + ".";
			next;
			mes "[Shorty]";
			mes "ท่านอาจารย์รู้จักผังสกิล (Skill Tree) ไหม? ถ้าท่านเอาเมาส์ชี้ไปที่สกิลที่อยากเรียน มันจะบอกว่าต้องเรียนสกิลไหนก่อนและเลเวลเท่าไหร่";
			next;
			mes "[Shorty]";
			mes "นั่นแหละคือผังสกิล อย่างไรก็ตาม แต้มสกิลที่ใช้เรียนสกิลมีจำกัด ดังนั้นเจ้าเรียนสกิลทั้งหมดไม่ได้หรอกนะ";
			next;
			mes "[Shorty]";
			mes "มีแต้มสกิลทั้งหมด 49 แต้มที่ใช้เรียนได้ก่อนที่ท่านจะเปลี่ยนเป็นอาชีพที่สูงขึ้น เงื่อนไขนี้เรียกว่าการหยุดนิ่ง (Stagnation)";
			next;
			mes "[Shorty]";
			mes "^0000cdถ้าเลเวลอาชีพของเจ้าถึง 40 เจ้าจะเปลี่ยนเป็นอาชีพขั้นที่สองได้^000000 แต่ขอแนะนำให้เปลี่ยนอาชีพตอนเลเวล 50 นะ";
			next;
			mes "[Shorty]";
			mes "เพราะมันจะช่วยให้ขั้นตอนการเปลี่ยนอาชีพง่ายขึ้น จะมีเหตุผลอะไรที่ยอดเยี่ยมไปกว่านี้อีกล่ะ?";
			next;
			mes "[Shorty]";
			mes "อย่ากังวลมากเกินไปถ้าเจ้าคิดว่าอัพสกิลผิด";
			next;
			mes "[Shorty]";
			mes "เจ้าสามารถรีเซ็ตได้ทุกเมื่อจนกว่าท่านจะเปลี่ยนอาชีพเป็นอาชีพที่สูงขึ้น";
			next;
			mes "[Shorty]";
			mes "ไปหา <NAVI>[Izlude: The Hypnotist]<INFO>izlude,127,175,0,101,0</INFO></NAVI> เพื่อขอคำแนะนำได้เลย";
			close;
		case 2:
			mes "[Shorty]";
			mes "ไหนดูสิท่านอาจารย์...";
			next;
			mes "[Shorty]";
			mes "ข้าได้ยินมาว่าสำหรับ Taekwon เจ้าควรอัพหลักๆ คือ Str, Agi, และ Dex สำหรับข้อมูลเพิ่มเติม";
			mes "โปรดไปหาที่ <NAVI>[Payon: Ahri]<INFO>payon,245,295,0,101,0</INFO></NAVI>";
			next;
			mes "[Shorty]";
			mes "อย่ากังวลมากเกินไปถ้าเจ้าคิดว่าอัพสเตตัสผิด";
			next;
			mes "[Shorty]";
			mes "เจ้าสามารถรีเซ็ตได้ทุกเมื่อจนกว่าท่านจะเปลี่ยนอาชีพเป็นอาชีพที่สูงขึ้น";
			mes "ไปหา <NAVI>[Izlude: The Hypnotist]<INFO>izlude,127,175,0,101,0</INFO></NAVI> เพื่อขอคำแนะนำได้เลย";
			close;
		case 3:
			mes "[Shorty]";
			mes "เรียกข้าได้ทุกเมื่อถ้ามีคำถามนะ ข้าจะสำรวจแถวนี้หน่อยแล้วจะกลับเข้าไปในกระเป๋า";
			close;
		}
		end;
	case 5:
		switch( select( "การควบคุมพื้นฐาน", "คู่มือปุ่มลัด", "ยกเลิก" ) ) {
		case 1:
			while(1) {
				mes "[Shorty]";
				mes "เจ้าสงสัยเรื่องอะไรล่ะ?";
				next;
				switch( select( "การเคลื่อนที่และโจมตี", "การคุยกับ NPC", "การเช็คเควสต์", "การเช็คสเตตัสและสกิล", "การถอดอุปกรณ์", "การซื้อขาย" ) ) {
				case 1:
					mes "[Shorty]";
					mes "^0000cd<เคลื่อนที่ (Move)>^000000 - คลิกจุดที่อยากไป ^0000cdด้วยเมาส์^000000";
					mes "^0000cd<โจมตี (Attack)>^000000 - คลิกที่เป้าหมาย ^0000cdเพื่อโจมตีด้วยเมาส์^000000";
					next;
					mes "[Shorty]";
					mes "เป็นไงล่ะ? ง่ายใช่ไหม? เจ้ายังหมุนมุมมองได้ด้วยการกดปุ่มขวาของเมาส์ค้างไว้แล้วหมุนด้วยนะ";
					break;
				case 2:
					mes "[Shorty]";
					mes "<คุย (Conversation)> - คลิกที่เป้าหมาย ^0000cdเพื่อคุยด้วยเมาส์^000000";
					next;
					mes "[Shorty]";
					mes "บางครั้งเจ้าต้องเข้าไปใกล้ๆ เพื่อคุยกับใครสักคนนะ";
					break;
				case 3:
					mes "[Shorty]";
					mes "คลิกที่ ^0000cdไอคอนเควสต์ในเมนูหลักที่อยู่มุมบนซ้าย";
					mes "ปุ่มลัด: ^0000cdAlt+U^000000";
					next;
					mes "[Shorty]";
					mes "เมื่อท่านอาจารย์ได้รับเควสต์ ข้อมูลเควสต์จะถูกแสดงสั้นๆ ที่ด้านขวาของหน้าจอ ท่านสามารถเช็คได้เลย";
					next;
					mes "[Shorty]";
					mes "ถ้ามีเควสต์ที่เจ้าไม่อยากเห็นในรายการ ให้เอาเมาส์ไปชี้ที่ชื่อเควสต์แล้วกดปุ่มขวาของเมาส์เพื่อส่งมันไปที่แถบรอดำเนินการ (Pending tab) นะ";
					break;
				case 4:
					mes "[Shorty]";
					mes "<เช็คสเตตัส> - คลิกที่ ^0000cdไอคอนสเตตัสในเมนูหลักที่อยู่มุมบนซ้าย^000000";
					mes "ปุ่มลัด: ^0000cdAlt+A^000000";
					next;
					mes "[Shorty]";
					mes "<เช็คสกิล> - คลิกที่ ^0000cdไอคอนสกิลในเมนูหลักที่อยู่มุมบนซ้าย^000000";
					mes "ปุ่มลัด: ^0000cdAlt+S^000000";
					next;
					mes "[Shorty]";
					mes "อย่าลืมนะ! สกิลต้องเปิดใช้งานโดยการดับเบิ้ลคลิกไอคอนที่อยากใช้ และร่ายโดยการกดปุ่มซ้ายของเมาส์หนึ่งครั้ง!";
					break;
				case 5:
					mes "[Shorty]";
					mes "<การสวมใส่และถอดอุปกรณ์> - คลิกที่ ^0000cdไอคอนอุปกรณ์ในเมนูหลักที่อยู่มุมบนซ้าย^000000";
					mes "ปุ่มลัด: ^0000cdAlt+Q^000000";
					next;
					mes "[Shorty]";
					mes "ปกติแล้ว เจ้าสวมใส่และถอดได้โดยการดับเบิ้ลคลิกปุ่มซ้ายของเมาส์ในช่องเก็บของหรือหน้าต่างอุปกรณ์";
					next;
					mes "[Shorty]";
					mes "ลูกธนูและกระสุนสวมใส่ได้โดยการลากพวกมันจากช่องเก็บของมาที่ตัวละครในหน้าต่างอุปกรณ์เท่านั้นนะ";
					next;
					mes "[Shorty]";
					mes "ถ้าท่านอาจารย์เป็น Merchant ท่านสามารถรถเข็นได้ ซึ่งยืมได้จากเจ้าหน้าที่ Kafra ในแต่ละเมืองนะ";
					break;
				case 6:
					mes "[Shorty]";
					mes "<การซื้อขายในร้านค้า> - คลิกที่ NPC พ่อค้า ^0000cdเพื่อทำการค้า^000000";
					mes "<การซื้อขายระหว่างผู้เล่น> - คลิกขวาที่ตัวละครที่ท่านอยากแลกเปลี่ยนด้วย แล้วเลือก ^0000cdแลกเปลี่ยน (Trade)^000000";
					next;
					mes "[Shorty]";
					mes "<การซื้อและขายของที่แผงลอย> - คลิกเมาส์ที่หน้าต่างแผงลอย";
					break;
				}
				next;
			}
			end;
		case 2:
			while(1) {
				mes "[Shorty]";
				mes "ท่านสงสัยเรื่องอะไรหรือ?";
				next;
				switch( select( "เคล็ดลับประจำวัน", "รายการคำสั่ง", "หน้าต่างคีย์ลัดสกิลและไอเทม", "รายการคีย์ลัดและการลงทะเบียน", "รายการคำสั่งระบบ", "รายการอีโมชัน" ) ) {
				case 1:
					mes "[Shorty]";
					mes "พิมพ์ ^0000cd/tip^000000 ในช่องสนทนาสิ";
					next;
					mes "[Shorty]";
					mes "ข้อมูลการใช้ชีวิตมากมายถูกรวมไว้ในนั้นแล้ว เวลาข้าเบื่อๆ ข้ามักจะหยิบพวกมันมาอ่านเสมอเลยล่ะ";
					break;
				case 2:
					mes "[Shorty]";
					mes "พิมพ์ ^0000cd/h^000000 ในช่องสนทนาสิ";
					next;
					mes "[Shorty]";
					mes "ท่านจะสามารถดูข้อมูลที่จำเป็นสำหรับนักผจญภัยมือใหม่ได้";
					break;
				case 3:
					mes "[Shorty]";
					mes "กดปุ่ม ^0000cdF12^000000 เพื่อลงทะเบียนไอเทมสิ้นเปลือง, สกิล และอุปกรณ์ โดยการลากพวกมันมาวางในหน้าต่างคีย์ลัด";
					next;
					mes "[Shorty]";
					mes "ท่านรู้ไหมว่าการกดใช้ไอเทมและการสลับอุปกรณ์อย่างรวดเร็ว เป็นสิ่งจำเป็นมากสำหรับการต่อสู้ที่มีประสิทธิภาพ?";
					break;
				case 4:
					mes "[Shorty]";
					mes "กดปุ่ม ^0000cdAlt+M^000000 เพื่อลงทะเบียนคำสั่งคีย์ลัดที่ใช้งานบ่อยๆ นะ~";
					next;
					mes "[Shorty]";
					mes "ข้าใช้ฟังก์ชันนี้แหละในการลงทะเบียนคำสั่งที่ต้องใช้บ่อยๆ";
					break;
				case 5:
					mes "[Shorty]";
					mes "กดปุ่ม ^0000cdAlt+Y^000000 เพื่อตรวจสอบรายการคำสั่งและเปิด/ปิดการใช้งาน";
					next;
					mes "[Shorty]";
					mes "ท่านสามารถตรวจสอบและเปิด/ปิดตัวเลือกต่างๆ ได้ตามความต้องการของท่านเลย";
					break;
				case 6:
					mes "[Shorty]";
					mes "กดปุ่ม ^0000cdAlt+L^000000 เพื่อดูและใช้งานรายการอีโมชันและคำสั่งลัดต่างๆ";
					next;
					mes "[Shorty]";
					mes "นายท่าน ท่านรู้ไหมว่าการสนทนาไม่ได้มีแค่ตัวอักษรนะ? การใช้อีโมชันที่เหมาะสมจะช่วยให้การสนทนาดูมีสีสันมากขึ้นเยอะเลย";
					break;
				}
				next;
			}
			end;
		case 3:
			mes "[Shorty]";
			mes "รับทราบ หากต้องการอะไรก็เรียกข้าอีกนะ";
			close;
		}
		end;
	case 6:
		mes "[Shorty]";
		mes "นายท่านไม่อยากประสบความสำเร็จงั้นเหรอ? Salty บอกว่าเธอกำลังฝึกฝนอย่างหนักเพราะเธออยากจะประสบความสำเร็จนะ";
		next;
		mes "[Shorty]";
		mes "ประเพณีดั้งเดิมคือการ ^0000cdไปเกิดใหม่ใน Valhalla และกลับมาเป็น Novice อีกครั้ง^000000 แต่ไม่ใช่ Novice ธรรมดานะ เขาเรียกกันว่า High Novice";
		next;
		mes "[Shorty]";
		mes "พูดง่ายๆ ก็คือการวิวัฒนาการนั่นแหละ คนที่สืบทอดกันมาบอกว่า การได้กลับไปประกอบอาชีพเดิมอีกครั้งมีข้อดีมากมายเลย";
		next;
		mes "[Shorty]";
		mes "ค่าสถานะต่างๆ จะเพิ่มขึ้น และที่สำคัญที่สุดคือถ้าท่านมีอาชีพขั้นที่สอง ท่านจะสามารถเพิ่ม ^0000cdJob Level ได้ถึง 70^000000 เลยล่ะ ท่านจะสามารถใช้สกิลได้มากขึ้นด้วยนะ";
		next;
		mes "[Shorty]";
		mes "ถ้าท่านอยากจะประสบความสำเร็จ ลองไปหาคนชื่อ";
		mes "<NAVI>[Metheus Sylph]<INFO>yuno_in02,88,164,0,101,0</INFO></NAVI> ^0000cdที่ Sage Academy ใน Yuno^000000 ดูนะ เขาจะคอยให้คำแนะนำท่านเอง";
		next;
		mes "[Shorty]";
		mes "ถ้าท่านไม่สนใจเรื่องการจุติ (Transcending) ท่านก็สามารถเปลี่ยนอาชีพไปตามปกติได้เลย แต่เขาว่ากันว่าการจุติใหม่ก่อนจะดีกว่ามากเมื่อมองถึงอนาคต";
		next;
		mes "[Shorty]";
		mes "หากท่านไม่สนใจเรื่องการเปลี่ยนอาชีพหรือการจุติ...";
		next;
		mes "[Shorty]";
		mes "ทวีปนี้เต็มไปด้วยเทศกาลตลอดทั้งปีเลยนะ สนใจไปร่วมงานเทศกาลเวลาที่ท่านว่างดูไหมล่ะ?";
		next;
		mes "[Shorty]";
		mes "ตอนนี้...";
		next;
		mes "[Shorty]";
		mes "ข้าได้ยินมาว่าจะมีการแข่งขันล่าสัตว์ในฤดูหนาวจัดขึ้นโดยราชวงศ์ที่พระราชวังด้วยล่ะ! ข้าอยากไปแข่งล่าสัตว์! อยากไปจริงๆ นะ! ไปสู้กับมอนสเตอร์กัน!!! ไปที่ Prontera กันเถอะ!!!!";
		next;
		mes "[Shorty]";
		mes "ท่านต้องพาข้าไปด้วยแน่ๆ ใช่ไหม?";
		close;
	case 7:
		mes "[Shorty]";
		mes "นายท่าน ข้าจะบอกพื้นฐานให้ท่านฟังนะ แต่มันคือแค่พื้นฐานจริงๆ เท่านั้นล่ะ";
		next;
		mes "[Shorty]";
		mes "สำหรับข้อมูลเพิ่มเติม";
		mes "ลองเข้าไปดูที่";
		mes "<URL>[Rune Midgarts Library]<INFO>http://ro.gnjoy.com/guide/runemidgarts/,1024,768</INFO></URL>";
		next;
		mes "[Shorty]";
		mes "ท่านสงสัยข้อมูลส่วนไหนล่ะ?";
		next;
		switch( select("ข้อมูลสถานที่", "ข้อมูลเควสต์", "ข้อมูลพื้นที่ล่า", "ข้อมูลระบบ", "ยกเลิก" ) ) {
		case 1:
			while(1) {
				mes "[Shorty]";
				mes "ท่านสงสัยที่ไหนล่ะ?";
				next;
				switch( select( "ทวีป Midgard", "ทวีป Far-Star", "Malangdo", "พื้นที่ท้องถิ่น", "โลกต่างมิติ (Otherworld)", "ข้าไม่สนใจ" ) ) {
				case 1:
					mes "[Shorty]";
					mes "^0000cdทวีป Midgard^000000 ว่ากันว่าเป็นพื้นที่ที่มีอิทธิพลมากที่สุดของสามอาณาจักร คือ Arunafelts, Schwaldzwald, และ Rune-Midgarts แต่ละทวีปจะมีเรือเหาะที่ท่านสามารถใช้บริการได้อย่างสะดวกสบาย";
					next;
					mes "[Shorty]";
					mes "^0000cdRune-Midgarts^000000 คืออาณาจักรที่ปกครองโดยเจ็ดตระกูลขุนนางและมีพลังแห่งเวทมนตร์ครอบคลุมอยู่";
					next;
					mes "[Shorty]";
					mes "เมืองหลวงคือ Prontera, เมืองดาวเทียม Izlude, เมือง Geffen ที่เหล่านักเวทมารวมตัวกัน, Payon ทางทิศตะวันออก, Alberta เมืองท่าที่ท่านสามารถเดินทางไปยังภูมิภาคต่างๆ ได้, Aldebaran ที่มีหอนาฬิกา และ Morroc";
					next;
					mes "[Shorty]";
					mes "พวกเราเจอกันที่ Izlude ในอาณาจักร Rune-Midgarts จำได้ไหม? แล้วก็มี Payon ที่ Salty อยู่ และ Paros ที่ข้าจากมา ทั้งหมดล้วนอยู่ใน Rune-Midgarts ทั้งนั้น";
					next;
					mes "[Shorty]";
					mes "^0000cdArunafelz^000000 ที่นี่เป็นดินแดนแห่งศาสนจักรที่บูชาเทพธิดา ดังนั้นจึงมีพระสันตะปาปา (Pope) ปกครองอยู่ แน่นอนว่าท่านไม่ใช่จะเจอใครได้ง่ายๆ แต่ก็นะ เป็นไปได้ไหม? บางทีเราอาจจะได้พบอย่างน้อยสักครั้ง";
					next;
					mes "[Shorty]";
					mes "Arunafelz มีเมืองหลวงแห่งวิหารอย่าง Rachel และเมืองในหุบเขาอย่าง Veins";
					next;
					mes "[Shorty]";
					mes "สาธารณรัฐ ^0000cdSchwaldzwald^000000 ตามชื่อเลย ที่นี่ปกครองโดยประธานาธิบดี เมืองหลวงคือ Yuno เป็นเมืองที่ลอยอยู่บนฟ้า มันคุ้มค่าที่จะไปเยือนสักครั้งนะ ต้องงดงามมากแน่ๆ";
					next;
					mes "[Shorty]";
					mes "และยังมี Lighthalzen ซึ่งเป็นที่ตั้งสำนักงานใหญ่ของ Rekenberg, เมืองแห่งการทำเหมือง Einbroch, เหมือง Einbech และยังมีหมู่บ้านชนบทอย่าง Hugel ด้วย";
					next;
					mes "[Shorty]";
					mes "นอกจากนี้ยังมีหมู่บ้าน Utan อย่าง Umbala หรือ Niflheim ดินแดนแห่งคนตาย เป็นสถานที่ที่ข้าอยากลองไปสักครั้งมาตลอดเลยล่ะ!";
					break;
				case 2:
					mes "[Shorty]";
					mes "หลายคนเคยคิดว่าบนโลกนี้มีเพียงทวีป Midgard เท่านั้น จนกระทั่งวันหนึ่ง มีผู้คนในรูปลักษณ์ของ ^0000cdแมว^000000 ปรากฏตัวขึ้น ไม่มีใครรู้ว่าพวกเขามาจากไหน";
					next;
					mes "[Shorty]";
					mes "คนกลุ่มนี้เรียกว่า Doram มาจากสถานที่ที่อยู่ไกลออกไปโพ้นทะเล เรียกว่า ^0000cdทวีป Far-Star^000000 พวกเขาบอกว่าเส้นทางเพิ่งจะเปิดให้เดินทางได้เมื่อไม่นานมานี้เอง";
					next;
					mes "[Shorty]";
					mes "ในทวีป Far-Star นั้น เมืองท่า ^0000cdLasagna^000000 มีชื่อเสียงที่สุด ข้าเองก็อยากไปที่นั่นเหมือนกัน";
					next;
					mes "[Shorty]";
					mes "การจะมาที่นี่ ต้องนั่งเรือมาจาก Malangdo หรือ Izlude ท่านจำได้ไหมว่าพวกเราลงเรือที่ไหนกัน?";
					break;
				case 3:
					mes "[Shorty]";
					mes "ท่านคงเคยเห็นคนในรูปลักษณ์แมวที่เรียกว่า Doram เดินผ่านไปผ่านมาบ้างแล้ว พวกเขาไม่ได้ติดต่อกับทวีป Midgard มาตั้งแต่แรก แต่ตอนนี้พวกเขาได้เริ่มฝังรากลึกและตั้งรกรากอยู่ไปทั่วทุกแห่ง";
					next;
					mes "[Shorty]";
					mes "ปรากฏว่าในขณะที่พวกเขากำลังล่องเรืออยู่ในที่ห่างไกลที่เรียกว่าทวีป Far-Star พวกเขาเจอพายุแล้วตกลงมายังสถานที่ที่เรียกว่า Malangdo พูดง่ายๆ ก็คือเรือตกลงมาจากฟ้านั่นแหละ";
					next;
					mes "[Shorty]";
					mes "พวก Doram มีพรสวรรค์ด้านเวทมนตร์มากทีเดียว ถ้าท่านไปที่ ^0000cdMalangdo^000000 ท่านจะต้องมีช่วงเวลาที่ดีแน่ๆ";
					next;
					mes "[Shorty]";
					mes "^0000cdท่านสามารถนั่งเรือ^000000 ไปจาก Izlude ได้ หรือถ้าท่านมี Canned Malangdo ที่หามาได้ด้วยวิธีพิเศษ ท่านก็สามารถใช้มันเดินทางไปที่นั่นได้ทันทีนะ";
					break;
				case 4:
					mes "[Shorty]";
					mes "ถ้าท่านอยากสัมผัสบรรยากาศของต่างแดน ท่านควรไปที่ ^0000cdพื้นที่ท้องถิ่น^000000 เหล่านั้นดูนะ ^0000cdถ้าท่านไปที่ท่าเรือของ Alberta^000000 ท่านจะพบกับกะลาสีที่จะพาท่านไปยังแต่ละภูมิภาค";
					next;
					mes "[Shorty]";
					mes "Amatsu, Kunlun, Moscovia, Brasilis, Ayothaya, Dewata, Port Malaya, Rock Ridge และอื่นๆ โลกที่ท่านยังไม่รู้จักกำลังรอท่านอยู่";
					next;
					mes "[Shorty]";
					mes "แค่ได้ยินชื่อก็ทำให้หัวใจเต้นแรงแล้วใช่ไหมล่ะ? ไว้เจอกันใหม่นะนายท่าน";
					break;
				case 5:
					mes "[Shorty]";
					mes "อ้อ ข้ารู้จักนะ ที่นี่น่ะ ถ้าท่านไปทางทิศตะวันออกของ Morroc หรือคุยกับ Agent Catson ใน Paradise พวกเขาจะส่งท่านไปที่นั่น";
					next;
					mes "[Shorty]";
					mes "ท่านจะได้พบกับผู้คนที่ไม่เคยเห็นมาก่อน";
					next;
					mes "[Shorty]";
					mes "จากฐานทัพของกองกำลังสำรวจสามอาณาจักร ท่านสามารถเดินทางไปยัง Splendid, Eclage, Manuk หรือ El Dicastes ได้";
					break;
				case 6:
					mes "[Shorty]";
					mes "เข้าใจแล้วล่ะ ถ้าอย่างนั้นข้าขอไปสำรวจแถวนี้หน่อยแล้วจะกลับเข้าไปในย่ามนะ";
					close;
				}
				next;
			}
			end;
		case 2:
			while(1) {
				mes "[Shorty]";
				mes "ท่านสงสัยเควสต์ไหนอยู่เหรอ?";
				next;
				switch( select( "เควสต์ทั่วไป", "กิจกรรมรายเดือน", "Paradise Group", "ข้าไม่สนใจ" ) ) {
				case 1:
					mes "[Shorty]";
					mes "^0000cdทำไมไม่ลองไปพบ Paul และ Anne ที่^000000 ด้านนอกประตูทิศตะวันตกของ Prontera ดูล่ะ? ตอนข้าเดินผ่านมา ข้าได้ยินว่าพวกเขาต้องการความช่วยเหลือนะ";
					next;
					mes "[Shorty]";
					mes "หรือจะลองไปฟังเรื่องราวจากชายวัยกลางคนที่ดูวิตกกังวลใน ^0000cdPayon^000000 ดูไหมล่ะ?";
					next;
					mes "[Shorty]";
					mes "สำหรับข้อมูลเพิ่มเติมเกี่ยวกับเควสต์";
					mes "ลองดูที่นี่ด้วยสิ";
					mes "<URL>[Rune Midgarts Library]<INFO>http://ro.gnjoy.com/guide/runemidgarts/questlist.asp,1024,768</INFO></URL>";
					next;
					mes "[Shorty]";
					mes "ท่านจะสามารถเรียนรู้เกี่ยวกับความเป็นไปของโลกนี้และรู้ว่ามีเหตุการณ์สำคัญอะไรเกิดขึ้นบ้าง";
					close;
				case 2:
					mes "[Shorty]";
					mes "ทวีปนี้เต็มไปด้วยเทศกาลตลอดทั้งปี สนใจไปร่วมงานเทศกาลเวลาที่ท่านว่างดูไหมล่ะ?";
					next;
					mes "[Shorty]";
					mes "ตอนนี้...";
					next;
					mes "[Shorty]";
					mes "ข้าได้ยินมาว่าจะมีการแข่งขันล่าสัตว์ในฤดูหนาวจัดขึ้นโดยราชวงศ์ที่พระราชวังด้วยล่ะ! อยากไปแข่งล่าสัตว์จัง! อยากทำจริงๆ! สู้กับมอนสเตอร์!!! ไปที่ Prontera กันเถอะ!!!!";
					next;
					mes "[Shorty]";
					mes "ท่านจะพาข้าไปด้วยแน่นอนใช่ไหม?";
					break;
				case 3:
					mes "[Shorty]";
					mes "^0000cdท่านเคยไปที่ Paradise หรือยัง^000000?";
					next;
					mes "[Shorty]";
					mes "ที่นั่นมีเควสต์มากมายรอท่านอยู่ แถมพวกเขายังมีอุปกรณ์สำหรับนักผจญภัยมือให้อีกด้วยนะ";
					next;
					mes "[Shorty]";
					mes "แน่นอนว่ามันไม่ได้ให้ฟรีๆ หรอกนะ ท่านต้องแก้เควสต์ง่ายๆ ของ Paradise ก่อน นอกจากนี้เขายังมีอุปกรณ์ Shadow ให้เช่าด้วยล่ะ";
					next;
					mes "[Shorty]";
					mes "ถ้าท่านมีคำถามอะไร ลองไปที่สำนักงาน Paradise ดูนะ";
					break;
				case 4:
					mes "[Shorty]";
					mes "เข้าใจแล้วล่ะ ถ้าอย่างนั้นข้าขอไปสำรวจแถวนี้หน่อยแล้วจะกลับเข้าไปในย่ามนะ";
					close;
				}
				next;
			}
			end;
		case 3:
			while(1) {
				mes "[Shorty]";
				mes "ข้าสงสัยว่าที่ไหนดีนะ..";
				next;
				switch( select( "คู่มือการล่าในฟิลด์", "คู่มือดันเจี้ยน", "คู่มือดันเจี้ยนอื่นๆ", "พอแล้วล่ะ" ) ) {
				case 1:
					mes "[Shorty]";
					mes "กดปุ่ม ^0000cdCtrl+`^000000 เพื่อเปิดแผนที่นะ";
					next;
					mes "[Shorty]";
					mes "ท่านเห็นกล่องสำหรับเลือกพื้นที่ที่ด้านบนซ้ายไหม? หลังจากเลือกพื้นที่ที่ต้องการแล้ว ลองตรวจสอบ ^0000cdแท็บมอนสเตอร์^000000 ที่มุมขวาบน หรือกดปุ่ม ^0000cdTab^000000 ท่านจะเห็นชื่อและเลเวลของมอนสเตอร์ที่มีอยู่มากที่สุดในพื้นที่นั้น";
					next;
					mes "[Shorty]";
					mes "มันจะดีกว่าถ้าท่านไปผจญภัยในที่ที่เหมาะสมกับเลเวลของท่าน ^0000cdหากเลเวลต่างจากมอนสเตอร์มากเกินไป ท่านจะไม่ได้รับค่าประสบการณ์ 100%^000000 ท่านต้องคำนวณส่วนต่างเลเวลให้ดีด้วยล่ะ จริงไหม?";
					next;
					mes "[Shorty]";
					mes "ถ้าท่านกดเลือกเมืองที่ต้องการไป เมนูนำทางจะปรากฏขึ้น กดปุ่ม ^0000cd<Guide to the target> ด้านล่าง^000000 แล้วปิดหน้าต่างแผนที่เพื่อเริ่มการนำทางได้เลย";
					break;
				case 2:
					mes "[Shorty]";
					mes "กดปุ่ม ^0000cdCtrl+`^000000 เพื่อเปิดแผนที่นะ";
					next;
					mes "[Shorty]";
					mes "^0000cdท่านเห็นกล่องสำหรับเลือกพื้นที่^000000 ที่ด้านบนซ้ายไหม? หากท่านเลือกพื้นที่ที่ต้องการตรวจสอบ ท่านจะเห็นมันถูกทำเครื่องหมายด้วยกรอบสีแดงในแผนที่พร้อมกับ ^0000cdชื่อของดันเจี้ยนหลัก^000000 เขียนอยู่บนนั้น" ;
					next;
					mes "[Shorty]";
					mes "จากนั้นตรวจสอบ ^0000cdแท็บมอนสเตอร์ที่มุมขวาบน หรือกดปุ่ม Tab^000000 เพื่อแสดงเลเวลของดันเจี้ยนนั้นๆ";
					next;
					mes "[Shorty]";
					mes "บางครั้ง ดันเจี้ยนบางแห่งจะเข้าได้เฉพาะผ่านเควสต์พิเศษเท่านั้น ดังนั้นท่านต้องรวบรวมข้อมูลด้วยเหมือนกันนะ เข้าใจไหม?";
					next;
					mes "[Shorty]";
					mes "ทำไมไม่ลองไปที่ท่อระบายน้ำใต้ดินดูก่อนล่ะ? สมาคม Swordsman มักจะรับคนอยู่เสมอ และข้าได้ยินว่ามันเหมาะสำหรับนักผจญภัยมือใหม่ในการเก็บเกี่ยวประสบการณ์ด้วยนะ";
					break;
				case 3:
					mes "[Shorty]";
					mes "มีดันเจี้ยนอีกมากมายเลย ทั้ง Memorial Dungeon, Nightmare Dungeon, Illusion Dungeon... และอื่นๆ";
					next;
					mes "[Shorty]";
					mes "^0000cdMemorial Dungeon^000000";
					mes "นอกเหนือจากดันเจี้ยนปกติแล้ว ยังมีดันเจี้ยนพิเศษอีก ปกติเขาว่ากันว่าเป็นสถานที่พิเศษที่สร้างขึ้นโดยพลังงานลึกลับในโลกนี้";
					next;
					mes "[Shorty]";
					mes "เหล่านักผจญภัยผู้มีประสบการณ์มักจะไปกัน แต่ก็มีบางแห่งที่เลเวลไม่สูงนักซึ่งสามารถไปได้หากฝึกฝนมาบ้าง ไม่ว่าจะในหมู่บ้าน Orc หรือ Rachel...";
					next;
					mes "[Shorty]";
					mes "ว่ากันว่าพวกมันตั้งอยู่ในหมู่บ้านและฟิลด์ทั่วไป และมีหลายแห่งที่ถูกเรียกว่า Dimensional Rifts (รอยแยกมิติ)";
					next;
					mes "[Shorty]";
					mes "^0000cdNightmare Dungeon^000000";
					mes "ว่ากันว่าเป็นดันเจี้ยนที่มีความท้าทายยากกว่าดันเจี้ยนปกติมาก แม้แต่มอนสเตอร์ก็ไม่เหมือนกับมอนสเตอร์ที่เรารู้จัก เมื่อท่านเจอดันเจี้ยน ถ้ามีใครบางคนที่ดูน่าสงสัยบอกท่านว่าเขาสามารถส่งท่านไปยังที่ที่ยากขึ้นได้ ก็ลองไปดูสิ" ;
					next;
					mes "[Shorty]";
					mes "^0000cdIllusion Dungeon^000000";
					mes "ไม่มีใครรู้ว่าดันเจี้ยนเหล่านี้คืออะไรกันแน่ ข้าเดาว่ามันคงเหมือนความฝันหรือภาพแฟนตาซีล่ะมั้ง มีหินพิเศษที่หาได้จากที่นี่เท่านั้น และทุกคนบอกว่าพวกเขาเล็งหินก้อนนั้นไว้และต่อสู้เพื่อมัน ว่ากันว่าหินนั้นสามารถนำไปแลกเป็นอาวุธที่ทรงพลังได้นะ ";
					break;
				case 4:
					mes "[Shorty]";
					mes "เข้าใจแล้วล่ะ ถ้าอย่างนั้นข้าขอไปสำรวจแถวนี้หน่อยแล้วจะกลับเข้าไปในย่ามนะ";
					close;
				}
				next;
			}
			end;
		case 4:
			while(1) {
				mes "[Shorty]";
				mes "แล้วข้อมูลพื้นฐานอะไรที่ท่านอยากรู้ล่ะ?";
				mes "ลองดูที่ <URL>[guide]<INFO>http://ro.gnjoy.com/guide/systeminfo/,1024,768</INFO></URL> สำหรับข้อมูลพื้นฐานนะ";
				next;
				switch( select( "ปาร์ตี้", "กิลด์และแคลน", "การตีบวก (Smelt)", "การออฟชั่น (Enchant)", "ครอบครัว", "การแชท" ) ) {
				case 1:
					mes "[Shorty]";
					mes "บางครั้งท่านคงอยากอยู่กับเพื่อนๆ ไม่ว่าจะสู้ในดันเจี้ยน สนุกกับการผจญภัยไปด้วยกัน หรือบางครั้งแค่มานั่งคุยกันเฉยๆ";
					next;
					mes "[Shorty]";
					mes "ในกรณีนั้น ให้สร้างปาร์ตี้แล้วทำสิ่งต่างๆ ไปด้วยกันสิ";
					next;
					mes "[Shorty]";
					mes "หน้าต่างปาร์ตี้สามารถเปิดได้ง่ายๆ ด้วยปุ่ม ^0000cdAlt+Z^000000 เมื่อทำการแชร์ค่าประสบการณ์ในการล่า ส่วนต่างของ ^0000cdเลเวลระหว่างสมาชิกปาร์ตี้^000000 จะต้องเท่ากับหรือน้อยกว่า 15 เลเวลนะ";
					next;
					mes "[Shorty]";
					mes "ถ้าท่านคลิกที่วงกลมด้านซ้ายในช่องสนทนาด้านล่าง ท่านจะสามารถระบุคู่สนทนาได้ และท่านสามารถส่งข้อความไปหาเฉพาะสมาชิกปาร์ตี้ที่นั่นได้ด้วยนะ มันมีประโยชน์มากเลยล่ะ";
					break;
				case 2:
					mes "[Shorty]";
					mes "บางครั้งท่านอาจต้องการสังกัดอยู่ที่ไหนสักแห่งและรู้สึกผูกพันกับใครบางคน ในกรณีนั้น ท่านก็แค่เข้าร่วม ^0000cdกิลด์หรือแคลน^000000";
					next;
					mes "[Shorty]";
					mes "ลักษณะเด่นที่สุดของกิลด์คือการต่อสู้แย่งชิงที่พำนัก หรือที่เรียกกันว่า สงครามกิลด์ (Siege)";
					next;
					mes "[Shorty]";
					mes "ท่านสามารถเข้าร่วมกิลด์ที่ถูกสร้างไว้แล้วผ่านการรับเชิญ หรือจะซื้อ Empelium ที่ขายในสำนักงานเพื่อสร้างกิลด์เองก็ได้";
					next;
					mes "[Shorty]";
					mes "นอกเหนือจากความรู้สึกเป็นส่วนหนึ่งแล้ว กิลด์ยังได้รับสิทธิประโยชน์ต่างๆ เช่น สกิลกิลด์ และคลังเก็บของส่วนตัวของกิลด์เองด้วย";
					next;
					mes "[Shorty]";
					mes "สำหรับรายละเอียด";
					mes "ดูได้ที่ <URL>[Guild Guide]<INFO>http://ro.gnjoy.com/guide/systeminfo/systemGuild1.asp,1024,768</INFO></URL>";
					next;
					mes "[Shorty]";
					mes "หากการเข้าร่วมกิลด์มันดูเป็นภาระเกินไป งั้น ^0000cdแคลน (Clan)^000000 ก็น่าจะดีนะ แม้มันอาจจะไม่ได้รู้สึกใกล้ชิดกันเหมือนกิลด์ แต่แคลนเองก็มีสิทธิประโยชน์มากมายเหมือนกัน";
					next;
					mes "[Shorty]";
					mes "^0000cdอาคารที่อยู่มุมล่างซ้ายของน้ำพุกลางเมือง Prontera^000000 คือสำนักงานใหญ่ของแคลนนะ ลองไปดูสิ";
					break;
				case 3:
					mes "[Shorty]";
					mes "ท่านต้องการทำให้อุปกรณ์ที่ท่านมีแข็งแกร่งขึ้นไหม? ในกรณีนั้น ท่านสามารถไปหา ^0000cdช่างตีเหล็ก^000000 ได้ในทุกๆ เมือง";
					next;
					mes "[Shorty]";
					mes "การตีบวกต้องการไอเทมพิเศษและเงิน Zeny ซึ่งจะขึ้นอยู่กับประเภทและเลเวลของอุปกรณ์นั้นๆ";
					next;
					mes "[Shorty]";
					mes "สิ่งหนึ่งที่ต้องระวังคือ มันมีระดับการตีบวกที่ปลอดภัยอยู่ และหากท่านตีบวกเกินระดับที่กำหนด อุปกรณ์ก็มีโอกาสที่จะถูกทำลายได้ ท่านต้องพึ่งดวงหน่อยล่ะนะ";
					next;
					mes "[Shorty]";
					mes "อ้อ นอกจากช่างตีเหล็กในแต่ละเมืองแล้ว ข้าได้ยินมาว่าที่ ^0000cd<Item Mall>^000000 จะมีโอกาสสำเร็จในการตีบวกที่สูงกว่านะ";
					next;
					mes "[Shorty]";
					mes "ถ้าท่านสงสัย ลองไปหา ^0000cd<cash sales guide>^000000 ในแต่ละเมืองดู พวกเขาจะนำทางท่านไปที่นั่นเอง";
					break;
				case 4:
					mes "[Shorty]";
					mes "ถ้าท่านต้องการให้อุปกรณ์แข็งแกร่งขึ้นหรือมอบความสามารถพิเศษให้มัน ทำไมไม่ลอง ^0000cdออฟชั่น (Enchant)^000000 มันดูล่ะ?";
					next;
					mes "[Shorty]";
					mes "^0000cd<Malangdo>, <Item Mall>^000000, หรือ ^0000cd<Seyablem> และ <Leyablem>^000000 ซึ่งพบได้ตามเมืองหลักๆ มักจะรับทำเรื่องนี้อยู่";
					next;
					mes "[Shorty]";
					mes "อุปกรณ์ที่ได้รับจากดันเจี้ยนเฉพาะหรือพื้นที่เฉพาะ บางครั้งจะสามารถ ^0000cdออฟชั่น^000000 ได้แค่ในพื้นที่นั้นๆ เท่านั้น";
					next;
					mes "[Shorty]";
					mes "อย่างไรก็ตาม ไม่ใช่ทุกอุปกรณ์ที่จะออฟชั่นได้นะ ท่านควรตรวจสอบเงื่อนไขให้ดีก่อนล่ะ";
					break;
				case 5:
					mes "[Shorty]";
					mes "ท่านแน่ใจแล้วใช่ไหมว่าอยากจะอยู่กับใครอีกคนไปตลอดชีวิต? ถ้าอย่างนั้นท่านก็สามารถเริ่มสร้าง ^0000cdครอบครัว^000000 ได้นะ";
					next;
					mes "[Shorty]";
					mes "^0000cdท่านสามารถแต่งงานกับคนที่ท่านชอบ^000000 หรือจะรับ ^0000cdลูกบุญธรรม^000000 มาเลี้ยงก็ได้ หรือแม้แต่การที่ท่านถูกรับไปเลี้ยงเป็นลูกของคู่รักคู่อื่นก็เป็นไปได้เหมือนกันนะ";
					next;
					mes "[Shorty]";
					mes "ว่ากันว่าท่านจะได้รับสกิลการแต่งงานพิเศษเพื่อใช้ร่วมกันในครอบครัวด้วย";
					next;
					mes "[Shorty]";
					mes "ถ้าท่านสนใจจะแต่งงาน ลองไปที่โบสถ์ใน Prontera หรือหอจัดงานวิวาห์ใน Lasagna ดูสิ ท่านต้องเตรียมสิ่งของบางอย่างก่อนจะแต่งงานนะ และโบสถ์ใน Prontera จะอนุญาตให้เฉพาะมนุษย์แต่งงานกันเท่านั้น";
					next;
					mes "[Shorty]";
					mes "การรับเลี้ยงบุตรบุญธรรมกำหนดให้คู่รักต้องมีเลเวล 70 ขึ้นไป และเด็กที่จะถูกรับเลี้ยงต้องเป็น Novice หรืออาชีพคลาส 1 เท่านั้น";
					next;
					mes "[Shorty]";
					mes "แผนกครอบครัวที่สำนักงานเขต Prontera บอกว่าท่านสามารถไปซื้อไอเทมที่จำเป็นสำหรับงานแต่งงาน หรือตรวจสอบเรื่องที่เกี่ยวข้องกับครอบครัวได้ที่นั่น ดังนั้นถ้าต้องการก็ลองไปดูนะ";
					next;
					mes "[Shorty]";
					mes "ท่านสามารถอ้างอิงข้อมูลเพิ่มเติมได้ที่";
					mes "<URL>[Family]<INFO>http://ro.gnjoy.com/guide/systeminfo/systemLovers1.asp,1024,768</INFO></URL> นะ";
					break;
				case 6:
					mes "[Shorty]";
					mes "ท่านเห็นหน้าต่างข้อความด้านล่างไหม? ท่านสามารถแชทกับคนอื่นได้โดยการใส่ข้อความที่นั่น หรือจะส่งข้อความประเภทอื่นๆ ก็ได้เหมือนกัน";
					next;
					mes "[Shorty]";
					mes "หากท่านต้องการแชทกับคนกลุ่มเล็กๆ ที่ไม่ใช่ทั้งสมาชิกปาร์ตี้หรือกิลด์ ท่านสามารถเปิดห้องสนทนาได้นะ";
					next;
					mes "[Shorty]";
					mes "ท่านสามารถเปิดหน้าต่างห้องสนทนาได้ง่ายๆ ด้วยปุ่ม ^0000cdAlt+C^000000 ท่านยังสามารถกำหนดจำนวนผู้เข้าร่วมสูงสุดและตั้งรหัสผ่านได้ด้วยล่ะ";
					break;
				}
				next;
			}
			end;
		case 5:
			mes "[Shorty]";
			mes "เข้าใจแล้วล่ะ ถ้าอย่างนั้นข้าขอไปสำรวจแถวนี้หน่อยแล้วจะกลับเข้าไปในย่ามนะ";
			close;
		}
		end;
	case 8:
		mes "[Shorty]";
		mes "โอ้ว!!! ข้าชื่อ Shorty! นายท่านชื่ออะไรเหรอ?";
		next;
		select( "นายท่าน?" );
		mes "[Shorty]";
		mes "คนแรกที่เจอควรจะเรียกว่านายท่านสิ";
		next;
		select( "ท่านหมายถึงอาจารย์งั้นเหรอ?" );
		mes "[Shorty]";
		mes "ใช่เลย แบบนั้นแหละ ข้าคือ Galapago Shorty ข้าน่ะคุยเก่งนะจะบอกให้";
		next;
		select("ทำไมท่านถึงมาอยู่ที่นี่ล่ะ?" );
		mes "[Shorty]";
		mes "Salty! Salty เป็นเพื่อนข้า ข้ามาที่นี่เพื่อมาเจอ Salty เพราะงั้นท่านต้องพาข้าไปหา Salty นะ";
		next;
		mes "[Shorty]";
		mes "เอาล่ะ ไปกันเถอะ!";
		close;
	case 9:
		mes "[Shorty]";
		mes "อะไรนะ...? นายท่าน...? อะไรกัน? เราจะแยกทางกันเหรอ? ที่นี่? ตอนนี้เนี่ยนะ? ท่านพูดจริงเหรอ?";
		next;
		mes "[Shorty]";
		mes "มีอะไรที่ท่านไม่พอใจงั้นเหรอ? ข้าอยู่ในย่ามไม่เงียบพอเหรอ? อ้อ นั่นแหละที่ท่านไม่ชอบ ใช่ไหม... งั้นข้าก็คงทำอะไรไม่ได้แล้วล่ะ";
		next;
		mes "[Shorty]";
		mes "ถ้าอย่างนั้นท่านจะไม่พาข้าไปหา ^0000cdSalty^000000 แล้วเหรอ? Salty จะต้องขอบคุณท่านมากแน่ๆ ที่พาข้าไปหาเธอ ข้าจะตอบแทนท่านเองเพราะ Salty น่ะรู้จักบุญคุณคนนะ";
		next;
		if (select( "เราแยกทางกันเถอะ", "งั้นไปสิ ไปด้วยกันจนกว่าจะถึงตอนนั้นแหละ" ) == 2) {
			mes "[Shorty]";
			mes "โอเค ท่านพูดได้ดีมากนายท่าน ถึงข้าจะติดอยู่ในย่าม ข้าก็จะรีบออกมาให้ไวเลย";
			next;
			mes "[Shorty]";
			mes "เรามาช่วยกันและกันอีกสักนิดเถอะนะ ตกลงไหม?";
			close;
		}
		mes "[Shorty]";
		mes "ใช่สินะ นายท่านเป็นคนใจแข็งจริงๆ ถ้าอย่างนั้นก็คงทำอะไรไม่ได้แล้วล่ะ ข้าคิดว่าความสัมพันธ์ของเราคงจบลงตรงนี้";
		next;
		mes "[Shorty]";
		mes "ข้าไม่รู้ว่าท่านรู้สึกยังไงกับข้า แต่ข้าน่ะสนุกมากเลยนะช่วงเวลาที่เราอยู่ด้วยกัน";
		next;
		mes "[Shorty]";
		mes "ดูแลตัวเองด้วยนะ และข้าขอให้ท่านโชคดี ข้าหวังว่านายท่านจะไม่ลืมว่าเคยมี Galapago ตัวนี้อยู่ด้วยนะ";
		next;
		mes "[Shorty]";
		mes "เอาล่ะนายท่าน ลาก่อน ลาก่อนนะ!";
		switch( isbegin_quest(14693) ) {
		case 0:
			setquest 14693;
			completequest 14693;
			break;
		case 1:
			completequest 14693;
			break;
		case 2:
			break;
		}
		if (isbegin_quest(14694) > 0)
			erasequest 14694;
		delitem 23278,1;	// HelpmeShorty
		close;
	}
}
