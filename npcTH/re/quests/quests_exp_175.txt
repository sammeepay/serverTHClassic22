//===== rAthena Script =======================================
//= Quests EXP
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- NPC quests related to new dungeons.
//===== Changelog: ===========================================
//= 1.0 Initial release of Glast Heim Abyss quests. [OptimusM]
//= 1.1 Corrections here and there. [Capuche]
//= 1.2 Initial release of Magma Dungeon 3 quests. [Capuche]
//= 1.3 Initial release of Einbech Dungeon 3 quests. [Capuche]
//============================================================

glast_01,191,273,3	script	Magician Nillem#glabyss01	4_M_JOB_WIZARD,{
	if (BaseLevel < 175) {
		mes "[Nillem]";
		mes "เจ้ามาทำอะไรที่นี่น่ะ?";
		mes "ใช่แล้ว ข้ากำลังรอนักผจญภัยที่แข็งแกร่งพอจะมาตอบสนองความอยากรู้อยากเห็นของข้าอยู่ยังไงล่ะ!";
		next;
		mes "[Nillem]";
		mes "ถ้าเจ้าเลเวลถึง 175 เมื่อไหร่ เจ้าจะได้เป็นคู่หูที่พรหมลิขิตกำหนดไว้ของข้า-กู";
		close;
	}
	mes "[Nillem]";
	mes "ข้าคิดว่าข้าเพิ่งค้นพบสิ่งที่ยิ่งใหญ่เข้าให้แล้วล่ะ ทำไมเจ้าไม่ลองไปกับข้าดูล่ะ?";
	next;
	switch( select( "ไปกันเถอะ!", "ช่วยอธิบายหน่อย", "สิ่งที่ข้าค้นพบ", "ปฏิเสธ" )) {
	case 1:
		mes "[Nillem]";
		mes "นี่แหละที่รักของข้า!";
		mes "เจ้ากำลังจะได้กระโดดลงไปในขุมนรกกับข้า! ข้าอดใจไม่ไหวที่จะ รั-ก-เ-จ้-า จริงๆ";
		close2;
		warp "gl_cas01_",200,26;
		end;
	case 2:
		mes "[Nillem]";
		mes "อย่างที่เจ้ารู้ ข้าเป็นจอมเวทย์หลวงที่เก่งกาจใช่ไหมล่ะ?";
		mes "จอมเวทย์อัจฉริยะที่สามารถทำนู่นทำนี่เกี่ยวกับมิติได้น่ะ!";
		next;
		mes "[Nillem]";
		mes "มีรายงานเกี่ยวกับพลังเวทมนตร์ประหลาดใกล้กับปราสาทกลางของ Glast Heim";
		mes "ข้าก็เลยมาตรวจสอบที่นี่แล้วก็พบมันเข้า";
		next;
		mes "[Nillem]";
		mes "รอยแยกแห่งมิติที่อ่อนกำลังลง!";
		mes "เจ้ารู้ไหม จอมเวทย์อัจฉริยะคนนี้เก่งเรื่องการเชื่อมต่อรอยแยกของมิติที่สุด";
		next;
		mes "[Nillem]";
		mes "ข้าเลยบังคับการเชื่อมต่อแล้วเข้าไปข้างในมา....";
		mes "เศษเสี้ยวแห่งเวลาที่หยุดนิ่งอยู่ในกระแสไหลเวียน.... อืม... มันอธิบายยากแฮะ..";
		next;
		mes "[Nillem]";
		mes "เอาเป็นว่า ฝูงสัตว์ประหลาดที่ติดอยู่ข้างในมาอย่างยาวนานพุ่งเข้าหาข้าเหมือนระลอกคลื่นเลยล่ะ!";
		next;
		mes "[Nillem]";
		mes "ข้าเกือบจะ ต-า-ย อยู่ที่นั่นแล้ว";
		mes "ข้าก็เลยกำลังมองหานักผจญภัยมาเข้าร่วมการสำรวจนี้ด้วยกัน";
		close;
	case 3:
		mes "[Nillem]";
		mes "ถูกต้อง ไม่มีภารกิจที่นี่หรอก แต่ความอยากรู้อยากเห็นมักตามมาด้วยการค้นพบ!";
		mes "เจ้าคงสังเกตเห็นก่อนที่ข้าจะบอกซะอีกว่ามีบางอย่างที่ข้าอยากจะทำร่วมกับเจ้า ที่-รั-ก-ข-อ-ง-ข้-า";
		next;
		switch( select( "สิ่งที่ว่างเปล่า", "สิ่งที่มีจุดเยือกแข็งสูง", "สิ่งที่เหมือนกับกระจก", "เอ่อ ไม่เป็นไรดีกว่า" )) {
		case 1:
			mes "[Nillem]";
			mes "เหนือรอยแยกของมิตินี้ มีสิ่งที่ว่างเปล่าอยู่..";
			mes "^0000FFContaminated Raydric^000000 และ ^0000FFContaminated Raydric Archer^000000...";
			mes "การปนเปื้อน";
			next;
			mes "[Nillem]";
			mes "^0000FFContaminated Raydric 20 ตัว^000000,";
			mes "^0000FFContaminated Raydric Archer 20 ตัว^000000!! กำจัดพวกมันแล้วกลับมา แล้วจอมเวทย์อัจฉริยะคนนี้จะทำการศึกษามันเอง";
			mes "เจ้าคิดว่ายังไงล่ะ ที่รัก?";
			next;
			if (select( "ไม่ยากเกินไปหรอก", "ข้ากำลังยุ่งน่ะ" ) == 2) {
				mes "[Nillem]";
				mes "ถ้าเจ้ายุ่ง ข้าก็บังคับเจ้าไม่ได้";
				mes "แต่ที่รัก โปรดช่วยข้าเมื่อเจ้า มี-เ-ว-ล-า-ว่-า-ง-น-ะ-!";
				close;
			}
			switch( checkquest(8542,PLAYTIME) ) {
			case -1:
				break;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "วันนี้ข้าขอให้เจ้าช่วยมากเกินไปแล้วล่ะที่รัก";
				mes "เปลี่ยนเป็นมาแบ่งปันการค้นพบใหม่ๆ ด้วยกันในวันพรุ่งนี้แทนนะ ต-ก-ล-ง-ไ-ห-ม-?";
				close;
			case 2:
				mes "^4d4dff - เจ้าสามารถรับภารกิจนี้ได้อีกครั้ง คุยกับ Mage Nillem อีกครั้งเพื่อตอบตกลง - ^000000";
				erasequest 8542;
				break;
			}
			switch( checkquest(8541,HUNTING) ) {
			case -1:
				mes "[Nillem]";
				mes "นั่นแหละเด็กดีของข้า!";
				mes "มันยากที่จะเห็นผลลัพธ์ที่นั่นเพราะเวทมนตร์ประหลาดน่ะ";
				mes "ถ้าเจ้าทำเควสเสร็จแล้ว ออกไปจากที่นั่นก่อน แ-ล้-ว-ค่-อ-ย-ม-า-เ-ช็-ค-กั-น-!";
				setquest 8541;
				close;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "^0000FFContaminated Raydric 20 ตัว^000000,";
				mes "^0000FFContaminated Raydric Archer 20 ตัว^000000.";
				mes "วู้วฮู เจ้าไม่รู้สึกตื่นเต้นกับผลกระทบจากสิ่งที่ว่างเปล่าบ้างเหรอ?";
				close;
			case 2:
				mes "[Nillem]";
				mes "ฮ่า~ ผลลัพธ์มันชัดเจนมากเมื่อออกมาข้างนอกนี้ ไหนขอดูอาวุธ... และเกราะของเจ้าหน่อยสิ? อืม...";
				mes "อื้มมม!!";
				mes "ฮ่าฮ่า นี่มันการค้นพบแห่งศตวรรษเลยนะเนี่ย!";
				next;
				select( "เจ้าได้อะไรมางั้นเหรอ?" );
				mes "[Nillem]";
				mes "มันอธิบายให้เจ้าฟังยากน่ะ แต่สรุปสั้นๆ คือ เ-จ้-า-ทำ-สำเร็จ-แ-ล้-ว-!!";
				mes "แต่นี่ยังไม่พอหรอกนะ พรุ่งนี้ช่วยมาทำอีกรอบล่ะ";
				erasequest 8541;
				setquest 8542;
				getexp 8142552,5674080;
				close;
			}
			end;
		case 2:
			mes "[Nillem]";
			mes "มีสิ่งที่มีจุดเยือกแข็งสูงอยู่เหนือรอยแยกของมิตินี้";
			mes "^0000FFFrozen Gargoyle^000000 และ ^0000FFPrison Breaker^000000...";
			next;
			mes "[Nillem]";
			mes "^0000FFFrozen Gargoyle 5 ตัว^000000,";
			mes "^0000FFPrison Breaker 5 ตัว^000000!! กำจัดพวกมันแล้วกลับมา แล้วจอมเวทย์อัจฉริยะคนนี้จะทำการศึกษามันเอง";
			mes "เจ้าคิดว่ายังไงล่ะ ที่รัก?";
			next;
			if (select( "ไม่ยากเกินไปหรอก", "ข้ากำลังยุ่งน่ะ" ) == 2) {
				mes "[Nillem]";
				mes "ถ้าเจ้ายุ่ง ข้าก็บังคับเจ้าไม่ได้";
				mes "แต่ที่รัก โปรดช่วยข้าเมื่อเจ้า มี-เ-ว-ล-า-ว่-า-ง-น-ะ-!";
				close;
			}
			switch( checkquest(8545,PLAYTIME) ) {
			case -1:
				break;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "วันนี้ข้าขอให้เจ้าช่วยมากเกินไปแล้วล่ะที่รัก";
				mes "เปลี่ยนเป็นมาแบ่งปันการค้นพบใหม่ๆ ด้วยกันในวันพรุ่งนี้แทนนะ ต-ก-ล-ง-ไ-ห-ม-?";
				close;
			case 2:
				mes "^4d4dff - เจ้าสามารถรับภารกิจนี้ได้อีกครั้ง คุยกับ Mage Nillem อีกครั้งเพื่อตอบตกลง - ^000000";
				erasequest 8545;
				break;
			}
			switch( checkquest(8544,HUNTING) ) {
			case -1:
				mes "[Nillem]";
				mes "นั่นแหละเด็กดีของข้า!";
				mes "มันยากที่จะเห็นผลลัพธ์ที่นั่นเพราะเวทมนตร์ประหลาดน่ะ";
				mes "ถ้าเจ้าทำเควสเสร็จแล้ว ออกไปจากที่นั่นก่อน แ-ล้-ว-ค่-อ-ย-ม-า-เ-ช็-ค-กั-น-!";
				setquest 8544;
				close;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "^0000FFFrozen Gargoyle 5 ตัว^000000,";
				mes "^0000FFPrison Breaker 5 ตัว^000000!!";
				mes "วู้วฮู เจ้าไม่ตื่นเต้นกับสิ่งที่มีจุดเยือกแข็งสูงบ้างเหรอ?";
				close;
			case 2:
				mes "[Nillem]";
				mes "ฮ่า~ ผลลัพธ์มันชัดเจนมากเมื่อออกมาข้างนอกนี้ ไหนขอดูอาวุธ... และเกราะของเจ้าหน่อยสิ? อืม...";
				mes "อื้มมม!!";
				mes "ฮ่าฮ่า นี่มันการค้นพบแห่งศตวรรษเลยนะเนี่ย!";
				next;
				select( "เจ้าได้อะไรมางั้นเหรอ?" );
				mes "[Nillem]";
				mes "มันอธิบายให้เจ้าฟังยากน่ะ แต่สรุปสั้นๆ คือ เ-จ้-า-ทำ-สำเร็จ-แ-ล้-ว-!!";
				mes "แต่นี่ยังไม่พอหรอกนะ พรุ่งนี้ช่วยมาทำอีกรอบล่ะ";
				erasequest 8544;
				setquest 8545;
				getexp 2036754,1425726;
				close;
			}
			end;
		case 3:
			mes "[Nillem]";
			mes "เหนือรอยแยกของมิตินี้ มีสิ่งที่เหมือนเป็นกระจกเงาของกันและกันอยู่";
			mes "^0000FFIce Ghost^000000 และ ^0000FFFlame Ghost^000000...";
			mes "พวกมันดูแตกต่างกันโดยสิ้นเชิง แต่รอยแยกของมิตินั้นเป็นสิ่งที่คาดเดาไม่ได้เลย";
			next;
			mes "[Nillem]";
			mes "^0000FFIce Ghost 10 ตัว^000000,";
			mes "^0000FFFlame Ghost 10 ตัว^000000!! กำจัดพวกมันแล้วกลับมา แล้วจอมเวทย์อัจฉริยะคนนี้จะทำการศึกษามันเอง";
			mes "เจ้าคิดว่ายังไงล่ะ ที่รัก?";
			next;
			if (select( "ไม่ยากเกินไปหรอก", "ข้ากำลังยุ่งน่ะ" ) == 2) {
				mes "[Nillem]";
				mes "ถ้าเจ้ายุ่ง ข้าก็บังคับเจ้าไม่ได้";
				mes "แต่ที่รัก โปรดช่วยข้าเมื่อเจ้า มี-เ-ว-ล-า-ว่-า-ง-น-ะ-!";
				close;
			}
			switch( checkquest(8548,PLAYTIME) ) {
			case -1:
				break;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "วันนี้ข้าขอให้เจ้าช่วยมากเกินไปแล้วล่ะที่รัก";
				mes "เปลี่ยนเป็นมาแบ่งปันการค้นพบใหม่ๆ ด้วยกันในวันพรุ่งนี้แทนนะ ต-ก-ล-ง-ไ-ห-ม-?";
				close;
			case 2:
				mes "^4d4dff - เจ้าสามารถรับภารกิจนี้ได้อีกครั้ง คุยกับ Mage Nillem อีกครั้งเพื่อตอบตกลง - ^000000";
				erasequest 8548;
				break;
			}
			switch( checkquest(8547,HUNTING) ) {
			case -1:
				mes "[Nillem]";
				mes "นั่นแหละเด็กดีของข้า!";
				mes "มันยากที่จะเห็นผลลัพธ์ที่นั่นเพราะเวทมนตร์ประหลาดน่ะ";
				mes "ถ้าเจ้าทำเควสเสร็จแล้ว ออกไปจากที่นั่นก่อน แ-ล้-ว-ค่-อ-ย-ม-า-เ-ช็-ค-กั-น-!";
				setquest 8547;
				close;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "^0000FFIce Ghost 10 ตัว^000000,";
				mes "^0000FFFlame Ghost 10 ตัว^000000!!";
				mes "วู้วฮู เจ้าไม่ตื่นเต้นกับสิ่งที่เหมือนเป็นกระจกเงาบ้างเหรอ?";
				close;
			case 2:
				mes "[Nillem]";
				mes "ฮ่า~ ผลลัพธ์มันชัดเจนมากเมื่อออกมาข้างนอกนี้ ไหนขอดูอาวุธ... และเกราะของเจ้าหน่อยสิ? อืม...";
				mes "อื้มมม!!";
				mes "ฮ่าฮ่า นี่มันการค้นพบแห่งศตวรรษเลยนะเนี่ย!";
				next;
				select( "เจ้าได้อะไรมางั้นเหรอ?" );
				mes "[Nillem]";
				mes "มันอธิบายให้เจ้าฟังยากน่ะ แต่สรุปสั้นๆ คือ เ-จ้-า-ทำ-สำเร็จ-แ-ล้-ว-!!";
				mes "แต่นี่ยังไม่พอหรอกนะ พรุ่งนี้ช่วยมาทำอีกรอบล่ะ";
				erasequest 8547;
				setquest 8548;
				getexp 4138800,2897160;
				close;
			}
			end;
		case 4:
			mes "[Nillem]";
			mes "โอ้ๆ ไม่เป็นไรนะที่รัก เจ้าพูดถูกแล้ว";
			mes "ถ้าเจ้าเหนื่อย ก็พักผ่อนซะเถอะ";
			mes "อ-ย่-า-ฝื-น-ตั-ว-เ-อ-ง-นั-ก-เ-ล-ย---!";
			close;
		}
		end;
	case 4:
		mes "[Nillem]";
		mes "โอ้ ที่รักของข้า วันนี้เจ้าใจร้ายจังเลยนะ";
		mes "Nillem เสียใจนะเนี่ย *กระซิก*" ;
		close;
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8541,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8544,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8547,HUNTING) == 2" );

	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8542,PLAYTIME) == -1 && checkquest(8541,HUNTING) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8545,PLAYTIME) == -1 && checkquest(8544,HUNTING) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8548,PLAYTIME) == -1 && checkquest(8547,HUNTING) == -1" );

	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8542,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8545,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8548,PLAYTIME) == 2" );
	end;
}

gl_cas01_,205,36,5	script	Magician Nillem#glabyss02	4_M_JOB_WIZARD,{
	mes "[Nillem]";
	mes "วู้วฮู! ข้าตื่นเต้นทุกครั้งเลยที่มาที่นี่!";
	mes "ข้าชอบความอ้างว้างแบบนี้จริงๆ!";
	next;
	if (select( "ออกไปจากที่นี่กันเถอะ!", "ข้าอยากสำรวจแถวนี้ต่อ" ) == 2) {
		mes "[Nillem]";
		mes "แล้วเจอกัน!";
		close;
	}
	mes "[Nillem]";
	mes "แน่นอน!";
	mes "อ-อ-ก-ไ-ป-ข้-า-ง-น-อ-ก-กั-น-เ-ถ-อ-ะ-!";
	close2;
	warp "glast_01",191,270;
	end;
}


// mag_dun03
yuno_fild03,38,146,5	script	Lily Blanc	-1,{
	if (BaseLevel < 175) {
		mes "[Lily]";
		mes "...?";
		mes "...เอ่อ ข้าไม่คิดว่าพวกยามจะยอมให้เจ้าเข้าไปหรอกนะ...";
		next;
		mes "[Lily]";
		mes "ถ้าเจ้ามีคุณสมบัติพอจะเข้าชั้น 3 ของ Nogg Road ได้ เจ้าจะรับเควสไหม?";
		mes "เรามีงานที่ต้องทำเยอะมากเลยล่ะ";
		mes "พวกยามบอกว่าเลเวล 175 ก็น่าจะพอแล้ว...";
		close;
	}
	mes "[Lily]";
	mes "ท่านนักผจญภัยผู้มีน้ำใจ รบกวนช่วยเราทำการวิจัยสำหรับรายงานการประชุมหน่อยจะได้ไหม?";
	mes "สายตาของนักผจญภัยเท่านั้นคือทางรอดเดียวของเรา";
	next;
	switch( select( "ฟังคำร้องขอ", "ดูน่าสงสัย...", "ปฏิเสธ" ) ) {
	case 1:
		mes "[Lily]";
		mes "นักผจญภัยผู้ใจกว้าง! ขอบคุณมากนะ";
		mes "คำขอคือที่ชั้น 3 ของ Nogg Road สถานที่ที่ข้ายังเข้าไปไม่ได้ แต่ทางที่ประชุมต้องการรายงาน...";
		next;
		setarray .@quest_list[1], 8550, 8553, 8556, 8559;
		.@s = select( "Dealer ที่ทำให้เกิดวิวัฒนาการที่ผิดปกติ", "กำจัดฝันร้ายที่กลายเป็นความจริง", "ตรวจสอบความหนาแน่น", "สิ่งที่แตกสลายได้ง่าย", "ไว้คราวหน้า" );

		if (.@s < 5) {
			.@quest_first = .@quest_list[.@s];
			.@quest_delay = .@quest_first + 1;
			.@quest_daily = .@quest_first + 2;

			callsub( S_CheckDelay, .@quest_delay );

			if (isbegin_quest(.@quest_first) < 2)
				.@quest_ongoing = .@quest_first;
			else
				.@quest_ongoing = .@quest_daily;
		}

		switch( .@s ) {
		case 1:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "ด็อกเตอร์ของเราบอกว่ามอนสเตอร์จะเปลี่ยนลักษณะและรูปร่างเมื่อพวกมันอาศัยอยู่ในที่ที่ต่างออกไป...";
					mes "ทั้งด็อกเตอร์และตัวข้าเอง... การต่อสู้ไม่ใช่สาขาวิชาหลักของเราเลยล่ะ";
					next;
					mes "[Lily]";
					mes "ดังนั้นที่นี่... ไหนดูซิ";
					mes "^0000FFมีรายงานว่า " + getmonsterinfo("DELETER2_H", MOB_NAME) + " และ " + getmonsterinfo("DELETER1_H", MOB_NAME) + "^000000 เป็นต้นเหตุของวิวัฒนาการที่ผิดปกติ";
					mes "แต่ข้าไม่สามารถเข้าไปใกล้พวกมันได้เลยเพราะนิสัยที่ดุร้ายของมัน...";
					next;
				}
				mes "[Lily]";
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road,";
				mes "^0000FFกำจัด " + getmonsterinfo("DELETER1_H", MOB_NAME) + " 15 ตัว^000000";
				mes "และ ^0000FF" + getmonsterinfo("DELETER2_H", MOB_NAME) + " 15 ตัว^000000 แล้วบอกข้าทีว่าพวกมันรับมือยากแค่ไหน";
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road,";
				mes "^0000FFกำจัด " + getmonsterinfo("DELETER1_H", MOB_NAME) + " 15 ตัว^000000";
				mes "และ ^0000FF" + getmonsterinfo("DELETER2_H", MOB_NAME) + " 15 ตัว^000000 แล้วบอกข้าทีว่าพวกมันรับมือยากแค่ไหน";
				close;
			case 2:
				mes "[Lily]";
				mes "เป็นยังไงบ้าง?";
				mes "มันแตกต่างจาก Deleter บนบกหรือบนอากาศทั่วไปยังไง?";
				next;
				mes "[Lily]";
				mes "อืม... ท่านไม่เป็นไรใช่ไหม?";
				mes "อื้มมม โอเค";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 4852278,3396600 );
			}
			end;
		case 2:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "" + getmonsterinfo("NIGHTMARE_TERROR_H", MOB_NAME) + " คือ Nightmare Terror ที่แข็งแกร่งขึ้น...";
					mes "ข้าเคยเห็นแค่ Nightmare Terror ปกติ แค่มองมันก็ทำให้ข้าตัวแข็งทื่อจนขยับไม่ได้แล้ว แม้ว่าศาสตราจารย์จะลากข้าออกมาก็เถอะ...";
					next;
					mes "[Lily]";
					mes "ดังนั้นที่นี่... ไหนดูซิ";
				}
				else {
					mes "[Lily]";
				}
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road,";
				mes "^0000FFปราบ " + getmonsterinfo("NIGHTMARE_TERROR_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าทีว่ามันรับมือยากแค่ไหน";
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road";
				mes "^0000FF ปราบ " + getmonsterinfo("NIGHTMARE_TERROR_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าทีว่ามันรับมือยากแค่ไหน";
				close;
			case 2:
				mes "[Lily]";
				mes "เป็นยังไงบ้าง?";
				mes "มันแตกต่างจาก Nightmare Terror ปกติยังไง?";
				next;
				mes "[Lily]";
				mes "หืม... จริงเหรอ?";
				mes "ข้าควรทำยังไงดี... พระเจ้า มันเป็นเรื่องจริงเหรอเนี่ย?";
				mes "อื้มมม โอเค";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 1661868,1163304 );
			}
			end;
		case 3:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "ข้าได้ยินมาว่ามีความแตกต่างกันมากในเรื่องความหนาแน่นระหว่าง Lava Golem และ " + getmonsterinfo("LAVA_GOLEM_H", MOB_NAME) + ".";
				}
				else {
					mes "[Lily]";
				}
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road";
				mes "^0000FFปราบ " + getmonsterinfo("LAVA_GOLEM_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าถึงความแตกต่างของความหนาแน่น";
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road";
				mes "^0000FFปราบ " + getmonsterinfo("LAVA_GOLEM_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าถึงความแตกต่างของความหนาแน่น";
				close;
			case 2:
				mes "[Lily]";
				mes "เป็นยังไงบ้าง?";
				mes "มันแตกต่างจาก Lava Golem ปกติยังไง?";
				next;
				mes "[Lily]";
				mes "เอ่อ... ใช่แล้ว เปลือกของมัน...";
				mes "คะ? ฝ่าเท้าเหรอ? จริงด้วยสิ...";
				mes "อื้มมม โอเค";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 1590396,1113275 );
			}
			end;
		case 4:
			switch( checkquest(.@quest_ongoing,HUNTING) ) {
			case -1:
				if (.@quest_ongoing == .@quest_first) {
					mes "[Lily]";
					mes "" + getmonsterinfo("EXPLOSION_H", MOB_NAME) + " และ " + getmonsterinfo("BLAZZER_H", MOB_NAME) + " ว่ากันว่ามันแตกสลายได้ง่ายกว่า Explosion และ Blazer ปกติมาก...";
					mes "...ศาสตราจารย์ของเราบอกไว้น่ะ";
					next;
					mes "[Lily]";
					mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road,";
					mes "^0000FFกำจัด " + getmonsterinfo("EXPLOSION_H", MOB_NAME) + " 10 ตัว^000000";
					mes "^0000FFและ " + getmonsterinfo("BLAZZER_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าว่าพวกมันเป็นยังไง";
					mes "ท่านไม่ต้องไปสัมผัสมันก็ได้ แค่ดูก็รู้แล้ว...";
				}
				else {
					mes "[Lily]";
					mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road,";
					mes "^0000FFกำจัด " + getmonsterinfo("EXPLOSION_H", MOB_NAME) + " 10 ตัว^000000";
					mes "^0000FFและ " + getmonsterinfo("BLAZZER_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าว่าพวกมันเป็นยังไง";
				}
				callsub( S_Give, .@quest_ongoing );
			case 0:
			case 1:
				mes "[Lily]";
				mes "ที่สิงสถิตอยู่บนชั้น 3 ของ Nogg Road,";
				mes "^0000FFกำจัด " + getmonsterinfo("EXPLOSION_H", MOB_NAME) + " 10 ตัว^000000";
				mes "^0000FFและ " + getmonsterinfo("BLAZZER_H", MOB_NAME) + " 10 ตัว^000000 แล้วบอกข้าว่าพวกมันเป็นยังไง";
				close;
			case 2:
				mes "[Lily]";
				mes "เป็นยังไงบ้าง?";
				mes "มันแตกต่างจาก Explosion หรือ Blazer ปกติยังไง?";
				next;
				mes "[Lily]";
				mes "เอ่อ... ใช่แล้ว ผ่าน...";
				mes "มันแตกสลายได้ง่ายกว่าจริงๆ...";
				mes "อ้อ ฝุ่นเยอะกว่ามากเลยสินะ";
				mes "อื้มมม โอเค";
				callsub( S_SetDelay, .@quest_ongoing, .@quest_first, .@quest_delay, 3251388,2275966 );
			}
			end;
		case 5:
			mes "[Lily]";
			mes "อา...";
			mes "เมื่อไหร่ข้าจะหลุดพ้นจากมนตราของศาสตราจารย์คนนี้ได้ซะทีนะ...";
			close;
		}
		end;
	case 2:
		mes "[Lily]";
		mes "อย่างที่ท่านเห็น ข้าเป็นเพียง Sage ที่ไร้ความสามารถคนหนึ่ง";
		mes "ศาสตราจารย์ที่ข้ารับใช้อยู่นั้นค่อนข้างดื้อรั้น... ข้าหมายถึง ใครบางคนต้องออกไปสำรวจน่ะ";
		next;
		mes "[Lily]";
		mes "แต่ข้าก็ยังไม่ได้รับอนุญาตให้เข้าไปในชั้น 3 ของ Nogg Road";
		mes "ข้าไปถึงหน้าทางเข้าแล้วแต่พวกเขาก็บอกว่าจะไม่มีวันยอมให้ข้าเข้าไป";
		mes "นั่นสิ... ไม่ใช่ที่ที่เหมาะกับ Sage เลย";
		next;
		mes "[Lily]";
		mes "ถ้าอย่างนั้นข้าจะทำอะไรได้ล่ะ? ทำไมเหล่านักกวีถึงร้องเพลงเฉพาะสิ่งที่พวกเขาเห็นและได้ยินล่ะ?";
		mes "บางคนได้ยินเรื่องนู้นเรื่องนี้มา บางคนบอกข้ามา มันก็ต้องเป็นอย่างนั้นแหละ";
		next;
		mes "[Lily]";
		mes "ไม่ต้องห่วง ข้าจะใช้ชื่อของนักผจญภัยตอนที่รายงานท่านศาสตราจารย์...";
		mes "ใช่... " + strcharinfo(0) + ".";
		mes "ตกลงตามนั้น";
		next;
		mes "[Lily]";
		mes "นี่คือรายชื่อมอนสเตอร์ที่จะใช้เป็นวัตถุดิบในการวิจัย โปรดตรวจสอบเควสหากท่านสนใจ";
		close;
	case 3:
		mes "[Lily]";
		mes "อ๊ะ ไม่นะ...!";
		mes "นักผจญภัยผู้ใจดี โปรดช่วยให้ข้าเป็นอิสระจากมนตราของศาสตราจารย์ด้วยเถิด...!";
		close;
	}
	end;

S_CheckDelay:
	switch( checkquest(getarg(0),PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		mes "[Lily]";
		mes "ท่านทำรายงานของวันนี้เสร็จเรียบร้อยแล้ว";
		mes "ท่านจะช่วยข้าทำรายงานของวันพรุ่งนี้ด้วยไหม...?";
		close;
	case 2:
		erasequest getarg(0);
		break;
	}
	return;

S_Give:
	next;
	if (select( "ข้าตกลง", "ข้ากำลังยุ่ง" ) == 2) {
		mes "[Lily]";
		mes "เดิมที งานวิจัยต้องอาศัยความเพียรพยายามนะ";
		mes "ช่วยข้าตอนที่ท่านไม่ยุ่งก็ได้";
		close;
	}
	mes "[Lily]";
	mes "ขอบคุณมาก!";
	mes "ข้าจะรออยู่ที่นี่นะ";
	setquest getarg(0);
	close;

S_SetDelay:
	.@quest_ongoing = getarg(0);
	.@quest_first = getarg(1);
	.@quest_delay = getarg(2);
	next;
	mes "[Lily]";
	mes "ขอบคุณสำหรับคำอธิบายของท่าน";
	mes "แต่ข้าไม่คิดว่าท่านจะเข้าใจเรื่องราวทั้งหมดได้ด้วยเรื่องแค่นั้นหรอก แม้ว่านี่จะเป็นเสน่ห์ของการร้องขอให้ตรวจสอบก็เถอะ...";
	mes "ข้าจะขอรบกวนท่านอีกครั้งในวันพรุ่งนี้";
	if (.@quest_ongoing == .@quest_first)
		completequest .@quest_ongoing;
	else
		erasequest .@quest_ongoing;
	setquest .@quest_delay;
	getexp getarg(3), getarg(4);
	close;

OnInit:
	setunitdata getnpcid(0), UNPC_CLASS, JOB_SAGE;
	setunitdata getnpcid(0), UNPC_SEX, SEX_FEMALE;
	setunitdata getnpcid(0), UNPC_HAIRCOLOR, 6;
	setunitdata getnpcid(0), UNPC_HAIRSTYLE, 22;
	setunitdata getnpcid(0), UNPC_HEADTOP, 316;

	// completed first quest
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8550,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8553,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8556,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8559,HUNTING) == 2" );

	// completed daily
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8552,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8555,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8558,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8561,HUNTING) == 2" );

	// timeout
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8551,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8554,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8557,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8560,PLAYTIME) == 2" );

	// not started first quest
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8550) == 0 && checkquest(8551,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8553) == 0 && checkquest(8553,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8556) == 0 && checkquest(8555,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8559) == 0 && checkquest(8557,PLAYTIME) == -1" );

	// not started daily
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8550) == 2 && isbegin_quest(8552) == 0 && checkquest(8551,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8553) == 2 && isbegin_quest(8555) == 0 && checkquest(8554,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8556) == 2 && isbegin_quest(8558) == 0 && checkquest(8557,PLAYTIME) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "isbegin_quest(8559) == 2 && isbegin_quest(8561) == 0 && checkquest(8560,PLAYTIME) == -1" );
	end;
}


// ein_dun03
einbech,143,244,3	script	Apprentice Researcher Sally#extdq01	4_F_LGTGIRL,{
	if (BaseLevel < 180) {
		mes "[Sally]";
		mes "สวัสดี นักผจญภัย!";
		mes "ช่วยแนะนำนักผจญภัยที่มีฝีมือให้ข้าหน่อยสิ!";
		mes "เรื่องนี้มันสำคัญมากๆ เลยนะ เพราะมันหมายถึงหน้าที่การงานของข้าเลย..!";
		next;
		mes "^4d4dffมีเควสสำหรับเลเวล 180 ขึ้นไป^000000";
		close;
	}
	if (checkquest(7879,HUNTING) == 2) {
		mes "[Sally]";
		mes "เราเก็บข้อมูลของ " + getmonsterinfo("POISONOUS", MOB_NAME) + " และ " + getmonsterinfo("TOXIOUS", MOB_NAME) + " ได้เพียงพอแล้วล่ะ!";
		erasequest 7879;
		setquest 7880;
		getexp 27512240,19258560;
		next;
	}
	if (checkquest(7881,HUNTING) == 2) {
		mes "[Sally]";
		mes "ข้าเก็บข้อมูลของ " + getmonsterinfo("MINERAL_G", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_R", MOB_NAME) + " ได้เพียงพอแล้ว!";
		erasequest 7881;
		setquest 7882;
		getexp 27836640,19485600;
		next;
	}
	if (checkquest(7883,HUNTING) == 2) {
		mes "[Sally]";
		mes "ข้อมูลของ " + getmonsterinfo("MINERAL_W", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_P", MOB_NAME) + " มีดังนี้ มันช่วยได้มากเลยล่ะ";
		erasequest 7883;
		setquest 7884;
		getexp 27838400,19486880;
		next;
	}
	if (checkquest(7885,HUNTING) == 2) {
		mes "[Sally]";
		mes "ข้อมูลของ " + getmonsterinfo("ABYSSMAN", MOB_NAME) + " นี่น่าทึ่งอยู่เสมอเลยนะ";
		erasequest 7885;
		setquest 7886;
		getexp 6938800,4857160;
		next;
	}
	if (checkquest(7887,HUNTING) == 2) {
		mes "[Sally]";
		mes "เจ้าหาข้อมูลของ " + getmonsterinfo("JEWELIANT", MOB_NAME) + " และ " + getmonsterinfo("PORCELLIO_W", MOB_NAME) + " มาได้สำเร็จสินะ!";
		erasequest 7887;
		setquest 7888;
		getexp 13854240,9697960;
		next;
	}
	mes "[Sally]";
	mes "เมื่อไหร่ชีวิตเด็กฝึกงานต้อยต่ำคนนี้จะจบลงซะทีนะ?";
	next;
	mes "[Sally]";
	mes "ข้ากำลังมองหาความช่วยเหลือจากนักผจญภัยที่มีฝีมืออย่างสิ้นหวังน่ะ";
	mes "อย่างเช่นเจ้ายังไงล่ะ!";
	mes "เจ้าอยากจะช่วยรักษาหน้าที่การงานของนักวิจัยฝึกหัดที่น่าสงสารคนนี้ไหม?";
	next;
	switch( select( "วิจัยพิษชนิดใหม่", "วิจัยแร่พิเศษ 1", "วิจัยแร่พิเศษ 2", "วิจัย Abyssman", "วิจัย Mineworm", "ทำไมเจ้าถึงทำแบบนี้ล่ะ?" ) ) {
	case 1:
		switch( checkquest(7880,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "อืม อย่าหักโหมนักเลย";
			mes "โอกาสน่ะยังมีมาเสมอแหละ";
			close;
		case 2:
			erasequest 7880;
			break;
		}
		switch( checkquest(7879,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "ข้าต้องการข้อมูลเพื่อวิจัยมอนสเตอร์แก๊สพิษที่เพิ่งค้นพบใหม่ใน Einbech Mine Dungeon น่ะ";
			mes "มันยากที่จะรับมือกับมอนสเตอร์ที่ยึดครองเหมืองด้วยกำลังคนที่มีอยู่ที่นี่ ดังนั้นเราต้องจัดการทั้งสองทาง";
			next;
			mes "[Sally]";
			mes "ข้าจะเก็บข้อมูลในขณะที่นักผจญภัยกำลังต่อสู้กับ ^4d4dff" + getmonsterinfo("POISONOUS", MOB_NAME) + " และ " + getmonsterinfo("TOXIOUS", MOB_NAME) + "^000000";
			mes "เป็นข้อเสนอที่ดีใช่ไหมล่ะ?";
			next;
			if (select( "รับคำร้องขอ", "ปฏิเสธ" ) == 2) {
				mes "[Sally]";
				mes "ไม่เป็นไรหรอก";
				mes "โลกนี้ยังกว้างใหญ่และยังมีนักผจญภัยอีกมากมาย";
				close;
			}
			mes "[Sally]";
			mes "ถ้าอย่างนั้น โปรดช่วยจัดการ ^4d4dff" + getmonsterinfo("POISONOUS", MOB_NAME) + " และ " + getmonsterinfo("TOXIOUS", MOB_NAME) + "^000000 ที่ปรากฏตัวบน ^4d4dffชั้น 3 ของ Einbech Mine Dungeon^000000 ให้ข้าที!";
			setquest 7879;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "" + getmonsterinfo("POISONOUS", MOB_NAME) + " และ " + getmonsterinfo("TOXIOUS", MOB_NAME) + " อยู่ที่นี่ บนชั้น 3 ของ Einbech Mine Dungeon";
			mes "พวกมันรับมือยากเกินไปสำหรับคนธรรมดาอย่างข้า ดังนั้นข้าจะแอบดูเจ้าทำงานแล้วเก็บข้อมูลอยู่ห่างๆ นะ";
			close;
		case 2:
			end;
		}
		end;
	case 2:
		switch( checkquest(7882,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "อืม อย่าหักโหมนักเลย";
			mes "โอกาสน่ะยังมีมาเสมอแหละ";
			close;
		case 2:
			erasequest 7882;
			break;
		}
		switch( checkquest(7881,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "แร่ธาตุนั้นมีหลายประเภท แร่ที่เราต้องการตรวจสอบในครั้งนี้คือ ^4d4dff" + getmonsterinfo("MINERAL_G", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_R", MOB_NAME) + "^000000";
			mes "มาเก็บข้อมูลที่จำเป็นสำหรับการวิจัยไปพร้อมกับควบคุมจำนวนของพวกมันกันเถอะ!";
			next;
			if (select( "รับคำร้องขอ", "ปฏิเสธ" ) == 2) {
				mes "[Sally]";
				mes "ไม่เป็นไรหรอก";
				mes "โลกนี้ยังกว้างใหญ่และยังมีนักผจญภัยอีกมากมาย";
				close;
			}
			mes "[Sally]";
			mes "ถ้าอย่างนั้น โปรดช่วยจัดการ ^4d4dff" + getmonsterinfo("MINERAL_G", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_R", MOB_NAME) + "^000000 ที่ปรากฏตัวบน ^4d4dffชั้น 3 ของ Einbech mine dungeon^000000 ให้ข้าที!";
			setquest 7881;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "" + getmonsterinfo("MINERAL_G", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_R", MOB_NAME) + " สามารถพบได้ที่นี่ บนชั้น 3 ของ Einbech Mine Dungeon";
			mes "ระวังตัวด้วยนะ พวกมันอันตรายมาก";
			close;
		case 2:
			end;
		}
		end;
	case 3:
		switch( checkquest(7884,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "อืม อย่าหักโหมนักเลย";
			mes "โอกาสน่ะยังมีมาเสมอแหละ";
			close;
		case 2:
			erasequest 7884;
			break;
		}
		switch( checkquest(7883,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "แร่ธาตุนั้นมีหลายประเภท แร่ที่เราต้องการตรวจสอบในครั้งนี้คือ ^4d4dff" + getmonsterinfo("MINERAL_W", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_P", MOB_NAME) + "^000000";
			mes "มาเก็บข้อมูลที่จำเป็นสำหรับการวิจัยไปพร้อมกับควบคุมจำนวนของพวกมันกันเถอะ!";
			next;
			if (select( "รับคำร้องขอ", "ปฏิเสธ" ) == 2) {
				mes "[Sally]";
				mes "ไม่เป็นไรหรอก";
				mes "โลกนี้ยังกว้างใหญ่และยังมีนักผจญภัยอีกมากมาย";
				close;
			}
			mes "[Sally]";
			mes "ถ้าอย่างนั้น โปรดช่วยจัดการ ^4d4dff" + getmonsterinfo("MINERAL_W", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_P", MOB_NAME) + "^000000 ที่ปรากฏตัวบน ^4d4dffชั้น 3 ของ Einbech Mine Dungeon^000000 ให้ข้าที!";
			setquest 7883;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "" + getmonsterinfo("MINERAL_W", MOB_NAME) + " และ " + getmonsterinfo("MINERAL_P", MOB_NAME) + " สามารถพบได้ที่นี่ บนชั้น 3 ของ Einbech Mine Dungeon";
			mes "ระวังตัวด้วยนะ พวกมันอันตรายมาก";
			close;
		case 2:
			end;
		}
		end;
	case 4:
		switch( checkquest(7886,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "อืม อย่าหักโหมนักเลย";
			mes "โอกาสน่ะยังมีมาเสมอแหละ";
			close;
		case 2:
			erasequest 7886;
			break;
		}
		switch( checkquest(7885,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "ถ้าเจ้าจ้องมองไปในขุมนรก ว่ากันว่าเจ้าจะถูกขุมนรกกลืนกิน ไม่ใช่ส่งเสียงเมี๊ยวใส่เจ้าหรอกนะ";
			mes "ข้าคิดว่าชื่อเรียกทั่วไปของ " + getmonsterinfo("ABYSSMAN", MOB_NAME) + " ก็เป็นแบบนั้นเหมือนกัน";
			mes "ดังนั้นเราจึงต้องการข้อมูลของ ^4d4dff" + getmonsterinfo("ABYSSMAN", MOB_NAME) + "^000000";
			next;
			if (select( "รับคำร้องขอ", "ปฏิเสธ" ) == 2) {
				mes "[Sally]";
				mes "ไม่เป็นไรหรอก";
				mes "โลกนี้ยังกว้างใหญ่และยังมีนักผจญภัยอีกมากมาย";
				close;
			}
			mes "[Sally]";
			mes "ถ้าอย่างนั้น โปรดช่วยจัดการ ^4d4dff" + getmonsterinfo("ABYSSMAN", MOB_NAME) + "^000000 ที่ปรากฏตัวบน ^4d4dffชั้น 3 ของ Einbech Mine Dungeon^000000 ให้ข้าที!";
			setquest 7885;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "" + getmonsterinfo("ABYSSMAN", MOB_NAME) + " อยู่ที่นี่ บนชั้น 3 ของ Einbech Mine Dungeon";
			mes "ระวังตัวด้วยนะ พวกมันอันตรายมาก";
			close;
		case 2:
			end;
		}
		end;
	case 5:
		switch( checkquest(7888,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Sally]";
			mes "อืม อย่าหักโหมนักเลย";
			mes "โอกาสน่ะยังมีมาเสมอแหละ";
			close;
		case 2:
			erasequest 7888;
			break;
		}
		switch( checkquest(7887,HUNTING) ) {
		case -1:
			mes "[Sally]";
			mes "พวกแมลงชนิดพิเศษมักจะอาศัยอยู่ในที่มืดๆ อย่างในดันเจี้ยน";
			mes "ข้อมูลที่ต้องเก็บรวบรวมในครั้งนี้คือ ^4d4dff" + getmonsterinfo("PORCELLIO_W", MOB_NAME) + " และ " + getmonsterinfo("JEWELIANT", MOB_NAME) + "^000000 หรือที่รู้จักกันในชื่อ Mineworm";
			mes "มันแตกต่างจาก Porcellio ทั่วไปมากไหมนะ?";
			next;
			if (select( "รับคำร้องขอ", "ปฏิเสธ" ) == 2) {
				mes "[Sally]";
				mes "ไม่เป็นไรหรอก";
				mes "โลกนี้ยังกว้างใหญ่และยังมีนักผจญภัยอีกมากมาย";
				close;
			}
			mes "[Sally]";
			mes "ถ้าอย่างนั้น โปรดช่วยจัดการ ^4d4dff" + getmonsterinfo("PORCELLIO_W", MOB_NAME) + " และ " + getmonsterinfo("JEWELIANT", MOB_NAME) + "^000000 ที่ปรากฏตัวบน ^4d4dffชั้น 3 ของ Einbech Mine Dungeon^000000 ให้ข้าที!";
			setquest 7887;
			close;
		case 0:
		case 1:
			mes "[Sally]";
			mes "" + getmonsterinfo("PORCELLIO_W", MOB_NAME) + " และ " + getmonsterinfo("JEWELIANT", MOB_NAME) + " อยู่ที่นี่ บนชั้น 3 ของ Einbech Mine Dungeon";
			mes "ระวังตัวด้วยนะ พวกมันอันตรายมาก";
			close;
		case 2:
			end;
		}
		end;
	case 6:
		mes "[Sally]";
		mes "หืม? นั่นก็เพื่อให้ได้เป็นพนักงานประจำยังไงล่ะ!";
		mes "เมื่อไม่นานมานี้ ข้าได้เข้าร่วม Regenschrum ในฐานะนักวิจัยฝึกหัด และนี่คือสิ่งที่ข้าได้รับมอบหมายให้ทำ";
		mes "มันเกี่ยวกับการเก็บข้อมูลมอนสเตอร์ในเหมือง ดังนั้นการประเมินผลงานของข้าจึงขึ้นอยู่กับเรื่องนี้";
		next;
		mes "[Sally]";
		mes "และมีข้อร้องเรียนมาจากสำนักงานใหญ่ตลอดว่าต้องทำยังไงกับพวกมอนสเตอร์ในเหมืองพวกนี้ดี";
		mes "ในขณะที่เก็บข้อมูล มันก็เป็นการควบคุมจำนวนประชากรของพวกมันไปด้วย การประเมินผลนี้ถือว่าวิน-วิน (win-win) กันทั้งสองฝ่ายเลยล่ะ!";
		close;
	}
	end;

OnInit:
	// completed
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7879,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7881,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7883,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7885,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7887,HUNTING) == 2" );

	// timeout
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7880,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7882,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7884,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7886,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7888,PLAYTIME) == 2" );

	// not started
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "BaseLevel >= 180 && isbegin_quest(7879) == 0 && checkquest(7880,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "BaseLevel >= 180 && isbegin_quest(7881) == 0 && checkquest(7882,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "BaseLevel >= 180 && isbegin_quest(7883) == 0 && checkquest(7884,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "BaseLevel >= 180 && isbegin_quest(7885) == 0 && checkquest(7886,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "BaseLevel >= 180 && isbegin_quest(7887) == 0 && checkquest(7888,PLAYTIME) == -1" );
	end;
}

einbech,156,243,3	script	Need help#exsk	1_M_SMITH,{
	mes "[Olive]";
	mes "ยอดสั่งซื้อยังคงทะลักเข้ามาไม่หยุด แต่ทุกคนที่ทำงานที่นั่นกลับพากันหนีไปหมด... เรื่องใหญ่แล้ว เรื่องใหญ่จริงๆ";
	close;
}

einbech,154,244,5	script	Need help#exsk_	4_F_JOB_BLACKSMITH,{
	mes "[Atri]";
	mes "ให้ตายสิ มันน่าจะเร็วกว่าถ้าข้าไปหัดใช้ดาบ! ข้าใช้แต่จอบขุดแร่มาตั้ง 10 ปีแล้ว เดี๋ยวข้าก็คงจะเรียนรู้มันได้ในไม่ช้าแหละ";
	close;
}
