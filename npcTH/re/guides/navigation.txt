//===== rAthena Script ======================================= 
//= Navigation Functions
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Controls the navigation system for Renewal guides.
//= Clients before 2011-10-10 do not support navigation
//= links, and will default to colored or plain text.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Added 'is_clientver' command. [Euphy]
//= 1.2 Dropped 'is_clientver' command. [Lemongrass]
//============================================================ 

function	script	F_Navi	{

	if ( PACKETVER < 20111010 )
		set .@disabled,1;

	// ฟังก์ชันนี้รับพารามิเตอร์ 0 ~ 3 ตัว
	switch(getargcount()) {
	case 0:	// ตรวจสอบว่าระบบถูกปิดใช้งานหรือไม่
		return .@disabled;
	case 1:	// แสดงข้อความข้อมูล หากเปิดใช้งานระบบ
		if (!.@disabled) {
			next;
			mes getarg(0);
			mes "เมื่อคุณคลิกที่ ^B9062F[ชื่อสถานที่]^000000 คุณจะได้รับบริการ ^B9062Fระบบนำทาง (Navigation)^000000 ที่ทันสมัยที่สุด!";
		}
		return;
	default: // แสดงลิงก์นำทาง หากเปิดใช้งานระบบ; มิฉะนั้นให้ตั้งค่าสีข้อความ หากมีการกำหนดไว้
		if (!.@disabled)
			return "<NAVI>[" + getarg(0) + "]<INFO>" + getarg(1) + ",0,000,0</INFO></NAVI>";
		else if (getargcount() == 3)
			return getarg(2)+getarg(0)+"^000000";
		else
			return getarg(0);
	}
}
