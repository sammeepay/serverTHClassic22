//===== rAthena Script ======================================= 
//= Izlude Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Izlude.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Updated to match the new Izlude Map. [Masao]
//= 1.2 Added Renewal Izlude viewpoint coordinates.
//= 1.3 Navigation system update. [Euphy]
//=     Optimized.
//= 1.4 Added Izlude duplicates. [Euphy]
//============================================================ 

-	script	::GuideIzlude	-1,{
	cutin "prt_soldier",2;
	mes "[Izlude Guide]";
	mes "ยินดีต้อนรับสู่ ^8B4513Izlude^000000,";
	mes "เมืองบริวารของ Prontera";
	mes "คุณต้องการความช่วยเหลือในการนำทางภายในเมืองไหม?";
	F_Navi("[Izlude Guide]");
	next;
	set .@map$, strnpcinfo(4);
	while (1) {
		switch(select("[ สถานที่สำคัญ ]:[ พ่อค้าและผู้ช่วย ]:ลบเครื่องหมายออกจาก Mini-Map:ยกเลิก")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Cryptura Academy ]:[ ท่าเรือ ]:[ เรือเหาะระหว่างประเทศ ]:[ Arena ]:[ ร้านขายอุปกรณ์ ]:[ สมาคม Swordsman ]:[ สมาคม Enchant ]:[ โรงตีเหล็ก ]:เมนูก่อนหน้า")) {
				case 1:
					mes "[Izlude Guide]";
					mes "นักผจญภัยมือใหม่ควรไปที่ "+F_Navi("Cryptura Academy","iz_ac01,100,39")+" เพื่อรับข้อมูลเพิ่มเติมและการฝึกฝนนะ!";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,128,260,0,0xD2691E;
					next;
					break;
				case 2:
					mes "[Izlude Guide]";
					mes "คุณสามารถใช้บริการ "+F_Navi("Marina",.@map$+",197,205","^B9062F");
					mes "เพื่อเดินทางไปยัง ^8B4513Alberta^000000 หรือ ^8B4513Byalan Island^000000";
					mes "โปรดระวัง ^8B4513Izlude Dungeon^000000";
					mes "บนเกาะ Byalan ด้วย เพราะที่นั่น";
					mes "เต็มไปด้วยมอนสเตอร์ที่น่ากลัว";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,197,205,1,0xFF0000;
					next;
					break;
				case 3:
					mes "[Izlude Guide]";
					mes " "+F_Navi("International Airship",.@map$+",202,75","^0000FF");
					mes "มุ่งหน้าไปยัง ^8B4513Juno^000000 และ ^8B4513Rachel^000000";
					mes "เดี๋ยวฉันจะทำเครื่องหมายตำแหน่งของมัน";
					mes "บน mini-map ของคุณให้";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,210,73,2,0x0A82FF;
					next;
					break;
				case 4:
					mes "[Izlude Guide]";
					mes " "+F_Navi("Arena",.@map$+",213,161","^FF5400");
					mes "เป็นสถานที่ที่เหมาะสำหรับ";
					mes "การทดสอบความสามารถในการต่อสู้ของคุณ";
					mes "เดี๋ยวฉันจะทำเครื่องหมายตำแหน่งของมัน";
					mes "บน mini-map ของคุณให้";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,213,161,3,0xFF5400;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Kit Shop",.@map$+",110,182","^006400");
					viewpoint 1,108,179,4,0xAAFF00;
					next;
					break;
				case 6:
					mes "[Izlude Guide]";
					mes " "+F_Navi("Swordsman Guild",.@map$+",51,172")+" คือที่ที่คุณสามารถ ^800080เปลี่ยนอาชีพเป็น Swordsman^000000 ได้";
					mes "เดี๋ยวฉันจะทำเครื่องหมายตำแหน่งของมัน";
					mes "บน mini-map ของคุณให้";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,52,172,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("Enchant Association",.@map$+",97,125","^B9062F");
					viewpoint 1,97,125,6,0xDA70D6;
					next;
					break;
				case 8:
					callsub L_Mark, F_Navi("Forge",.@map$+",153,126");
					viewpoint 1,153,126,7,0xAAFF00;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ เจ้าหน้าที่เทเลพอร์ต Eden ]:[ พนักงาน Kafra ]:[ นักสะกดจิต ]:[ ผู้ช่วยคู่แต่งงานใหม่ ]:[ กิลด์ทหารรับจ้าง ]:[ คนดูแลสัตว์เลี้ยง ]:เมนูก่อนหน้า")) {
				case 1:
					mes "[Izlude Guide]";
					mes " "+F_Navi("Eden Teleport Officer",.@map$+",131,148","^0000FF");
					mes "ตอนนี้พักอยู่ด้านใน";
					mes "อาคารสมาคม Swordsman";
					mes "เดี๋ยวฉันจะทำเครื่องหมายตำแหน่งของเขาบน mini-map ให้";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,131,148,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Kafra Employee",.@map$+",128,148","^FF5400"),1;
					viewpoint 1,128,148,9,0xFF5400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Hypnotist",.@map$+",127,175"),1;
					viewpoint 1,127,175,10,0x006400;
					next;
					break;
				case 4:
					mes "[Izlude Guide]";
					mes "หากคุณต้องการจะไปฮันนีมูนที่";
					mes "^8B4513Jawaii^000000 สรวงสวรรค์ของคู่แต่งงานใหม่";
					mes "โปรดไปคุยกับ ";
					mes F_Navi("Newlywed Helper",.@map$+",180,224","^B9062F")+" นะ";
					mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
					viewpoint 1,180,224,11,0xFF0000;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Mercenary Guildsman",.@map$+",47,170","^0000FF");
					viewpoint 1,47,170,12,0xDA70D6;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Pet Groomer","izlude_in,72,98"),1;
					viewpoint 1,108,179,13,0x006400;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Izlude Guide]";
			mes "ได้เลย ฉันจะลบเครื่องหมายทั้งหมดออกจาก mini-map ของคุณให้";
			mes "มีอะไรอย่างอื่นที่ฉันพอจะช่วยได้อีกไหม?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Izlude Guide]";
			mes "ขอให้มีความสุขกับการพักผ่อนนะ";
			mes "บ๊ายบาย~!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Izlude Guide]";
	if (getarg(1,0)) {
		mes "เดี๋ยวฉันจะทำเครื่องหมายตำแหน่งของ";
		mes getarg(0);
	} else
		mes "เดี๋ยวฉันจะทำเครื่องหมายตำแหน่งของ "+getarg(0);
	mes "บน mini-map ของคุณให้";
	mes "คุณต้องการตรวจสอบสถานที่อื่นๆ อีกไหม?";
	return;
}

izlude,129,175,4	duplicate(GuideIzlude)	Guide#01izlude	105
izlude_a,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_a	105
izlude_b,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_b	105
izlude_c,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_c	105
izlude_d,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_d	105

izlude,133,113,4	duplicate(GuideIzlude)	Guide#02izlude	105
izlude_a,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_a	105
izlude_b,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_b	105
izlude_c,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_c	105
izlude_d,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_d	105

//izlude,120,206,4	duplicate(GuideIzlude)	Guide#03izlude	105
