//===== rAthena Script ======================================= 
//= Clothes Dyer
//===== By: ================================================== 
//= Usnul
//===== Current Version: ===================================== 
//= 2.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Clothes dyer with standard palletes
//===== Additional Comments: ================================= 
//= Fully working
//= 2.1 Shortened some labels to make this script loading,
//= Added Black+White Colors desc [Lupus]
//= 2.1a added adv classes + baby class support [Lupus]
//= 2.2 Spell checked. [Nexon]
//= 2.3 Fixed dis $hit! [Poki#3]
//= 2.4 Disabled cloth dyeing since it's not implemented
//	   on official servers and cause your client to crash
//	   [Playtester]
//= 2.5 Optimized. [Euphy]
//============================================================

prt_in,284,168,2	script	Dyer Ginedin Rephere	55,{
function Dyes;

	set .@EnableDyes,1;	// ปิดใช้งาน (ตั้งค่าเป็น 0) หากไฟล์ Palette ทำให้เกิดข้อผิดพลาด [ดูหมายเหตุ v2.4]

	set .n$,"[Dyer Ginedin Rephere]";
	mes .n$;
	mes "11... 12... อืม... ดี ข้าคิดว่าข้าน่าจะทำงานให้เสร็จได้ก่อนงานปาร์ตี้คืนนี้จะเริ่มนะ โอ้! ข้าไม่ทันสังเกตเลยว่าคุณอยู่ตรงนี้ด้วย!";
	while(1) {
		next;
		mes .n$;
		mes "มีอะไรให้ข้าช่วยไหม?";
		next;
		switch(select("- พูดคุย:- ย้อมสีเสื้อผ้า:- รายการราคา:- ^777777ยกเลิก^000000")) {
		case 1:
			mes .n$;
			mes "ชีวิตอาจจะดีขึ้นบ้างนิดหน่อยนะ แต่พอข้าดูรายการสั่งของทั้งหมดที่ได้รับมา... เฮ้อ!";
			mes "ดูเหมือนว่าพวกหญิงสาวใน Rune-Midgarts พักนี้คงจะมีฐานะร่ำรวยกันน่าดูเลยสินะ";
			next;
			mes .n$;
			mes "ไม่ใช่ว่าข้าคิดว่าพวกนางกำลังลุ่มหลงในความหรูหราอะไรหรอกนะ บอกไว้ก่อน";
			mes "ข้าไม่คิดว่ามันจะมีอะไรผิดหรอก สำหรับการไขว่คว้าหาความงามและการเป็นคนทันสมัย";
			next;
			mes .n$;
			mes "สำหรับพวกเราที่เป็นมนุษย์ ซึ่งไม่มีขนที่มีสีสันหรือเกล็ดที่สวยงามเหมือนสัตว์อื่น เสื้อผ้าจึงเป็นหนึ่งในไม่กี่วิธีที่เรามีไว้เพื่อแสดงออกถึงบุคลิกภาพ สไตล์ และความงดงามของตนเอง";
			next;
			mes .n$;
			mes "ฮ่าฮ่า... พอข้าลองคิดดูแล้ว ข้ารู้สึกจริงๆ ว่างานของข้านั้นมีคุณค่า ข้าเชื่อว่าข้าได้มอบบริการที่ผู้คนปรารถนา";
			next;
			mes .n$;
			mes "คุณรู้สึกได้เหมือนกันไหม? พลังงานที่ถูกปลดปล่อยออกมาในยามที่ผืนผ้าหยาบๆ และแผ่นหนังถูกปลุกให้มีชีวิตชีวาด้วยสีสัน?.....";
			next;
			mes .n$;
			mes "แม้ว่าขั้นตอนการทำจะน่าเบื่อหน่ายและใช้เวลามากเพียงใด แต่ความปิติและความสุขที่ข้ารู้สึกเมื่อชุดกระโปรงตัวหนึ่งเสร็จสมบูรณ์ มันก็คุ้มค่าเกินกว่าความยากลำบากทั้งหมดที่ลงแรงไปเสียอีก!!";
			break;
		case 2:
			mes .n$;
			if (!.@EnableDyes) {
				mes "ขออภัยด้วย ข้ากำลังยุ่งกับงานอื่นจนไม่สามารถย้อมสีเสื้อผ้าให้คุณได้ในตอนนี้";
				break;
			}
			mes "โอ้... คุณต้องการงานฝีมือของข้างั้นรึ? อืม... ได้สิ ฟังดูดีเลย";
			next;
			mes .n$;
			mes "โปรดเลือกสี";
			mes "ที่เหมาะสมกับคุณ";
			next;

			// Dyes(<ค่าสี>,<หมายเลขสีของ Palette>{,...});
			// สีที่มี: [1]ส้ม, [2]ม่วง, [3]แดง, [4]ดำ, [5]เขียว, [6]น้ำเงิน, [7]ขาว, [8]เหลือง
			switch(BaseJob) {
			case Job_Novice:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(6,1,3,2,5,3,4,4);
			case Job_Swordman:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(3,1,5,3,4,4);
			case Job_Mage:
				if (Sex == SEX_MALE) Dyes(3,1,2,2,1,3,7,4); else Dyes(3,1,2,2,7,3,4,4);
			case Job_Archer:
				if (Sex == SEX_MALE) Dyes(4,1,2,2,5,3); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Acolyte:
				if (Sex == SEX_MALE) Dyes(6,1,3,2,7,3,4,4); else Dyes(3,1,5,3,4,4);
			case Job_Merchant:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(2,1,5,3,4,4);
			case Job_Thief:
				if (Sex == SEX_MALE) Dyes(5,1,3,2,7,4); else Dyes(3,1,7,3,4,4);
			case Job_Knight:
			case Job_Crusader:
				if (Sex == SEX_MALE) Dyes(2,1,3,3,4,4); else Dyes(6,1,7,3,4,4);
			case Job_Priest:
			case Job_Monk:
				if (Sex == SEX_MALE) Dyes(6,1,5,2,3,3,7,4); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Wizard:
			case Job_Sage:
				if (Sex == SEX_MALE) Dyes(6,1,5,3,4,4); else Dyes(3,1,6,2,7,3,5,4);
			case Job_Blacksmith:
			case Job_Alchemist:
				if (Sex == SEX_MALE) Dyes(4,4,5,2,7,3); else Dyes(3,1,5,2,2,3,4,4);
			case Job_Hunter:
			case Job_Bard:
			case Job_Dancer:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(6,1,5,3,2,4);
			case Job_Assassin:
			case Job_Rogue:
				if (Sex == SEX_MALE) Dyes(6,1); else Dyes(4,1,8,2,7,3);
			case Job_Super_Novice:
				if (Sex == SEX_MALE) Dyes(2,1,6,2,5,3,4,4); else Dyes(6,1,3,2,5,3,4,4);
			default: 
				mes "ว้าว ข้าไม่เคยเห็นเสื้อผ้าแบบนั้นมาก่อนเลย! ข้าขอโทษด้วยนะ แต่ข้าไม่คิดว่าจะสามารถย้อมสีมันได้";
				close;
			}
		case 3:
			mes .n$;
			mes "การย้อมแต่ละสีมีค่าใช้จ่าย 10,000 Zeny และสีย้อม (Dye) 1 ขวด:";
			mes " ";
			mes " - ^FF4422สีแดง^000000: Scarlet Dyestuffs";
			mes " - ^D5A500สีเหลือง^000000: Lemon Dyestuffs";
			mes " - ^AA00AAสีม่วง^000000: Violet Dyestuffs";
			mes " - ^FF8800สีส้ม^000000: Orange Dyestuffs";
			mes " - ^4422FFสีน้ำเงิน^000000: CobaltBlue Dyestuffs";
			mes " - ^009500สีเขียว^000000: DarkGreen Dyestuffs";
			mes " - สีดำ: Black Dyestuffs";
			mes " - สีขาว: White Dyestuffs";
			break;
		case 4:
			goto L_End;
		}
	}

L_End:
	mes .n$;
	mes "ทำตัวตามสบายนะ แม้ว่าข้าจะไม่มีเวลาว่างมาต้อนรับคุณก็ตาม";
	close;

function Dyes {
	setarray .@DyeName$[0],"สีดั้งเดิม","สีส้ม","สีม่วง","สีแดง","สีดำ","สีเขียว","สีน้ำเงิน","สีขาว","สีเหลือง";
	setarray .@DyeItems[0],0,980,981,975,983,979,978,982,976;
	set .@menu$,"- สีดั้งเดิม:";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+2)
		set .@menu$, .@menu$+"- "+.@DyeName$[getarg(.@i)]+":";
	set .@menu$, .@menu$+"- ^777777ยกเลิก^000000";
	set .@s, select(.@menu$)-1;
	if (.@s == (getargcount()/2)+1) goto L_End;
	mes .n$;
	if (.@s == 0) {
		mes "ข้าสามารถเปลี่ยนสีเสื้อผ้าของคุณ";
		mes "กลับเป็นสีดั้งเดิมให้ฟรี";
		mes "คุณแน่ใจนะ?";
		next;
		switch(select("ใช่:ไม่")) {
		case 1:
			mes .n$;
			setlook 7,0;
			mes "เสื้อผ้าของคุณถูกเปลี่ยนเป็น "+.@DyeName$[0]+" เรียบร้อยแล้ว";
			close;
		case 2:
			goto L_End;
		}
	}
	if (!countitem(.@DyeItems[getarg((.@s-1)*2)])) {
		mes "หากต้องการให้ข้าย้อมสีเสื้อผ้า ข้าจำเป็นต้องใช้สีย้อม (Dyestuffs) ที่ถูกต้อง โปรดตรวจสอบข้อมูลได้จากรายการราคาของข้า";
		close;
	}
	if (Zeny < 10000) {
		mes "ข้าขอโทษนะ แต่ดูเหมือนคุณจะมีเงินไม่เพียงพอ";
		close;
	}
	delitem .@DyeItems[getarg((.@s-1)*2)],1;
	set Zeny, Zeny-10000;
	setlook 7, ((.@s)?(getarg((.@s-1)*2+1)):0);
	mes "เสื้อผ้าของคุณถูกย้อมเป็น "+((.@s)?.@DyeName$[getarg((.@s-1)*2)]:.@DyeName$[0])+" เรียบร้อยแล้ว";
	close;
}
}
