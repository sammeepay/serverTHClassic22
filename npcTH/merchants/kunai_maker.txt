//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	Kunai Merchant Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "ข้าคือ Kashin ผู้จำหน่าย";
		mes "คุไนสำหรับนินจา ลองเลือก";
		mes "ดูรอบๆ ก่อนสิ แล้วบอกข้า";
		mes "หากเจ้าสนใจสินค้า";
		mes "ชิ้นไหนของข้าบ้าง";
		next;
		switch(select("Fell Poison Kunai 10 อัน:Icicle Kunai 10 อัน:High Wind Kunai 10 อัน:Black Earth Kunai 10 อัน:Heat Wave Kunai 10 อัน:ยกเลิก")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "ถ้าอย่างนั้น ก็ขอบใจ";
			mes "ที่แวะมาที่ร้านของข้า";
			mes "โปรดมาหาข้าได้เสมอเมื่อ";
			mes "เจ้าต้องการซื้อ";
			mes "คุไน ลาไปก่อนล่ะ~";
			close;
		}
	}
	mes "[Kashin]";
	mes "ข้าคือ Kashin ผู้จำหน่าย";
	mes "คุไนสำหรับนินจา หากเจ้า";
	mes "มีเพื่อนคนไหนที่เป็น";
	mes "นินจา เจ้าอาจจะอยาก";
	mes "บอกพวกเขาเรื่องเกี่ยวกับข้านะ";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[Kashin]";
	mes "เจ้าสามารถแลกเปลี่ยน";
	mes getitemname(.@item_req[0]) + " " + .@req_amount[0] + " อัน และ";
	mes getitemname(.@item_req[1]) + " " + .@req_amount[1] + " อัน สำหรับ";
	mes "เซ็ต " + getitemname(.@item_id) + " ทุกๆ 10 อัน";
	next;
	mes "[Kashin]";
	mes "ข้าสามารถให้คุไนเจ้าได้สูงสุดครั้งละ 500 เซ็ตเท่านั้น";
	mes "หากเจ้าต้องการยกเลิกการแลกเปลี่ยน โปรดใส่เลข ''0'' เจ้าต้องการ";
	mes "คุไนกี่เซ็ตดีล่ะ?";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "เอ๊ะ? ข้าขอโทษด้วยนะ แต่";
		mes "ข้าไม่สามารถให้เซ็ตคุไน";
		mes "จำนวนมากขนาดนั้นได้ โปรด";
		mes "ใส่ค่าที่น้อยกว่า 500 ด้วย";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[Kashin]";
		mes "หืม ดูเหมือนเจ้าจะมี";
		mes "ไอเทมไม่เพียงพอสำหรับการ";
		mes "แลกเปลี่ยนคุไนนี้ โปรด";
		mes "ตรวจสอบไอเทมของเจ้าอีกครั้ง";
		close;
	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[Kashin]";
		mes "หืม ดูเหมือนว่าช่องเก็บของ";
		mes "ในตัวเจ้าจะไม่มีที่ว่าง";
		mes "พอจะเก็บไอเทมเพิ่มแล้วนะ";
		mes "เจ้าควรจะไปเพิ่มที่ว่าง";
		mes "ในตัวให้เรียบร้อยก่อน";
		close;
	}
	mes "[Kashin]";
	mes "เยี่ยมเลย ทุกอย่างถูกต้อง";
	mes "เรียบร้อย งั้นเรามาเริ่ม";
	mes "การแลกเปลี่ยนนี้กันเลย";
	mes "ข้ามั่นใจว่าเจ้าจะ";
	mes "พึงพอใจกับคุไน";
	mes "เหล่านี้แน่นอน";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
