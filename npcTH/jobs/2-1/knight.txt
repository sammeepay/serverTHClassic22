//===== rAthena Script ======================================= 
//= Knight Job Quest
//===== By: ================================================== 
//= PGRO TEAM (Aegis).
//= Converted by kobra_k88
//= Further bugfixed and tested by Lupus
//===== Current Version: ===================================== 
//= 3.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job Change quest for Knight class.
//===== Additional Comments: ================================= 
//= 1.0 Fully working.  Please comment out any pre-existing warps for the
//= test rooms in any other files so that the ones specified here can work.
//= 1.1 Fixed a major bug.  Now using the initnpctimer command,
//= donpcevent, and new waitingroom event commands.  No more addtimer
//= spamming.  No longer have to talk to the npc to take the test.  Just enter the chat room.
//= 1.2 More bug fixes.  Changed global variable names to unique ones.
//= Added second set of items to first test. Added Awake pots award for job change.
//= 1.3 Added Baby Class support [Lupus]
//= 1.5 Fixed possible EXP abuse [Lupus]
//= 1.6 Added a func which prevent advanced classes passing
//= 2nd Job Quests again. It also guides adv classes where
//= to go. [Lupus]
//= 2.0 Changed numbers to constants. [Vicious]
//= 2.1 Merged JFunc [Lupus]
//= 2.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 2.3 Several bugfixes with the help of Barron-Monster. [L0ne_W0lf]
//= 2.4 Minor fixes to timer#knt. [L0ne_W0lf]
//= 2.5 Fixes a small bug with making claymores. [L0ne_W0lf]
//=     Corrected usages of killmonster and killmonsterall.
//= 2.6 Corrected an improper areawarp destination. [L0ne_W0lf]
//= 2.7 Deleted unused variables. [Samuray22]
//= 2.7a Corrected a Typo error ";;". [Samuray22]
//= 2.8 Now uses enable and disable waitingroomevent. [L0ne_W0lf]
//= 2.9 Added Quest Log commands. [Kisuka]
//= 3.0 Removed the need for use of 'goto.' [L0ne_W0lf]
//= 3.1 Modified the quest to renewal. [Vali] (Thanks to Muad_Dib for the necessary info).
//= 3.2 More Renewal updates and some optimization. [Euphy]
//= 3.3 Fixed missing changequest. [Joseph]
//============================================================ 

prt_in,88,101,4	script	Chivalry Captain#knt	56,{
	mes "[Captain Herman]";
	if (Upper == 1) {
		mes "หืม? คุณคือ... เกิดอะไรขึ้นกับคุณกันแน่? ฉันเป็นอัศวินผู้ทรงเกียรติมานาน แต่ฉันไม่สามารถเข้าใจความรู้สึกที่ได้รับจากตัวคุณได้เลย...";
		next;
		mes "[Captain Herman]";
		mes "ขอพระเจ้าอวยพรแก่ร่างกายและจิตวิญญาณของท่าน นักรบเอ๋ย ฉันหวังว่าคุณจะแสดงความกล้าหาญและปกป้องเหล่าผู้ที่อ่อนแอกว่าคุณ";
		close;
	}
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) {
			mes "อ้อ สมาชิกแห่ง Chivalry ของเรานี่เอง";
			mes "ฉันหวังว่าคุณจะใช้ชีวิตได้สมกับความคาดหวังของฉันนะ พวกเราได้สาบานตนว่าจะเข้มแข็งเพื่ออาณาจักร แม้ความตายจะมาอยู่ตรงหน้าก็ตาม...";
			close;
		}
		else if (BaseClass == Job_Novice) {
			mes "ยินดีต้อนรับ,";
			mes "ที่นี่คือ";
			mes "Prontera Chivalry.";
			mes "อะไรพาคุณมาที่นี่ล่ะ?";
			next;
			switch(select("ฉันต้องการเปลี่ยนอาชีพเป็น Swordman.:ฉันต้องการเปลี่ยนอาชีพเป็น Knight.:แค่มาเยี่ยมชมน่ะ")) {
			case 1:
				mes "[Captain Herman]";
				mes "อาฮะ~";
				mes "Swordman งั้นรึ?";
				mes "ฉันเสียใจด้วยนะ "+((Sex==SEX_MALE)?"เจ้าหนู":"แม่หนู")+" แต่ว่าคุณ";
				mes "มาผิดที่แล้วล่ะ!";
				next;
				mes "[Captain Herman]";
				mes "ที่นี่ไม่ใช่ Swordsman guild แต่ที่นี่คือ Prontera Chivalry! หากคุณปรารถนาจะเป็น Swordman ให้ไปที่ Swordman Guild ซึ่งตั้งอยู่ที่ Izlude นะ";
				close;
			case 2:
				mes "[Captain Herman]";
				mes "อ้อ ฉันเห็นว่าคุณมีความทะเยอทะยานที่ยิ่งใหญ่ แต่คุณต้องเป็น Swordman เสียก่อนที่จะมาเป็น";
				mes "Knight นะ ทีละขั้นตอนสิ...";
				next;
				mes "[Captain Herman]";
				mes "อันดับแรก ไปที่ Swordman guild ใน Izlude ก่อน แล้วค่อยกลับมาหาเราอีกครั้งเมื่อคุณกลายเป็น Swordman ที่มีประสบการณ์มากพอแล้ว";
				close;
			case 3:
				mes "[Captain Herman]";
				mes "อาฮ่า~";
				mes "คุณคงจะมีเวลาว่างมากสินะ ทำไมไม่ลองไปล่ามอนสเตอร์แทนที่จะมาเดินเตร่ไปมาอย่างไร้จุดหมายดูล่ะ?";
				close;
			}
		}
		else {
			mes "ยินดีต้อนรับ พวกเรา เหล่าอัศวินผู้ภาคภูมิใจแห่ง Prontera Chivalry จะมอบชีวิตนี้เพื่อกษัตริย์และประเทศชาติ! ขอให้มีความสุขกับการมาเยือนนะ";
			close;
		}
	}
	if (KNIGHT_Q == 0) {
		mes "ยินดีต้อนรับ ที่นี่คือ";
		mes "Prontera Chivalry.";
		mes "อะไรพาคุณมาที่นี่ล่ะ?";
		next;
		if (select("ฉันต้องการเปลี่ยนอาชีพเป็น Knight.:แค่มาเยี่ยมชมน่ะ") == 1) {
			mes "[Captain Herman]";
			mes "โอ้...";
			mes "คนหนุ่มสาวตัวน้อย "+((Sex==SEX_MALE)?"ชาย":"หญิง")+" ผู้ปรารถนา";
			mes "จะเป็น Knight!";
			mes "Prontera Chivalry ของเรา";
			mes "จะช่วยเหลือคุณเอง";
			next;
			mes "[Captain Herman]";
			mes "ก่อนอื่นเลย ฉันคือหัวหน้าอัศวินแห่ง Prontera Chivalry, Herman Phon Efesirsus ฉันยินดีมากที่ได้พบคนหนุ่มสาวที่กระตือรือร้นจะเข้าร่วมกับ Prontera Chivalry";
			next;
			mes "[Captain Herman]";
			mes "เรารับเฉพาะ Swordman";
			mes "ที่มี Job Level 40 เป็นอย่างน้อยเท่านั้น";
			mes "เราไม่สามารถรับพิจารณาผู้สมัครที่ยังไม่มีประสบการณ์มากพอที่จะเป็น Knight ได้";
			next;
			mes "[Captain Herman]";
			mes "เมื่อคุณสมัคร และหากเราพบว่า";
			mes "คุณมีคุณสมบัติเหมาะสม เราจะเริ่มขั้นตอนการเปลี่ยนอาชีพ คุณ";
			mes "ต้องการสมัครตอนนี้เลยไหม?";
			next;
			if (select("ใช่ ฉันต้องการสมัคร:ฉันขอไปคิดดูอีกทีนะ") == 1) {
				mes "[Captain Herman]";
				if (JobLevel < 40) {
					mes "อา คุณยังไม่พร้อมที่จะเป็น Knight หรอก! ฉันไม่ได้ระบุไว้ชัดเจนแล้วเหรอว่าต้องมี Job Level 40 น่ะ?";
					next;
					mes "[Captain Herman]";
					mes "แน่นอนว่าฉันเข้าใจถึงความปรารถนาอันแรงกล้าที่จะเข้าร่วมกับเรา แต่ตอนนี้ยังไม่ใช่เวลา ไปออกไปสู้กับมอนสเตอร์ให้มากกว่านี้เสียก่อน เราจะรออยู่ที่นี่";
					close;
				}
				if (SkillPoint) {
					mes "อา...!";
					mes "คุณไม่สามารถเปลี่ยนอาชีพได้หากยังมีแต้มสกิลที่ไม่ได้ใช้หลงเหลืออยู่ กลับมาเมื่อคุณใช้";
					mes "แต้มสกิลทั้งหมดของคุณเรียบร้อยแล้วนะ";
					close;
				}
				set KNIGHT_Q,1;
				setquest 9000;
				mes "ไหนขอฉันดูหน่อย...";
				mes "ชื่อของคุณคือ";
				mes strcharinfo(0) + "...";
				mes "ใช่ไหม?";
				next;
				mes "[Captain Herman]";
				mes "ให้ฉันอธิบายขั้นตอนการเปลี่ยนอาชีพนะ คุณต้องไปพบกับเหล่าอัศวินตามลำดับและผ่านการทดสอบ";
				mes "ของพวกเขาแต่ละคน";
				next;
				mes "[Captain Herman]";
				mes "เมื่อการทดสอบทั้งหมดเสร็จสิ้น อัศวินทุกท่านที่เกี่ยวข้องในการทดสอบของคุณจะมารวมตัวกันและหารือเกี่ยวกับผลงานของคุณ";
				next;
				mes "[Captain Herman]";
				mes "เหล่าอัศวินจะต้องมีมติเป็นเอกฉันท์ในการอนุมัติก่อนที่คุณจะสามารถเข้าร่วม Prontera Chivalry ได้ หากมีเพียงคนเดียวที่คัดค้าน คุณจะต้อง";
				mes "เริ่มใหม่ทั้งหมด";
				next;
				mes "[Captain Herman]";
				mes "แต่ฉันเชื่อว่าหากคุณพยายามด้วยใจที่แน่วแน่ คุณจะได้รับการยอมรับจากเหล่าอัศวิน และได้รับยอมรับให้เป็นสมาชิกแห่ง Chivalry ของเราในที่สุด";
				next;
				mes "[Captain Herman]";
				mes "ดังนั้น อย่าเสียเวลาคุยกันอีกเลย! ไปพบอัศวินเหล่านี้และเริ่มการทดสอบของพวกเขาได้แล้ว เมื่อคุณทำบททดสอบทั้งหมดเสร็จสิ้น ให้กลับมาหาฉันนะ";
				close;
			}
			mes "[Captain Herman]";
			mes "โอ้...!";
			mes "เอาล่ะ ฉันไม่อยากกดดันคุณ ใช้เวลาของคุณคิดทบทวนดูให้ดี กลับมาเมื่อคุณพร้อมที่จะ";
			mes "เปลี่ยนอาชีพ เพราะเราจะรออยู่ที่นี่";
			close;
		}
		mes "[Captain Herman]";
		mes "จะว่าไป คุณเป็น Swordman ไม่ใช่เหรอ? ดูเหมือนคุณจะผ่านการปะทะกับศัตรูมามากมายในสนามรบนะ";
		next;
		mes "[Captain Herman]";
		mes "คุณควรพิจารณาเรื่องการเปลี่ยนอาชีพเป็น Knight ได้แล้วล่ะ มาคุยกับฉันถ้าคุณสนใจ";
		next;
		mes "[Captain Herman]";
		mes "เชิญใช้เวลา";
		mes "เดินดูรอบๆ";
		mes "ตามสบายนะ";
		mes "ขอให้เป็นวันที่ดี";
		close;
	}
	else if (KNIGHT_Q == 1) {
		mes "อืม?";
		mes strcharinfo(0) + ",";
		mes "มีอะไรให้ฉันช่วยล่ะ?";
		mes "อา คุณไม่รู้ว่า";
		mes "ต้องไปหาใครต่อใช่ไหม?";
		next;
		mes "[Captain Herman]";
		mes "ฉันเชื่อว่าเหล่าอัศวินที่ดูแลการทดสอบได้กำหนดลำดับที่คุณต้องไปพบพวกเขาไว้แล้ว ฉันคิดว่ามันจะช่วยให้กระบวนการทดสอบราบรื่นขึ้นนะ";
		next;
		mes "[Captain Herman]";
		mes "อันดับแรก ไปพบ";
		mes "Sir Andrew สำหรับการทดสอบแรกของคุณ อย่าประหม่านักเลย เขาจะอธิบายทุกอย่างให้ฟังเมื่อคุณได้คุยกับเขา";
		close;
	}
	else if (KNIGHT_Q == 2) callsub L_Mission,0;
	else if (KNIGHT_Q == 3) callsub L_Mission,0;
	else if (KNIGHT_Q == 4) {
		callsub L_Mission,1;
		mes "ดูเหมือนว่าคุณจะเสร็จสิ้นการทดสอบไปหนึ่งอย่างแล้ว ไหนดูสิ Sir Andrew ใครคือคนต่อไปที่ Swordman คนนี้ต้องไปพบล่ะ?";
		next;
		mes "[Sir Andrew]";
		mes "ฉันบอกให้ไป";
		mes "พบ Sir Siracuse นะ";
		mes "แปลกจัง ฉันนึกว่าฉันบอก";
		mes "คุณไปแล้ว หรือว่าฉันจะลืมกันนะ...?";
		next;
		mes "[Captain Herman]";
		mes "ได้ยินแล้วใช่ไหม?";
		mes "ไปหา Sir Siracuse และรับบททดสอบจากเขา เมื่อคุณทำบททดสอบของเขาเสร็จแล้ว อย่าลืมดูล่ะว่าคุณต้องไปพบใครเป็นคนต่อไปด้วย";
		close;
	}
	else if (KNIGHT_Q == 5) callsub L_Mission,0;
	else if (KNIGHT_Q == 6) {
		callsub L_Mission,1;
		mes "ไหนดูสิ...";
		mes "คุณผ่านการทดสอบมาสองอย่างแล้ว";
		mes "Sir Siracuse ใครคือคนต่อไปที่ Swordman คนนี้ต้องไปพบล่ะ?";
		next;
		mes "[Sir Siracuse]";
		mes "โอ้...!";
		mes "เอ่อ ใครคือคนต่อไปนะ...?";
		mes "อ้อ! Sir Windsor!";
		next;
		mes "[Captain Herman]";
		mes "มุ่งหน้าไปหา";
		mes "Sir Windsor Benedict เพื่อรับการทดสอบครั้งต่อไป ฟังคำสั่งจากอัศวินที่คุมการทดสอบให้ดี จะได้ไม่รู้สึกหลงทาง ตกลงไหม?";
		close;
	}
	else if (KNIGHT_Q == 7) callsub L_Mission,0;
	else if (KNIGHT_Q == 8) {
		callsub L_Mission,1;
		mes "Sir Windsor...?";
		mes "ใครคือคนต่อไปที่";
		mes "Swordman คนนี้";
		mes "ต้องไปพบ?";
		next;
		mes "[Sir Windsor]";
		mes "...";
		next;
		mes "[Sir Windsor]";
		mes "...";
		mes "......";
		next;
		mes "[Sir Windsor]";
		mes "...Amy Beatrice.";
		next;
		mes "[Captain Herman]";
		mes "อา ไปพบกับ";
		mes "Lady Amy และรับบททดสอบของเธอซะ ตรวจสอบให้แน่ใจว่าคุณใส่ใจกับคนที่จะต้องไปรับการทดสอบถัดไปของคุณด้วยนะ";
		close;
	}
	else if (KNIGHT_Q == 9) callsub L_Mission,0;
	else if (KNIGHT_Q == 10) {
		callsub L_Mission,1;
		mes "ไหนดูสิ...";
		mes "Lady Amy ใคร";
		mes "คือคนต่อไปที่ Swordman";
		mes "คนนี้ต้องไปพบ?";
		next;
		mes "[Lady Amy]";
		mes "โอ้...";
		mes "ฉันบอกให้ไปพบ";
		mes "Sir Edmond ค่ะ!";
		mes "ฮี่ๆ~";
		next;
		mes "[Captain Herman]";
		mes "เอาล่ะ ไปคุย";
		mes "กับ Sir Edmond ซะ เขาจะ";
		mes "เป็นคนดูแลบททดสอบ";
		mes "ถัดไปของคุณ";
		close;
	}
	else if (KNIGHT_Q == 11) callsub L_Mission,0;
	else if (KNIGHT_Q == 12) {
		callsub L_Mission,1;
		mes "คุณเหลือแค่ต้องไปพบอีกเพียงคนเดียวไม่ใช่เหรอ? อัศวินที่";
		mes "ดูแลการทดสอบครั้งสุดท้าย";
		mes "คือ Sir Gray Prospheiro";
		next;
		mes "[Sir Edmond]";
		mes "โลกใบนี้ดำเนินไป";
		mes "ตามกฎของเหตุและผล";
		mes "ทุกอย่างจะถูกเปิดเผยในตอนท้าย";
		next;
		mes "[Captain Herman]";
		mes "ตื่นตัวเข้าไว้และทำ";
		mes "ให้ดีที่สุด เพราะนี่คือ";
		mes "การทดสอบสุดท้ายแล้ว";
		next;
		mes "[Captain Herman]";
		mes "กลับมาหาฉัน";
		mes "หลังจากที่คุณได้";
		mes "ทำบททดสอบ";
		mes "สุดท้ายเสร็จสิ้น";
		close;
	}
	else if (KNIGHT_Q == 13) {
		mes "ทำบททดสอบสุดท้ายให้เสร็จ";
		mes "เมื่อเสร็จสิ้นแล้ว อัศวินทุกคนที่เกี่ยวข้องกับการทดสอบของคุณจะมารวมตัวกัน และเราจะประเมินผลงานของคุณ";
		close;
	}
	else if (KNIGHT_Q == 14) {
		if (SkillPoint) {
			mes "โอ้...!";
			mes "คุณไม่สามารถเปลี่ยนอาชีพได้หากยังมีแต้มสกิลที่ไม่ได้ใช้หลงเหลืออยู่ กลับมาเมื่อคุณจัดสรรแต้มสกิลทั้งหมดของคุณเรียบร้อยแล้วนะ";
			close;
		}
		mes "โอ้ คุณทำบททดสอบทั้งหมดเสร็จแล้วรึ? แต่ไม่ใช่ทุกคนที่ทำบททดสอบเสร็จแล้วจะได้";
		mes "กลายเป็น Knight หรอกนะ";
		next;
		mes "[Captain Herman]";
		mes "ในระหว่างการทดสอบ เราได้ดูว่าคุณมีความจงรักภักดี มีเกียรติ และแข็งแกร่งเพียงใด เรายังดูอีกด้วยว่าคุณเป็นคนสุภาพ และคุณรู้ถึงคุณค่าของความอ่อนน้อมถ่อมตัวและความเคารพยำเกรงหรือไม่";
		next;
		mes "[Captain Herman]";
		mes "ตลอดกระบวนการนี้ ฉันได้สังเกตการกระทำของคุณเช่นกัน ความเห็นของพวกเราทั้งเจ็ดคนจะถูกสะท้อนออกมาในการตัดสินใจเปลี่ยนอาชีพของคุณ";
		next;
		mes "[Captain Herman]";
		mes "เอาล่ะ...";
		mes "เรามาฟังความคิดเห็น";
		mes "ของทุกคนกันเลย!";
		mes "Andrew, คุณคิดยังไงบ้าง?";
		next;
		mes "[Sir Andrew]";
		if (JobLevel == 50) {
			mes "จะให้ฉันพูดอะไรได้ล่ะ?";
			mes "ฉันอนุมัติ!";
			mes "การใช้ชีวิตในฐานะ";
			mes "Swordsman มา";
			mes "จนถึงตอนนี้นั้น";
			mes "มันเพียงพอแล้วล่ะ";
		}
		else {
			mes "คนคนนี้ได้";
			mes "รวบรวมไอเทม";
			mes "ที่หามาได้ยากลำบาก";
			mes "ฉันอนุมัติ!";
			mes "ฉันเชื่อว่า "+((Sex==SEX_MALE)?"เขา":"เธอ")+" จะยังคงมีความจงรักภักดีต่อไปหลังจากที่ได้เป็น Knight แล้ว";
		}
		next;
		mes "[Captain Herman]";
		mes "อืมม.";
		mes "เป็นการวิจารณ์ที่ดีมาก";
		mes "Siracuse, คุณคิดยังไงล่ะ?";
		next;
		mes "[Sir Siracuse]";
		mes "เหอะ ก็ดี แม้จะไม่ค่อยตรงกับ";
		mes "ที่ฉันต้องการนัก แต่หวังว่าจะดีขึ้นในอนาคตนะ";
		next;
		mes "[Sir Siracuse]";
		mes "หลังจากเป็น Knight แล้ว คุณต้องสร้างชื่อเสียงที่ดีผ่านเกียรติยศ เอิ่ม... ฉันอนุมัติ";
		next;
		mes "[Captain Herman]";
		mes "โอเค...";
		mes "Windsor,";
		mes "แล้วคุณล่ะ?";
		next;
		mes "[Sir Windsor]";
		mes "...";
		next;
		mes "[Sir Windsor]";
		mes "...";
		mes "......";
		next;
		mes "[Sir Windsor]";
		mes "....อนุมัติ";
		next;
		mes "[Captain Herman]";
		mes "ฉันไม่คิดว่า";
		mes "เขาจะคัดค้านนะ";
		mes "งั้นเรามาฟังความเห็น";
		mes "ของ Amy กันบ้าง";
		next;
		mes "[Lady Amy]";
		if (Sex == SEX_MALE) {
			mes "อืมม~ เขาสุภาพมากเลยค่ะ!";
			mes "เขาจะเติบโตขึ้นเป็น Knight ที่ยอดเยี่ยมแน่นอน แถมเขายังมีแก้มน่ารักน่าหยิกด้วยนะ~ ฮี่ ฮี่!";
		}
		else {
			mes "อืมม~ เธอต้องยอดเยี่ยมแน่ๆ!";
			mes "เธอสุภาพมากแล้วก็น่ารักมากด้วย งั้นขอเพิ่มคะแนนให้หน่อยละกัน! เฮะ~";
			mes "ฉันไม่ควรพูดอะไรแบบนี้เลยแฮะ!";
		}
		next;
		mes "[Captain Herman]";
		mes "เอาล่ะ...";
		mes "เป็นการวิจารณ์ที่แปลกดีนะ";
		mes "แต่ฉันเชื่อว่า";
		mes "เธออนุมัติ";
		mes "Edmond, บอกความในใจ";
		mes "ของคุณมาสิ";
		next;
		mes "[Sir Edmond]";
		if (Sex == SEX_MALE) {
			mes "เขาดูจะกระด้างไปบ้าง แต่มีบางสิ่งที่สว่างไสวเปล่งประกายอยู่ในตัวเขา ด้วยการขัดเกลาและฝึกฝน คุณค่าที่แท้จริงของเขาจะส่องสว่างออกมา";
			mes "ประดุจดั่งดวงอาทิตย์";
		}
		else {
			mes "มันยากที่จะมองเห็น แต่มีความงามทางจิตวิญญาณอยู่ภายในตัวเธอ ด้วยการขัดเกลาและฝึกฝน คุณค่าที่แท้จริงของเธอจะเปล่งประกายงดงาม";
			mes "ประดุจดั่งดวงจันทร์";
		}
		next;
		mes "[Captain Herman]";
		mes "สุดท้าย...";
		mes "Gray. ฉันอยาก";
		mes "ฟังความคิดเห็นของคุณ";
		next;
		mes "[Gray]";
		mes "การที่หนุ่มสาวตัวน้อย "+((Sex==SEX_MALE)?"สุภาพบุรุษ":"สุภาพสตรี")+" มาที่นี่ด้วยความมุ่งมั่นที่จะเป็น Knight นั้น มันก็เพียงพอแล้ว...";
		next;
		mes "[Captain Herman]";
		mes "ทุกคน";
		mes "อนุมัติแล้ว";
		mes "ไม่มีใครคัดค้าน";
		mes "งั้นฉันจะบอก";
		mes "ความคิดเห็นของฉันให้ฟัง";
		next;
		mes "[Captain Herman]";
		mes "การตัดสินใจของฉันคือ...";
		next;
		mes "[Captain Herman]";
		mes "ฉันอนุมัติ";
		next;
		mes "[Captain Herman]";
		mes "คุณอาจจะไม่ได้ทำการทดสอบทั้งหมดอย่างสมบูรณ์แบบ แต่คุณมีคุณสมบัติที่จำเป็นครบถ้วนที่จะเป็น";
		mes "Knight";
		next;
		completequest 9012;
		callfunc "Job_Change",Job_Knight;
		callfunc "F_ClearJobVar"; // Clears all job variables for the current player
		mes "[Captain Herman]";
		mes "ฉันขอประกาศให้";
		mes "คุณเป็นสมาชิกของ";
		mes "Prontera Chivalry ณ บัดนี้";
		mes "จงปกป้องผู้ที่อ่อนแอและ";
		mes "ใช้ชีวิตอย่างมีเกียรติ";
		next;
		getitem 656,7; //Awakening_Potion
		mes "[Captain Herman]";
		mes "โอ้...";
		mes "เราได้เตรียมของขวัญเล็กน้อยเพื่อแสดงความยินดีกับการเปลี่ยนอาชีพของคุณ โปรดใช้มันเมื่อคุณอยู่ในการต่อสู้ในขณะที่คุณปกป้องผู้อื่นอย่างมีเกียรติ";
		next;
		mes "[Captain Herman]";
		mes "จงก้าวต่อไป!";
		mes "อนาคตของ";
		mes "Rune-Midgarts";
		mes "ฝากไว้บน";
		mes "บ่าของคุณแล้ว!";
		close;
	}
L_Mission:
	mes "อืม?";
	mes "Swordman " + strcharinfo(0) + ".";
	mes "การทดสอบเป็นอย่างไรบ้าง?";
	if (getarg(0)) {
		mes "อา~ คุณไม่รู้ว่า";
		mes "ต้องไปหาใครต่อใช่ไหม?";
		next;
		mes "[Captain Herman]";
		return;
	} else {
		mes "มันอาจจะยากลำบากสักหน่อย,";
		mes "แต่พยายามให้เต็มที่นะ";
		close;
	}
}

prt_in,75,107,4	script	Sir Andrew#knt	65,{
	mes "[Sir Andrew]";
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) {
			mes "คุณต้องเป็น";
			mes "สมาชิกแห่ง";
			mes "Chivalry แน่ๆ";
			mes "เป็นยังไงบ้างล่ะ?";
			next;
			mes "[Sir Andrew]";
			mes "คุณต้องทำงานอย่างขยันขันแข็งเพื่อรวบรวมอาหารและเก็บเงิน Zeny เพื่อซื้ออุปกรณ์ เก็บทุกอย่างที่";
			mes "คุณพบจากการต่อสู้ แม้แต่ Jellopy ชิ้นเล็กๆ ก็ตาม";
			next;
			mes "[Sir Andrew]";
			mes "แต่มันก็ไม่ดีนะ";
			mes "ถ้าจะโลภเกินไป";
			mes "ยังไงซะ พวกเราก็คืออัศวิน";
			close;
		}
		else if (BaseClass == Job_Novice) {
			mes "เฮ้ หวัดดี,";
			mes "Novice ตัวน้อย";
			mes "ยินดีต้อนรับสู่";
			mes "Prontera Chivalry.";
			next;
			mes "[Sir Andrew]";
			mes "คุณอาจจะคิดว่าคุณ";
			mes "อ่อนแอในตอนนี้ แต่สักวันหนึ่ง";
			mes "คุณจะแข็งแกร่งขึ้น";
			next;
			mes "[Sir Andrew]";
			mes "จงฝันถึงอนาคตที่สดใส และมองไปข้างหน้าบนเส้นทางที่คุณเลือกเดินนะ";
			close;
		}
		else {
			mes "พวกเรา เหล่าสมาชิกแห่ง";
			mes "Prontera Chivalry กำลังพยายามอย่างเต็มที่เพื่อปกป้องสันติภาพของโลกใบนี้";
			next;
			mes "[Sir Andrew]";
			mes "แม้ในระหว่างการต่อสู้ที่เราเผชิญอยู่ในทุกๆ วัน เราก็ยังฝันถึง";
			mes "อนาคตที่สดใสที่กำลังจะมาถึง";
			close;
		}
	}
	if (KNIGHT_Q == 0) {
		mes "พวกเรา เหล่าสมาชิกแห่ง";
		mes "Prontera Chivalry กำลังพยายามอย่างเต็มที่เพื่อปกป้องสันติภาพของโลกใบนี้";
		next;
		mes "[Sir Andrew]";
		mes "แม้ในระหว่างการต่อสู้ที่เราเผชิญอยู่ในทุกๆ วัน เราก็ยังฝันถึง";
		mes "อนาคตที่สดใสที่กำลังจะมาถึง";
		close;
	}
	else if (KNIGHT_Q == 1) {
		mes "สวัสดียามเช้า";
		mes "มีอะไรให้ฉันช่วย";
		mes "คุณไหม?";
		next;
		if (select("ฉันต้องการรับการทดสอบ:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
			mes "[Sir Andrew]";
			mes "อา...";
			mes "คุณปรารถนา";
			mes "ที่จะเป็น Knight";
			mes "ชื่อของคุณคือ";
			mes strcharinfo(0) + ",";
			mes "ใช่ไหม?";
			next;
			mes "[Sir Andrew]";
			mes "ฉันคืออัศวินแห่ง";
			mes "Prontera Chivalry,";
			mes "Andrew Shylock.";
			mes "ฉันเป็นคนดูแล";
			mes "การทดสอบแรกของคุณ";
			next;
			mes "[Sir Andrew]";
			mes "ฉันจะทดสอบความจงรักภักดีของคุณ อัศวินทุกคนต้องมีคุณธรรมข้อนี้ สำหรับการสอบนี้ คุณจะต้องไปรวบรวมรางวัลจาก";
			mes "สนามรบมา";
			next;
			if (JobLevel == 50) {
				mes "[Sir Andrew]";
				mes "อืมม...?";
				mes "เดี๋ยวก่อนนะ";
				mes "คุณดูเหมือนคนที่";
				mes "เชี่ยวชาญการเป็น";
				mes "Swordsman อย่างสมบูรณ์แล้ว";
				next;
				mes "[Sir Andrew]";
				mes "น่าประทับใจมาก...!";
				mes "มาคิดดูอีกที";
				mes "ฉันคิดว่าความจงรักภักดี";
				mes "ของคุณไม่จำเป็นต้อง";
				mes "รับการทดสอบแล้วล่ะ";
				next;
				mes "[Sir Andrew]";
				mes "โปรดไปหาเพื่อนอัศวินของฉัน Sir Siracuse เขาจะเป็นคนมอบบททดสอบถัดไปให้คุณ ทำได้ดีมากที่ฝึกฝนอาชีพ Swordman จนเชี่ยวชาญ";
				set KNIGHT_Q,4;
				changequest 9000,9003;
				close;
			}
			mes "[Sir Andrew]";
			mes "เพื่อไม่ให้เป็นการ";
			mes "เสียเวลา";
			mes "เรามาเริ่มกันเลย!";
			mes "ไปรวบรวมไอเทม";
			mes "ดังต่อไปนี้มา...";
			next;
			mes "[Sir Andrew]";
			switch(rand(1,2)) {
			//Elder_Pixie's_Beard, Wing_Of_Red_Bat, Orcish_Voucher, Moth_Dust, Reptile_Tongue, Wild_Boar's_Mane
			case 1: setarray .@items[0], 1040,5, 7006,5, 931,5, 1057,5, 903,5, 1028,5, 2; break;
			//Short_Leg, Heart_Of_Mermaid. Blossom_Of_Maneater, Flesh_Of_Clam, Old_Frying_Pan, Snail's_Shell
			case 2: setarray .@items[0], 1042,5, 950,5, 1032,5, 966,5, 7031,5, 946,5, 3; break;
			}
			set KNIGHT_Q,.@items[12];
			if (KNIGHT_Q == 2) changequest 9000,9001;
			else changequest 9000,9002;
			mes "^236B8E"+.@items[1]+" "+getitemname(.@items[0])+"^000000,";
			mes "^236B8E"+.@items[3]+" "+getitemname(.@items[2])+"^000000,";
			mes "^236B8E"+.@items[5]+" "+getitemname(.@items[4])+"^000000,";
			mes "^236B8E"+.@items[7]+" "+getitemname(.@items[6])+"^000000,";
			mes "^236B8E"+.@items[9]+" "+getitemname(.@items[8])+"^000000 และ";
			mes "^236B8E"+.@items[11]+" "+getitemname(.@items[10])+"^000000,";
			next;
			mes "[Sir Andrew]";
			mes "ฉันจะ";
			mes "รอคุณอยู่ที่นี่";
			mes "เพื่อให้คุณนำไอเทม";
			mes "ตามรายการที่ฉันบอกมาให้";
			mes "แล้วเจอกันนะ~";
			close;
		}
		else {
			mes "[Sir Andrew]";
			mes "เอาล่ะ งั้น...";
			mes "ขอให้เป็นวันที่ดีนะ";
			close;
		}
	}
	else if (KNIGHT_Q == 2 || KNIGHT_Q == 3) {
		mes "ยินดีต้อนรับกลับมานะ~";
		mes "คุณรวบรวม";
		mes "ไอเทมครบหรือยัง?";
		mes "ไหนให้ฉันเช็คดูหน่อยสิ...";
		next;
		switch(KNIGHT_Q) {
		//Elder_Pixie's_Beard, Wing_Of_Red_Bat, Orcish_Voucher, Moth_Dust, Reptile_Tongue, Wild_Boar's_Mane
		case 2: setarray .@items[0], 1040,5, 7006,5, 931,5, 1057,5, 903,5, 1028,5, 0; break;
		//Short_Leg, Heart_Of_Mermaid. Blossom_Of_Maneater, Flesh_Of_Clam, Old_Frying_Pan, Snail's_Shell
		case 3: setarray .@items[0], 1042,5, 950,5, 1032,5, 966,5, 7031,5, 946,5, 0; break;
		}
		if (countitem(.@items[0]) >= .@items[1] && countitem(.@items[2]) >= .@items[3] && countitem(.@items[4]) >= .@items[5] && countitem(.@items[6]) >= .@items[7] && countitem(.@items[8]) >= .@items[9] && countitem(.@items[10]) >= .@items[11]) {
			mes "[Sir Andrew]";
			mes "สมบูรณ์แบบ! เราขอขอบคุณในความพยายามรวบรวมไอเทมเหล่านี้ของคุณ ไอเทมพวกนี้จะถูกนำไปใช้เพื่อสนับสนุนด้านการเงินของ Chivalry";
			next;
			delitem .@items[0],.@items[1];
			delitem .@items[2],.@items[3];
			delitem .@items[4],.@items[5];
			delitem .@items[6],.@items[7];
			delitem .@items[8],.@items[9];
			delitem .@items[10],.@items[11];
			if (KNIGHT_Q == 2) changequest 9001,9003;
			else changequest 9002,9003;
			set KNIGHT_Q,4;
			mes "[Sir Andrew]";
			mes "โปรดไปพบเพื่อนอัศวินของฉัน Sir Siracuse และทำการทดสอบต่อไปด้วยความทุ่มเทและความจงรักภักดีแบบที่คุณได้แสดงให้ฉันเห็นในวันนี้";
			close;
		}
		mes "[Sir Andrew]";
		mes "เดี๋ยวก่อนๆ...";
		mes "ฉันว่าคุณ";
		mes "ยังขาดไอเทม";
		mes "บางอย่างอยู่นะ เผื่อว่า";
		mes "คุณลืมไปแล้ว ให้ฉัน";
		mes "ช่วยเตือนความจำนะ...";
		next;
		mes "[Sir Andrew]";
		mes "^236B8E"+.@items[1]+" "+getitemname(.@items[0])+"^000000,";
		mes "^236B8E"+.@items[3]+" "+getitemname(.@items[2])+"^000000,";
		mes "^236B8E"+.@items[5]+" "+getitemname(.@items[4])+"^000000,";
		mes "^236B8E"+.@items[7]+" "+getitemname(.@items[6])+"^000000,";
		mes "^236B8E"+.@items[9]+" "+getitemname(.@items[8])+"^000000 และ";
		mes "^236B8E"+.@items[11]+" "+getitemname(.@items[10])+"^000000,";
		next;
		mes "[Sir Andrew]";
		mes "ทีนี้ โปรดรับการทดสอบนี้อย่างจริงจังและด้วยความจริงใจด้วย";
		mes "เอาล่ะ ฉันจะรอคุณ";
		mes "ทำภารกิจนี้ให้เสร็จนะ";
		close;
	}
	else if (KNIGHT_Q == 4) {
		mes "คุณมีอะไรที่ต้องการจะถามฉันอีกไหม? คุณควรจะไปรับการทดสอบถัดไปได้แล้วนะ เร็วเข้า Sir Siracuse กำลังรอคุณอยู่~";
		close;
	}
	else if (KNIGHT_Q == 14) {
		mes "คุณคงทำบททดสอบทั้งหมดเสร็จแล้ว ทำได้ดีมาก! คุณควรไปพบหัวหน้าของเรา เพื่อที่พวกเราทุกคนจะได้ให้การประเมิน";
		close;
	}
	else {
		mes "คุณมีอะไรที่ต้องการจะถามฉันอีกไหม? คุณควรจะไปรับการทดสอบถัดไปของคูณนะ พยายามเข้าล่ะ";
		mes "ฉันรู้ว่าคุณทำได้!";
		close;
	}
}

prt_in,71,91,0	script	Sir Siracuse#knt	65,{
	mes "[Sir Siracuse]";
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) {
			mes "สวัสดี!";
			mes "เป็นยังไงบ้าง?";
			mes "กลุ่มอัศวิน (Chivalry) ของเรา";
			mes "กำลังไปได้สวยเลยล่ะ";
			next;
			mes "[Sir Siracuse]";
			mes "พวกเรากำลัง";
			mes "ทดสอบสมาชิกใหม่ๆ อยู่";
			mes "แต่ก็ไม่ใช่ทุกคนหรอกนะ";
			mes "ที่จะดูมีอนาคต";
			mes "เท่ากับเจ้า";
			next;
			mes "[Sir Siracuse]";
			mes "ข้าหวังว่าเหล่าเด็กใหม่พวกนี้จะทำตัวดีๆ นะ และไม่";
			mes "สร้างความเสื่อมเสียให้กับกลุ่มอัศวิน";
			next;
			mes "[Sir Siracuse]";
			mes "ถ้าเจ้าพบว่ามีเด็กใหม่คนไหนทำตัวไม่เหมาะสมกับการเป็น Knight ช่วยตักเตือนพวกเขาแทนข้าหน่อยจะได้ไหม?";
			close;
		}
		else if (BaseClass == Job_Novice) {
			mes "โอ้?";
			mes "Novice มาทำ";
			mes "อะไรที่นี่ล่ะ?";
			next;
			mes "[Sir Siracuse]";
			mes "เจ้าสนใจอยากจะเป็น Knight งั้นเหรอ? เจ้าไม่สามารถเปลี่ยนจาก Novice เป็น Knight ได้เลยทันทีหรอกนะ รู้ไหม";
			next;
			mes "[Sir Siracuse]";
			mes "ก่อนอื่น เจ้าจะต้อง";
			mes "กลายเป็น Swordman";
			mes "ที่มีประสบการณ์มากพอ";
			mes "ก่อนที่เจ้าจะพิจารณา";
			mes "เรื่องการเป็น Knight";
			close;
		}
		else {
			mes "การรุกและการรับ";
			mes "จะมีวิธีไหนไหมนะ ที่จะได้มาทั้งสองอย่างโดยไม่ต้องสูญเสียอย่างใดอย่างหนึ่งไป?";
			next;
			mes "[Sir Siracuse]";
			mes "อาวุธประเภทดาบสองมือช่วย";
			mes "เพิ่มพลังโจมตีอย่างมาก แต่ก็ทำให้พลังป้องกันลดลง จะมีอะไรที่สามารถเอาชนะจุดอ่อนนี้ได้ไหมนะ?";
			next;
			mes "[Sir Siracuse]";
			mes "อาวุธหรือว่า";
			mes "ทักษะบางอย่างแบบนั้น";
			mes "จะช่วยเหล่า Knight ได้มากเลยทีเดียว...";
			close;
		}
	}
	if (KNIGHT_Q == 0) {
		mes "การรุกและการรับ";
		mes "จะมีวิธีไหนไหมนะ ที่จะได้มาทั้งสองอย่างโดยไม่ต้องสูญเสียอย่างใดอย่างหนึ่งไป?";
		next;
		mes "[Sir Siracuse]";
		mes "อาวุธประเภทดาบสองมือช่วย";
		mes "เพิ่มพลังโจมตีอย่างมาก แต่ก็ทำให้พลังป้องกันลดลง จะมีอะไรที่สามารถเอาชนะจุดอ่อนนี้ได้ไหมนะ?";
		next;
		mes "[Sir Siracuse]";
		mes "อาวุธหรือว่า";
		mes "ทักษะบางอย่างแบบนั้น";
		mes "จะช่วยเหล่า Knight ได้มากเลยทีเดียว...";
		close;
	}
	else if (KNIGHT_Q == 1) {
		mes "เอ๊ะ?";
		mes "เจ้ามี";
		mes "อะไรจะ";
		mes "ถามข้าหรือเปล่า?";
		next;
		if (select("ข้าต้องการเข้ารับการทดสอบเพื่อเปลี่ยนอาชีพครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร") == 1) {
			mes "[Sir Siracuse]";
			mes "โอ้ อยากจะเป็น";
			mes "Knight งั้นเหรอ? จะว่าไป";
			mes "เจ้าคือ"+((Sex==SEX_MALE)?"ชายหนุ่ม":"หญิงสาว")+"ที่";
			mes "เพิ่งมาสมัครใช่ไหม?";
			next;
			mes "[Sir Siracuse]";
			mes "ไหนดูซิ...";
			mes "เจ้าชื่อว่า";
			mes strcharinfo(0) + ".";
			next;
			mes "[Sir Siracuse]";
			mes "แต่ว่า ก่อนที่เจ้าจะมาหาข้า เจ้าต้องไปพบคนอื่นๆ ก่อน ในมุมมอง";
			mes "ของข้า เจ้ายังไม่ได้พิสูจน์เลยว่าเจ้ามีความรู้พื้นฐานดีพอ แต่ข้าจะ";
			mes "พิจารณาอีกครั้ง เมื่อเจ้า";
			mes "ผ่านการทดสอบแรกมาได้";
			close;
		}
		mes "[Sir Siracuse]";
		mes "หืมมม...?";
		mes "เอาเถอะ";
		mes "ก็แค่เห็น";
		mes "เจ้าทำหน้า";
		mes "เหมือนมีอะไรจะพูด";
		close;
	}
	else if (KNIGHT_Q == 2 || KNIGHT_Q == 3) {
		mes "เอ๊ะ?";
		mes "เจ้ามี";
		mes "อะไรจะ";
		mes "ถามข้าหรือเปล่า?";
		next;
		if (select("ข้าต้องการเข้ารับการทดสอบเพื่อเปลี่ยนอาชีพครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร") == 1) {
			mes "[Sir Siracuse]";
			mes "ฮ่าฮ่าฮ่า~!";
			mes "เจ้าควรจะกำลังรับการทดสอบของ Andrew อยู่ไม่ใช่เหรอ? เจ้าจะข้ามขั้นตอนไม่ได้นะรู้ไหม! การทดสอบทุกอย่างของเราล้วนสำคัญทั้งนั้น";
			next;
			mes "[Sir Siracuse]";
			mes "ไปคุยกับ Sir Andrew ก่อน";
			mes "บททดสอบของข้าจะมีขึ้นหลังจากที่เจ้าผ่านการทดสอบของเขาแล้ว";
			close;
		}
		mes "[Sir Siracuse]";
		mes "หืมมม...?";
		mes "เอาเถอะ";
		mes "ก็แค่เห็น";
		mes "เจ้าทำหน้า";
		mes "เหมือนมีอะไรจะพูด";
		close;
	}
	else if (KNIGHT_Q == 4 || KNIGHT_Q == 5) {
		if (KNIGHT_Q == 4) {
			mes "โอ้?";
			mes "เจ้ามี";
			mes "อะไรจะ";
			mes "ถามข้าหรือเปล่า?";
			next;
			if (select("Sir Andrew ส่งข้ามาเพื่อรับการทดสอบครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร") == 1) {
				mes "[Sir Siracuse]";
				mes "เข้าใจแล้ว เจ้าผ่านการทดสอบแรกมาแล้วสินะ ดีมาก ข้าจะสละเวลาให้เจ้าหน่อยแล้วกัน ก่อนอื่นข้าขอแนะนำตัวก่อน ข้าชื่อ James Siracuse";
				next;
				mes "[Sir Siracuse]";
				mes "การทดสอบนี้จะวัดว่าเจ้ารู้เรื่องเกี่ยวกับวิถีแห่งอัศวินมากแค่ไหน และที่สำคัญกว่านั้น ข้าอยากรู้ความคิดของเจ้าเกี่ยวกับเรื่องเกียรติยศ";
				next;
				mes "[Sir Siracuse]";
				mes "ไม่ต้องประหม่าไป ข้าไม่รั้งเจ้าไว้นานหรอก มันเป็นคำถามสั้นๆ แถมเจ้ายังต้องไปพบคนอื่นๆ อีกใช่ไหมล่ะ?";
				next;
				mes "[Sir Siracuse]";
				mes "ถ้าอย่างนั้น";
				mes "เรามาเริ่มกันเลย";
				mes "กรุณาตอบ";
				mes "โดยทันทีนะ";
				next;
			}
			else {
				mes "[Sir Siracuse]";
				mes "หืมมม...?";
				mes "เอาเถอะ";
				mes "ก็แค่เห็น";
				mes "เจ้าทำหน้า";
				mes "เหมือนมีอะไรจะพูด";
				close;
			}
		}
		else if (KNIGHT_Q == 5) {
			mes "อะไรกัน...";
			mes "เจ้าอีกแล้วเหรอ?";
			next;
			if (select("ข้าขอเข้ารับการทดสอบอีกครั้งครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร") == 1) {
				mes "[Sir Siracuse]";
				mes "งั้นเหรอ?";
				mes "เจ้าแน่ใจนะว่า";
				mes "ครั้งนี้เตรียมตัวมาพร้อมแล้ว?";
				next;
				mes "[Sir Siracuse]";
				mes "ตกลง ถ้าอย่างนั้น";
				mes "เริ่มกันอีกครั้ง...";
				next;
			}
			else {
				mes "[Sir Siracuse]";
				mes "หืมมม...?";
				mes "เอาเถอะ";
				mes "ก็แค่เห็น";
				mes "เจ้าทำหน้า";
				mes "เหมือนมีอะไรจะพูด";
				close;
			}
		}
		mes "[Sir Siracuse]";
		mes "อัศวินต้องมีทั้งพละกำลัง, พลังป้องกัน, ความเร็ว และทักษะในการใช้ดาบสองมือ อาวุธชนิดใดต่อไปนี้ที่ไม่ได้รับผลจากสกิล Two Hand Quicken?";
		next;
		if (select("Katana:Slayer:Broadsword:Flamberge") != 4) {
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "ผิด!";
			mes "นั่นมันดาบสองมือนนะ!";
			mes "เจ้าแน่ใจเหรอว่าอยากจะเป็น Knight? เจ้ายังไม่รู้แม้กระทั่งเรื่องพื้นฐานเลย...";
			next;
			mes "[Sir Siracuse]";
			mes "ถ้าเจ้าไม่แน่ใจเรื่องอะไร ให้ไปในเมืองแล้วถาม Knight คนไหนก็ได้ เจ้าจำเป็นต้องเรียนรู้เรื่องของ Knight ให้มากกว่านี้ก่อนจะมาสมัครงานนี้!";
			close;
		}
		mes "[Sir Siracuse]";
		mes "ดีมาก ต่อไปข้าจะถามเกี่ยวกับสกิล ข้อใดต่อไปนี้ไม่จำเป็นต้องเรียนเพื่อที่จะได้สกิล Bowling Bash?";
		next;
		if (select("Two Handed Sword Mastery Lv.5:Magnum Break Lv.3:Provoke Lv.10:Bash Lv.10") != 3) {
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "ผิด!";
			mes "เจ้าจำเป็นต้องใช้สกิลนั้นเพื่อเรียน Bowling Bash! เจ้าควรจะเรียนรู้เกี่ยวกับอาชีพ Knight ให้มากกว่านี้ก่อนจะมาสมัคร!";
			close;
		}
		mes "[Sir Siracuse]";
		mes "Knight สามารถใช้หอกได้ด้วย ซึ่งต่างจากอาชีพอื่น และมีสกิลที่เกี่ยวข้องกับหอกเช่นกัน สกิลใดไม่จำเป็นสำหรับการเรียนสกิล Brandish Spear?";
		next;
		if (select("Pierce Lv.5:Spear Stab Lv.3:Spear Boomerang Lv.3:Peco Peco Ride Lv.1") != 3) {
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "ผิด! เจ้าจำเป็นต้องเรียนสกิลนั้นเพื่อที่จะเรียน Brandish Spear! เจ้าไม่รู้เรื่องเกี่ยวกับ Knight ได้ยังไงในเมื่อเจ้าอยากจะเป็น Knight?";
			next;
			mes "[Sir Siracuse]";
			mes "ถ้าเจ้าไม่แน่ใจเรื่องอะไร ให้ไปในเมืองแล้วถาม Knight คนไหนก็ได้";
			mes "เพื่อขอความช่วยเหลือ แล้วค่อยกลับมาหลังจากที่เรียนรู้เรื่อง Knight มากขึ้นแล้ว";
			close;
		}
		mes "[Sir Siracuse]";
		mes "หอกบางชนิดมีคุณสมบัติทางเวทมนตร์เหมือนกับคาถา จากตัวเลือกต่อไปนี้ หอกชนิดใดที่สามารถโจมตี Nightmare ซึ่งมีธาตุเป็น Ghost (วิญญาณ) ได้?";
		next;
		if (select("Zephyrus:Lance:Bill Guisarme:Crescent Scythe") != 1) {
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "ผิด! เจ้าจะโจมตีไม่เข้าเลยถ้าใช้หอกประเภทนั้น! กลับไปเรียนรู้เรื่อง Knight มาใหม่ซะ!";
			next;
			mes "[Sir Siracuse]";
			mes "ถ้าเจ้ามีคำถาม ก็แค่ถาม Knight คนไหนก็ได้ในเมือง นี่เป็นความรู้พื้นฐานสำหรับพวกเราเลยนะ!";
			close;
		}
		mes "[Sir Siracuse]";
		mes "เมื่อเจ้าเป็น Knight เจ้าจะสามารถขี่ Peco Peco ได้ อย่างไรก็ตาม ความเร็วในการโจมตีของเจ้าจะลดลงเมื่อขี่มัน";
		next;
		mes "[Sir Siracuse]";
		mes "แต่เจ้าสามารถแก้ปัญหานี้ได้เมื่อเรียนสกิล Cavalier Mastery เจ้าจะมีความเร็วในการโจมตีกี่เปอร์เซ็นต์ของปกติ หลังจากเรียน Cavalier Mastery เลเวล 3?";
		next;
		if (select("70 % ของความเร็วโจมตีปกติ:80 % ของความเร็วโจมตีปกติ:90 % ของความเร็วโจมตีปกติ:100 % ของความเร็วโจมตีปกติ") != 2) {
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "ผิด!";
			mes "อย่าคิดจะขี่ Peco Peco เลยถ้าเจ้ายังไม่รู้เรื่อง Cavalier Mastery!";
			next;
			mes "[Sir Siracuse]";
			mes "เจ้าควรกลับมาหลังจากที่ได้เรียนรู้เรื่อง Knight เพิ่มขึ้นอีกสักนิดนะ!";
			close;
		}
		mes "[Sir Siracuse]";
		mes "ดีมาก ดีมาก...";
		mes "ข้าค่อนข้างมั่นใจว่าเจ้ารู้เรื่องเกี่ยวกับ Knight มากพอสมควรแล้ว ตอนนี้ข้าขอถามคำถามส่วนตัวเกี่ยวกับอัศวินบ้าง";
		next;
		mes "[Sir Siracuse]";
		mes "เจ้าควรทำอย่างไรเมื่อเจอ Novice ขอความช่วยเหลือในเมือง?";
		next;
		switch(select("แนะนำแหล่งเก็บเลเวลที่เหมาะสมให้แก่ Novice:ให้ Novice สู้ไป ส่วนเจ้ารับดาเมจแทน:ให้เงิน Zeny และไอเทมจำนวนมากแก่ Novice")) {
		case 1:
			mes "[Sir Siracuse]";
			mes "แน่นอน แม้แต่ Novice ก็ต้องเรียนรู้วิธีที่จะพึ่งพาตนเอง การให้คำแนะนำที่ดีแก่ Novice คือหนึ่งในสิ่งที่ดีที่สุดที่เราทำได้";
			next;
			break;
		case 2:
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "เจ้าเข้าใจผิดแล้ว เจ้าเชื่อจริงๆ เหรอว่านั่นคือการช่วย Novice? ให้ปลาแก่เขา เขาจะมีกินแค่เพียงวันเดียว แต่ถ้าสอนเขาตกปลา เขาจะมีกินไปตลอดชีวิต!";
			close;
		case 3:
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "เจ้าเชื่อจริงๆ เหรอว่าสิ่งนี้จะช่วย Novice ที่น่าสงสารได้จริงๆ? มันอาจจะดูใจดี แต่มันจะทำให้พวกเขาไม่รู้ซึ้งถึงคุณค่าของเงินและไอเทมจนกว่าพวกเขาจะหามันมาได้ด้วยตัวเอง";
			close;
		}
		mes "[Sir Siracuse]";
		mes "เอาล่ะ...";
		mes "แล้วเจ้าควรจะ";
		mes "ปฏิบัติตัวอย่างไร";
		mes "เมื่ออยู่ในปาร์ตี้?";
		next;
		switch(select("ปกป้องทุกคนโดยยืนอยู่แนวหน้าของการต่อสู้:ลากมอนสเตอร์มารวมกันแล้วกำจัดในรวดเดียว:เก็บไอเทมให้ได้มากที่สุด โดยไม่สนว่าจะต้องแลกด้วยอะไร")) {
		case 1:
			mes "[Sir Siracuse]";
			mes "ถูกต้อง! พละกำลังและการโจมตีของเราสำคัญมากในปาร์ตี้ Knight ทุกคนควรเข้าสู่การต่อสู้ด้วยความคิดเช่นนั้น";
			next;
			break;
		case 2:
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "เจ้าบ้าไปแล้วเหรอ? เจ้าไม่รู้ถึงข้อเสียของความคิดแบบนั้นหรือไง? เจ้าไม่สามารถควบคุมมอนสเตอร์ฝูงใหญ่ขนาดนั้นได้หรอก ถ้าพวกมันฆ่าเจ้าล่ะ? แล้วใครจะคอยปกป้องผู้บริสุทธิ์?";
			close;
		case 3:
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "ข้าเห็นความโลภในตัวเจ้า และเราจะไม่ยอมให้มีสิ่งนั้นที่นี่! ดูเหมือนว่าเจ้าจะไม่ได้ใส่ใจผู้อื่นจริงๆ เลย!";
			mes "ไปซะ!";
			close;
		}
		mes "[Sir Siracuse]";
		mes "สุดท้ายแล้ว...";
		mes "คุณค่าที่สำคัญที่สุด";
		mes "ที่อัศวินต้องมีคืออะไร?";
		next;
		switch(select("เกียรติยศ:ความมั่งคั่ง:สถานะทางสังคม")) {
		case 1:
			mes "[Sir Siracuse]";
			mes "ถูกต้อง เหนือสิ่งอื่นใด อัศวินต้องมีเกียรติ! เราอยู่และตายเพื่อเกียรติยศ! จงจำไว้ให้ขึ้นใจเสมอ";
			next;
			break;
		case 2:
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "เจ้านี่มันสวะ! เจ้าพยายามจะเป็นอัศวินเพื่อความมั่งคั่งส่วนตัวงั้นเหรอ? ไปซะ! เราจะไม่ยอมรับคนอย่างเจ้าเข้าสู่กลุ่มอัศวินของเรา!";
			close;
		case 3:
			set KNIGHT_Q,5;
			mes "[Sir Siracuse]";
			mes "นี่เจ้าพยายามจะเป็นคนดังผ่านทางกลุ่มอัศวินงั้นเหรอ? ช่างน่าสมเพช เราจะไม่ยอมรับคนอย่างเจ้าเข้าสู่กลุ่มอัศวินของเรา!";
			close;
		}
		set KNIGHT_Q,6;
		changequest 9003,9004;
		mes "[Sir Siracuse]";
		mes "เอาล่ะ";
		mes "นี่คือจุดสิ้นสุด";
		mes "ของบททดสอบของข้า";
		next;
		mes "[Sir Siracuse]";
		mes "สำหรับการทดสอบ";
		mes "ครั้งต่อไป โปรดไป";
		mes "พบ Sir Windsor";
		mes "เขาเป็นคนเงียบขรึม";
		mes "แต่อย่าให้สิ่งนั้น";
		mes "กวนใจเจ้าล่ะ";
		close;
	}
	else if (KNIGHT_Q == 6) {
		mes "โอ้?";
		mes "เจ้ามี";
		mes "อะไรจะ";
		mes "ถามข้าหรือเปล่า?";
		next;
		if (select("ข้าต้องการเข้ารับการทดสอบเพื่อเปลี่ยนอาชีพครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร") == 1) {
			mes "[Sir Siracuse]";
			mes "เฮ้...";
			mes "เจ้าผ่านการทดสอบของข้าไปแล้วไม่ใช่เหรอ? เจ้าเสร็จธุระที่นี่แล้ว เจ้าควรไปหา Sir Windsor ได้แล้วนะ...";
			close;
		}
		mes "[Sir Siracuse]";
		mes "หืมมม...?";
		mes "เอาเถอะ";
		mes "ก็แค่เห็น";
		mes "เจ้าทำหน้า";
		mes "เหมือนมีอะไรจะพูด";
		close;
	}
	else if (KNIGHT_Q == 14) {
		mes "หืมมม...?";
		mes "เจ้าผ่านการทดสอบ";
		mes "ของคนอื่นๆ";
		mes "หมดแล้วงั้นเหรอ?";
		next;
		mes "[Sir Siracuse]";
		mes "ถ้าอย่างนั้น";
		mes "ไปพบท่านกัปตันได้เลย";
		mes "พวกเราทุกคนจะไปที่นั่น";
		mes "เพื่อประเมินผลการ";
		mes "ทดสอบของเจ้า";
		close;
	}
	else {
		mes "เฮ้ เจอกันอีกแล้ว";
		mes "ต้องการอะไรหรือเปล่า?";
		mes "ขอโทษทีนะ ตอนนี้ข้ากำลังยุ่งอยู่ เจ้าควรจะไปทำบททดสอบที่เหลือให้เสร็จนะ";
		close;
	}
}

prt_in,79,94,4	script	Sir Windsor#knt	733,{
	mes "[Sir Windsor]";
	mes "...";
	next;
	mes "[Sir Windsor]";
	mes "...";
	mes "......";
	next;
	mes "[Sir Windsor]";
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) mes "ปกป้อง";
		else if (BaseClass == Job_Novice) {
			mes "...ไปเล่น";
			mes "ข้างนอกไป";
		}
		else mes "...เหอะ";
		close;
	}
	if (KNIGHT_Q == 0) {
		mes "...อะไร?";
		close;
	}
	else if (KNIGHT_Q >= 1 && KNIGHT_Q <= 5) {
		mes "...อะไร?";
		next;
		if (select("ข้าต้องการเข้ารับการทดสอบเพื่อเปลี่ยนอาชีพครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร") == 1) {
			mes "[Sir Windsor]";
			mes "...";
			next;
			mes "[Sir Windsor]";
			mes "...";
			mes "......";
			next;
			mes "[Sir Windsor]";
			mes "...ยังไม่ถึงคิวข้า";
			close;
		}
		mes "[Sir Windsor]";
		mes "...";
		close;
	}
	else if (KNIGHT_Q == 6 || KNIGHT_Q == 7) {
		if (KNIGHT_Q == 6) {
			set .@mes$,"Sir Siracuse ส่งข้ามาหาท่านครับ/ค่ะ:อ๋อ เปล่าครับ/ค่ะ ไม่มีอะไร";
			mes ".....อะไร?";
			next;
		}
		else {
			set .@mes$,"ข้าต้องการลองอีกครั้งครับ/ค่ะ!:...";
			next;
		}
		if (select(.@mes$) == 1) {
			mes "[Sir Windsor]";
			mes "...";
			next;
			set KNIGHT_Q,7;
			if (checkquest(9004) != -1) {
				changequest 9004,9005;
			}
			mes "[Sir Windsor]";
			mes "...";
			mes "......";
			next;
			mes "[Sir Windsor]";
			if (KNIGHT_Q == 6)
				mes "...ตามมา";
			else {
				mes "...ก็ได้";
				next;
				mes "[Sir Windsor]";
				mes "...ทางนี้";
			}
			close2;
			if(checkquest(9006) == -1) changequest 9005,9006;
			warp "job_knt",89,101;
			end;
		}
		mes "[Sir Windsor]";
		mes "...";
		close;
	}
	else if (KNIGHT_Q == 14) {
		mes "...ไปคุยกับ";
		mes "ท่านกัปตัน";
		close;
	}
	else {
		mes "...เจ้าเสร็จธุระ";
		mes "ตรงนี้แล้ว";
		close;
	}
}

// Test 2 part 1 (บททดสอบที่ 2 ส่วนที่ 1)
//==========================================================
job_knt,89,106,4	script	Knight Windsor#knt	733,{
	mes "[Sir Windsor]";
	mes "...";
	next;
	mes "[Sir Windsor]";
	mes "...";
	mes "......";
	next;
	mes "[Sir Windsor]";
	mes "...คำถาม?";
	next;
	set .@i, select("นี่คือการทดสอบแบบไหน?:ข้าจะเข้ารับการทดสอบได้อย่างไร?:ข้าต้องการออกไป:ไม่มี");
	mes "[Sir Windsor]";
	mes "...";
	if (.@i == 4) close;
	next;
	mes "[Sir Windsor]";
	mes "...";
	mes "......";
	switch(.@i) {
	case 1:
		next;
		mes "[Sir Windsor]";
		mes "...สู้กับมอนสเตอร์";
		next;
		mes "[Sir Windsor]";
		mes "...ฆ่าพวกมันให้หมด";
		next;
		mes "[Sir Windsor]";
		mes "...";
		next;
		mes "[Sir Windsor]";
		mes "...มี 3 ด่าน";
		mes "ผ่านให้ได้ทั้งหมด";
		next;
		mes "[Sir Windsor]";
		mes "......3 นาที";
		mes "สำหรับแต่ละด่าน";
		next;
		mes "[Sir Windsor]";
		mes "..........";
		close;
	case 2:
		next;
		mes "[Sir Windsor]";
		mes "...ไปรอที่";
		mes "ห้องรับรอง";
		next;
		mes "[Sir Windsor]";
		mes "...แล้วมัน";
		mes "จะเริ่มขึ้นเอง";
		next;
		mes "[Sir Windsor]";
		mes "...";
		next;
		mes "[Sir Windsor]";
		mes "...ต้องรอ";
		mes "ถ้ามีคนทดสอบอยู่";
		next;
		mes "[Sir Windsor]";
		mes "...เข้าต่อจาก";
		mes "คนคนนั้นได้เลย";
		next;
		mes "[Sir Windsor]";
		mes "...";
		close;
	case 3:
		close2;
		warp "prt_in",80,100;
		end;
	}
}

job_knt,89,106,4	script	Windsor Benedict#knt	733,{
	end;

OnInit:
	//disablenpc "Windsor Benedict#knt";
	hideonnpc "Windsor Benedict#knt";
	waitingroom "Waiting Room",20,"Windsor Benedict#knt::OnStartArena",1;
	enablewaitingroomevent;
	end;

OnStartArena:
	killmonster "job_knt", "Knight1::OnMyMobDead";
	killmonster "job_knt", "Knight2::OnMyMobDead";
	killmonster "job_knt", "Knight3::OnMyMobDead";
	warpwaitingpc "job_knt",43,146;
	donpcevent "Knight1::OnEnable";
	disablewaitingroomevent;
	end;

OnStart:
	enablewaitingroomevent;
	end;
}

-	script	Knight1	-1,{
OnInit:
	disablenpc "Knight1";
	end;

OnEnable:
	enablenpc "Knight1";
	if (!checkre(0)) {
		set .MyMobs,12;
		monster "job_knt",39,150,"Dustiness",1114,1,"Knight1::OnMyMobDead";
		monster "job_knt",47,150,"Dustiness",1114,1,"Knight1::OnMyMobDead";
		monster "job_knt",39,142,"Dustiness",1114,1,"Knight1::OnMyMobDead";
		monster "job_knt",47,142,"Dustiness",1114,1,"Knight1::OnMyMobDead";
	}
	else
		set .MyMobs,8;
	monster "job_knt",43,137,"Piere",1160,1,"Knight1::OnMyMobDead";
	monster "job_knt",43,137,"Andre",1095,1,"Knight1::OnMyMobDead";
	monster "job_knt",43,137,"Deniro",1105,1,"Knight1::OnMyMobDead";
	monster "job_knt",43,155,"Piere",1160,1,"Knight1::OnMyMobDead";
	monster "job_knt",43,155,"Andre",1095,1,"Knight1::OnMyMobDead";
	monster "job_knt",43,155,"Deniro",1105,1,"Knight1::OnMyMobDead";
	monster "job_knt",35,146,"Argos",1100,1,"Knight1::OnMyMobDead";
	monster "job_knt",52,146,"Argos",1100,1,"Knight1::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster "job_knt", "Knight1::OnMyMobDead";
	disablenpc "Knight1";
	end;

OnMyMobDead:
	set .MyMobs,.MyMobs-1;
	if (.MyMobs < 1) {
		mes "[Sir Windsor]";
		mes "...";
		next;
		mes "[Sir Windsor]";
		mes "...ไปต่อ";
		mes "ระดับถัดไป";
		close2;
		warp "job_knt",43,52;
		donpcevent "Knight1::OnDisable";
		donpcevent "Knight2::OnEnable";
		stopnpctimer;
	}
	end;

OnTimer180000:
	donpcevent "Knight1::OnDisable";
	end;

OnTimer181000:
	areawarp "job_knt", 24, 126, 63, 165, "prt_in",80,100;
	end;

OnTimer182000:
	stopnpctimer;
	donpcevent "Knight1::OnDisable";
	donpcevent "Windsor Benedict#knt::OnStart";
	end;
}

// Test 2 part 2
//==========================================================
// Test 2 part 2
//==========================================================
-	script	Knight2	-1,{
OnInit:
	disablenpc "Knight2";
	end;

OnEnable:
	enablenpc "Knight2";
	if (!checkre(0)) {
		set .MyMobs,12;
		monster "job_knt",43,42,"Desert Wolf",1106,1,"Knight2::OnMyMobDead";
		monster "job_knt",43,62,"Desert Wolf",1106,1,"Knight2::OnMyMobDead";
		monster "job_knt",60,68,"Anacondaq",1030,1,"Knight2::OnMyMobDead";
		monster "job_knt",27,68,"Anacondaq",1030,1,"Knight2::OnMyMobDead";
		monster "job_knt",60,35,"Anacondaq",1030,1,"Knight2::OnMyMobDead";
		monster "job_knt",27,35,"Anacondaq",1030,1,"Knight2::OnMyMobDead";
	}
	else
		set .MyMobs,6;
	monster "job_knt",53,52,"Frilldora",1119,1,"Knight2::OnMyMobDead";
	monster "job_knt",34,52,"Frilldora",1119,1,"Knight2::OnMyMobDead";
	monster "job_knt",58,52,"Drainliar",1111,1,"Knight2::OnMyMobDead";
	monster "job_knt",58,52,"Drainliar",1111,1,"Knight2::OnMyMobDead";
	monster "job_knt",29,52,"Drainliar",1111,1,"Knight2::OnMyMobDead";
	monster "job_knt",29,52,"Drainliar",1111,1,"Knight2::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster "job_knt", "Knight2::OnMyMobDead";
	disablenpc "Knight2";
	end;

OnMyMobDead:
	set .MyMobs,.MyMobs-1;
	if (.MyMobs < 1) {
		mes "[Sir Windsor]";
		mes "...หืม";
		next;
		mes "[Sir Windsor]";
		mes "...เหลืออีกแค่ด่านเดียว";
		close2;
		warp "job_knt",143,152;
		donpcevent "Knight2::OnDisable";
		donpcevent "Knight3::OnEnable";
		stopnpctimer;
	}
	end;

OnTimer180000:
	donpcevent "Knight2::OnDisable";
	end;

OnTimer181000:
	areawarp "job_knt", 24, 32, 63, 71, "prt_in",80,100;
	end;

OnTimer182000:
	stopnpctimer;
	donpcevent "Knight2::OnDisable";
	donpcevent "Windsor Benedict#knt::OnStart";
	end;
}

// Test 2 part 3
//==========================================================
-	script	Knight3	-1,{
OnInit:
	disablenpc "Knight3";
	end;

OnEnable:
	enablenpc "Knight3";
	if (!checkre(0)) {
		set .MyMobs,7;
		monster "job_knt",147,167,"Goblin Archer",1258,1,"Knight3::OnMyMobDead";
		monster "job_knt",136,158,"Steam Goblin",1280,1,"Knight3::OnMyMobDead";
	}
	else
		set .MyMobs,5;
	monster "job_knt",136,152,"Goblin",1122,1,"Knight3::OnMyMobDead";
	monster "job_knt",150,152,"Goblin",1123,1,"Knight3::OnMyMobDead";
	monster "job_knt",143,145,"Goblin",1124,1,"Knight3::OnMyMobDead";
	monster "job_knt",143,167,"Goblin",1125,1,"Knight3::OnMyMobDead";
	monster "job_knt",139,167,"Goblin",1126,1,"Knight3::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster "job_knt", "Knight3::OnMyMobDead";
	disablenpc "Knight3";
	end;

OnMyMobDead:
	set .MyMobs,.MyMobs-1;
	if (.MyMobs < 1) {
		mes "[Sir Windsor]";
		mes "...";
		next;
		mes "[Sir Windsor]";
		mes "...ดีมาก";
		next;
		mes "[Sir Windsor]";
		mes "...ไปคุยกับ";
		mes "Amy Beatrice ได้แล้ว";
		close2;
		set KNIGHT_Q,8;
		if(checkquest(9007) == -1) {
			changequest 9006,9007;
		}
		warp "prt_in",80,100;
		donpcevent "Knight3::OnDisable";
		donpcevent "Windsor Benedict#knt::OnStart";
		stopnpctimer;
	}
	end;

OnTimer180000:
	donpcevent "Knight3::OnDisable";
	end;

OnTimer181000:
	areawarp "job_knt", 124, 132, 163, 171, "prt_in",80,100;
	end;

OnTimer182000:
	stopnpctimer;
	donpcevent "Knight3::OnDisable";
	donpcevent "Windsor Benedict#knt::OnStart";
	end;
}

// Test 3 (Manners)
//==========================================================
prt_in,69,107,6	script	Lady Amy#knt	728,{
	mes "[Lady Amy]";
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) {
			mes "โอ้...!";
			mes "ข้าสงสัยจังว่า";
			mes "ทำไมเจ้าถึง";
			mes "มาเยี่ยมข้าล่ะ?";
			next;
			mes "[Lady Amy]";
			mes "เจ้าไม่ได้มี";
			mes "ปัญหาในการเป็น Knight";
			mes "หรอกใช่ไหม? อืม ข้าว่า";
			mes "เจ้าก็ทำได้ดีนะ~";
			next;
			mes "[Lady Amy]";
			mes "แน่นอนอยู่แล้ว~";
			mes "เจ้าคือสมาชิกของ";
			mes "สมาคม Prontera Chivalry นี่นา~";
			close;
		}
		else if (BaseClass == Job_Novice) {
			mes "อุ๊ยย~";
			mes "ช่างเป็น Novice";
			mes "ตัวน้อยที่น่ารัก";
			mes "เสียนี่กะไร! น่าร้ากมาก!";
			next;
			mes "[Lady Amy]";
			mes "อิอิ...";
			mes "เจ้าสนใจที่จะ";
			mes "เป็น Knight ใน";
			mes "อนาคตไหมจ๊ะ? เจ้าต้องเป็น";
			mes "Knight ที่ยอดเยี่ยมแน่ๆ เลย~";
			next;
			mes "[Lady Amy]";
			mes "จำไว้นะว่าเจ้า";
			mes "กำลังจะได้เป็น Knight";
			mes "ตกลงไหม? สัญญาไหมจ๊ะ?";
			close;
		}
		else {
			mes "ยินดีต้อนรับสู่";
			mes "Prontera Chivalry จ้า~";
			next;
			mes "[Lady Amy]";
			mes "พวกเราเป็นเพียงกลุ่ม Knight";
			mes "แต่ก็หวังว่าเจ้าจะมีความสุข";
			mes "กับการอยู่ที่นี่นะ";
			mes "อิอิ~";
			close;
		}
	}
	if (KNIGHT_Q == 0) {
		mes "โอ๊ะ เจ้าเป็น";
		mes "Swordsman งั้นเหรอ...?";
		mes "เจ้ามาที่นี่เพื่อ";
		mes "เปลี่ยนอาชีพเป็น";
		mes "Knight ใช่ไหมล่ะ?";
		next;
		mes "[Lady Amy]";
		mes "หากจะสมัคร ให้คุย";
		mes "กับท่านหัวหน้ากอง";
		mes "ที่อยู่ตรงโน้นเลยจ๊ะ";
		mes "ฮี่ฮี่~";
		close;
	}
	else if (KNIGHT_Q >= 1 && KNIGHT_Q <= 7) {
		mes "หืมมม?";
		mes "ทำไมเจ้าถึง";
		mes "มาหา Amy ล่ะจ๊ะ?";
		next;
		if (select("ข้าต้องการเข้ารับการทดสอบเปลี่ยนอาชีพครับ/ค่ะ:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
			mes "[Lady Amy]";
			mes "อื้มม~";
			mes "เจ้าสมัครเปลี่ยนอาชีพแล้วสินะ! โอเคเลย! ด้วยความมุ่งมั่นแบบนั้น อีกไม่นานเจ้าจะได้เป็น Knight แน่นอนจ๊ะ!";
			next;
			mes "[Lady Amy]";
			mes "แต่ว่า...";
			mes "เจ้าต้องไปพบ";
			mes "Knight ท่านอื่นๆ";
			mes "ก่อนที่จะมาคุยกับ Amy นะจ๊ะ";
			next;
			mes "[Lady Amy]";
			mes "ข้าก็อยากจะทดสอบ";
			mes "เจ้าตั้งแต่แรกอยู่หรอกนะ";
			mes "แต่ข้าไม่ได้รับอนุญาตให้ทำน่ะ";
			mes "ฮี่ฮี่~";
			close;
		}
		mes "[Lady Amy]";
		mes "โธ่~";
		mes "ก็ได้จ๊ะ...";
		close;
	}
	else if ((KNIGHT_Q == 8) || (KNIGHT_Q == 9)){
		if (KNIGHT_Q == 8) {
			mes "หืมมม?";
			mes "ทำไมเจ้าถึง";
			mes "มาหา Amy ล่ะจ๊ะ?";
			next;
			if (select("Sir Windsor บอกให้ข้ามาพบท่านครับ/ค่ะ--:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
				if(checkquest(9008) == -1) changequest 9007,9008;
				mes "[Lady Amy]";
				mes "โอ้!";
				mes "ไม่ต้องพูด";
				mes "อะไรไปมากกว่านี้แล้ว";
				mes "ยินดีต้อนรับ! ถึงเวลา";
				mes "รับการทดสอบของ Amy แล้วจ้า!";
				next;
				mes "[Lady Amy]";
				mes "ข้าชื่อ Amy Beatrice";
				mes "เลดี้ไนท์ผู้ภาคภูมิใจแห่ง Prontera Chivalry การทดสอบของ Amy จะทดสอบมารยาทการเป็น Knight ของเจ้าจ้า~";
				next;
				mes "[Lady Amy]";
				mes "ข้าจะเล่าสถานการณ์ให้ฟัง แล้วเจ้าจงเลือกคำตอบเมื่อ";
				mes "ข้าถามคำถาม มารยาทของเจ้าจะถูกตัดสินจากคำตอบเหล่านั้น";
				next;
				mes "[Lady Amy]";
				mes "ตั้งใจฟังให้ดีๆ นะ";
				mes "และตอบราวกับว่าเจ้า";
				mes "เป็น Knight แล้วจริงๆ ตกลงไหม?";
				next;
				mes "[Lady Amy]";
				mes "ถ้าอย่างนั้น";
				mes "เรามาเริ่มกันเลย!";
				next;
			}
			else {
				mes "[Lady Amy]";
				mes "โธ่...";
				mes "ก็ได้จ้า~";
				close;
			}
		}
		else if (KNIGHT_Q == 9) {
			mes "หืมมม?";
			mes "ทำไมเจ้าถึง";
			mes "มาหา Amy ล่ะจ๊ะ?";
			next;
			if (select("ข้าต้องการเข้ารับการทดสอบเปลี่ยนอาชีพครับ/ค่ะ:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
				mes "[Lady Amy]";
				mes "อื้มม~?";
				mes "เจ้าได้เรียนรู้แล้วหรือยัง";
				mes "ว่าเจ้าทำอะไรผิดไป";
				mes "เมื่อคราวก่อน? ถ้าเจ้า";
				mes "สอบตกอีกรอบล่ะก็ ข้าจะ";
				mes "โกรธจริงๆ ด้วยนะ!";
				next;
				mes "[Lady Amy]";
				mes "ฉะนั้น ตั้งใจฟังให้ดี";
				mes "และตอบคำถามราวกับว่า";
				mes "เจ้าเป็น Knight";
				mes "เอาล่ะ";
				mes "เริ่มกันเลย!";
				next;
				mes "[Lady Amy]";
				mes "เจ้าเป็น Knight และเจ้ากำลังมองหาปาร์ตี้ในเมือง Morocc";
				mes "เจ้าจะทำอย่างไรเพื่อหาปาร์ตี้?";
				next;
			}
			else {
				mes "[Lady Amy]";
				mes "โธ่...";
				mes "ก็ได้จ้า~";
				close;
			}
		}
		mes "เจ้าเป็น Knight และเจ้ากำลังมองหาปาร์ตี้ในเมือง Morocc";
		mes "เจ้าจะทำอย่างไรเพื่อหาปาร์ตี้?";
		next;
		if (select("ตะโกนบอกทุกคนว่ากำลังหาปาร์ตี้อยู่:เปิดห้องแชทแล้วรอ:มองหาคนที่กำลังต้องการ Knight เข้าปาร์ตี้") != 1)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "เจ้าได้ตั้งปาร์ตี้กับผู้เล่นเลเวลใกล้เคียงกัน ในกลุ่มประกอบด้วย Priest, Wizard, Hunter, Assassin และ Blacksmith...";
		next;
		mes "[Lady Amy]";
		mes "พวกเจ้าทั้ง 6 คนตกลงใจจะไปเก็บเลเวล และได้ตัดสินใจไปที่ Pyramids";
		next;
		mes "[Lady Amy]";
		mes "เจ้าเดินทางไปถึงชั้น 4";
		mes "ของ Pyramids";
		mes "พร้อมกับปาร์ตี้ของเจ้า";
		mes "เจ้าควรจะทำอย่างไร?";
		next;
		if (select("ตรวจสอบพื้นที่และวางแผนล่วงหน้า:ลากมอนสเตอร์มาให้สมาชิกในปาร์ตี้รุมตี:เดินนำหน้าปาร์ตี้ไปอย่างช้าๆ") != 2)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "แต่ทว่า มีผู้เล่นไร้มารยาทลากมอนสเตอร์ฝูงใหญ่มาใส่กลุ่มเจ้าแล้วหนีไป! เจ้าควรทำอย่างไร?";
		next;
		if (select("กันมอนสเตอร์ไม่ให้เข้าถึงตัวสมาชิกในปาร์ตี้:คอยป้องกันในขณะที่สมาชิกคนอื่นถอยร่น:ขี่ Peco Peco หนีไปคนเดียว") != 3)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "โชคดีที่พวกเจ้าผ่านพ้นวิกฤตมาได้ แต่ขณะที่เดินไป เจ้าพบใครคนหนึ่งที่ไม่ใช่คนในปาร์ตี้ สลบอยู่บนพื้น";
		next;
		mes "[Lady Amy]";
		mes "คนคนนั้นขอความช่วยเหลืออย่างสุภาพ เจ้าควรทำอย่างไร?";
		next;
		if (select("ขอให้ Priest ในปาร์ตี้ช่วยเขา:บอกว่าจะช่วยแต่ต้องจ่าย Zeny มา:เมินเฉยแล้วเดินผ่านไป") == 1)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "เจ้าต้องกล่าวอำลาสมาชิกในปาร์ตี้เพราะเจ้ามีธุระต้องไปที่อื่น";
		next;
		mes "[Lady Amy]";
		mes "แต่เจ้าดันพบ";
		mes "ไอเทมหายากในระหว่าง";
		mes "การต่อสู้เข้าพอดี เจ้า";
		mes "ควรจะทำอย่างไร?";
		next;
		if(select("มอบไอเทมให้แก่คนที่คู่ควรที่สุด:ทำเหมือนไม่มีอะไรเกิดขึ้นแล้วเก็บไว้เอง:ปรึกษากับคนในปาร์ตี้ว่าใครควรจะได้ไป") != 2)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "สุดท้ายเจ้าเป็นคนได้ไอเทมนั้นมา และเจ้าไปที่ Prontera เพื่อจะขายมัน มีผู้คนมากมายเปิดร้านและห้องแชทเพื่อขายของกันเต็มไปหมด";
		next;
		mes "[Lady Amy]";
		mes "เจ้าควรจะทำ";
		mes "อย่างไรเพื่อขายไอเทม?";
		next;
		if (select("ตะโกนเสียงดังบอกทุกคน:เปิดห้องแชทแล้วรอ:สอบถามว่ามีใครสนใจไอเทมนี้บ้างหรือไม่") != 1)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "ในขณะที่เจ้ารออยู่นั้น";
		mes "มีคนเดินมาขอ";
		mes "ไอเทมและเงิน Zeny";
		mes "เจ้าจะทำอย่างไร?";
		next;
		if (select("ให้เงินและไอเทมแก่เขาไปบ้าง:ทำเป็นไม่สนใจเขา:ให้คำแนะนำเกี่ยวกับสถานที่ที่เขาควรไปล่ามอนสเตอร์") == 3)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "บัดนี้เจ้าตัดสินใจจะไปที่ Hidden Temple เพียงลำพัง เจ้าขี่ Peco Peco ของเจ้าไปอย่างมีความสุข";
		next;
		mes "[Lady Amy]";
		mes "แต่เจ้าดันไปเจอ";
		mes "ใครคนหนึ่งที่กำลังหลงทาง";
		mes "เจ้าควรจะทำอย่างไร?";
		next;
		if(select("บอกทางไปสู่ทางออกให้แก่เขา:เดินนำเขาไปจนถึงทางออก:มอบ Butterfly Wing ให้เขาไป") != 3)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "เจ้าล่ามอนสเตอร์มาได้สักพักแล้ว และตอนนี้ HP ของเจ้าเหลือต่ำมาก!";
		mes "ตอนนี้หลอดเลือดเป็นสีแดงแล้ว ซึ่งอันตรายสุดๆ";
		next;
		mes "[Lady Amy]";
		mes "อา แล้วตอนนั้นเองมี Priest";
		mes "คนหนึ่งเดินผ่านมาพอดี";
		mes "เจ้าจะขอให้ Priest";
		mes "ช่วย Heal ให้ได้อย่างไร?";
		next;
		if (select("พอจะกรุณาช่วย Heal ให้หน่อยได้ไหมครับ/ค่ะ?:Heal ให้หน่อยได้ไหม?:Heal plz!!") == 1)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "ตอนนี้เจ้า";
		mes "เหนื่อยมากแล้ว และถึงเวลา";
		mes "ที่จะต้องกลับเข้าเมือง";
		next;
		mes "[Lady Amy]";
		mes "ทันใดนั้นเจ้าก็พบ";
		mes "ไอเทมหายากตกอยู่";
		mes "บนถนน";
		mes "เจ้าควรจะ";
		mes "ทำอย่างไร?";
		next;
		if (select("เก็บมันขึ้นมาแล้วเอาไว้เอง:สอบถามคนแถวนั้นเพื่อตามหาเจ้าของ:เดินผ่านไปเฉยๆ") != 1)
			set .@knight_t,.@knight_t+10;
		mes "[Lady Amy]";
		mes "โอเคจ้า";
		mes "นั่นคือคำถาม";
		mes "ข้อสุดท้ายของการทดสอบ!";
		next;
		mes "[Lady Amy]";
		if (.@knight_t == 100) {
			set KNIGHT_Q,10;
			changequest 9008,9009;
			mes "ทำได้ดีมาก ความคิดแบบนั้นแหละคือสิ่งที่ Knight ต้องการ! สำหรับการทดสอบต่อไป โปรดไปพบ Sir Edmond นะจ๊ะ~";
			next;
			mes "[Lady Amy]";
			mes "ข้าจะรายงานเรื่องดีๆ เกี่ยวกับเจ้าให้ท่านหัวหน้าทราบด้วยล่ะ ตั้งใจทำแบบทดสอบที่เหลือให้ดีนะจ๊ะ ตกลงไหม?";
			close;
		}
		else if (.@knight_t == 90) {
			set KNIGHT_Q,10;
			changequest 9008,9009;
			mes "อืม ถึงมันจะไม่สมบูรณ์แบบ";
			mes "แต่ข้าว่าเจ้ารู้เรื่องมารยาท";
			mes "ดีพอที่จะเป็น";
			mes "Knight ที่ดีได้จ๊ะ";
			next;
			mes "[Lady Amy]";
			mes "บัดนี้ ถึงเวลาที่เจ้าต้องไปหา Sir Edmond สำหรับการทดสอบขั้นต่อไปแล้วล่ะ ตั้งใจทำให้ดีกับบททดสอบที่เหลือนะจ๊ะ สัญญาแล้วนะ~";
			close;
		}
		set KNIGHT_Q,9;
		mes "อืมม...";
		mes "บอกตรงๆ นะ ข้ายังไม่คิดว่าทัศนคติของเจ้าจะดีพอที่จะเป็น Knight ในตอนนี้จ๊ะ";
		next;
		mes "[Lady Amy]";
		mes "ถ้าเจ้าทำตัวแบบนั้นจริงๆ ทุกคนจะมองว่า Knight เป็นพวกไร้มารยาทนะ! ลองไปทบทวนคำตอบของเจ้าดูแล้วกลับมาใหม่ภายหลังเถอะ";
		next;
		mes "[Lady Amy]";
		mes "ถ้าเจ้าต้องการ";
		mes "ข้าจะอนุญาตให้";
		mes "เจ้าสอบแก้ตัวได้ ตกลงไหม?";
		close;
	}
	else if (KNIGHT_Q == 10) {
		mes "หืมมม?";
		mes "ทำไมเจ้าถึง";
		mes "มาหา Amy ล่ะจ๊ะ?";
		next;
		mes "[Lady Amy]";
		mes "เจ้าต้องไปพบ";
		mes "Sir Edmond เพื่อรับ";
		mes "การทดสอบถัดไปนะจ๊ะ ตกลงไหม?";
		close;
	}
	else if (KNIGHT_Q == 14) {
		mes "ว้าวว~";
		mes "ถึงเวลาที่";
		mes "ทุกคนจะตัดสิน";
		mes "เรื่องการเปลี่ยนอาชีพของเจ้าแล้ว!";
		next;
		mes "[Lady Amy]";
		mes "ไปคุยกับท่าน";
		mes "หัวหน้ากองกันเถอะ ไม่ต้องกังวล";
		mes "ไปหรอกนะ ทุกอย่าง";
		mes "ต้องผ่านไปได้ด้วยดีจ๊ะ";
		close;
	}
	else {
		mes "หืมมม?";
		mes "ทำไมเจ้าถึง";
		mes "มาหา Amy ล่ะจ๊ะ?";
		next;
		mes "[Lady Amy]";
		mes "เจ้ายังเหลือ";
		mes "บททดสอบอื่นที่ต้องทำนะ";
		mes "รีบไปทำให้เสร็จล่ะ~";
		close;
	}
}

// Test 4 (patience)
//==========================================================
prt_in,70,99,6	script	Sir Edmond#knt	734,{
	mes "[Sir Edmond]";
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) {
			mes "จงคิดว่า";
			mes "จิตใจของเจ้า";
			mes "เปรียบเสมือนสายน้ำที่ไหลริน";
			next;
			mes "[Sir Edmond]";
			mes "สายน้ำที่ไหลเอื่อย";
			mes "จะคอยหลบหลีกอุปสรรค";
			mes "และมุ่งหน้าต่อไปตามทางของมัน...";
			next;
			mes "[Sir Edmond]";
			mes "Knight จะต้อง";
			mes "สามารถปล่อยผ่านสิ่งต่างๆ ได้";
			mes "เหมือนกับน้ำที่นิ่งสงบ ใน";
			mes "ทุกๆ สถานการณ์";
			close;
		}
		else if (BaseClass == Job_Novice) {
			mes "ต้นไม้ที่รากหยั่งลึกย่อมไม่หวั่นไหวไปตามแรงลม ความจริงที่ว่าทักษะที่ทรงพลังต้องสร้างขึ้นจากพื้นฐานที่แข็งแกร่งนั้นเป็นสิ่งที่ไม่เปลี่ยนแปลง...";
			next;
			mes "[Sir Edmond]";
			mes "อนาคตของเจ้า";
			mes "สามารถถูกตัดสิน";
			mes "ได้ตั้งแต่วันนี้...";
			close;
		}
		else {
			mes "ทุกสิ่งในโลกนี้อยู่ร่วมกันอย่างสมดุล การดำเนินชีวิตโดยไม่ทำลายความสมดุลนี้คือวิถีทางที่ถูกต้องของการมีชีวิตอยู่...";
			close;
		}
	}
	if (KNIGHT_Q == 0) {
		mes "ผู้ที่มีความคิดอัปมงคลย่อมฝันเห็นแต่สิ่งชั่วร้าย การไม่มีความฝันเสียเลยยังดีกว่ามีความฝันที่เต็มไปด้วยความเศร้าและสิ้นหวัง";
		close;
	}
	else if (KNIGHT_Q >= 1 && KNIGHT_Q <= 9) {
		mes "มีอะไรหรือ...";
		mes "Swordsman ผู้พเนจร?";
		next;
		if (select("ข้าต้องการเข้ารับการทดสอบเปลี่ยนอาชีพครับ/ค่ะ:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
			mes "[Sir Edmond]";
			mes "เมล็ดพันธุ์ต้องฝังตัว";
			mes "อยู่ในดินก่อนที่มันจะผลิยอด จากนั้น ยอดอ่อนต้องแตกใบก่อนที่ตุ่มตาจะเบ่งบานเป็นดอกไม้...";
			next;
			mes "[Sir Edmond]";
			mes "มิฉะนั้น...";
			mes "ดอกไม้นั้นจะ";
			mes "ไม่สมบูรณ์";
			next;
			mes "[Sir Edmond]";
			mes "จงไปหาผู้อื่น";
			mes "ก่อนเถิด เพื่อที่เจ้า";
			mes "จะได้ค้นพบเส้นทางของเจ้าเอง...";
			close;
		}
		mes "[Sir Edmond]";
		mes "ชีวิตในแบบที่";
		mes "เจ้าต้องการจะมา";
		mes "ปรากฏอยู่ตรงหน้าเจ้า";
		mes "ทุกสิ่งทุกอย่าง";
		mes "จะมาตามลำดับ";
		mes "ที่เหมาะสมของมันเอง";
		close;
	}
	else if (KNIGHT_Q == 10) {
		mes "มีอะไรหรือ...";
		mes "Swordsman ผู้พเนจร";
		next;
		if (select("Lady Amy ส่งข้ามาครับ/ค่ะ:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
			mes "[Sir Edmond]";
			mes "ถึงเวลาเข้ารับการทดสอบของข้าแล้ว โปรดทำให้ดีที่สุด เหมือนที่เจ้าทำได้ในการทดสอบก่อนๆ";
			next;
			mes "[Sir Edmond]";
			mes "ข้าชื่อ";
			mes "Edmond Groster";
			mes "เป็นสมาชิกของ";
			mes "Prontera Chivalry";
			next;
			mes "[Sir Edmond]";
			mes "Knight คือผู้อยู่ในตำแหน่งที่ผู้อื่นจะดำเนินรอยตาม ดังนั้น เจ้าต้องรู้จักคิดทบทวนระเบียบของโลกอย่างถ่อมตน และมีบุคลิกภาพที่เหมาะสมกับบทบาทที่เจ้าได้รับ";
			next;
			mes "[Sir Edmond]";
			mes "เจ้าต้องไม่ตัดสินใจสิ่งใดอย่างสะเพร่า เจตจำนงของเจ้าควรจะโอนอ่อนได้ดังต้นอ้อ หรือมั่นคงแข็งแกร่งดั่งหินผาเมื่อสถานการณ์เรียกร้อง";
			next;
			mes "[Sir Edmond]";
			mes "เจ้าต้องไม่สังหารมอนสเตอร์โดยไม่มีเหตุผล และต้องไม่ยินดีในการกระทำเช่นนั้น จงใช้เวลานี้เพื่อคิดทบทวนถึงสิ่งนี้อย่างเงียบๆ ด้วยตัวเจ้าเอง...";
			next;
			mes "[Sir Edmond]";
			mes "ถ้าอย่างนั้น";
			mes "เรามาเริ่มการทดสอบกัน";
			mes "จงระลึกถึง";
			mes "คุณธรรมแห่ง";
			mes "ความยับยั้งชั่งใจเอาไว้";
			close2;
			set KNIGHT_Q,11;
			changequest 9009,9010;
			warp "job_knt",143,57;
			end;
		}
		mes "[Sir Edmond]";
		mes "ชีวิตในแบบที่เจ้าต้องการ";
		mes "จะมาปรากฏอยู่ตรง";
		mes "หน้าเจ้าในไม่ช้า";
		close;
	}
	else if (KNIGHT_Q == 11) {
		mes "มีอะไรหรือ...";
		mes "Swordsman ผู้พเนจร?";
		next;
		if (select("ข้าขอโทษครับ/ค่ะ ข้าไม่ได้ตั้งใจจะ...:อ้อ ไม่มีอะไรครับ/ค่ะ") == 1) {
			mes "[Sir Edmond]";
			mes "เจ้าประมาทเกินไปในการทดสอบครั้งที่แล้ว ดาบของ Knight มีไว้เพื่อปกป้องผู้อื่น ไม่ใช่เพื่อข่มเหงมอนสเตอร์ที่อ่อนแอกว่า";
			next;
			mes "[Sir Edmond]";
			mes "ในโลกที่ทุกสิ่งอยู่ร่วมกันอย่างสมดุล เจ้าจะปล่อยให้มนุษย์ทำลายล้างไปเรื่อยๆ โดยไร้จุดหมายไม่ได้ หลักการนี้ใช้ได้กับโลกแห่งความจริง ไม่ใช่เฉพาะในการทดสอบนี้เท่านั้น";
			next;
			mes "[Sir Edmond]";
			mes "การทดสอบ";
			mes "จะเริ่มต้นขึ้น";
			mes "จงแสดงให้ข้าเห็น";
			mes "ความอดทนของเจ้า...";
			close2;
			warp "job_knt",143,57;
			end;
		}
		mes "[Sir Edmond]";
		mes "ชีวิตในแบบที่";
		mes "เจ้าต้องการจะมา";
		mes "ปรากฏอยู่ตรงหน้าเจ้า";
		mes "ทุกสิ่งทุกอย่าง";
		mes "จะมาตามลำดับ";
		mes "ที่เหมาะสมของมันเอง";
		close;
	}
	else if (KNIGHT_Q == 12 || KNIGHT_Q == 13) {
		mes "ข้าได้เห็นตัวตนของเจ้าด้วยตัวเองแล้ว บัดนี้ถึงเวลาที่เจ้าต้องเข้ารับการทดสอบสุดท้าย จงไปคุย";
		mes "กับ Sir Gray เสียเถิด...";
		close;
	}
	else {
		mes "จงไปคุย";
		mes "กับหัวหน้ากองของเรา";
		mes "เวลาได้มาถึงแล้ว";
		mes "ที่พวกเราทุกคนจะ";
		mes "ประเมินผลงาน";
		mes "ของเจ้า";
		close;
	}
}

job_knt,1,1,1	script	Timer#knt	107,{
OnTimer300000:
	enablenpc "Warp#knt";
	end;

OnTimer300500:
	donpcevent "Timer#knt::OnDisable";
	disablenpc "Warp#knt";
	end;

OnTimer301500:
	stopnpctimer;
	donpcevent "Timer#knt::OnEnable";
	end;

OnInit:
OnEnable:
	enablenpc "Timer#knt";
	monster "job_knt",141,57,"Poring",1002,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",145,57,"Poring",1002,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",143,55,"Poring",1002,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",143,59,"Poring",1002,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",141,55,"Lunatic",1063,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",141,59,"Lunatic",1063,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",145,55,"Lunatic",1063,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",145,59,"Lunatic",1063,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",139,57,"Chonchon",1011,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",147,57,"Chonchon",1011,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",143,53,"Chonchon",1011,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",143,61,"Chonchon",1011,1,"Timer#knt::OnMyMobDead";
	//These monsters were out of bounds. Adjusted to put them in bounds.
	//มอนสเตอร์เหล่านี้เคยอยู่นอกขอบเขต ได้ทำการปรับเปลี่ยนให้อยู่ในขอบเขตแล้ว
	monster "job_knt",165,54,"Mushroom",1182,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",165,57,"Mushroom",1182,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",122,54,"Mushroom",1182,1,"Timer#knt::OnMyMobDead";
	monster "job_knt",122,57,"Mushroom",1182,1,"Timer#knt::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster "job_knt","Timer#knt::OnMyMobDead";
	disablenpc "Timer#knt";
	disablenpc "Warp#knt";
	end;

OnMyMobDead:
	warp "prt_fild05",353,251;
	end;
}

job_knt,143,57,1	script	Warp#knt	107,22,22,{
OnInit:
	disablenpc "Warp#knt";
	end;
OnTouch_:
	set KNIGHT_Q,12;
	changequest 9010,9011;
	warp "prt_in",80,100;
	end;
}

// Test 5, and creates Claymores for knights
//==========================================================
prt_in,87,92,4	script	Sir Gray#knt	119,{
	mes "[Sir Gray]";
	if (BaseJob != Job_Swordman) {
		if (BaseJob == Job_Knight) {
			mes "แสงประกายที่สะท้อน";
			mes "ออกมาจากใบดาบเล่มนี้ไม่สามารถบรรยายออกมาเป็นคำพูดได้ นี่คืออาวุธที่ Knight ทุกคนต้องมีไว้ครอบครอง";
			next;
			mes "[Sir Gray]";
			mes "ใช่แล้ว...";
			mes "^3355FFClaymore^000000!";
			mes "Knight ทุกคน";
			mes "ต่างก็อยากได้มัน!";
			next;
			switch(select("เกี่ยวกับ ^3355FFClaymore^000000:ซื้อ Claymore:จบการสนทนา")) {
			case 1:
				mes "[Sir Gray]";
				mes "Claymore หนึ่งในดาบที่มีชื่อเสียงที่สุดที่เจ้าสามารถหาได้ใน Prontera แห่ง Rune-Midgarts!! คุณค่าของมันประเมินค่าไม่ได้เลยสำหรับ Knight";
				next;
				mes "[Sir Gray]";
				mes "ตอนนี้สมาคม Knight แห่ง Prontera กำลังผลิต Claymore ที่ยอดเยี่ยมเหล่านี้ขึ้นมา สำหรับเหล่า Knight ราคาเพียงแค่ ^3355FF74,000^000000 Zeny เท่านั้น";
				next;
				mes "[Sir Gray]";
				mes "แต่ไม่เพียงแค่นั้น เจ้ายังต้องใช้";
				mes "1 ^3355FFSteel^000000 เนื่องด้วยคุณลักษณะเฉพาะของ Claymore หากเจ้าต้องการ ข้าสามารถสร้างมันให้เจ้าได้ เพื่อเกียรติยศของสมาคม Knight แห่ง Prontera!";
				close;
			case 2:
				if (MaxWeight - Weight < 1800) {
					mes "[Sir Gray]";
					mes "โอ้ ไม่นะ...";
					mes "ดูเหมือนว่าเจ้าจะแบกของหนักเกินไป เจ้าไม่มีที่ว่างเพียงพอสำหรับ Claymore ที่หนักอึ้งในกระเป๋าของเจ้า";
					next;
					mes "[Sir Gray]";
					mes "ทำไมเจ้าไม่";
					mes "ลองไปจัดการ";
					mes "ไอเทมของเจ้าก่อนล่ะ";
					close;
				}
				else {
					if ((Zeny > 73999) && (countitem(999) > 0) && (BaseJob == Job_Knight)) {
						mes "[Sir Gray]";
						mes "เจ้าพร้อมแล้ว!";
						mes "เจ้าคงจะรู้";
						mes "คุณค่าที่แท้จริง";
						mes "ของ Claymore!";
						mes "ข้าจะสร้างมัน";
						mes "ให้เจ้าเดี๋ยวนี้เลย!!";
						next;
						mes "[Sir Gray]";
						mes "พื้นฐานของการ";
						mes "สร้าง Claymore";
						mes "นั้นง่ายมาก ดูนะ~!";
						next;
						mes "^3355FF*คนๆๆ*^000000";
						mes "^3355FF*อึนช่า อึนช่า*^000000";
						next;
						mes "[Sir Gray]";
						mes "เรียบร้อย มันเสร็จแล้ว!";
						mes "ความภาคภูมิใจของ Knight ทุกคน:";
						mes "^3355FFClaymore^000000 ชั้นยอด";
						mes "เจ้าได้รับไอเทมที่เชื่อถือได้แล้ว";
						mes "มันจะเป็นสหายที่ดีในการผจญภัยของเจ้า";
						delitem 999,1; //Steel
						set Zeny, Zeny - 74000;
						getitem 1163,1; //Claymore
						close;
					}
					mes "[Sir Gray]";
					mes "ข้าเข้าใจว่าเจ้าอาจจะอยากได้ Claymore จริงๆ แต่ข้าไม่สามารถสร้างมันได้โดยไม่มีวัตถุดิบ";
					mes "^3355FF74,000 zeny^000000 และ ^3355FF1 Steel!^000000";
					next;
					mes "[Sir Gray]";
					mes "กลับมาเมื่อ";
					mes "เจ้าเตรียมทุกอย่าง";
					mes "พร้อมแล้ว ข้าจะ";
					mes "รอเจ้าอยู่...";
					close;
				}
			case 3:
				mes "[Sir Gray]";
				mes "Knight ทุกคนควรจะสามารถกวัดแกว่ง Claymore ได้ราวกับว่าเป็นส่วนหนึ่งของร่างกาย ข้าเคยตั้งตารอที่จะได้กวัดแกว่ง Claymore ของข้าในสนามรบ...";
				close;
			}
		}
		else if (BaseClass == Job_Novice) {
			mes "เชื่อหรือไม่ก็ตาม";
			mes "ข้าเองก็";
			mes "เคยเป็น Novice";
			mes "มาก่อนเหมือนกัน";
			next;
			mes "[Sir Gray]";
			mes "ข้าไม่เคยคิดมาก่อนเลยว่าจะได้เป็น Knight แต่ข้าตัดสินใจที่จะเป็นคนที่แข็งแกร่ง และในระหว่างการเดินทางของข้า ข้าก็ได้ลงเอยด้วยการเข้าร่วมกับสมาคม Knight แห่ง Prontera ฮ่า ฮ่า ฮ่า!";
			close;
		}
		mes "เจ้าหนุ่ม,";
		mes "จงใช้เวลาของเจ้า";
		mes "อย่างคุ้มค่า";
		next;
		mes "[Sir Gray]";
		mes "ไม่มีประโยชน์ที่จะ";
		mes "มานั่งเสียใจ";
		mes "เมื่อเวลาผ่านพ้นไปแล้ว";
		close;
	}
	if (KNIGHT_Q == 0) {
		mes "เจ้าหนุ่ม,";
		mes "จงใช้เวลาของเจ้า";
		mes "อย่างคุ้มค่า";
		next;
		mes "[Sir Gray]";
		mes "ไม่มีประโยชน์ที่จะ";
		mes "มานั่งเสียใจ";
		mes "เมื่อเวลาผ่านพ้นไปแล้ว";
		close;
	}
	else if ((KNIGHT_Q == 12) || (KNIGHT_Q == 13)) {
		if (KNIGHT_Q == 12) {
			mes "โอ้...";
			mes "Swordman หนุ่ม";
			mes "ใช่แล้ว มีอะไร";
			mes "ให้ข้าช่วยล่ะ?";
			next;
			if (select("ข้าต้องการรับการทดสอบเพื่อเปลี่ยนอาชีพ:โอ้ ไม่มีอะไรครับ") == 1) {
				mes "[Sir Gray]";
				mes "โฮ่โฮ่ ข้าเข้าใจแล้ว";
				mes "แสดงว่าเจ้าผ่าน";
				mes "การทดสอบของ";
				mes "คนอื่นๆ มาหมดแล้วล่ะสิ?";
				next;
				mes "[Sir Gray]";
				mes "ถ้าอย่างนั้น";
				mes "เรามาเริ่มของข้ากันไหม?";
				mes "มันก็ไม่ใช่การทดสอบ";
				mes "จริงๆ หรอกนะ";
				next;
				mes "[Sir Gray]";
				mes "มาคุยกัน";
				mes "แบบสบายๆ";
				mes "ดีไหม?";
				next;
				mes "[Sir Gray]";
				mes "ก่อนอื่น...";
				mes "ทำไมเจ้าถึง";
				mes "ตัดสินใจที่จะเป็น";
				mes "Knight ล่ะ?";
				next;
			}
			else {
				mes "[Sir Gray]";
				mes "ดูแลตัวเองด้วย!";
				close;
			}
		}
		else if (KNIGHT_Q == 13) {
			mes "อา เจ้าอีกแล้ว";
			mes "อะไรพาเจ้า";
			mes "มาหาข้าล่ะ?";
			next;
			if (select("ข้าได้ลองคิดทบทวนดูอย่างหนักแล้ว:โอ้ ไม่มีอะไรครับ") == 1) {
				mes "[Sir Gray]";
				mes "อย่างนั้นรึ...";
				mes "ข้าสงสัยว่าเจ้า";
				mes "ได้คิดจริงๆ หรือเปล่า...";
				next;
				mes "[Sir Gray]";
				mes "ถ้าอย่างนั้น...";
				mes "เหมือนครั้งที่แล้ว";
				mes "ข้าจะถามอีกครั้ง...";
				next;
				mes "[Sir Gray]";
				mes "ก่อนอื่น...";
				mes "ทำไมเจ้าถึง";
				mes "ตัดสินใจที่จะเป็น";
				mes "Knight ล่ะ?";
				next;
			}
			else {
				mes "[Sir Gray]";
				mes "ดูแลตัวเองด้วย!";
				mes "สุขภาพคือ";
				mes "สมบัติล้ำค่า";
				mes "ของมนุษย์ทุกคน!";
				close;
			}
		}
		switch(select("เพื่อที่จะแข็งแกร่งขึ้น...:เพื่อช่วยกิลด์ของข้า...:เพราะข้ายังไม่พอใจในตัวเองตอนนี้...")) {
		case 1:
			mes "[Sir Gray]";
			mes "เพื่อที่จะแข็งแกร่งขึ้นงั้นรึ?";
			mes "ใช่แล้ว Knight นั้นแข็งแกร่งจริงๆ";
			mes "แต่เจ้าจะเอาความแข็งแกร่งไปเพื่ออะไร?";
			next;
			mes "[Sir Gray]";
			mes "เพื่อเอาไปอวดคนอื่น? เพื่อให้ได้ชื่อเสียง? หรือเจ้ามีเหตุผลอื่น? เจ้าคิดว่าข้อดีของการแข็งแกร่งขึ้นในฐานะ Knight คืออะไร?";
			next;
			switch(select("เพื่อเงินทองและชื่อเสียง:เพื่อที่ข้าจะปกป้องตัวเองได้:เพื่อที่ข้าจะปกป้องผู้อื่นได้")) {
			case 1:
				set .@knight_t,.@knight_t+10;
				mes "[Sir Gray]";
				mes "แน่นอน เงินทองและชื่อเสียงย่อมมีที่ทางของมันในโลกนี้ แต่พวกเราในฐานะ Knight ต้องมีชีวิตอยู่เพื่อคุณธรรมที่สูงส่งกว่านั้น";
				next;
				break;
			case 2:
				mes "[Sir Gray]";
				mes "ความคิดที่ดี เจ้าต้องปกป้องตัวเองให้ได้ก่อนเพื่อที่จะปกป้องผู้อื่น ด้วยเหตุนี้ เจ้าต้องหมั่นฝึกฝนอยู่เสมอ และอย่าได้ยอมแพ้ต่อความเกียจคร้าน";
				next;
				break;
			case 3:
				mes "[Sir Gray]";
				mes "อา เป็นความคิดที่วิเศษมาก ความแข็งแกร่งของ Knight ต้องถูกใช้เพื่อปกป้องผู้อ่อนแอและพิทักษ์ความยุติธรรม";
				next;
				mes "[Sir Gray]";
				mes "น่าเสียดายที่มี Knight บางคนทำให้พวกเราต้องอับอายด้วยการลืมอุดมการณ์ซึ่งเป็นพื้นฐานของความเป็น Knight...";
				next;
				break;
			}
			break;
		case 2:
			mes "[Sir Gray]";
			mes "อา เพื่อช่วยกิลด์ หรืออาจจะช่วยปาร์ตี้ของเจ้า King Tristram ที่ 3 ผู้ชาญฉลาดและเมตตาของพวกเราเคยกล่าวคำพูดอันล้ำค่าไว้ว่า...";
			next;
			mes "[Sir Gray]";
			mes "^8B7500เหนือแม่น้ำอันสงบเงียบ คือน้ำตกที่อันตราย ดังนั้น เจ้าต้องเตรียมพร้อมสำหรับทุกสิ่งอยู่เสมอ...^000000";
			next;
			mes "[Sir Gray]";
			mes "แล้วเจ้าคิดว่า";
			mes "เจ้าจะช่วย";
			mes "กิลด์ของเจ้าได้อย่างไร?";
			next;
			switch(select("กิลด์ต้องการข้า:ข้าสามารถช่วยหาทุนให้กิลด์ได้:ข้าสามารถปกป้องสมาชิกกิลด์ของข้าได้")) {
			case 1:
				mes "[Sir Gray]";
				mes "ใครก็ตาม ไม่ว่าจะอยู่ที่ใดในโลกใบนี้";
				mes "ย่อมมีที่ที่พวกเขาถูกต้องการ อย่าได้ละเลยผู้ที่เดือดร้อน แม้ว่าเขาคนนั้นจะไม่ใช่สมาชิกกิลด์ก็ตาม";
				next;
				break;
			case 2:
				set .@knight_t,.@knight_t+10;
				mes "[Sir Gray]";
				mes "แน่นอนว่าความมั่งคั่งนั้นสำคัญ";
				mes "แต่พวกเราเหล่า Knight ต้องอยู่เพื่อคุณธรรมที่สูงส่งกว่านั้น";
				next;
				break;
			case 3:
				mes "[Sir Gray]";
				mes "อา เป็นความคิดที่วิเศษมาก ความแข็งแกร่งของ Knight ต้องถูกใช้เพื่อปกป้องผู้อ่อนแอและพิทักษ์ความยุติธรรม";
				next;
				mes "[Sir Gray]";
				mes "น่าเสียดายที่มี Knight บางคนทำให้พวกเราต้องอับอายด้วยการลืมอุดมการณ์ซึ่งเป็นพื้นฐานของความเป็น Knight...";
				next;
				break;
			}
			break;
		case 3:
			set .@knight_t,.@knight_t+5;
			mes "[Sir Gray]";
			mes "ความพึงพอใจงั้นรึ";
			mes "ดูเหมือนว่าเจ้าจะเป็น";
			mes "Swordman ที่เก่งกาจอยู่แล้ว";
			mes "มีเหตุผลพิเศษอะไรที่เจ้าอยากจะเป็น Knight หรือเปล่า?";
			next;
			mes "[Sir Gray]";
			mes "ข้าไม่รู้ว่าสำหรับ";
			mes "Swordman เป็นยังไง แต่ Knight จะไม่ยอมให้มีความปล่อยตัวปล่อยใจ มีคนที่ลุ่มหลงกับการแสวงหาความแข็งแกร่งจนไม่สามารถควบคุมตนเองได้";
			next;
			mes "[Sir Gray]";
			mes "แล้ว...";
			mes "ส่วนไหนของตัวเจ้า";
			mes "ที่เจ้ายังไม่พอใจ";
			mes "ในตอนนี้ล่ะ?";
			next;
			switch(select("ทักษะการต่อสู้:เป้าหมาย:รูปลักษณ์ภายนอก")) {
			case 1:
				set .@knight_t,.@knight_t+5;
				mes "[Sir Gray]";
				mes "ทักษะคือสิ่งที่เจ้าจะได้มาพร้อมกับประสบการณ์ในฐานะ Knight มันไม่ควรเป็นเป้าหมายสูงสุดของเจ้า มิฉะนั้น เจ้าจะไม่มีวันพอใจในฐานะ Knight เลย";
				next;
				break;
			case 2:
				set .@knight_t,.@knight_t-5;
				mes "[Sir Gray]";
				mes "ข้าเข้าใจ...";
				mes "การมีเป้าหมายอยู่เสมอนั้นสำคัญมาก เจ้าอาจจะมีไอเดียมากมายเมื่อได้เป็น Knight แต่สิ่งนั้นอาจเปลี่ยนไปตามกาลเวลา";
				next;
				break;
			case 3:
				set .@knight_t,.@knight_t+5;
				mes "[Sir Gray]";
				mes "โอ้ ไม่นะ...";
				mes "สิ่งที่เจ้าเห็นไม่ใช่สิ่งสำคัญจริงๆ หรอก Swordman อาจจะแข็งแกร่งกว่า Knight ก็ได้ และแม้แต่ Knight ก็อาจจะอ่อนแอลงได้หากเขาเกียจคร้าน";
				next;
				break;
			}
			break;
		}
		mes "[Sir Gray]";
		mes "ข้าเข้าใจความคิดของเจ้า";
		mes "แต่ก็มีคนจำพวกที่อยากจะ";
		mes "เป็น Knight โดยไม่ยั้งคิด";
		next;
		mes "[Sir Gray]";
		mes "คนพวกนั้นแหละที่เป็นคนก่อปัญหาและทำให้เสียเกียรติของ Knight ซึ่งนำไปสู่ผลลัพธ์ที่ไม่อาจแก้ไขได้";
		next;
		mes "[Sir Gray]";
		mes "สำหรับเจ้าก็เช่นกัน เมื่อเจ้าได้เป็น Knight แล้ว เจ้าจะไม่สามารถกลับไปเป็น Swordman ได้อีก หน้าที่และความรับผิดชอบของ Knight จะอยู่กับเจ้าตลอดไป";
		next;
		mes "[Sir Gray]";
		mes "หากเจ้าได้เป็น Knight ในทันที สิ่งแรกที่เจ้าจะทำคืออะไร?";
		next;
		switch(select("ข้าจะมุ่งตรงไปที่สนามรบเลย:มีคนกำลังรอข้าอยู่:ข้าจะเรียนรู้เกี่ยวกับ Knight ให้มากขึ้น")) {
		case 1:
			mes "[Sir Gray]";
			mes "สนามรบรึ...?";
			mes "แล้วยังไงต่อล่ะ?";
			next;
			switch(select("ข้าจะเติบโตขึ้นภายในระยะเวลาอันสั้น:ข้าอยากจะทดสอบความสามารถของข้าในฐานะ Knight:ข้าอยากจะไปยังสถานที่ที่ท้าทายยิ่งขึ้น")) {
			case 1:
				set .@knight_t,.@knight_t+10;
				mes "[Sir Gray]";
				mes "อย่ารีบร้อนที่จะแข็งแกร่งจนเกินไป แม้ว่าเจ้าจะได้เป็น";
				mes "Knight แล้ว เจ้าก็ยังเป็นตัวของตัวเองอยู่ดี";
				next;
				break;
			case 2:
				mes "[Sir Gray]";
				mes "การทดสอบตัวเองเป็นสิ่งที่ดี มันเป็นเรื่องปกติที่จะยินดีกับความเปลี่ยนแปลงของตัวเอง แต่อย่าได้ลืมคุณสมบัติที่แท้จริงของการเป็น Knight";
				next;
				break;
			case 3:
				mes "[Sir Gray]";
				mes "แม้เจ้าจะได้เป็น Knight เจ้าก็ไม่ได้เปลี่ยนตัวตนภายในของเจ้า ไม่จำเป็นต้องหักโหมจนเกินไป";
				mes "ผ่อนคลายและค่อยๆ ทำไปทีละก้าว";
				next;
				break;
			}
			break;
		case 2:
			mes "[Sir Gray]";
			mes "ใครที่";
			mes "กำลังรอเจ้าอยู่?";
			next;
			switch(select("เพื่อนของข้า:สมาชิกในกิลด์ของข้า:คนรักของข้า")) {
			case 1:
				mes "[Sir Gray]";
				mes "ข้าเข้าใจ พวกเขาคงจะร่วมยินดีในความสำเร็จของเจ้า อย่าได้สูญเสียจิตใจที่โอบอ้อมอารี และจงช่วยเหลือเพื่อนพ้องอยู่เสมอ";
				next;
				break;
			case 2:
				mes "[Sir Gray]";
				mes "คนที่จะร่วมทุกข์ร่วมสุขกับเจ้า ในฐานะ Knight เจ้าต้องปกป้องพวกเขาเสมอ";
				next;
				break;
			case 3:
				mes "[Sir Gray]";
				mes "โอ้ ความเยาว์วัย!";
				mes "การเป็น Knight";
				mes "เพื่อคนที่เจ้าบริพาร!";
				mes "จงปกป้อง "+((Sex==SEX_MALE)?"นาง":"เขา")+" ตลอดไป...";
				mes "แม้จะต้องสละ";
				mes "ชีวิตของตัวเองก็ตาม!";
				next;
				mes "[Sir Gray]";
				mes "และอีกอย่าง...";
				mes "จงรักพวกเขาตลอดไป";
				mes "ความรักที่จริงใจนั้น";
				mes "หาได้ยากยิ่ง";
				next;
				break;
			}
			break;
		case 3:
			mes "[Sir Gray]";
			mes "ทัศนคติที่ดี...";
			mes "เจ้าวางแผนที่";
			mes "จะเรียนรู้อะไรล่ะ?";
			next;
			switch(select("สถานที่ที่เหมาะสมสำหรับ Knight จะไป...:เส้นทางที่แตกต่างกันของ Knight...:วิธีหาเงินให้ได้มากขึ้นในฐานะ Knight...")) {
			case 1:
				set .@knight_t,.@knight_t+5;
				mes "[Sir Gray]";
				mes "มีสถานที่มากมายในโลกนี้ที่สะดวกสบายหรือไม่สะดวกสบาย อย่างไรก็ตาม Knight ต้อง";
				mes "สามารถเอาตัวรอดได้ในทุกที่";
				next;
				break;
			case 2:
				mes "[Sir Gray]";
				mes "มี Knight อีกมากมายที่เหมือนกับเจ้าในโลกภายนอก จงคิดซะว่าพวกเขาเป็นรุ่นพี่และลองถามคำถามดูเยอะๆ";
				next;
				break;
			case 3:
				set .@knight_t,.@knight_t+15;
				mes "[Sir Gray]";
				mes "โอ้ ไม่นะ เจ้าให้ความสำคัญกับความมั่งคั่งเป็นอันดับหนึ่งของการเป็น Knight รึ? พวกเราไม่ได้ถูกสร้างมาเพื่อเป็นแบบนั้น กลับไปคิดทบทวนดูใหม่";
				mes "ให้ดีกว่านี้เถอะ...";
				next;
				break;
			}
			break;
		}
		mes "[Sir Gray]";
		mes "โอ้ ไม่นะ พวกเรา";
		mes "คุยกันนานเกินไปแล้ว...";
		mes "ข้าต้องขออภัยที่";
		mes "รั้งเจ้าไว้ที่นี่";
		mes "นานขนาดนี้";
		next;
		if (.@knight_t == 0) {
			set KNIGHT_Q,14;
			changequest 9011,9012;
			mes "[Sir Gray]";
			mes "ข้าสนุกที่ได้คุยกับเจ้า เจ้าทำให้ข้านึกถึงตัวเองตอนเป็นทหารใหม่ เราควรไปคุยกับหัวหน้าและตัดสินใจเรื่องการ";
			mes "เปลี่ยนอาชีพของเจ้าดีไหม?";
			next;
			mes "[Sir Gray]";
			mes "ไม่ต้องกังวลไป";
			mes "ข้าประทับใจในตัวเจ้ามาก";
			mes "ตอนนี้ ไปเถอะ~";
			close;
		}
		else if (.@knight_t == 5) {
			set KNIGHT_Q,14;
			changequest 9011,9012;
			mes "[Sir Gray]";
			mes "ข้าสนุกที่ได้พูดคุยกับเจ้า เจ้าสามารถไปคิดถึงหลักการของความเป็น Knight เพิ่มเติมได้เมื่อเจ้าเป็น Knight แล้ว";
			next;
			mes "[Sir Gray]";
			mes "ถ้าอย่างนั้น เราไปหาหัวหน้าเพื่อตัดสินใจเรื่องการเปลี่ยนอาชีพของเจ้ากันเถอะ ไม่ต้องกังวลไป เจ้าดีพอที่จะเป็น Knight แล้ว!";
			close;
		}
		else if (.@knight_t == 10) {
			set KNIGHT_Q,14;
			changequest 9011,9012;
			mes "[Sir Gray]";
			mes "ข้าสนุกที่ได้คุยกับเจ้า ถึงแม้ว่าจะมีบางอย่าง";
			mes "ที่รบกวนใจข้าอยู่บ้าง...";
			next;
			mes "[Sir Gray]";
			mes "เจ้าควรไปพบ";
			mes "หัวหน้า เพื่อที่";
			mes "เราจะตัดสินใจเรื่อง";
			mes "การเปลี่ยนอาชีพของเจ้า";
			next;
			mes "[Sir Gray]";
			mes "ไม่ต้องกังวลจนเกินไป การที่เจ้ามาทดสอบกับข้าได้ หมายความว่าคนอื่นๆ ก็ยอมรับในตัวเจ้าแล้วเช่นกัน";
			mes "ไปได้แล้ว...!";
			close;
		}
		else {
			set KNIGHT_Q,13;
			mes "[Sir Gray]";
			mes "การได้สนทนา";
			mes "กับคนหนุ่มสาว";
			mes "เป็นเรื่องที่สนุกเสมอ...";
			next;
			mes "[Sir Gray]";
			mes "แต่ดูเหมือนว่าความฝันของเจ้าจะอยู่ที่อื่น หรือความตั้งใจของเจ้ายังไม่แน่วพอ จงใช้เวลาในฐานะ Swordman ต่อไปอีกสักพัก แล้วค่อยกลับมา";
			mes "หาข้าใหม่ในภายหลัง";
			next;
			mes "[Sir Gray]";
			mes "หากเจ้าปรารถนาจะพิสูจน์ตนเองเพื่อเป็น Knight จริงๆ เจ้าต้องเปลี่ยนทัศนคติเสียก่อน แล้วเราค่อยมาดูกัน";
			close;
		}
	}
	else if (KNIGHT_Q == 14) {
		mes "ข้าบอกเจ้าแล้ว";
		mes "ให้ไปพบ";
		mes "หัวหน้า";
		next;
		mes "[Sir Gray]";
		mes "ทุกคนจะพิจารณา";
		mes "การตัดสินใจ";
		mes "อย่างรอบคอบ";
		mes "ดังนั้น ไปได้แล้ว!";
		close;
	}
	else {
		mes "โอ้...";
		mes "Swordman หนุ่ม";
		mes "มีอะไรให้ข้าช่วยล่ะ?";
		next;
		if (select("ข้าต้องการรับการทดสอบเพื่อเปลี่ยนอาชีพ:โอ้ ไม่มีอะไรครับ") == 1) {
			mes "[Sir Gray]";
			mes "โฮ่โฮ่~";
			mes "มี Knight หนุ่ม";
			mes "คนอื่นๆ อีกมากมาย";
			mes "อยู่ที่นี่";
			next;
			mes "[Sir Gray]";
			mes "หากเจ้าไปคุย";
			mes "กับพวกเขาครบทุกคน";
			mes "ข้าอาจจะพิจารณา";
			mes "ตัวเจ้าด้วยเช่นกัน";
			close;
		}
		mes "[Sir Gray]";
		mes "ดูแลตัวเองด้วย!";
		mes "สุขภาพคือ";
		mes "สมบัติล้ำค่า";
		mes "ของมนุษย์ทุกคน!";
		close;
	}
}
