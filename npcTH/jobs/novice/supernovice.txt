//===== rAthena Script ======================================= 
//= Super Novice Job Quest
//===== By: ================================================== 
//= Darkchild
//===== Current Version: ===================================== 
//= 2.4
//===== Compatible With: ===================================== 
//= rAthena Project; SVN r15340+
//===== Description: ========================================= 
//= [Official Conversion]
//= Super Novice Job Change Npc
//= Super Novice Cart Rental Npc
//= Some Other Useless Super Novice Npc
//===== Additional Comments: ================================= 
//= 1.9 Rescripted to the Aegis 10.3 Standars. [Samuray22]
//= 2.0 Rescripted again, this time using an iRO base. [L0ne_W0lf]
//= 2.1 Swapped use of baseclass for basejob. [L0ne_W0lf]
//= 2.2 Added Quest Log commands. [Kisuka]
//= 2.3 NPC "Esseray" calls Ex. Super Novice script when enabled. [Euphy]
//= 2.4 Added an Ex. Super Novice dialog check. [Euphy]
//============================================================ 

aldeba_in,223,167,3	script	Tzerero#sn	709,{
	if (BaseJob == Job_Super_Novice) {
		mes "[Tzerero]";
		mes "ข้าเชื่อว่าเจ้ากำลังสนุกกับ";
		mes "ชีวิตในฐานะ Super Novice สินะ? อา";
		mes "ดีแล้ว ดีมาก... เป็นไปตามที่ข้าคาดไว้";
		mes "ความสว่างไสวแห่งความธรรมดาสามัญกำลังเปล่งประกายอย่างเจิดจ้าในตัวเจ้า...";
		next;
		mes "[Tzerero]";
		mes "ข้าสนับสนุนให้เจ้าใช้";
		mes "ชีวิตเหมือนที่ท่าน Kimu-Shaun เคยทำ...";
		mes "จงเป็นผู้รู้สารพัดนึก (Jack of All Trades)...";
		mes "...แต่ไม่ต้องเชี่ยวชาญสิ่งใดเลยสักอย่าง";
		close;
	}
	if (countitem(938) > 29 && countitem(907) > 29 && SUPNOV_Q == 1) {
		delitem 938,30; //Sticky_Mucus
		delitem 907,30; //Resin
		mes "[Tzerero]";
		mes "อา เจ้าได้นำไอเทม";
		mes "ที่ข้าขอมาให้แล้ว!";
		mes "เจ้าได้พิสูจน์แล้วว่า";
		mes "คู่ควรที่จะเข้าร่วม";
		mes "สมาคม Super Novice ของเรา";
		next;
		mes "[Tzerero]";
		mes "ถ้าอย่างนั้น ตามที่สัญญาไว้ ข้าจะ";
		mes "เปลี่ยนอาชีพของเจ้าให้กลายเป็น";
		mes "^3355FFSuper Novi--^000000หือ?";
		mes "นั่นอะไรอยู่ข้างหลังเจ้าน่ะ?";
		next;
		select("หือ?:นั่นอะไรน่ะ?");
		mes "^3355FFคุณหันไปมองข้างหลัง แต่...^000000";
		mes "^3355FFไม่มีอะไรอยู่ตรงนั้นเลย^000000";
		mes "^3355FFดูเหมือนจะมีอะไรไม่ชอบมาพากลเกิดขึ้นที่นี่!^000000";
		next;
		completequest 6010;
		callfunc "F_ClearJobVar";
		getitem 2339,1; //G_Strings
		if (Upper==0) jobchange Job_Super_Novice;
		if (Upper==2) jobchange Job_Super_Baby;
		mes "[Tzerero]";
		mes "บว่ะฮ่าฮ่า! ข้าหลอกเจ้าได้!";
		mes "เป็นไง... มุกของข้าตลกไหมล่ะ?";
		mes "เอาเถอะ ลืมเรื่องนั้นไปซะ...";
		next;
		mes "[Tzerero]";
		mes "รวมถึงเหตุผลที่เป็นไปได้ว่า";
		mes "ทำไมชายวัยผู้ใหญ่";
		mes "อย่างข้าถึงได้พก";
		mes "กางเกงใน (Panties) ติดตัวไว้";
		next;
		mes "[Tzerero]";
		mes "สิ่งที่สำคัญก็คือ...";
		mes "เจ้าได้เข้าร่วมอันดับที่น่าภาคภูมิใจ";
		mes "ของเหล่า Super Novice ผู้ยิ่งใหญ่แล้ว";
		next;
		mes "[Tzerero]";
		mes "ถือว่ากางเกงในนี่เป็นของขวัญนะ...";
		mes "เสื้อผ้าชิ้นนี้มีข่าวลือว่าเคยถูก";
		mes "สวมใส่โดยท่าน Kimu-Shaun";
		mes "ผู้ก่อตั้งสมาคมในตำนานของเรา ใน";
		mes "สมัยที่เขายังเริ่มต้นแสวงหา";
		mes "ความธรรมดาสามัญที่เป็นแบบอย่าง";
		next;
		mes "[Tzerero]";
		mes "จงออกไป และสนุกกับชีวิตใหม่";
		mes "ในฐานะ Super Novice! ออกผจญภัย";
		mes "ไปข้างหน้าและช่วยเหลือผู้คน";
		mes "ในขณะที่เป็นหนึ่งในพวกเขา";
		mes "ไปพร้อมๆ กัน!";
		close;
	}
	if (SUPNOV_Q == 1) {
		mes "[Tzerero]";
		mes "หือ? เจ้าลืมไปแล้วเหรอว่าข้า";
		mes "ต้องการอะไรจากเจ้า? ตกลง";
		mes "ข้าจะบอกให้เจ้าทราบ";
		mes "อีกครั้ง โปรดจำให้ดี";
		mes "ในครั้งนี้ล่ะ...";
		next;
		mes "[Tzerero]";
		mes "ข้าขอให้เจ้านำ";
		mes "^FF000030 Sticky Mucus^000000 และ";
		mes "^FF000030 30 Resin^000000 มาให้ข้า";
		close;
	}
	if (BaseJob == Job_Novice && Upper != 1) {
		mes "[Tzerero]";
		mes "...หืม?";
		mes "หยุดก่อน";
		mes "ขอข้าดูเจ้าหน่อย";
		next;
		mes "[Tzerero]";
		mes ".....";
		mes ".......";
		mes ".........";
		emotion ET_THINK;
		next;
		mes "[Tzerero]";
		mes "ข้าเห็นว่าแสงสว่าง";
		mes "แห่งความธรรมดาสามัญนั้น";
		mes "ส่องประกายอย่างเจิดจ้าในตัวเจ้า...";
		next;
		mes "[Tzerero]";
		mes "ทำไมเจ้าไม่มาร่วมกับเราล่ะ";
		mes "Novice น้อย? มาร่วมกับ";
		mes "เราและเรียนรู้ความยิ่งใหญ่";
		mes "ที่ซ่อนอยู่ในความ";
		mes "ธรรมดาสามัญ...";
		next;
		mes "[Tzerero]";
		mes "ยอมรับข้อเสนอของข้าสิ...";
		mes "โยนเสื้อผ้าสีน้ำตาล";
		mes "ที่เต็มไปด้วยฝุ่นเหล่านั้นทิ้งไป";
		mes "แล้วเบ่งบานกลายเป็น...";
		next;
		mes "[Tzerero]";
		mes "...^CE6300Super Novice^000000 ผู้ยิ่งใหญ่";
		next;
		switch(select("ยอมรับข้อเสนอ:ปฏิเสธข้อเสนอ:ฟังอย่างตั้งใจมากขึ้น")) {
		case 1:
			if (!callfunc("F_CanChangeJob")) {
				mes "[Tzerero]";
				mes "หืม... แต่เจ้าเห็นคุณค่า";
				mes "ที่แท้จริงของการ";
				mes "ค้นหาความแข็งแกร่งใน";
				mes "ความอ่อนแอหรือเปล่า? เจ้าต้อง";
				mes "พิสูจน์ให้ข้าเห็นก่อนว่า";
				mes "เจ้าคือผู้ที่มีผลงานธรรมดาๆ อย่างแท้จริง";
				next;
				mes "[Tzerero]";
				mes "จงใช้ชีวิตในฐานะ Novice ต่อไป...";
				mes "และกลับมาเมื่อเจ้า";
				mes "เชี่ยวชาญทักษะ";
				mes "Basic Skills แล้ว...";
				mes "จงเติบโตในความธรรมดาและ";
				mes "กลายเป็น Novice เลเวล 10...";
				close;
			}
			if (BaseLevel < 45) {
				mes "[Tzerero]";
				mes "หืม... แต่เจ้าเห็นคุณค่า";
				mes "ของวิถีชีวิตที่ผ่อนคลาย";
				mes "ของนักผจญภัยที่แสนธรรมดาจริงๆ หรือ?";
				mes "พิสูจน์ให้ข้าเห็นว่าเจ้าไม่ได้";
				mes "กระหายในพลังอำนาจ...";
				next;
				mes "[Tzerero]";
				mes "จงใช้ชีวิตอย่างเรียบง่ายต่อไป...";
				mes "เป็น Novice เลเวล 45 ให้ได้...";
				mes "เมื่อนั้นแหละที่เจ้าจะสามารถเข้าร่วมกับเราได้...";
				close;
			}
			mes "[Tzerero]";
			mes "ข้าเห็นแววตาของเจ้า";
			mes "ถึงความมุ่งมั่นที่จะ";
			mes "ใช้ชีวิตอย่างเรียบง่าย...";
			next;
			mes "[Tzerero]";
			mes "มีเพียงผู้ที่ชาญฉลาดอย่างแท้จริงเท่านั้น";
			mes "ที่มองเห็นว่าการเป็นคนปกติ";
			mes "และแสนธรรมดานั้นคือวิถี";
			mes "การใช้ชีวิตที่ดีที่สุด";
			next;
			mes "[Tzerero]";
			mes "อย่างไรก็ตาม เราไม่ต้อนรับ";
			mes "ใครก็ได้เข้าสู่สมาคมของเรา";
			mes "เจ้าต้องผ่านการทดสอบ";
			mes "คุณสมบัติของเราเสียก่อน";
			next;
			mes "[Tzerero]";
			mes "สำหรับการทดสอบนี้ เจ้าต้อง";
			mes "นำไอเทมบางอย่างที่";
			mes "ดรอปจากมอนสเตอร์ปกติ";
			mes "ที่ไม่โดดเด่นมาให้ข้า";
			next;
			mes "[Tzerero]";
			mes "อืมมมม...";
			mes "^FF000030 Sticky Mucus^000000";
			mes "และ ^FF000030 30 Resin^000000";
			mes "^000000น่าจะเหมาะสมสำหรับการทดสอบ";
			mes "ความสามารถในการต่อสู้กับศัตรูที่อ่อนแอ";
			next;
			mes "[Tzerero]";
			mes "นอกจากนี้ เลข 30";
			mes "นั้นมีความหมายนะ มันไม่ใช่";
			mes "อะไรที่พิเศษเลย... มันก็แค่";
			mes "ตัวเลขธรรมดาๆ เลขหนึ่ง";
			mes "ฮ่าฮ่าฮ่าฮ่าฮ่า~";
			next;
			set SUPNOV_Q,1;
			setquest 6010;
			mes "[Tzerero]";
			mes "โชคดีนะ เพื่อนของข้า";
			close;
		case 2:
			mes "[Tzerero]";
			mes "เอาล่ะ เอาล่ะ... ข้าเดาว่า";
			mes "คุณค่าของชีวิตที่เรียบง่าย";
			mes "คงเป็นเรื่องยากสำหรับเจ้า";
			mes "ที่จะทำความเข้าใจ... ไม่เป็นไร...";
			mes "ชีวิตเป็นของเจ้าเอง";
			next;
			mes "[Tzerero]";
			mes "แต่ถ้าวันใดที่เจ้ามองเห็น";
			mes "แสงสว่างแห่งความธรรมดา เจ้า";
			mes "สามารถมาหาข้าได้";
			mes "ทุกเมื่อ...";
			next;
			mes "[Tzerero]";
			mes "เมื่อชมรม Novice ของเรา";
			mes "ได้รับความนิยมมากขึ้นและ";
			mes "เรามีผู้ติดตามมากขึ้น";
			mes "เราอาจจะพิจารณาใช้";
			mes "การทดสอบที่ยากขึ้นกว่านี้...";
			close;
		case 3:
			mes "[Tzerero]";
			mes "สมาคม Novice ของเราก่อตั้งขึ้น";
			mes "โดยท่าน Kimu-Shaun";
			mes "ในตำนาน... ซึ่งอาจจะเป็น";
			mes "ชายที่ยิ่งใหญ่ที่สุดในยุคของเรา";
			next;
			mes "[Tzerero]";
			mes "เขาตระหนักว่ามีการ";
			mes "ทนทุกข์มากมายใน";
			mes "โลกนี้ โดยเฉพาะในหมู่";
			mes "สามัญชนทั่วไปของ";
			mes "Midgard...";
			next;
			mes "[Tzerero]";
			mes "เขาได้เรียนรู้ทักษะมากมายจาก";
			mes "ผู้คนหลากหลายที่เขาได้";
			mes "พบเจอ... แต่เนื่องจากเขาไม่ได้";
			mes "อยู่ที่ใดที่หนึ่งนานนัก";
			mes "เขาจึงกลายเป็นผู้รู้สารพัดนึก...";
			mes "แต่ไม่เชี่ยวชาญสิ่งใดเลย";
			next;
			mes "[Tzerero]";
			mes "ในการร่วมแบ่งปันความเจ็บปวด";
			mes "ของสามัญชน เขาจึงได้";
			mes "กลายเป็นหนึ่งในพวกเขา...";
			mes "ชายธรรมดาๆ ที่ยิ่งใหญ่";
			mes "ที่สุดเท่าที่เคยมีมา";
			next;
			mes "[Tzerero]";
			mes "สมาชิกในสมาคมของเรา";
			mes "พยายามใช้ชีวิตแบบที่ท่าน";
			mes "Kimu-Shaun ทำ ตาม";
			mes "หลักการที่เขาวางไว้ให้พวกเรา...";
			close;
		}
	}
	else {
		if (BaseJob == Job_Super_Novice) {
			mes "[Tzerero]";
			mes "เจ้าชอบการใช้ชีวิต";
			mes "อย่างเรียบง่ายในฐานะ Super";
			mes "Novice ไหม? ข้ามั่นใจว่า";
			mes "เจ้ากำลังสนุกกับมันอยู่แน่ๆ~";
			next;
			mes "[Tzerero]";
			mes "โปรดเติบโตในฐานะ Super";
			mes "Novice ด้วยการช่วยเหลือ";
			mes "เหล่าสามัญชน ในขณะที่";
			mes "เป็นหนึ่งในพวกเขาไปพร้อมกัน...";
			next;
			mes "[Tzerero]";
			mes "ข้าสนับสนุนให้เจ้า";
			mes "เติบโตในความเป็น Super";
			mes "Novice ของเจ้า และเป็น";
			mes "แบบอย่างในการดำเนินชีวิต";
			mes "ที่แสนธรรมดาอย่าง";
			mes "ยอดเยี่ยมที่สุด";
			close;
		}
		mes "[Tzerero]";
		mes "สวัสดี ข้าคือ Tzerero";
		mes "ผู้อำนวยการบริหารอย่างไม่";
		mes "เป็นทางการของสมาคม";
		mes "Novice ผู้ยิ่งใหญ่";
		next;
		mes "[Tzerero]";
		mes "หืม... เจ้าดูเหมือนจะเป็นคนที่";
		mes "อยู่เหนือค่าเฉลี่ยของคนทั่วไปนะ";
		mes "ใช่แล้ว เจ้ามีความ 'พิเศษ'";
		mes "มากกว่าที่จะเป็นคน 'ธรรมดา'...";
		next;
		mes "[Tzerero]";
		mes "*เฮ้อ* ข้าเดาว่าเจ้า";
		mes "คงไม่มีวันเข้าใจ";
		mes "วิถีชีวิตของเรา ความยิ่งใหญ่";
		mes "ที่ซ่อนอยู่ในความธรรมดา";
		mes "แต่ไม่เป็นไรหรอก";
		next;
		mes "[Tzerero]";
		mes "เอาล่ะ... มีผู้คนนับพัน";
		mes "ที่แตกต่างกันในโลกใบนี้";
		mes "ดังนั้นข้าแค่พยายามยอมรับ";
		mes "ความแตกต่างทั้งหมดของเรา";
		mes "ข้าหวังว่าเจ้าจะทำเช่นเดียวกัน";
		close;
	}
}

aldeba_in,216,169,5	script	Esseray#sn	86,{
	if (Class == Job_Super_Novice_E || Class == Job_Super_Baby_E) {
		mes "[Esseray]";
		mes "เจ้านั่นเอง! แข็งแกร่งขึ้นกว่าเมื่อก่อนนะ";
		mes "ข้าว่าแล้วว่าเจ้าต้องผ่านการทดสอบได้แน่ๆ~";
		close;
	}
	if (BaseJob == Job_Super_Novice) {
		if (checkre(0))
			callfunc "Esseray_Ex";
		mes "[Esseray]";
		mes "หืม? เฮ้ เจ้าเป็นสมาชิก";
		mes "ของสมาคม Novice ผู้ยิ่งใหญ่ของเรา";
		mes "ใช่ไหม? นี่มันคือชมรม";
		mes "ที่ดีที่สุดเท่าที่เคยมีมาเลยใช่ไหมล่ะ?!";
		next;
		mes "[Essaray]";
		mes "การใช้ชีวิตอย่างเรียบง่าย";
		mes "ตามหลักการของท่าน";
		mes "Kimu-Shaun...";
		mes "การเป็นคนธรรมดานี่มันยอดเยี่ยมจริงๆ!";
		next;
		mes "[Esseray]";
		mes "มาลองใช้ชีวิตของพวกเรา";
		mes "ให้ปกติที่สุดเท่าที่จะทำได้กันเถอะ!";
		mes "เผื่อเจ้าอยากรู้นะ ข้านี่แหละ";
		mes "สมาชิกหมายเลขหนึ่ง";
		mes "รองจากท่าน Tzerero แน่นอนอยู่แล้ว!";
		close;
	}
	if (BaseJob == Job_Novice && Upper != 1) {
		mes "[Esseray]";
		mes "ฮ่า~ เจ้ายังไม่รู้อะไร";
		mes "เกี่ยวกับการเป็นคนธรรมดาเลยสักนิด!";
		mes "ทำไมไม่ลองเข้าร่วมสมาคม Novice";
		mes "ของเราดูล่ะ? ชมรมนี้ดีที่สุดในโลกเลยนะ~";
		next;
		mes "[Esseray]";
		mes "ใช่แล้ว Super Novice";
		mes "คือตัวละครที่เจ๋งที่สุด!";
		mes "ฮ่าฮ่าฮ่าฮ่าฮ่าฮ่าฮ่าฮ่า!";
		close;
	}
	else {
		mes "[Esseray]";
		mes "เหอะ~! เจ้ามันดีเกินกว่า";
		mes "ค่าเฉลี่ย... ให้ตายสิ เจ้าอาจจะ";
		mes "ถึงขั้นเป็นพวก 'พิเศษ' เลยก็ได้";
		mes "ช่างน่าเสียดายจริงๆ! เอาเถอะ...";
		mes "ข้าหวังว่าเจ้าจะยังคงใช้ชีวิต";
		mes "ในแง่บวกต่อไปนะ";
		close;
	}
}

aldebaran,54,238,5	script	Kafra Employee#sn	117,{
	if (BaseJob == Job_Super_Novice) {
		mes "[Kafra Employee]";
		if (Sex == SEX_MALE)
			mes "สวัสดีค่ะ ท่าน Super Novice~";
		else
			mes "สวัสดีค่ะ คุณผู้หญิง Super Novice";
		mes "คุณคงจะรู้สึกผิดหวัง";
		mes "มากเลยสินะที่พนักงาน";
		mes "Kafra คนอื่นๆ ไม่ยอม";
		mes "ให้คุณเช่ารถเข็นน่ะ";
		mes "แต่ตอนนี้ไม่ต้องกังวลไปนะคะ...";
		next;
		mes "[Kafra Employee]";
		mes "ฉันมาที่นี่เพื่อสนับสนุนพวกคุณ";
		mes "ด้วยการให้บริการรถเข็นค่ะ...";
		next;
		mes "[Kafra Employee]";
		mes "^3355FF*กระซิบ*^000000";
		mes "^555555ฉันไม่ควรทำแบบนี้หรอกค่ะ";
		mes "เพราะมันผิดนโยบายบริษัท";
		mes "แต่ฉันรู้สึกสงสารเหล่า Super Novice";
		mes "จริงๆ... ฉันก็เลยมาอยู่ตรงนี้ยังไงล่ะคะ^000000";
		next;
		mes "[Kafra Employee]";
		mes "เอาเป็นว่า คุณต้องการเช่ารถเข็นไหมคะ? ค่าบริการคือ 1,900 zeny ค่ะ";
		next;
		if (select("เช่ารถเข็น:ยกเลิก") == 1) {
			mes "[Kafra Employee]";
			mes "โอ้ มีเรื่องหนึ่งค่ะ...";
			mes "คุณได้เรียนรู้ทักษะ 'Push Cart'";
			mes "หรือยังคะ? ฉันสามารถให้เช่า";
			mes "รถเข็นได้ แต่ถ้าคุณยังไม่";
			mes "เรียนรู้วิธีเข็นมัน คุณก็จะ";
			mes "เสียเงิน zeny ไปเปล่าๆ นะคะ";
			next;
			mes "[Kafra Employee]";
			mes "ดังนั้น ตรวจสอบให้แน่ใจว่าคุณ";
			mes "มีทักษะ 'Push Cart' อยู่แล้วนะคะ";
			next;
			if (select("เช่ารถเข็น:ยกเลิก") == 1) {
				if (checkcart()) {
					mes "[Kafra Employee]";
					mes "โอ้ คุณมีรถเข็นติดตั้งอยู่แล้วนี่คะ";
					close;
				}
				if (Zeny >= 1899) {
					set RESRVPTS, RESRVPTS + 190;
					set Zeny, Zeny-1900;
					if (getskilllv("MC_PUSHCART") > 0) setcart;
					mes "[Kafra Employee]";
					mes "ขอบคุณที่ใช้บริการของฉันนะคะ";
					mes "ถึงแม้สิ่งที่ฉันทำอยู่อาจจะไม่";
					mes "ถูกต้องในสายตาพนักงาน Kafra";
					mes "คนอื่นๆ แต่ฉันเชื่อมั่นอย่างแรงกล้า";
					mes "ว่าฉันกำลังทำในสิ่งที่ถูกต้องเพื่อลูกค้าค่ะ";
					close;
				}
				else {
					mes "[Kafra Employee]";
					mes "ขอโทษด้วยนะคะ แต่คุณมีเงิน zeny ไม่เพียงพอ ค่าบริการคือ 1,900 zeny ค่ะ";
					close;
				}
			}
			mes "[Kafra Employee]";
			mes "แล้วมาใหม่นะคะ";
			mes "ขอบคุณที่ใช้บริการของฉันค่ะ";
			close;
		}
		mes "[Kafra Employee]";
		mes "แล้วมาใหม่นะคะ";
		mes "ขอบคุณที่ใช้บริการของฉันค่ะ";
		close;
	}
	mes "[Kafra Employee]";
	mes "สวัสดีค่--อุ๊ย...!";
	mes "ฉันคิดว่าฉันไม่สามารถให้";
	mes "บริการที่คุณต้องการได้ค่ะ...";
	mes "โปรดไปคุยกับพนักงาน Kafra";
	mes "คนอื่นนะคะ ต้องขออภัยใน";
	mes "ความไม่สะดวกด้วยจริงๆ ค่ะ...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= Dialogs From Some Other Scripts (Most Of It)
//= 1.3-1.5 Added Baby class support (and to the Kafta SN Cart
//=     renter, too) [Lupus]
//= 1.6 Added a fix which prevent High Novices passing
//=     this Job Quests again. [Lupus]
//= 1.7 Fixed Baby Class Support. [Lupus]
//= 1.8 Now a Baby can become a Super Baby [Lupus]
//============================================================ 
