//===== rAthena Script ======================================= 
//= Soul Linker Job Quest
//===== By: ================================================== 
//= Celestria & Samuray22
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job quest for Soul Linker class.
//= [Vars Used]
//= SOUL_Q = Job Change Phase. (Max 4)
//===== Additional Comments: ================================= 
//= 1.2 Rescripted to Aegis 10.3 standards. [Samuray22]
//= 1.3 Slight updates and fixes to the script. [L0ne_W0lf]
//=     Removes the use of labels, and corrects other errors.
//=     Removed the GM-use only NPC from the sec_in map. 
//=     No longer used a global var, uses an NPC var instead.
//= 1.3a Deleted Empty Color Tag. (bugreport:1572) [Samuray22]
//= 1.4 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.5 Added Quest Log commands. [Kisuka]
//= 1.6 Updated to match the latest official script. [Euphy]
//============================================================ 

morocc_in,174,30,6	script	Kid#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "ขอให้โชคดีในการ";
		mes "เดินทางของท่าน เมื่อท่านเชี่ยวชาญ";
		mes "ทักษะของ Soul Linker มากขึ้น";
		mes "ท่านจะสามารถดึงพลัง";
		mes "ของเหล่าวิญญาณออกมาได้มากขึ้น";
		mes "เพื่อมอบให้แก่เพื่อนพ้องของท่าน...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Kid]";
		if (Sex == SEX_MALE) {
			mes "ท่านคือนักรบแห่ง";
			mes "ดวงอาทิตย์ไม่ใช่หรือ? ข้าคุ้นเคย";
		}
		else {
			mes "ท่านคือนักรบแห่ง";
			mes "ดวงจันทร์ไม่ใช่หรือ? ข้าคุ้นเคย";
		}
		mes "กับวิถีของท่าน เพราะอย่างไรแล้ว";
		mes "พื้นฐานทักษะของเราทั้งคู่";
		mes "ก็มีรากฐานมาจาก";
		mes "อาชีพ Taekwon Do เหมือนกันใช่ไหม?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Kid]";
		mes "อืม? ข้าไม่มีอะไรจะ";
		mes "มอบให้ท่านหรอก แต่ถ้าท่านรู้จัก";
		mes "ผู้ฝึกฝน Taekwon Do";
		mes "ที่มีประสบการณ์สูงล่ะก็";
		mes "พวกเขาอาจจะได้ประโยชน์";
		mes "จากสิ่งที่ข้ารู้ก็ได้";
		close;
	}
	if (JobLevel < 40) {
		mes "[Kid]";
		mes "ดังนั้นท่านกำลังศึกษา";
		mes "Taekwon Do อยู่สินะ นั่นดีแล้ว";
		mes "มันดีมากจริงๆ จงหมั่น";
		mes "ขัดเกลาทักษะเหล่านั้นและ";
		mes "ยึดมั่นในการฝึกฝนของท่านต่อไป";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Kid]";
			mes "...";
			mes "นี่ ท่านน่ะ";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "เจ้าเรียกข้าเหรอ?";
			next;
			mes "[Kid]";
			mes "ใช่ ข้าเรียกท่านนั่นแหละ";
			mes "อย่าให้ข้าต้อง";
			mes "ตะโกนเรียกสิ";
			mes "เดินมานี่หน่อย";
			next;
			if (select("เจ้ามันเด็กที่ไร้มารยาทชะมัด!:ไม่ต้องสนใจเขา") == 1) {
				mes "[Kid]";
				mes "ท่านโชคดีนะที่ข้า";
				mes "กำลังให้ความสนใจ";
				mes "ในตัวท่าน! ข้าอาจจะดู";
				mes "เหมือนเด็ก แต่ข้าอายุ";
				mes "มากกว่าสามร้อยปีแล้วนะ!";
				emotion ET_HNG;
				next;
				mes "[Kid]";
				mes "คราวนี้ฟังนะ...";
				mes "ข้ารู้ว่าท่านเป็น";
				mes "ศิษย์แห่ง Taekwon Do";
				mes "มันเป็นศาสตร์ที่น่านับถือ แต่";
				mes "ข้ามีข้อเสนอให้";
				mes "ถ้าท่านอยากจะลองฟังดู";
				emotion ET_SMILE;
				next;
				mes "[Kid]";
				mes "ข้ามองดูท่านแล้ว ข้าบอกได้";
				mes "ทันทีเลยว่าท่านมีพรสวรรค์";
				mes "ทางจิตวิญญาณมาก ท่านมี";
				mes "ศักยภาพมากมายที่ข้าไม่อยาก";
				mes "เห็นมันสูญเปล่า ทำไมท่านไม่";
				mes "ลองมาเป็น ''Soul-Linker?'' ดูล่ะ";
				next;
				if (select("ฮ่า! เจ้าเด็กน้อยไร้สาระ~:Soul Linker?") == 1) {
					mes "[Kid]";
					mes "ท่าน... ท่าน ม-ไม่";
					mes "เชื่อข้าเหรอ? ข้าพูด";
					mes "จริงจังนะ ช่วยลืมเรื่อง";
					mes "ที่ข้าดูเหมือนเด็กน้อย";
					mes "สักนาทีนึงได้ไหม? *เชอะ* ...พวกคนหนุ่มสาว";
					close;
				}
				mes "[Kid]";
				mes "Soul Linker สื่อสารกับ";
				mes "ดวงวิญญาณของนักรบที่ล่วงลับ";
				mes "ซึ่งยังคงปรารถนาจะต่อสู้ใน";
				mes "โลกของคนเป็น คราวนี้ วิญญาณ";
				mes "นักรบเหล่านี้ไม่สามารถต่อสู้";
				mes "ด้วยตัวเองในโลกของเราได้";
				next;
				mes "[Kid]";
				mes "อย่างไรก็ตาม ในเมื่อท่านมี";
				mes "พรสวรรค์ทางจิต วิญญาณ";
				mes "เหล่านี้จึงถูกดึงดูดเข้าหาท่าน";
				mes "ด้วยการฝึกฝนที่เพียงพอ ท่านจะ";
				mes "มอบพลังของวิญญาณเหล่านี้ให้แก่พวกพ้องได้ชั่วคราว";
				next;
				mes "[Kid]";
				mes "แต่ท่านไม่สามารถมอบพลัง";
				mes "วิญญาณให้แก่ตัวเองได้ นอกจากนี้";
				mes "ขึ้นอยู่กับทักษะของท่านในฐานะ";
				mes "Soul Linker ท่านสามารถมอบ";
				mes "พลังเสริมให้แก่ตัวละครในบางอาชีพเท่านั้น";
				next;
				mes "[Kid]";
				mes "ท่านจะต้องเข้าสู่";
				mes "โลกที่แตกต่างออกไปอย่างสิ้นเชิง";
				mes "เพื่อกลายเป็น Soul Linker";
				mes "แต่ข้ารู้ว่ามันเป็นไปได้";
				mes "สำหรับท่าน แล้วท่านจะว่ายังไง?";
				next;
				if (select("ไม่ล่ะ อย่างน้อยก็ไม่ใช่ตอนนี้...:ตกลง ข้าต้องทำยังไงบ้าง?") == 1) {
					mes "[Kid]";
					mes "อา ตกลง เอาเป็นว่า";
					mes "ถ้าท่านตัดสินใจได้แล้ว";
					mes "ว่าจะมาเป็น Soul Linker";
					mes "ก็กลับมาคุยกับข้า";
					mes "ได้ทุกเมื่อนะ";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Kid]";
				mes "สรุปคือท่านอยากจะเป็น";
				mes "Soul Linker สินะ? เยี่ยม!";
				mes "เอาล่ะ อย่างแรกข้าต้องการ";
				mes "ให้ท่านนำไอเทมสองสามอย่างมา";
				mes "ไม่ต้องห่วง เดี๋ยวข้าจะอธิบาย";
				mes "ทีหลังว่าทำไมท่านถึงต้องใช้มัน";
				next;
				mes "[Kid]";
				mes "คราวนี้ นำสิ่งเหล่านี้มาให้ข้า";
				mes "^0000FF1 3 Carat Diamond^000000 1 เม็ด,";
				mes "^0000FF1 Immortal Heart^000000 1 ชิ้น และ";
				mes "^0000FF1 Witherless Rose^000000 1 ดอก";
				mes "และอย่าให้ข้าต้อง";
				mes "รอนานนักล่ะ เข้าใจไหม?";
				close;
			}
			mes "[Kid]";
			mes "หา...?";
			mes "เดี๋ยวสิ ท่านกำลัง";
			mes "จะไปไหน? ข้า...";
			mes "ข้ากำลังพูดกับท่านอยู่นะ!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Kid]";
				mes "กลับมาแล้วเหรอ?";
				mes "ได้นำ";
				mes "^0000FF1 3 Carat Diamond^000000,";
				mes "^0000FF1 Immortal Heart^000000 และ";
				mes "^0000FF1 Witherless Rose^000000";
				mes "มาตามที่ข้าบอกหรือเปล่า?";
				next;
				if (select("นี่ไงล่ะ:ยังเลย...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; //Crystal_Jewel__
						delitem 929,1; //Immortal_Heart
						delitem 748,1; //Witherless_Rose
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Kid]";
						mes "เยี่ยม ข้าเห็นว่าท่าน";
						mes "นำทุกอย่างมาครบแล้ว แต่";
						mes "ก่อนที่เราจะเริ่ม ให้ข้าได้";
						mes "แนะนำตัวก่อน ข้าชื่อ";
						mes "Maia และข้ามีชีวิตอยู่มานานกว่าสามร้อยปีแล้ว";
						next;
						mes "[Maia]";
						mes "โดยไม่เปิดเผยรายละเอียดมากจนเกินไป";
						mes "ข้าได้รับมอบหมายหน้าที่อันศักดิ์สิทธิ์ในการค้นหา";
						mes "และคัดเลือก Soul Linker เพิ่มเติม นั่นคือเหตุผลส่วนหนึ่ง";
						mes "ที่ข้ายังคง... ท่านก็รู้... ยังไม่จากโลกนี้ไป";
						next;
						mes "[Maia]";
						mes "อย่างไรก็ตาม ข้ายังต้องเตรียมการ";
						mes "ด้วยวัตถุดิบที่ท่านเพิ่งนำมาให้";
						mes "ดังนั้นช่วยกลับมาในอีกสักครู่ได้ไหม?";
						mes "แล้วเราจะคุยกันอีกครั้ง";
						close;
					}
					mes "[Kid]";
					mes "หืม...?";
					mes "นี่ ท่านลืมของ";
					mes "บางอย่างนะ คราวนี้";
					mes "จงกลับไปและนำ";
					mes "ทุกอย่างที่ข้าขอมาให้";
					mes "ครบถ้วนล่ะ ตกลงไหม?";
					emotion ET_HNG;
					next;
					mes "[Kid]";
					mes "ข้ารู้ว่าข้าเพิ่งบอกท่านไปว่า";
					mes "เราต้องการอะไร แต่ข้าจะ";
					mes "เตือนท่านอีกครั้งหนึ่ง:";
					mes "^0000FF1 3 Carat Diamond^000000,";
					mes "^0000FF1 Immortal Heart^000000 และ";
					mes "^0000FF1 Witherless Rose^000000";
					close;
				}
				mes "[Kid]";
				mes "อืม ไม่เป็นไร";
				mes "แม้ว่าข้าจะมีเวลาเหลือเฟือ";
				mes "ในโลกนี้ แต่ข้าก็";
				mes "ไม่ชอบรออะไรนานๆ หรอกนะ";
				close;
			}
			set SOUL_Q,0;
			mes "[Kid]";
			mes "ท่านกลายเป็นนักรบแห่ง";
			mes "ดวงอาทิตย์ ดวงจันทร์ และ";
			mes "ดวงดาวไปแล้วแทนงั้นเหรอ? ข้าไม่";
			mes "รู้เลยว่าท่านจะมีศักยภาพด้านนั้น";
			mes "ข้าเดาว่าคงจะโทษท่านไม่ได้ล่ะนะ...";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Maia]";
				mes "ท่านยังมีแต้มทักษะ";
				mes "ที่ยังไม่ได้ใช้งานเหลืออยู่";
				mes "จงใช้มันให้หมดเพื่อเรียนรู้ทักษะ";
				mes "Taekwon Do แล้วจึงค่อย";
				mes "กลับมาเมื่อท่านพร้อม";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "ในขณะนี้ มีคนอื่นกำลัง";
				mes "ทำพิธีเพื่อเข้าสู่การเป็น";
				mes "Soul Linker อยู่";
				mes "โปรดรอจนกว่าพิธีจะ";
				mes "เสร็จสิ้น แล้วเมื่อข้า";
				mes "ว่างแล้ว ข้าจะมาดูแลท่าน";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "ยอดเยี่ยม ข้าเตรียมการ";
			mes "เสร็จสิ้นแล้ว คราวนี้";
			mes "เราจะดำเนินพิธีเพื่อ";
			mes "เปลี่ยนท่านให้กลายเป็น";
			mes "Soul Linker";
			mes "คราวนี้จงหลับตาลง...";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[Maia]";
			mes "ท่านพร้อมที่จะเข้าสู่";
			mes "ส่วนลึกในจิตใจของท่าน";
			mes "อีกครั้งหรือยัง?";
			next;
			if (select("ไม่:ใช่") == 1) {
				mes "[Maia]";
				mes "ถ้าอย่างนั้น ก็จง";
				mes "กลับมาหาข้าเมื่อท่าน";
				mes "คิดว่าตัวเองพร้อมแล้ว";
				mes "ข้าจะรออยู่ตรงนี้";
				mes "จนกว่าจะถึงตอนนั้น";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "ในขณะนี้ มีคนอื่นกำลัง";
				mes "ทำพิธีเพื่อเข้าสู่การเป็น";
				mes "Soul Linker อยู่";
				mes "โปรดรอจนกว่าพิธีจะ";
				mes "เสร็จสิ้น แล้วเมื่อข้า";
				mes "ว่างแล้ว ข้าจะมาดูแลท่าน";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "ตกลง ถ้าอย่างนั้นจงหลับตา";
			mes "และทำใจให้สบาย";
			mes "เราจะกลับเข้าไปสู่";
			mes "ส่วนลึกในจิตใจของท่าน";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch_:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			mes "[Maia]";
			mes "หืม? ท่านมาที่นี่ได้";
			mes "อย่างไร? ท่านยังไม่มีคุณสมบัติ";
			mes "สำหรับพิธีนี้ มาเถอะ ข้าจะส่งท่านกลับไปยัง Morocc...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Maia]";
			mes "ท่านจำที่นี่ได้ไหม? ตอนนี้เรา";
			mes "อยู่ในจิตใจของท่าน วิญญาณ";
			mes "ของเหล่านักรบที่ล่วงลับไปแล้ว";
			mes "วนเวียนอยู่ที่นี่ เพื่อรอให้ท่าน";
			mes "เรียกใช้พลังของพวกเขา";
			next;
			mes "[Maia]";
			mes "ตอนนี้มีพวกเขาอยู่เพียงไม่กี่ตน";
			mes "ที่นี่ แต่ถ้าท่านยังคงฝึกฝน";
			mes "ต่อไป ท่านจะสามารถ";
			mes "เรียกวิญญาณได้มากขึ้น";
			mes "ในฐานะ Soul Linker";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Maia]";
			mes "เราสามารถอยู่ในจิตใจ";
			mes "ของท่านได้เพียง 3 นาที";
			mes "ข้าแนะนำให้ท่านลองพูดคุย";
			mes "กับเหล่าวิญญาณในขณะที่";
			mes "ท่านมีโอกาส";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[Maia]";
			mes "จงฟังในสิ่งที่";
			mes "เหล่าวิญญาณกำลังจะบอก";
			mes "มันมีเหตุผลว่าทำไม";
			mes "พวกเขาถึงไม่สามารถไปสู่";
			mes "โลกหน้าได้";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[Maia]";
			mes "ข้าเชื่อว่าตอนนี้ท่าน";
			mes "พร้อมแล้วที่จะกลายเป็น";
			mes "Soul Linker อย่างไรก็ตาม";
			mes "ท่านสามารถพูดคุยกับ";
			mes "เหล่าวิญญาณต่อไปได้";
			mes "หากนั่นคือสิ่งที่ท่านปรารถนา";
			next;
			if (select("สนทนากับเหล่าวิญญาณต่อ:กลายเป็น Soul Linker") == 1) {
				mes "[Maia]";
				mes "ตกลง พยายามรีบหน่อยนะ";
				mes "เพราะเราสามารถอยู่ใน";
				mes "จิตใจของท่านได้จำกัด";
				mes "เวลา แต่เรายังสามารถ";
				mes "กลับเข้ามาใหม่ได้ในภายหลัง";
				mes "ถ้าท่านมาคุยกับข้าอีกครั้ง...";
				close;
			}
			if (ismounting()) {
				mes "[Maia]";
				mes "ท่านกำลังขี่สัตว์เลี้ยงอยู่";
				mes "จึงไม่สามารถเปลี่ยนอาชีพได้";
				mes "โปรดนำสัตว์เลี้ยงออกก่อนแล้วลองใหม่อีกครั้ง!";
				close;
			}
			mes "[Maia]";
			mes "ถ้าอย่างนั้นเรามาเริ่ม";
			mes "พิธีกันเถอะ ไอเทมเหล่านี้จะ";
			mes "ถูกใช้เพื่อมอบความสามารถใน";
			mes "การขอยืมพลังของนักรบผู้ล่วงลับ";
			mes "และส่งต่อมันให้แก่เพื่อนพ้อง";
			mes "ของท่านในการต่อสู้";
			next;
			mes "[Maia]";
			mes "Witherless Rose นี้จะ";
			mes "เหี่ยวเฉาไปแทนตัวท่าน...";
			specialeffect EF_MAPPILLAR2,AREA,"Maia#link2";
			next;
			mes "[Maia]";
			mes "Witherless Rose นี้จะ";
			mes "เหี่ยวเฉาไปแทนตัวท่าน...";
			mes "Immortal Heart นี้จะหยุด";
			mes "สูบฉีดเลือด แทนที่หัวใจของท่าน";
			next;
			mes "[Maia]";
			mes "Witherless Rose นี้จะ";
			mes "เหี่ยวเฉาไปแทนตัวท่าน...";
			mes "Immortal Heart นี้จะหยุด";
			mes "สูบฉีดเลือด แทนที่หัวใจของท่าน Diamond นี้จะกลายเป็นผุยผง";
			mes "แทนที่ร่างมนุษย์ของท่าน";
			next;
			mes "[Maia]";
			mes "เหล่าผู้ล่วงลับที่ปรารถนา";
			mes "จะต่อสู้ต่อไป...";
			mes "จะต่อสู้เพื่อท่าน! จงใช้";
			mes "พลังของ Soul Linker อย่าง";
			mes "ชาญฉลาดและเพื่อเป้าหมายที่เที่ยงธรรม";
			next;
			if (SkillPoint) {
				mes "^0000ffท่านยังมีแต้มทักษะที่ยังไม่ได้ใช้ โปรดใช้แต้มทักษะที่เหลือทั้งหมดแล้วลองใหม่อีกครั้ง!^000000";
				close;
			}
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";	// clears all job variables for the current player
			set SOUL_Q,0;
			mes "[Maia]";
			mes "ข้าขอให้ท่านโชคดี";
			mes "ในชีวิตใหม่ จงอยู่ท่ามกลาง";
			mes "เหล่าพันธมิตร แล้วเหล่า";
			mes "วิญญาณจะสามารถปกป้อง";
			mes "และช่วยท่านต่อสู้ได้ ลาก่อนเพื่อนเอ๋ย";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
		mes "[Maia]";
		mes "หืม...?";
		mes "เวลาที่ท่านควรจะ";
		mes "มาอยู่ที่นี่ ยังมาไม่";
		mes "ถึง กลับไปที่";
		mes "Morocc กันเถอะ...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "ถึงเวลาแล้วที่ท่านจะ";
		mes "ออกไปผจญภัยในโลกกว้าง!";
		mes "เราต้องการ Soul Linker เพิ่มขึ้น";
		mes "อย่างแน่นอนในการต่อสู้กับความชั่วร้ายที่ยังดำเนินอยู่...";
	}
	else {
		mes "[Maia]";
		mes "แปลกจัง...";
		mes "ท่านไม่ควรจะมา";
		mes "อยู่ที่นี่ ให้ข้านำทาง";
		mes "ท่านกลับไปยัง Morocc นะ...";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	716

job_soul,30,35,6	script	Monk Spirit#link4	827,{
	if (SOUL_Q == 2) {
		mes "[Monk Spirit]";
		mes "ข้าเป็นใครกันนะ...?";
		mes "ข้าคิดว่า... ข้าคิดว่า";
		mes "มันจะดีที่สุดถ้าเจ้า";
		mes "ไปคุยกับ Maia ก่อน...";
		mes "ข้าเป็นใครและคืออะไรนั้นต้อง";
		mes "ใช้คำอธิบายที่ซับซ้อนทีเดียว...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Monk Spirit]";
		mes "เมื่อครั้งมีชีวิต เพื่อนพ้อง";
		mes "พยายามอย่างเต็มที่เพื่อให้ข้า";
		mes "บรรลุทุกสิ่งที่ข้าสามารถ";
		mes "ทำได้ในฐานะ Monk แต่ถึงอย่างนั้น...";
		mes "ข้าก็ไม่เคยรู้สึกพอใจ";
		mes "กับทักษะของข้าเลย";
		next;
		mes "[Monk Spirit]";
		mes "ในความตาย ข้ามีความเสียดายมากมาย";
		mes "ที่ไม่เคยมีโอกาสได้ถ่ายทอด";
		mes "ทักษะให้แก่เหล่า Monk รุ่นหลัง";
		mes "การมอบพลังของข้าให้แก่ผู้อื่น";
		mes "เป็นเพียงโอกาสเดียวที่ข้า";
		mes "จะสามารถทำสิ่งนี้ได้";
		next;
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		mes "[Monk Spirit]";
		mes "ข้าขอร้องเจ้า...";
		mes "ข้าต้องการให้เจ้าช่วย";
		mes "ข้าดึงศักยภาพที่แท้จริง";
		mes "ของเหล่า Monk";
		mes "ในยุคปัจจุบันออกมา";
		close;
	}
	mes "[Monk Spirit]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Sage Spirit#link5	754,{
	if (SOUL_Q == 2) {
		mes "[Sage Spirit]";
		mes "ไปคุยกับ Maia สิ";
		mes "ข้าเกรงว่าข้าอาจจะทำให้";
		mes "เจ้าสับสนถ้า Maia";
		mes "ยังไม่ได้อธิบายสถานการณ์";
		mes "ปัจจุบันของเจ้าให้ฟังเสียก่อน...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Sage Spirit]";
		mes "การแสวงหาความรู้ของข้า";
		mes "มอบพลังอันมหาศาลให้แก่ข้า:";
		mes "เมื่อครั้งมีชีวิต ข้าทำลายล้าง";
		mes "ทุกสิ่งที่ข้าต้องการได้ มี Sage";
		mes "เพียงไม่กี่คนที่จะเข้าถึงระดับข้าได้...";
		next;
		mes "[Sage Spirit]";
		mes "ข้าตายไปแล้ว แต่ข้าไม่เคย";
		mes "สามารถไปสู่โลกหน้าได้";
		mes "ข้ายังต้องการใช้ความสามารถ ข้าต้องการใช้ความรู้ของข้า";
		mes "เพื่อสร้างในสิ่งที่ข้าพึงใจ";
		mes "และทำลายในสิ่งที่ข้าต้องการ";
		next;
		mes "[Sage Spirit]";
		mes "มันเพียงพอแล้วถ้าข้าได้";
		mes "มอบพลังให้แก่ Sage";
		mes "ที่มีค่าพอจะได้รับมัน";
		mes "แต่การจะทำเช่นนั้น ข้าคง";
		mes "ต้องขอความช่วยเหลือจากเจ้า";
		mes "ข้าขอร้องล่ะ ให้ข้าเป็นวิญญาณพันธมิตรของเจ้าเถอะ";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Sage Spirit]";
		mes "ข้าเชื่อว่าเจ้าเป็น";
		mes "คนเดียวที่มีโอกาส";
		mes "ที่จะมอบการพักผ่อนให้แก่";
		mes "ดวงวิญญาณที่ทุกข์ทนของข้า...";
		close;
	}
	mes "[Sage Spirit]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Alchemist Spirit#link7	744,{
	if (SOUL_Q == 2) {
		mes "[Alchemist Spirit]";
		mes "โอ้! ข้าอยากจะคุยกับเจ้า";
		mes "จริงๆ นะ แต่สิ่งที่ข้า";
		mes "จะพูดมันคงไม่ค่อยเมคเซนส์";
		mes "เท่าไหร่ นอกจากเจ้าจะไปคุย";
		mes "กับ Maia ก่อน แต่ใช่แล้ว";
		mes "ข้าต้องการความช่วยเหลือจากเจ้าจริงๆ";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Alchemist Spirit]";
		mes "ไม่ได้โม้นะ ข้านี่แหละคือ";
		mes "Alchemist ที่รวดเร็วที่สุด";
		mes "ในยุคของข้า อันที่จริง ข้าอาจ";
		mes "เป็น Alchemist ที่ไวที่สุดตลอดกาลเลย";
		mes "แต่แล้วข้าก็ยโสเกินไป และ";
		mes "ทำตัวเองตายในอุบัติเหตุ";
		next;
		mes "[Alchemist Spirit]";
		mes "แต่ความตายไม่อาจหยุดยั้ง";
		mes "ทักษะของข้าได้ อันที่จริง ข้า";
		mes "ยังพัฒนาทักษะขึ้นไปอีกตั้งแต่";
		mes "ข้าตายไป ข้าไม่สามารถ";
		mes "ไปสู่โลกหน้าได้จนกว่าข้าจะได้";
		mes "ถ่ายทอดเทคนิคของข้าออกไป...";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Alchemist Spirit]";
		mes "ในฐานะวิญญาณ ข้าไม่มีพลัง";
		mes "แต่ด้วยความช่วยเหลือของเจ้า ข้า";
		mes "สามารถส่งผลต่อเหล่า Alchemist";
		mes "ในปัจจุบันและช่วยพวกเขาขัดเกลา";
		mes "ทักษะได้ ข้าขออ้อนวอนต่อเจ้า";
		mes "ได้โปรดให้โอกาสนี้แก่ข้าด้วยเถิด...";
		close;
	}
	mes "[Alchemist Spirit]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	111,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	stopnpctimer;
}

sec_in02,35,153,0	script	Soul Linker Var	871,{
	callfunc "F_GM_NPC";
	mes "[Soul Linker Var]";
	mes "ข้าสามารถรีเซ็ตค่าของ NPC";
	mes "Soul Linker ได้ หากผู้สมัคร";
	mes "เปลี่ยนอาชีพพบปัญหาในระหว่าง";
	mes "ช่วงท้ายของเควสเปลี่ยนอาชีพ โปรด";
	mes "อย่าใช้ฟังก์ชันนี้หากผู้เล่นยังอยู่ในแผนที่เควส";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Soul Linker Var]";
		mes "รหัสผ่าน";
		mes "ไม่ถูกต้อง";
		close;
	} else {
		mes "[Soul Linker Var]";
		mes "ท่านต้องการที่";
		mes "จะรีเซ็ตค่า Global Variable";
		mes "ของ Soul Linker หรือไม่?";
		next;
		switch(select("รีเซ็ต:ยกเลิก")) {
		case 1:
			mes "[Soul Linker Var]";
			mes "NPC สำหรับเควส";
			mes "เปลี่ยนอาชีพ Soul Linker";
			mes "ได้รับการรีเซ็ตแล้ว";
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			close;
		case 2:
			mes "[Soul Linker Var]";
			mes "ท่านได้ยกเลิก";
			mes "คำสั่งนี้";
			close;
		}
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Soul Linker Job Changer based on the kRO quest.
//= Quest info from RagnaInfo. Sprites from kRO screenshots
//= 1.0 Optimized and updated [Lupus]
//= 1.1 Fixed NPC names according to iRO [Lupus]
//============================================================ 
