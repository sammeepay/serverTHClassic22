//===== rAthena Script =======================================
//= Taekwon Job Quest
//===== Description: =========================================
//= [Official Conversion]
//= Taekwon Job Change Quest
//===== Changelogs: ==========================================
//= 1.0 Replaced previous TK Job Quest with this one [Tsuyuki]
//= 1.1 Rewrote to the Aegis Standars. [Samuray22]
//= 1.2 Fixed position the npc is facing. [Kisuka]
//= 1.3 Added Quest Log commands. [Kisuka]
//= 1.4 Removed use of 'goto', and fixed some indentation. [L0ne_W0lf]
//= 1.5 Split into renewal [Lemongrass]
//============================================================

-	script	Phoenix::PhoenixTKJobChange	-1,{
	if(Upper == 2) {
		mes "[Phoenix]";
		mes "สวัสดี, เด็กน้อย";
		close;
	} else if(Class == Job_Taekwon) {
		mes "[Phoenix]";
		mes "การฝึกฝนของเจ้า";
		mes "เป็นอย่างไรบ้าง? ในขณะที่";
		mes "เทคนิคของเจ้าเริ่มขัดเกลา";
		mes "จนดูประณีตหรือน่าทึ่งขึ้น";
		mes "จงอย่าลืมว่าเจ้าสามารถ";
		mes "พึ่งพาพื้นฐานได้เสมอ";
		close;
	} else if(Class > Job_Novice) || (Class == Job_Novice && TK_Q == 0) {
		mes "[Phoenix]";
		mes "ดินแดนแห่งนี้ โลกที่ครั้งหนึ่ง";
		mes "เคยสวยงามของเราถูกแปดเปื้อน";
		mes "ด้วยความชั่วร้าย: มีผู้คน";
		mes "มากมายที่ถูกความมืดมิดกัดกิน";
		mes "มีมอนสเตอร์มากเกินไปที่";
		mes "คอยคุกคามผู้บริสุทธิ์...";
		next;
		mes "[Phoenix]";
		mes "ความวุ่นวายที่ครอบงำโลกนี้";
		mes "มันมากเกินกว่าที่มนุษย์ธรรมดา";
		mes "จะรับไหว ซึ่งไม่สามารถหยัดยืน";
		mes "ต่อสู้ด้วยตนเองท่ามกลางอุปสรรค";
		mes "ที่ถาโถมมาได้ แต่อย่างไรก็ตาม";
		mes "คนเราต้องปรารถนาที่จะสู้กับมัน";
		next;
		mes "[Phoenix]";
		mes "และด้วยเหตุนี้ ข้าจึงอุทิศ";
		mes "ตนเองเพื่อความแข็งแกร่ง";
		mes "ข้าฝึกฝนเพื่อบรรลุถึง";
		mes "การหยั่งรู้ พัฒนาศาสตร์การเกลาจิตใจและร่างกาย";
		mes "ซึ่งข้าปรารถนาจะแบ่งปันให้กับโลกใบนี้";
		next;
		mes "[Phoenix]";
		mes "ข้าอาจจะไม่สามารถเปลี่ยน";
		mes "โลกใบนี้ได้ด้วยตัวคนเดียว แต่ข้า";
		mes "จะไม่มีวันหยุดฝึกฝนตนเอง";
		mes "ทั้งทางจิตวิญญาณและร่างกาย";
		mes "ข้าเชื่อว่าคำตอบนั้น";
		mes "จะปรากฏออกมาในไม่ช้า...";
		next;
		if (Class != Job_Novice) {
			mes "[Phoenix]";
			mes "นักผจญภัยผู้สูงส่ง:";
			mes "หากเจ้ารู้จักใครก็ตามที่";
			mes "ยังไม่ได้เลือกเส้นทางชีวิต";
			mes "ของเขา โปรดแนะนำเขา";
			mes "มาหาข้า หากเขาสนใจ";
			mes "ข้าอาจจะสอนศาสตร์ของข้าให้...";
			close;
		}
		mes "[Phoenix]";
		mes "หากเจ้ายังไม่ได้ตัดสินใจเลือก";
		mes "เส้นทางที่เจ้าปรารถนาจะเดิน";
		mes "ข้าอยากให้เจ้าพิจารณา";
		mes "การเป็นผู้สืบทอดศาสตร์ของข้า";
		mes "มันอาจจะไม่ใช่เรื่องง่าย แต่มันจะนำเจ้าไปสู่ความแข็งแกร่งที่ยิ่งใหญ่...";
		next;
		if(select("ตกลง ฉันจะเข้าร่วมกับท่าน.","ไม่ล่ะ ขอบคุณ.") == 1) {
			if(!callfunc("F_CanChangeJob")) {
				mes "[Phoenix]";
				mes "น่าเสียดายยิ่งนัก!";
				mes "เจ้ายังไม่พร้อมที่จะ";
				mes "เริ่มการฝึกฝนภายใต้";
				mes "การสั่งสอนของข้าด้วย";
				mes "Job Level ปัจจุบันของเจ้า โปรดกลับมาเมื่อเจ้ามี Job Level 9 หรือสูงกว่า";
				close;
			}
			mes "[Phoenix]";
			mes "ดีมาก... ข้ายอมรับเจ้า";
			mes "ในฐานะลูกศิษย์ ในการเริ่ม";
			mes "ฝึกฝนนั้น ร่างกายของเจ้า";
			mes "จะต้องได้รับการปรับสภาพก่อน";
			mes "เพื่อให้สามารถแสดงทักษะ";
			mes "ที่เจ้าจะได้เรียนรู้ต่อไป";
			next;
			if(BaseLevel > 19) {
				mes "[Phoenix]";
				mes "หืม ข้าเห็นว่าเจ้าผ่าน";
				mes "การฝึกฝนทางกายภาพในฐานะ";
				mes "Novice มาเพียงพอแล้ว ดีมาก ถ้าอย่างนั้นเรามาเตรียมตัวสำหรับการฝึก";
				mes "ทางจิตวิญญาณกัน หายใจเข้า";
				mes "ลึกๆ แล้วบอกข้าเมื่อเจ้าพร้อม";
				set TK_Q, 2;
				setquest 6001;
				close;
			}
			mes "[Phoenix]";
			mes "จิตใจไม่ได้ถูกจำกัดอยู่แค่";
			mes "ขอบเขตของร่างกาย แต่เจ้าจะไม่มีวันดึงศักยภาพที่แท้จริง";
			mes "ออกมาได้หากปราศจากการรวมจิตใจและร่างกายเข้าด้วยกัน ไปเถอะ ไปเพิ่ม ^FF0000Base Level อีก 1 เลเวล^000000 แล้วค่อยกลับมา";
			next;
			set TAEK_Q, BaseLevel;
			set TK_Q, 1;
			setquest 6000;
			mes "[Phoenix]";
			mes "ข้าเข้าใจว่านี่ไม่ใช่เรื่อง";
			mes "ง่ายสำหรับ Novice แต่เจ้า";
			mes "ต้องเตรียมใจรับความยากลำบาก";
			mes "สำหรับอาชีพนี้ ข้าคาดหวัง";
			mes "จะเห็นเจ้าแข็งแกร่งขึ้น";
			mes "ในครั้งหน้าที่เราพบกัน";
			close;
		}
		mes "[Phoenix]";
		mes "ข้าเข้าใจ ชีวิตของคนเรา";
		mes "ก้าวเดินได้หลายเส้นทาง แต่เจ้า";
		mes "สามารถเลือกเดินได้เพียงเส้นทาง";
		mes "เดียวในแต่ละครั้ง ข้าหวังว่าเจ้าจะมุ่งสู่";
		mes "การหยั่งรู้ในแบบฉบับของเจ้า";
		mes "เองนะ นักผจญภัย";
		close;
	}
	switch(TK_Q) {
	case 1:
		if(BaseLevel > TAEK_Q) {
			set TK_Q, 2;
			changequest 6000,6001;
			mes "[Phoenix]";
			mes "ดีมาก ข้าสัมผัสได้ว่าเจ้า";
			mes "เข้าถึงพลังภายในของเจ้า";
			mes "ได้มากขึ้น นั่นคือผลลัพธ์";
			mes "ตามธรรมชาติของการเลเวลอัป";
			mes "เราพร้อมที่จะเข้าสู่ขั้นตอน";
			mes "ถัดไปของการฝึกฝนแล้ว";
			close;
		}
		mes "[Phoenix]";
		mes "เจ้าต้องเพิ่ม ^FF0000Base Level อีก 1 เลเวล^000000 เพื่อพิสูจน์ว่า";
		mes "เจ้าสามารถอดทนต่อความยากลำบาก";
		mes "ที่อาชีพนี้ต้องเผชิญ อย่าได้";
		mes "ละเลยการฝึกฝนของเจ้าเด็ดขาด";
		close;
	case 2:
		mes "[Phoenix]";
		mes "สำหรับการฝึกทางจิตวิญญาณ";
		mes "ข้าจะถามคำถามชุดหนึ่ง";
		mes "เพื่อทดสอบจิตวิญญาณของเจ้า";
		mes "ทำตัวตามสบาย ตอบให้ตรง";
		mes "กับใจที่สุดเท่าที่เจ้าจะทำได้ ปณิธาน";
		mes "และความเชื่อมั่นของเจ้าจะถูกทดสอบ";
		next;
		mes "[Phoenix]";
		mes "ในฐานะผู้สืบทอดศาสตร์";
		mes "ของข้า ความสามารถในการ";
		mes "ตัดสินใจที่ยอดเยี่ยมอย่างรวดเร็ว";
		mes "เป็นสิ่งจำเป็นในการต่อสู้ เอาล่ะ";
		mes "เราจะเริ่มการตั้งคำถามกัน";
		next;
		mes "[Phoenix]";
		mes "เมื่อเจ้าต้องเผชิญกับ";
		mes "อุปสรรคที่ยากลำบาก โดยทั่วไป";
		mes "เจ้าจะตอบโต้อย่างไร?";
		next;
		switch(select( "ฉันจะเผชิญหน้ากับมันตรงๆ.:พยายามหลีกเลี่ยงมันด้วยวิธีใดวิธีหนึ่ง.:รวบรวมกำลังใหม่และวิเคราะห์ปัญหา.")) {
		case 1:
			mes "[Phoenix]";
			mes "ใช่ นั่นคือคำตอบที่ข้า";
			mes "ต้องการ ถึงแม้เจ้าจะยังไม่สามารถ";
			mes "รับมือกับปัญหาได้ในตอนแรก แต่ความ";
			mes "มุ่งมั่นที่แรงกล้าเช่นนั้นย่อมส่งผลดี";
			mes "อย่าให้อุปสรรคใดๆ มาหยุดเจ้าได้";
			next;
			mes "[Phoenix]";
			mes "แม้ว่าเจ้าจะล้มเหลว เจ้า";
			mes "ก็จะได้เรียนรู้จากประสบการณ์";
			mes "เมื่อเจ้าทุ่มเทจนสุดตัว การ";
			mes "พยายามแบบครึ่งๆ กลางๆ";
			mes "มักจะไม่ส่งผลลัพธ์ที่";
			mes "งอกเงยเท่าไหร่นัก";
			next;
			mes "[Phoenix]";
			mes "ในเมื่อเจ้าเข้าใจถึงความ";
			mes "สำคัญของปณิธานแล้ว เราจะ";
			mes "เข้าสู่คำถามข้อถัดไป";
			next;
			break;
		case 2:
			mes "[Phoenix]";
			mes "เจ้าคนเขลา! เจ้าหวังจะ";
			mes "เติบโตได้อย่างไรถ้าเจ้าเอาแต่";
			mes "วิ่งหนีความท้าทาย? ความกลัวอาจ";
			mes "เป็นปฏิกิริยาที่ช่วยรักษาชีวิต";
			mes "แต่นิสัยขี้ขลาดที่แท้จริงนั้น";
			mes "เป็นสิ่งที่น่ารังเกียจ";
			next;
			mes "[Phoenix]";
			mes "ข้าผิดหวังที่ได้ยินเจ้า";
			mes "พูดเช่นนั้น อย่าได้พูดสิ่งที่";
			mes "เหมือนกันนี้กับข้าอีก หึ ไปใคร่ครวญ";
			mes "ความหมายของความกล้าและความขี้ขลาดมาใหม่";
			mes "แล้วค่อยมาคุยกับข้าอีกครั้ง";
			close;
		case 3:
			mes "[Phoenix]";
			mes "รวบรวมกำลังใหม่รึ? มันเป็นเรื่องดีที่ต้องทำ";
			mes "หลังจากที่เจ้าพ่ายแพ้มาแล้ว";
			mes "แต่มันจะดีที่สุดหากเผชิญหน้ากับปัญหา";
			mes "ทันทีที่เจ้าพบเจอพวกมัน";
			mes "เจ้าจะไม่ได้มีโอกาสได้รับความ";
			mes "สะดวกในการรวบรวมกำลังเสมอไปหรอก";
			next;
			mes "[Phoenix]";
			mes "ปัญหาเป็นสิ่งที่คาดเดา";
			mes "และวิเคราะห์ได้ แต่ข้าคิดว่า";
			mes "การถอยหนีทันทีนั้นไม่ฉลาดนัก";
			mes "จงไปใคร่ครวญถึงความกลัวของเจ้า";
			mes "รวมถึงสิ่งที่เจ้าเรียกว่าความ";
			mes "ล้มเหลว แล้วค่อยกลับมาหาข้า";
			close;
		}
		mes "[Phoenix]";
		mes "ในการเดินทางของเจ้า เจ้าจะได้";
		mes "พบปะผู้คนมากมายที่มีพื้นฐาน";
		mes "และมุมมองที่แตกต่างกัน ซึ่งเลี่ยงไม่ได้";
		mes "ที่เจ้าจะต้องพบกับใครบางคนที่มี";
		mes "วิถีชีวิตที่เจ้าไม่สามารถทำความเข้าใจได้";
		next;
		mes "[Phoenix]";
		mes "ในทำนองเดียวกัน คนผู้นั้น";
		mes "ก็จะไม่เข้าใจวิถีชีวิตของเจ้า";
		mes "เมื่อมุมมองของเจ้าทั้งสองคน";
		mes "ขัดแย้งกัน จนเกิดการโต้เถียงที่รุนแรง";
		mes "เจ้าจะตอบโต้อย่างไร?";
		next;
		switch( select("ยืนกรานว่าฉันถูก โดยไม่สนอะไรทั้งสิ้น:ไม่สนใจมุมมองที่ขัดแย้งนั้น:ยอมรับความต่างและเรียนรู้จากมัน")) {
		case 1:
			mes "[Phoenix]";
			mes "มันเป็นเรื่องสำคัญที่ต้องมี";
			mes "ความคิดเห็นเป็นของตนเอง อย่างไรก็ตาม";
			mes "เจ้าต้องยอมรับว่าเจ้าอาจจะเป็น";
			mes "ฝ่ายที่ผิด และมุมมองที่แตกต่าง";
			mes "อาจจะมีข้อดีแฝงอยู่บ้าง";
			next;
			mes "[Phoenix]";
			mes "มันไม่มีคำตอบที่ถูกต้อง";
			mes "เพียงหนึ่งเดียว และแสงแห่ง";
			mes "ความจริงย่อมมีได้หลายเฉดสี";
			mes "นั่นคือวิถีของธรรมชาติ การ";
			mes "ยัดเยียดความคิดให้ผู้อื่นคือ";
			mes "การกระทำที่กดขี่";
			next;
			mes "[Phoenix]";
			mes "อย่าจำกัดตัวเองอยู่เพียง";
			mes "มุมมองเดียว และอย่าขัดขวาง";
			mes "การเติบโตของเจ้าด้วยการยึดติด";
			mes "อยู่กับความจริงเพียงหนึ่งเดียว";
			mes "จงไปใคร่ครวญเรื่องนี้ แล้วค่อย";
			mes "กลับมาคุยกับข้าอีกครั้ง";
			close;
		case 2:
			mes "[Phoenix]";
			mes "การเข้ากับผู้อื่นให้ได้เป็น";
			mes "เรื่องสำคัญ แต่เจ้าจะไม่มี";
			mes "คุณค่าใดๆ ต่อโลกใบนี้เลย";
			mes "หากปราศจากการอุทิศตน";
			mes "ความคิด และความเห็นที่เป็น";
			mes "เอกลักษณ์เฉพาะตัวของเจ้า";
			next;
			mes "[Phoenix]";
			mes "ความขัดแย้งทางอุดมการณ์ หาก";
			mes "กระทำด้วยความเคารพต่อตนเอง";
			mes "และผู้อื่น จะเป็นโอกาสอันดี";
			mes "ที่จะขยายความเข้าใจของเจ้า";
			mes "ต่อโลกในมุมที่คนอื่นมองเห็น";
			next;
			mes "[Phoenix]";
			mes "จงไปทบทวนแนวคิดเรื่องการ";
			mes "สร้างความกลมกลืนกับตนเอง";
			mes "และความกลมกลืนกับผู้อื่น";
			mes "แล้วค่อยกลับมาหาข้า";
			close;
		case 3:
			mes "[Phoenix]";
			mes "ดีมาก เจ้าต้องมองเห็นความ";
			mes "แตกต่างในแบบที่มันเป็นจริงๆ";
			mes "เจ้าต้องยอมรับคำวิจารณ์ต่อ";
			mes "มุมมองของเจ้าด้วยความ";
			mes "สง่างามและพิจารณาอย่างจริงใจ";
			next;
			mes "[Phoenix]";
			mes "มันเป็นไปไม่ได้ที่จะรู้ทุกอย่าง";
			mes "บนโลกใบนี้ และเป็นไปไม่ได้ที่จะ";
			mes "เข้าใจทุกมุมมอง แต่นั่นไม่ได้";
			mes "หมายความว่ามุมมองที่เจ้าไม่";
			mes "เข้าใจนั้นจะไม่มีค่าอะไรเลย";
			next;
			mes "[Phoenix]";
			mes "คนที่มีความเห็นไม่ตรงกับ";
			mes "เจ้า อาจจะมีคำตอบในสิ่งที่";
			mes "เจ้าไม่รู้ และในยามที่เจ้าอ่อนแอ";
			mes "คนผู้นี้อาจจะเป็นผู้ที่ช่วยเหลือเจ้า";
			mes "ได้มากที่สุด จงจำข้อนี้ไว้";
			next;
		}
		mes "[Phoenix]";
		mes "ข้าพอใจกับคำตอบที่เจ้า";
		mes "มอบให้ข้า โปรดนำสิ่งที่เราได้";
		mes "พูดคุยกันไปทบทวนดูสักครู่";
		mes "เมื่อจิตใจของเจ้าสงบนิ่งแล้ว";
		mes "จงกลับมาคุยกับข้า";
		set TK_Q, 3;
		changequest 6001,6002;
		close;
	case 3:
		mes "[Phoenix]";
		mes "เจ้ารู้สึกสงบและเป็นสุข";
		mes "ดีแล้วใช่ไหม? ข้ามีคำถาม";
		mes "ที่สำคัญมากจะถามเจ้า";
		mes "ขอให้ตอบข้าตามตรง";
		next;
		mes "[Phoenix]";
		mes "^FF0000เจ้าพร้อมที่จะอุทิศตน";
		mes "ให้กับศาสตร์พิเศษที่ข้าจะ";
		mes "สอนให้ และรักษาเกียรติแห่ง";
		mes "ปรัชญาของศาสตร์นี้หรือไม่?^000000";
		next;
		if( select( "พร้อมครับ/ค่ะ.","ไม่พร้อมครับ/ค่ะ.") == 1) {
			mes "[Phoenix]";
			mes "ดีมาก เจ้าไม่ใช่เพียงแค่";
			mes "นักเรียนอีกต่อไปแล้ว ตอนนี้";
			mes "เจ้าได้รับมอบอำนาจและความ";
			mes "รับผิดชอบในฐานะสาวกแห่ง ^FF0000Taekwon Do^000000";
			next;
			mes "[Phoenix]";
			mes "''Taekwon'' มีความหมายว่า";
			mes "''การชกและการเตะ'' และ";
			mes "''Do'' มีความหมายว่า ''ศาสตร์''";
			mes "ศิลปะการต่อสู้นี้จะมุ่งเน้นไป";
			mes "ที่ทักษะการใช้หมัดและเท้า";
			next;
			mes "[Phoenix]";
			mes "โปรดใช้ระเบียบวินัยนี้เพื่อ";
			mes "ขัดเกลาร่างกายและจิตใจของเจ้า";
			mes "และเรียนรู้ทักษะที่เหมาะสมกับ";
			mes "เจ้าที่สุด อย่าได้ละทิ้งการฝึกฝน";
			mes "หรือนำความเสื่อมเสียมาสู่ Taekwon Do";
			next;
			completequest 6002;
			callfunc "Job_Change",Job_Taekwon;
			callfunc "F_ClearJobVar";
			getitem 2101,1; // Guard[0]
			mes "[Phoenix]";
			mes "เจ้ายังเยาว์วัยนัก ข้าคิดว่า";
			mes "เจ้าคงอยากได้ชื่อเรียกอาชีพ";
			mes "อืม ในกรณีนี้ เจ้าก็คือ...";
			if(Sex == SEX_FEMALE) {
				mes "^FF0000Taekwon Girl^000000";
			} else {
				mes "^FF0000Taekwon Boy^000000";
			}
			mes "ใช่ ฟังดูดีทีเดียว";
			next;
			mes "[Phoenix]";
			mes "โปรดรับชุดฝึกซ้อมและ";
			mes "เครื่องป้องกันเซตนี้ไป: จงใช้";
			mes "ของขวัญเหล่านี้ให้เกิดประโยชน์";
			mes "ในขณะที่เจ้าเดินทางและฝึกฝน จงสอน";
			mes "ผู้อื่นเกี่ยวกับศาสตร์ของเรา และเรียนรู้จากพวกเขาเป็นการตอบแทน";
			next;
			mes "[Phoenix]";
			mes "ถึงเวลาแล้วที่เจ้าจะต้อง";
			mes "ออกเดินทางด้วยตัวเจ้าเอง";
			mes "เพื่อค้นหาความท้าทายใหม่ๆ";
			mes "ในการพัฒนาความแข็งแกร่ง";
			mes "จงก้าวเดินด้วยความภาคภูมิใจ";
			mes "ในฐานะผู้สืบทอด Taekwon Do...";
			next;
			mes "[Phoenix]";
			mes "ดีมาก ข้าขอให้เจ้าโชคดี";
			mes "ข้าหวังจะได้พบเจ้าอีกใน";
			mes "โอกาสหน้า, "+strcharinfo(0)+".";
			close;
		}
		mes "[Phoenix]";
		mes "หืม บางทีเจ้าอาจจะยัง";
		mes "ไม่พร้อมที่จะเลื่อนขั้นจาก";
		mes "สถานะนักเรียนไปเป็นสาวก";
		mes "อย่างเต็มตัว เมื่อเจ้าพร้อมแล้ว";
		mes "จงกลับมาคุยกับข้า";
		close;
	}
}

