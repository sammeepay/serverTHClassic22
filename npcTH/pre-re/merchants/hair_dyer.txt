//===== rAthena Script ======================================= 
//= Hair Dyer
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Hair Dyer with standard palletes
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Bugfix: fixed missing menu label LEnd->M_End [Lupus]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.3 Fixed problem what "freezes" the NPC. (bugreport:509) [Samuray22]
//= 1.4 Fixed getlook checking the wrong color. (bugreport:2392) [L0ne_W0lf]
//= 1.5 Added Lighthalzen Hair Dying NPC. [L0ne_W0lf]
//= 1.6 Moving NPC Jovovich to Pre-Renewal [JohnnyPlayy]
//============================================================ 

prt_in,243,168,4	script	Jovovich	91,{
	mes "[Hairdresser Jovovich]";
	mes "ยินดีต้อนรับค่า~!";
	mes "มีอะไรให้ฉันช่วยไหมคะ?";
	next;
	mes "[Hairdresser Jovovich]";
	if (Sex == SEX_MALE) mes "โอ้ ไม่นะ! ผมของคุณเสียมากเลย ดูเหมือนคุณจะต้องการการบำรุงโดยผู้เชี่ยวชาญแล้วล่ะ มานั่งตรงนี้สิคะ เชิญค่ะ มาเลย";
	else {
		if (rand(20) > 11) {
			mes "เอ๊ะ!? ตายแล้ว! โอ้ ไม่ๆๆๆๆ! ผมของคุณเสียม๊ากมาก! ไม่ดีแน่ถ้าคุณปล่อยผมไว้ในสภาพแบบนี้";
			next;
			mes "[Hairdresser Jovovich]";
			mes "จะยอมให้ฉันช่วยดูแลผมของคุณไหมคะ? ได้โปรดเถอะนะ?";
		}
		else mes "ว้าว! ผมของคุณจะดูสมบูรณ์แบบมากถ้าได้ย้อมสีใหม่สักหน่อย~ สนใจย้อมผมเพื่อเปลี่ยนลุคหน่อยไหมคะ?";
	}
	next;
	while(1) {
		switch(select("ย้อมสีผม:คำแนะนำและข้อมูล:ยกเลิก")) {
		case 1:
			mes "[Hairdresser Jovovich]";
			mes "ใช่แล้วๆ เป็นตัวเลือกที่ดีมากเลยค่า~";
			mes "เอาล่ะ นี่คือโอกาสที่คุณจะได้แปลงโฉม เป็นโอกาสที่จะได้เบ่งบานในความงาม! เลือกสีที่คุณต้องการได้เลยค่ะ";
			next;
			while(1) {
				if (.@choose_success == 1) {
					mes "[Hairdresser Jovovich]";
					mes "คุณคิดว่ายังไงบ้างคะ? อยากได้สีอื่นอีกไหม?";
					next;
					if (select("ใช่:ไม่") == 1) {
						mes "[Hairdresser Jovovich]";
						mes "ตกลงค่ะ! เลือกสีที่คุณต้องการได้เลย";
						next;
					}
					else  {
						mes "[Hairdresser Jovovich]";
						mes "หืม ฉันรู้สึกผิดหวังนิดหน่อยนะเนี่ย จริงๆ ฉันอยากจะทำให้ดีกว่านี้อีก แต่ฉันสัญญาว่าครั้งหน้าจะทำให้ดียิ่งขึ้นไปอีกค่ะ แวะมาใหม่นะคะ~";
						close;
					}
				}

				while(1) {
					switch(select("สีแดงครับ/ค่ะ:สีเหลืองครับ/ค่ะ:สีม่วงครับ/ค่ะ:สีส้มครับ/ค่ะ:สีเขียวครับ/ค่ะ:สีน้ำเงินครับ/ค่ะ:สีขาวครับ/ค่ะ:สีดำครับ/ค่ะ:จริงๆ แล้วฉันชอบสีผมเดิมอยู่แล้วล่ะ")) {
					case 1: set .@headpalette,8; break;
					case 2: set .@headpalette,1; break;
					case 3: set .@headpalette,2; break;
					case 4: set .@headpalette,3; break;
					case 5: set .@headpalette,4; break;
					case 6: set .@headpalette,5; break;
					case 7: set .@headpalette,6; break;
					case 8: set .@headpalette,7; break;
					case 9:
						if (.@choose_success != 0) {
							mes "[Hairdresser Jovovich]";
							mes "คุณคงจะชอบสีผมของคุณมากเลยสินะ~";
							close;
						}
						mes "[Hairdresser Jovovich]";
						mes "เอ๋~? คุณจะไม่ย้อมผมจริงๆ เหรอ? ฉันเสียใจนิดหน่อยนะเนี่ย...";
						close;
					}

					if (.@headpalette == getlook(VAR_HEADPALETTE)) {
						mes "[Hairdresser Jovovich]";
						mes "เอ๊ะ? แต่นั่นมันสีผมที่คุณมีอยู่แล้วนี่นา กรุณาเลือกสีอื่นเถอะค่ะ";
						next;
					}
					else {
						switch(.@headpalette) {
						case 1: callsub S_NoDye,975,"red"; break; // สลับตาม ID ใน Sub
						case 2: callsub S_NoDye,976,"yellow"; break;
						case 3: callsub S_NoDye,978,"violet"; break;
						case 4: callsub S_NoDye,980,"orange"; break;
						case 5: callsub S_NoDye,979,"green"; break;
						case 6: callsub S_NoDye,981,"blue"; break;
						case 7: callsub S_NoDye,982,"white"; break;
						case 8: callsub S_NoDye,983,"black"; break;
						}
						if (Zeny < 1000) {
							mes "[Hairdresser Jovovich]";
							mes "ค่าธรรมเนียมคือ 1000 zeny ค่ะ คุณมีเงินไม่พอเหรอ...?";
							close;
						}

						switch(.@headpalette) {
						case 1: delitem 976,1; break; //Lemon_Dyestuffs
						case 2: delitem 978,1; break; //Violet_Dyestuffs
						case 3: delitem 980,1; break; //Orange_Dyestuffs
						case 4: delitem 979,1; break; //Darkgreen_Dyestuffs
						case 5: delitem 981,1; break; //Cobaltblue_Dyestuffs
						case 6: delitem 982,1; break; //White_Dyestuffs
						case 7: delitem 983,1; break; //Black_Dyestuffs
						case 8: delitem 975,1; break; //Scarlet_Dyestuffs
						}
						set Zeny, Zeny-1000;
						setlook VAR_HEADPALETTE,.@headpalette;
						set .@choose_success,1;
						break;
					}

				}

			}

		case 2:
			mes "[Hairdresser Jovovich]";
			mes "ในเวลาที่คุณรู้สึกแย่, เมื่อคุณถูกทิ้ง, เมื่อคุณต้องการทำให้ใครบางคนประทับใจ หรือแม้แต่เวลาที่คุณแค่ยากจะทำตัวให้โดดเด่น...";
			next;
			mes "[Hairdresser Jovovich]";
			mes "เพื่อสถานที่และเวลาที่พิเศษเหล่านั้น คุณไม่อยากมีทรงผมในแบบที่เป็นตัวคุณเองเหรอคะ? ตราบใดที่คุณมีสีย้อมผมที่เหมาะสม ฉันจะทำให้ผมของคุณดูวิเศษสุดๆ ไปเลยค่ะ";
			next;
			mes "[Hairdresser Jovovich]";
			mes "และพยายามอย่ากังวลเรื่องค่าธรรมเนียมให้มากนักเลย การแสวงหาความงามก็เหมือนกับการแสวงหาทุกสิ่งทุกอย่างนั่นแหละค่ะ ฮี่ๆ~";
			next;
			mes "[Hairdresser Jovovich]";
			mes "* ค่าธรรมเนียมและข้อมูล *";
			mes "- สีย้อมผม (Dyestuffs) 1 ขวด ตามสีที่คุณต้องการ";
			mes "- ค่าธรรมเนียม 1,000 zeny";
			close;

		case 3:
			mes "[Hairdresser Jovovich]";
			mes "ไม่ว่าจะเป็นชายหรือหญิง...";
			mes "ทุกคนต่างมีสิทธิ์และหน้าที่ที่จะต้องทำตัวให้สวย/หล่อค่ะ";
			close;
		}

	}

S_NoDye: // ส่วนตรวจสอบไอเทมย้อมผม
	if (countitem(getarg(0)) == 0) {
		mes "[Hairdresser Jovovich]";
		mes "เอ๊ะ?! แต่คุณต้องมีไอเทม '"+getitemname(getarg(0))+"' เพื่อที่จะย้อมผมสี "+getarg(1)+" นะคะ...";
		close;
	}
	return;
}
