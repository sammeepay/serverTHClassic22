//===== rAthena Script ======================================= 
//= Acolyte Job Quest
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job quest for Acolyte classes
//===== Additional Comments: ================================= 
//= 1.0 Fully working.
//= 1.1 Added function calls for Priest Quest.
//= 1.2 Fixed Marthilda, Yosuke bugs. [Lupus]
//= 1.2a Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= 1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=     No longer uses function "F_ToHigh"
//=     Proper dialog for Priest job quest is unused (for now.)
//= 1.7a Functions now make use of proper priest quest dialog [L0ne_W0lf]
//= 1.8 Removed an Unnecessary "set job_acolyte_q,0;" [Samuray22]
//= 1.8a More bug fixes care of Crashy. [L0ne_W0lf]
//= 1.9 Changed Marthilda to Mathilda. [L0ne_W0llf]
//= 2.0 Fixed High Acolytesnot being given holy light. [L0ne_W0lf]
//= 2.1 Added Quest Log commands. [L0ne_W0lf]
//= 2.2 Fixed an issue with completequest by adding checkquest. [Kisuka]
//============================================================ 

prt_church,184,41,4	script	Cleric#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Father Mareusis]";
				mes "อา... ฉันสัมผัสได้ว่าคุณได้ผ่าน";
				mes "ประสบการณ์จากชีวิตในอดีตมาแล้ว คุณคงได้เรียนรู้อะไรมากมายก่อนที่จะเข้าไปสู่ Valhalla";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Father Mareusis]";
					mes "โชคร้ายหน่อยนะ ฉันคิดว่าคุณยังไม่พร้อมที่จะเป็น Acolyte ในตอนนี้ โปรดไปเรียนรู้ทักษะพื้นฐาน (Basic Skills) ทั้งหมดให้ครบถ้วนก่อน";
					next;
					mes "[Father Mareusis]";
					mes "ในระหว่างนี้";
					mes "ฉันจะรอจนกว่า";
					mes "คุณจะพร้อม";
					mes "ขอให้พระเจ้าสถิต";
					mes "อยู่กับคุณ";
					close;
				}
				mes "[Father Mareusis]";
				mes "เอาล่ะ ฉันขอต้อนรับคุณ";
				mes "กลับมาจาก Valhalla และ";
				mes "ขอให้คุณโชคดีกับการเดินทาง";
				mes "ในชีวิตครั้งใหม่นี้";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Acolyte_High;
				skill "AL_HOLYLIGHT",1,SKILL_PERM;
				mes "[Father Mareusis]";
				mes "ตอนนี้ จงออกเดินทางและตามหาผู้ที่ต้องการความช่วยเหลือจากคุณ ขอให้พระเจ้าส่องสว่างในเส้นทางของคุณ";
				close;
			}
			else {
				mes "[Father Mareusis]";
				mes "ตอนนี้ จงออกเดินทางเพื่อตามหาผู้คนที่ต้องการความช่วยเหลือจากคุณ ขอให้พระเจ้าประทานปัญญาในเส้นทางของคุณ";
				close;
			}
		}
		else {
			mes "[Father Mareusis]";
			mes "ฉันสัมผัสได้ว่าคุณได้ผ่านประสบการณ์จากชีวิตในอดีตมาแล้ว คุณคงได้เรียนรู้อะไรมากมายก่อนที่จะเข้าไปสู่ Valhalla";
			next;
			mes "[Father Mareusis]";
			mes "อย่างไรก็ตาม ฉันบอกได้เลยว่าคุณไม่เหมาะสมที่จะเป็น Acolyte โปรดระลึกให้ได้ว่าคุณเคยเป็นใครในชีวิตที่ผ่านมา และจงตามหาเส้นทางของตนเอง";
			close;
		}
	}
	mes "[Father Mareusis]";
	mes "อะไรคือสิ่งที่คุณกำลังตามหาล่ะ?";
	next;
	switch(select("ท่านบาทหลวงครับ ผมอยากเป็น Acolyte:เงื่อนไขการเป็น Acolyte:แค่เดินดูรอบๆ น่ะครับ")) {
	case 1:
		mes "[Father Mareusis]";
		if (BaseJob == Job_Acolyte) {
			mes "วันนี้คุณรู้สึกสบายดีไหม? ฉันบอกได้จากชุดที่คุณใส่ว่าคุณเป็น Acolyte อยู่แล้วนะ คุณไม่ได้กำลังล้อเล่นอยู่ใช่ไหม?";
			close;
		}
		else if (BaseJob != Job_Novice) {
			mes "ฉันเสียใจด้วยนะ แต่เราสามารถรับเฉพาะ Novice เท่านั้นในฐานะผู้สมัครเพื่อเปลี่ยนอาชีพเป็น Acolyte";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "คุณปรารถนา";
			mes "ที่จะกลายเป็น";
			mes "ผู้รับใช้พระเจ้าจริงๆ หรือ?";
			next;
			if (select("ครับท่านบาทหลวง ผมปรารถนาเช่นนั้น:เปล่าครับ ผมโกหก") == 1) {
				mes "[Father Mareusis]";
				mes "ดีมาก ฉันขอตอบรับความตั้งใจของ " + strcharinfo(0) + " ที่จะเข้าเป็น Acolyte คุณเข้าใจใช่ไหมว่าคุณต้องทำการล้างบาป (Penance) ก่อนที่จะกลายเป็นผู้รับใช้พระเจ้าได้?";
				next;
				mes "[Father Mareusis]";
				mes "เอาล่ะ ฉันจะ";
				mes "มอบภารกิจให้คุณ...";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					mes "โปรดไปพบกับ ^000077Father Rubalkabara^000000 สมาชิกของเขตศาสนจักร Prontera แล้วกลับมาที่นี่ เขาบำเพ็ญตบะอยู่ที่ ^000077ซากโบราณสถานทางทิศตะวันออกเฉียงเหนือของเมือง Prontera^000000";
					setquest 1001;
					break;
				case 1:
					set job_acolyte_q,3;
					mes "โปรดไปพบกับ ^000077Mother Mathilda^000000 แล้วกลับมาหาฉัน เธอแจ้งบำเพ็ญตบะอยู่ใกล้กับ ^000077เมือง Morocc ทางทิศตะวันตกเฉียงใต้ของเมือง Prontera^000000";
					setquest 1002;
					break;
				case 2:
					set job_acolyte_q,4;
					mes "โปรดไปพบกับ ^000077Father Yosuke^000000 แล้วกลับมาที่นี่ เขาบำเพ็ญตบะอยู่แถวๆ ^000077สะพานที่ไหนสักแห่งทางทิศตะวันตกเฉียงเหนือของ Prontera^000000";
					setquest 1003;
					break;
				}
				next;
				mes "[Father Mareusis]";
				mes "ขอพระเมตตาของพระเจ้าส่องนำทางและคุ้มครองคุณในระหว่างการเดินทางเพื่อล้างบาปนี้";
				close;
			}
			mes "[Father Mareusis]";
			mes "คุณโกหกอย่างนั้นรึ?";
			mes "เป็นเรื่องดีที่คุณ";
			mes "ได้สารภาพความผิด";
			mes "ของตนเอง จงไป";
			mes "โดยสวัสดิภาพเถิด ลูกเอ๋ย";
			close;
		}
		mes "โอ้ คุณกลับมาแล้ว ให้ฉันตรวจสอบดูหน่อยสิว่าคุณพร้อมที่จะรับใช้พระเจ้าหรือยัง ดูซิว่า...";
		next;
		mes "[Father Mareusis]";
		if (!callfunc("F_CanChangeJob")) {
			mes "พระเจ้าช่วย! คุณยังฝึกฝนทักษะพื้นฐาน (Basic Training) ไม่เสร็จอีกหรือ?! มันสำคัญมากที่คุณต้องทำมันให้เสร็จนะ!";
			next;
			mes "[Father Mareusis]";
			mes "คุณควรจะฝึกฝนมากกว่านี้! กลับไปและทำให้แน่ใจว่าคุณมี Job Level ของ Novice ถึงเลเวล 9 และเรียนรู้ Basic Skills ครบทุกทักษะแล้ว!";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "โอ้? ฉันไม่พบชื่อของคุณในรายชื่อผู้ลงทะเบียนเลย";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[Father Mareusis]";
				mes "โปรดไปพบกับ ^000077Father Rubalkabara^000000 สมาชิกของเขตศาสนจักร Prontera แล้วกลับมาที่นี่";
				next;
				mes "[Father Mareusis]";
				mes "เขาบำเพ็ญตบะอยู่ที่ ^000077ซากโบราณสถานทางทิศตะวันออกเฉียงเหนือของเมือง Prontera^000000";
				break;
			case 3:
				mes "[Father Mareusis]";
				mes "โปรดไปพบกับ ^000077Mother Mathilda^000000 แล้วกลับมาหาฉันที่นี่";
				next;
				mes "[Father Mareusis]";
				mes "เธอแจ้งบำเพ็ญตบะอยู่ใกล้กับ ^000077เมือง Morocc ซึ่งอยู่ทางทิศตะวันตกเฉียงใต้ของเมือง Prontera^000000";
				break;
			case 4:
				mes "[Father Mareusis]";
				mes "โปรดไปพบกับ ^000077 Father Yosuke ^000000 แล้วกลับมาหาฉันที่นี่";
				next;
				mes "[Father Mareusis]";
				mes "เขาบำเพ็ญตบะอยู่ใกล้กับ ^000077สะพานที่ไหนสักแห่งทางทิศตะวันตกเฉียงเหนือของ Prontera^000000";
				break;
			}
			next;
			mes "[Father Mareusis]";
			mes "ขอพระเมตตาของพระเจ้าช่วยทำให้เส้นทางของคุณสว่างไสวและนำทางคุณในการเดินทางเพื่อล้างบาปนี้";
			close;
		}
		mes "หืม...";
		mes "ชื่อของคุณอยู่ในรายชื่อแล้ว และคุณก็ได้พิสูจน์คุณสมบัติของคุณแล้ว";
		next;
		mes "[Father Mareusis]";
		mes "ฉันภูมิใจที่จะกล่าวว่า ตอนนี้คุณพร้อมที่จะกลายเป็น Acolyte แล้ว!";
		next;
		skill "NV_TRICKDEAD",0,SKILL_PERM;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if(checkquest(1001) != -1) {
			completequest 1001;
		}
		else if(checkquest(1002) != -1) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		mes "[Father Mareusis]";
		mes "จงระลึกถึงการขอบพระคุณพระเจ้าอยู่เสมอ ผู้ทรงดูแลพวกเราตลอดเวลา";
		next;
		mes "[Father Mareusis]";
		mes "จงใช้พรสวรรค์ของคุณเพื่อรับใช้พระองค์โดยการช่วยเหลือผู้อื่น ในยามที่สับสนวุ่นวายและเผชิญความยากลำบาก จงเผชิญหน้ากับมันด้วยความศรัทธาที่แน่วแน่";
		next;
		mes "[Father Mareusis]";
		mes "สุดท้ายนี้ ฉันขอแสดงความยินดีจากใจจริงที่คุณมีความมานะอุตสาหะผ่านการทดสอบล้างบาปมาได้";
		close;
	case 2:
		mes "[Father Mareusis]";
		mes "คุณปรารถนาจะเป็น Acolyte ใช่ไหม? คุณต้องทำตามเงื่อนไขสองประการให้ครบถ้วน";
		next;
		mes "[Father Mareusis]";
		mes "ประการแรก คุณต้องมี Job Level ของ Novice อย่างน้อยเลเวล 9 และเรียนรู้ Basic Skills ครบทั้งหมด ประการที่สอง คุณจะได้รับการทดสอบล้างบาปที่ต้องเอาชนะให้ได้";
		next;
		mes "[Father Mareusis]";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				mes "สำหรับการทดสอบของคุณ โปรดไปพบกับ ^000077Father Rubalkabara ^000000 แล้วกลับมาหาฉันที่นี่";
				next;
				mes "[Father Mareusis]";
				mes "เขาบำเพ็ยตบะอยู่ที่ ^000077ซากโบราณสถานทางทิศตะวันออกเฉียงเหนือของเมือง Prontera^000000";
				break;
			case 3:
				mes "สำหรับการทดสอบของคุณ โปรดไปพบกับ ^000077Mother Mathilda^000000 แล้วกลับมาหาฉันที่นี่";
				next;
				mes "[Father Mareusis]";
				mes "เธอแจ้งบำเพ็ญตบะอยู่ใกล้กับ ^000077เมือง Morocc ซึ่งอยู่ทางทิศตะวันตกเฉียงใต้ของเมือง Prontera^000000";
				break;
			default:
				mes "สำหรับการทดสอบของคุณ โปรดไปพบกับ ^000077Father Yosuke^000000 แล้วกลับมาหาฉันที่นี่";
				next;
				mes "[Father Mareusis]";
				mes "เขาบำเพ็ญตบะอยู่แถวๆ สะพานที่ไหนสักแห่งทาง ^000077ทิศตะวันตกเฉียงเหนือของ Prontera^000000";
				break;
			}
			next;
			mes "[Father Mareusis]";
			mes "ขอพระเมตตาของพระเจ้าส่องนำทางและคุ้มครองคุณในระหว่างการเดินทางเพื่อล้างบาปนี้";
		}
		else {
			mes "สถานที่สำหรับภารกิจทดสอบนี้จะถูกตัดสินเมื่อคุณกรอกแบบฟอร์มใบสมัครแล้ว";
		}
		next;
		mes "[Father Mareusis]";
		mes "โปรดกลับมาหลังจากทำตามเงื่อนไขทั้งสองประการที่ฉันขอไว้ให้ครบถ้วน ตราบใดที่ความปรารถนาจะรับใช้พระเจ้าและช่วยเหลือผู้อื่นของคุณนั้นจริงใจ คุณจะทำสำเร็จแน่นอน";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Ascetic#aco	89,{
	mes "[Father Rubalkabara]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "เดินทางปลอดภัยนะ ทางสมาคมน่าจะรู้แล้วว่าคุณมาพบฉันตอนที่คุณไปถึงวิหาร Prontera พอดี";
			next;
			mes "[Father Rubalkabara]";
			mes "ฉันได้ส่งนกพิราบสื่อสารไปพร้อมกับข้อความแล้วล่ะ หวังว่ามันจะไปถึงที่นั่นอย่างปลอดภัยนะ...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "โอ้...? คุณคงจะเป็นผู้ที่ปรารถนาจะเข้าเป็น Acolyte สินะ ฉันได้รับข่าวจากทางวิหารแล้วว่าคุณอาจจะมาที่นี่";
				next;
				mes "[Father Rubalkabara]";
				mes "เอาล่ะ คุณชื่อ " + strcharinfo(0) + " ใช่ไหม? ยอดเยี่ยมมาก ขอบคุณที่มาหาคนแก่อย่างฉันนะ";
				next;
				mes "[Father Rubalkabara]";
				mes "ฉันเชื่อว่าบาทหลวง Mareusis คงบอกคุณเรื่องเกี่ยวกับ Acolyte มาเยอะแล้วล่ะ แถมในวิหาร Prontera ก็มีคนเก่งๆ ที่พร้อมช่วยเหลือคุณอีกเพียบ";
				next;
				mes "[Father Rubalkabara]";
				mes "ฉันคิดว่าคงไม่มีอะไรต้องสอนคุณมากนักหรอก อีกอย่าง ฉันแน่ใจว่าคนรุ่นคุณอาจจะไม่อยากฟังตาแก่แบบฉันบ่นเท่าไหร่ ฮ่าๆๆ~";
				next;
				mes "[Father Rubalkabara]";
				mes "ถึงอย่างนั้น บทเรียนมักจะมาจากที่ที่คุณคาดไม่ถึงเสมอ พระเจ้าชอบสอนบุตรของพระองค์ด้วยวิธีที่แปลกประหลาด เดี๋ยวคุณก็จะได้เห็นเอง";
				next;
				mes "[Father Rubalkabara]";
				mes "เอาล่ะ ฉันจะส่งข้อความไปบอกพวกเขาว่าคุณมาหาฉันแล้ว ตอนนี้คุณกลับไปที่วิหาร Prontera ได้เลย";
				next;
				mes "[Father Rubalkabara]";
				mes "ลาก่อน";
				close2;
				savepoint "prt_fild03",361,255,1,1;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "โอ้...";
				mes "คุณเป็นหนึ่งใน";
				mes "ผู้สมัคร Acolyte หรือเปล่า...?";
				mes "ไหนขอฉันดูหน่อยสิ...";
				next;
				mes "[Father Rubalkabara]";
				mes "ชื่อของคุณคือ " + strcharinfo(0) + " งั้นรึ?";
				mes "ฉันไม่เห็นชื่อของคุณ";
				mes "อยู่ในรายชื่อของฉันเลย หืม...";
				next;
				mes "[Father Rubalkabara]";
				mes "ลองกลับไปที่วิหาร Prontera แล้วตรวจสอบดูอีกครั้งดีไหม?";
				close;
			}
		}
		else {
			mes "หือ? อะไรพาคุณมาที่นี่ล่ะ? ที่นี่มันอันตรายเกินไปสำหรับ Novice อย่างคุณนะ!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "ยินดีที่ได้พบ";
			next;
			mes "[Father Rubalkabara]";
			mes "ยินดีต้อนรับสู่ส่วนลึกของที่นี่ เชิญนั่งลงและใคร่ครวญถึงพระดำรัสของพระเจ้าไปพร้อมกับฉันได้นะ ที่นี่ช่างงดงาม แม้จะมีความอันตรายแฝงอยู่ในความเงียบสงบนี้ก็ตาม...";
			close;
		}
		else {
			mes "โอ้ โฮ้...";
			mes "คุณเข้ามาในที่ลึกขนาดนี้เพื่อฝึกฝนอย่างนั้นรึ? หรือคุณเป็นแค่คนพเนจรกันล่ะ?";
			next;
			mes "[Father Rubalkabara]";
			mes "ไม่ว่าคุณจะเป็นใคร โปรดดูแลตัวเองด้วยนะ มอนสเตอร์ที่นี่แข็งแกร่งจนน่าตกใจ ซึ่งสวนทางกับรูปร่างหน้าตาที่น่ารักของพวกมันเลยล่ะ";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Ascetic#2aco	95,{
	mes "[Mother Mathilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "ฉันจะส่งนกพิราบสื่อสารไปที่วิหาร Prontera นะ เมื่อคุณกลับไปถึง บาทหลวงที่นั่นน่าจะได้รับข้อความจากฉันแล้วล่ะ";
			next;
			mes "[Mother Mathilda]";
			mes "ฉันจะสวดอ้อนวอนต่อพระเจ้า และหวังว่าคุณจะได้เป็น Acolyte ในเร็ววันนะจ๊ะ";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "อา คุณคงจะเป็นหนึ่งในผู้สมัคร Acolyte สินะ ฉันขอต้อนรับด้วยใจจริงจ้ะ";
				next;
				mes "[Mother Mathilda]";
				mes "คุณชื่ออะไรนะ? " + strcharinfo(0) + " อย่างนั้นรึ? ไหนขอแม่ดูหน่อยสิ... อา มีชื่อคุณอยู่ในรายชื่อจริงๆ ด้วย";
				next;
				mes "[Mother Mathilda]";
				mes "แม่จะส่งข้อความไปที่วิหารเพื่อยืนยันว่าคุณ " + strcharinfo(0) + " มาพบแม่แล้ว และเสร็จสิ้นการล้างบาปแล้วจ้ะ";
				next;
				mes "[Mother Mathilda]";
				mes "โปรดกลับไปที่วิหาร Prontera และคุยกับบาทหลวงที่ดูแลเควสได้เลยจ้ะ";
				close2;
				savepoint "moc_fild07",35,355,1,1;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "อา...!";
				mes "คุณคงจะเป็นหนึ่งใน";
				mes "ผู้สมัคร Acolyte สินะ";
				mes "แม่ขอต้อนรับด้วยใจจริงจ้ะ";
				next;
				mes "[Mother Mathilda]";
				mes "เอาล่ะ คุณชื่ออะไรนะ?";
				mes "" + strcharinfo(0) + " งั้นรึ? ไหนแม่ขอเช็กหน่อย...";
				next;
				mes "[Mother Mathilda]";
				mes "หืม...";
				mes "ดูเหมือนชื่อของคุณ";
				mes "จะไม่อยู่ในรายชื่อของแม่นะ...";
				next;
				mes "[Mother Mathilda]";
				mes "บางทีคุณควรกลับไปที่วิหาร Prontera แล้วตรวจสอบสถานที่สำหรับการล้างบาปของคุณดูอีกครั้งนะจ๊ะ";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "สวัสดีจ้ะ~";
			next;
			mes "[Mother Mathilda]";
			mes "การฝึกฝนของคุณเป็นอย่างไรบ้างจ๊ะ? แม่หวังว่าคุณจะมีความสุขที่ได้อยู่ภายใต้พระเมตตาของพระเจ้านะ";
			close;
		}
		else {
			mes "ขอพระเจ้า";
			mes "สถิตอยู่กับคุณ...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Ascetic#3aco	98,{
	mes "[Father Yosuke]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "อะไรอีก?";
			next;
			mes "[Father Yosuke]";
			mes "มีธุระอะไรกับฉันอีกไหม?! ไม่มีใช่ไหม! งั้นก็กลับไปที่วิหารได้แล้ว!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "เฮ้";
				mes "ไม่ว่าแกจะเป็นใคร";
				mes "แกดูเหมือน";
				mes "พวกสมัคร Acolyte เลยนะ";
				mes "ใช่เปล่า?";
				next;
				mes "[Father Yosuke]";
				mes "ไม่เลว ไม่เลว แกทนกับการทดสอบล้างบาปได้ดีทีเดียว";
				mes "แล้วแกชื่ออะไรล่ะ?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + " สินะ?";
				next;
				mes "[Father Yosuke]";
				mes "โอเค ฉันจะส่งข้อความไปที่วิหารว่าแก " + strcharinfo(0) + " มาพบฉันแล้ว";
				next;
				mes "[Father Yosuke]";
				mes "คราวนี้ก็กลับไปที่วิหารแล้วเปลี่ยนอาชีพเป็น Acolyte ให้มันจบๆ ไปซะนะ ไอ้หนู";
				close2;
				savepoint "prt_fild00",206,230,1,1;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "เฮ้";
				mes "แกดูเหมือนพวกสมัคร Acolyte เลยนะ ฉันเดาถูกใช่ไหม?";
				next;
				mes "[Father Yosuke]";
				mes "ไม่เลวนี่นา แกถ่อมาได้จนถึงที่นี่จาก Prontera เลย แล้วแกชื่ออะไรล่ะไอ้หนู?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + " งั้นเหรอ? ทำไมชื่อแกไม่อยู่ในรายชื่อของฉันวะ?";
				next;
				mes "[Father Yosuke]";
				mes "แกคงมาผิดที่แล้วล่ะ กลับไปที่วิหารแล้วลองเช็กกับคุณบิชอปดูใหม่ไป๊";
				close;
			}
		}
		else {
			mes "แกน่ะ...";
			mes "Novice";
			mes "มีอะไรจะ";
			mes "บอกฉันหรือเปล่า?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "เฮ้...";
			next;
			mes "[Father Yosuke]";
			mes "ถ้าแกอยากทำล่ะก็ มานั่งลงตรงนี้กับฉันแล้วฝึกสมาธิเพื่อหาความจริงที่ยิ่งใหญ่ดูสิ ความยิ่งใหญ่ของพระเจ้านี่มันสร้างแรงบันดาลใจได้จริงๆ...";
			close;
		}
		else {
			mes "มีอะไรจะพูดไหม? เพราะบอกตรงๆ ว่าถึงแกจะมี";
			mes "ฉันก็ไม่มีคำตอบให้แกหรอก";
			close;
		}
	}
}

