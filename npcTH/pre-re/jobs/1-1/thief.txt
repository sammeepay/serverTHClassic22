//===== rAthena Script =======================================
//= Thief Job Quest
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Job quest for Thief classes
//===== Additional Comments: =================================
//= 1.0 Fully working.
//= 1.1 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= 1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//=     Fixed possible EXP abuse [Lupus]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc [Lupus] 1.6a typos
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=     No longer uses function "F_ToHigh"
//= 1.8 Added Quest Log commands. [L0ne_W0lf]
//============================================================

moc_prydb1,39,129,2	script	Thief Guide	69,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[Thief Guide]";
				mes "หือ? ฉันรู้จักคุณหรือเปล่า? แปลกจังที่คุณดูคุ้นหน้ามาก คุณไม่ได้มีฝาแฝดใช่ไหม?";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Thief Guide]";
					mes "อะไรกัน คุณอยากเป็นโจรเหรอ? เสียใจด้วยนะ แต่คุณดูเหมือนยังต้องฝึกฝนมากกว่านี้";
					next;
					mes "[Thief Guide]";
					mes "ใช้เวลาของคุณเรียนรู้ทักษะพื้นฐาน (Basic Skills) ให้ครบก่อนดีไหม? เอาเป็นว่า แล้วเจอกันใหม่นะ~!";
					close;
				}
				mes "[Thief Guide]";
				mes "เอาล่ะ ฉันมีความรู้สึกว่าคุณผ่านการต่อสู้มาอย่างโชกโชนตลอดชีวิต ดังนั้นฉันจะเลื่อนขั้นให้คุณเป็น Thief เดี๋ยวนี้เลย ฉันควรจะรีบมอบสิ่งที่พวกคนแกร่งๆ แบบคุณต้องการให้...";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Thief_High;
				skill "TF_SPRINKLESAND",1,SKILL_PERM;
				skill "TF_BACKSLIDING",1,SKILL_PERM;
				skill "TF_PICKSTONE",1,SKILL_PERM;
				skill "TF_THROWSTONE",1,SKILL_PERM;
				mes "[Thief Guide]";
				mes "ในเมื่อคุณได้เป็น Thief แล้ว ก็จงใช้ชีวิตแบบ Thief ซะ เอาเลย! รายต่อไป~";
				close;
			}
			else {
				mes "[Thief Guide]";
				if (Sex == SEX_MALE)
					mes "เฮ้ นาย";
				else
					mes "เฮ้ คนสวย~";
				close;
			}
		}
		else {
			mes "[Thief Guide]";
			if (Sex == SEX_MALE)
				mes "เฮ้ นาย";
			else
				mes "เฮ้ แม่สาวน้อย";
			mes "...เฮ้! คุณดูเป็นคนดีเกินไปที่จะอยากเป็นโจรนะ!! ไปให้พ้นเลย ฉันกำลังยุ่ง รายต่อไป!";
			close;
		}
	}
	if (BaseJob == Job_Thief) {
		mes "[Thief Guide]";
		mes "ถ้าคุณมีปัญหาอะไร ก็มาคุยกับฉันได้ตลอดเวลาเลยนะ ตกลงไหม?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Thief Guide]";
		mes "นี่มันบ้าอะไรกันเนี่ย...?";
		switch(Class) {
		case 1:
			mes "เหอะ";
			mes "นั่นมัน";
			mes "ดาบเล่มใหญ่จังนะ";
			next;
			mes "[Thief Guide]";
			mes "แล้วไง...";
			mes "กำลังพยายาม";
			mes "ชดเชยปมด้อยอะไร";
			mes "อยู่หรือเปล่า... พ่อรูปหล่อ?";
			break;
		case 2:
			mes "Mage มาทำรอะไรที่นี่? คุณไม่ควรไปเล่นมายากลการ์ดที่อื่นเหรอ? เอาเถอะ ประเทศนี้มันมีเสรีภาพนี่นา...";
			next;
			mes "[Thief Guide]";
			mes "โอ้ เดี๋ยวสิ";
			mes "มันไม่ใช่แบบนั้น...";
			mes "ออกไปจากที่นี่ซะ!";
			break;
		case 3:
			mes "พวนายที่เป็น Archer";
			mes "ควรจะไปวิ่งเล่น";
			mes "อยู่ในป่า";
			mes "หรืออะไรทำนองนั้นไม่ใช่เหรอ?";
			break;
		case 4:
			mes "คุณก็รู้ว่าพวกเราหาเลี้ยงชีพด้วยการขโมยใช่ไหม? แล้วคุณมาทำอะไรในสถานที่แบบนี้ล่ะ Acolyte?";
			break;
		case 5:
			mes "คุณเป็น Merchant";
			mes "ใช่ไหม? ทำไมคุณถึง";
			mes "เดินเข้ามาในรัง";
			mes "ของพวกโจรล่ะเนี่ย?!";
			next;
			mes "[Thief Guide]";
			mes "มันเหมือนกับคุณกำลังขอร้อง";
			mes "ให้พวกเราขโมยของของคุณเลย!";
			mes "มาเถอะ รีบๆ";
			mes "ออกไปจากที่นี่ซะ~";
			break;
		case 8:
			mes "โอ้ พระเจ้า...";
			mes "ฉันกำลังจะตายหรือเปล่าเนี่ย?";
			next;
			mes "[Thief Guide]";
			mes "ไม่อย่างนั้นทำไม Priest ถึงมาที่นี่ล่ะ? ฉันว่าฉันควรเริ่มสารภาพบาปความชั่วร้ายทั้งหมดที่เคยทำมาดีกว่า";
			close;
		case 12:
			mes "เมื่อก่อนคุณเคยเป็นพวกเดียวกับเราไม่ใช่เหรอ?! นายเปลี่ยนไปนะ ดูอันตรายขึ้นจริงๆ...";
			close;
		case 17:
			mes "นายดูเจ๋งขึ้นมาทันตาเห็นเลย! นายต้องมีทักษะบางอย่างที่ฉันได้แต่ฝันถึงแน่ๆ!";
			close;
		}
		next;
		mes "[Thief Guide]";
		mes "*เฮ้อ* ฟังนะ มันไม่มีความจำเป็นเลยที่คุณจะต้องมาอยู่ในสถานที่แบบนี้ คุณควรจะไปในที่ที่คุณควรไปนะ";
		close;
	}
	if (job_thief_q == 3 && countitem(1069) > 0 || countitem(1070) > 0) {
		mes "[Thief Guide]";
		mes "หืมมม?";
		mes "คุณเก็บเห็ดมาสำหรับ";
		mes "การทดสอบ Thief แล้วใช่ไหม?";
		next;
		mes "[Thief Guide]";
		mes "นี่ คุยกับอีกคนหนึ่งที่อยู่ข้างๆ ฉันนี่ เขาเป็นคนรับผิดชอบเรื่องการตรวจเช็คเห็ดของคุณ";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[Thief Guide]";
		mes "แล้วฟาร์มเห็ด";
		mes "เป็นยังไงบ้างล่ะ?";
		mes "สนุกไหม?";
		next;
		if (select("ก็ดีนะ เจ๋งดี:มันแย่มากเลย") == 1) {
			mes "[Thief Guide]";
			mes "เหอะๆ! ทัศนคติดีนี่ ในสายงานของเรา คุณต้องสนุกกับการทำให้มือเปื้อน ไม่ทางใดก็ทางหนึ่ง";
			close;
		}
		mes "[Thief Guide]";
		mes "งั้นเหรอ? ฉันก็เคยไปที่นั่นมาเหมือนกัน เลยเข้าใจว่าทำไมที่นั่นถึงไม่ใช่ที่โปรดของทุกคน แต่อย่างว่าแหละ การเป็นโจรมันไม่ได้มีแต่ความหรูหราหรือชีวิตกลางคืนที่ทันสมัยเสมอไปหรอกนะ";
		close;
	}
	if (job_thief_q == 2) {
		mes "[Thief Guide]";
		mes "เฮ้ มาทำอะไรที่นี่ล่ะ? คุณไม่ควรจะกำลังไปเก็บเห็ดอยู่เหรอ? หรือต้องให้ฉันอธิบายอีกรอบ?";
		next;
		if (select("ใช่ครับ อธิบายอีกที:ไม่ล่ะครับ ไม่เป็นไร") == 1) {
			mes "[Thief Guide]";
			mes "*เฮ้อ* ก็นะ มักจะมีคนแบบนี้อยู่เสมอ เอาล่ะ ฟังให้ดี";
			next;
			mes "[Thief Guide]";
			mes "เอาล่ะ สำหรับการทดสอบของคุณ คุณต้องไปขโมยเห็ดจากฟาร์ม ไม่ต้องห่วงหรอก เจ้าของฟาร์มนั่นสมควรโดนปล้นแล้วล่ะ";
			next;
			mes "[Thief Guide]";
			mes "อย่างไรก็ตาม คุณต้องเก็บเห็ดสองชนิด: ^0000FFOrange Net Mushroom^000000 และ ^0000FFOrange Gooey Mushroom^000000";
			next;
			mes "[Thief Guide]";
			mes "ระวังด้วยนะ เพราะมีมอนสเตอร์อยู่ที่ฟาร์มเพื่อเฝ้าเห็ดพวกนั้น งานนี้เลยไม่ใช่เรื่องง่ายๆ เหมือนเดินเล่นในสวนหรอก";
			next;
			mes "[Thief Guide]";
			mes "เมื่อคุณกลับมาที่นี่หลังจากเก็บเห็ดแล้ว คุณจะได้รับการให้คะแนนตามเห็ดที่คุณเก็บมาได้";
			next;
			mes "[Thief Guide]";
			mes "Orange Net Mushroom แต่ละดอกจะได้ 3 คะแนน และคุณจะได้ 1 คะแนนสำหรับ Orange Gooey Mushroom แต่ละดอก คุณต้องได้คะแนนรวมทั้งหมด 25 คะแนนถึงจะผ่านการทดสอบ";
			next;
			mes "[Thief Guide]";
			mes "ออกไปข้างนอกแล้วเดินตรงไปทางแผนที่ทิศตะวันออกของทางเข้าปิรามิด แล้วคุณจะเห็นเพื่อนร่วมงานของเราคนหนึ่งอยู่ระหว่างเสาสองต้น";
			next;
			mes "[Thief Guide]";
			mes "คุยกับหมอนั่น แล้วเขาจะพาคุณเข้าฟาร์มผ่านทางประตูลับ";
			next;
			mes "[Thief Guide]";
			mes "ในแผนที่นั้น ฉันคิดว่าพิกัดของเขาคือ '^FF0000141, 125^000000' แค่พิมพ์ ^3355FF/where^000000 ในช่องแชทด้านขวาเพื่อตรวจสอบพิกัดปัจจุบันของคุณ";
			close;
		}
		mes "[Thief Guide]";
		mes "หือ แวบหนึ่งฉันก็นึกว่าคุณมีเรื่องสำคัญจะบอกฉันจริงๆ ซะอีก";
		close;
	}
	mes "[Thief Guide]";
	if (job_thief_q == 0) {
		mes "อะไรนำพาคุณลงมา";
		mes "ถึงรูหนูแห่งนี้ล่ะ?";
	}
	else {
		mes "อา...";
		mes "คุณกลับมาแล้ว";
		mes "แน่ใจนะว่าคุณ";
		mes "พร้อมจะลองอีกครั้ง?";
	}
	next;
	if (select("เฮ้ ผมมาที่นี่เพื่อเป็นโจรนะ!:เปล่าครับ ผมแค่เดินดูรอบๆ") == 1) {
		if (job_thief_q == 0) {
			mes "[Thief Guide]";
			mes "เหอะ ฉันชอบความมั่นใจของคุณนะ แต่คุณก็รู้นี่ว่าการเป็นโจรมันไม่ได้สวยงามอย่างที่ใครๆ เขาว่ากันหรอก";
			next;
			mes "[Thief Guide]";
			mes "แต่ก็นะ...";
			mes "คุณอยากจะเป็น";
			mes "Thief จริงๆ";
			mes "น่ะเหรอ?";
			next;
			switch(select("ใช่ครับ:เปล่า แค่มาทำให้เสียเวลาเล่น:ทำไมคุณถึงมาเป็นโจรล่ะ?")) {
			case 1:
				mes "[Thief Guide]";
				mes "จริงเหรอ...";
				break;
			case 2:
				mes "[Thief Guide]";
				mes "อาฮะ...";
				mes "ฉันก็พอจะดูออก";
				break;
			case 3:
				mes "[Thief Guide]";
				mes "ฉันน่ะเหรอ...?";
				mes "ตอนนั้นฉันไม่มีทางเลือกน่ะสิ ไม่ขโมยก็อดตาย แต่ฉันไม่จำเป็นต้องเล่าประวัติชีวิตให้คุณฟังหรอกนะ";
				break;
			}
			next;
			mes "[Thief Guide]";
			mes "สรุปแล้วคุณอยากจะ";
			mes "สมัครเป็น";
			mes "Thief หรือไม่?";
			next;
			switch(select("ใช่ ผมสมัครครับ:ผมกลัวเกินกว่าจะเป็นโจรครับ!")) {
			case 1:
				mes "[Thief Guide]";
				mes "เอาล่ะ บอก";
				mes "ชื่อของคุณมา";
				next;
				mes "[Thief Guide]";
				mes "" + strcharinfo(0) + "?";
				mes "ชื่อ " + strcharinfo(0) + " เนี่ยนะ? เอาเถอะ รอฉันสักครู่";
				set job_thief_q,1;
				next;
				mes "[Thief Guide]";
				mes "เรียบร้อย การลงทะเบียนของคุณได้รับการประมวลผลแล้ว โอเค คุณสามารถเริ่มการทดสอบได้ถ้าคุณพร้อม";
				next;
				switch(select("ครับ ผมพร้อมแล้ว:ไม่ครับ ผมยังไม่พร้อม")) {
				case 1:
					break;
				case 2:
					mes "[Thief Guide]";
					mes "ไม่พร้อมงั้นเหรอ?";
					mes "คุณจะไม่พร้อม";
					mes "ได้ยังไงกัน?!";
					close;
				}
				break;
			case 2:
				mes "[Thief Guide]";
				mes "กลัวงั้นเหรอ?!?";
				mes "ฮ่าๆๆๆๆๆๆๆๆๆ!";
				mes "โอ้ ได้โปรดเถอะ...!";
				mes "นั่นมันตลกสิ้นดี!";
				close;
			}
		}
		else {
			mes "[Thief Guide]";
			mes "โอเค...";
			mes "รอฉัน";
			mes "สักวินาทีนะ";
			next;
		}
		mes "[Thief Guide]";
		mes "ชื่อของคุณคือ...";
		mes strcharinfo(0) + "? เอิ่ม อยู่ไหนนะ? อ๊ะ เจอแล้ว ไหนดูซิ...";
		next;
		mes "[Thief Guide]";
		if (!callfunc("F_CanChangeJob")) {
			mes "แหม น่ารักจังนะ ฉันเห็นนะว่าคุณมีความทะเยอทะยาน แต่คุณต้องเรียนรู้ทักษะพื้นฐาน (Basic Skills) ทั้งหมดก่อนถึงจะมาเป็น Thief ได้";
			close;
		}
		mes "เอาล่ะ ฉันดูประวัติอาชญากรรมของคุณแล้ว และดูเหมือนคุณจะมีประวัติที่น่าสนใจมากทีเดียว คุณอาจจะมีคุณสมบัติพอที่จะเป็น Thief ก็ได้";
		next;
		mes "[Thief Guide]";
		mes "เพราะฉันรู้สึกอารมณ์ดี ตอนนี้ฉันขอประกาศว่าคุณผ่านการสัมภาษณ์นี้แล้ว ทำได้ดีมาก!";
		set job_thief_q,2;
		setquest 1013;
		next;
		mes "[Thief Guide]";
		mes "คราวนี้ ความสามารถจริงๆ ของคุณต้องได้รับการทดสอบแล้วล่ะ คุณรู้อะไรเกี่ยวกับการทดสอบบ้างไหม?";
		next;
		switch(select("รู้ครับ:ขอโทษทีครับ ผมไม่รู้เลย")) {
		case 1:
			mes "[Thief Guide]";
			mes "โอ้ งั้นเหรอ? เยี่ยมเลย แบบนี้ค่อยคุยกันง่ายหน่อย";
			break;
		case 2:
			mes "[Thief Guide]";
			mes "เอาล่ะ งั้นฉันจะอธิบายให้ฟัง ฟังให้ดีล่ะ การทดสอบนี้จะตัดสินว่าคุณคู่ควรที่จะเป็น Thief หรือไม่";
			next;
			mes "[Thief Guide]";
			mes "คุณจะต้องลอบเข้าไปในฟาร์มของ Shibu เขาเป็น Merchant ที่นิสัยแย่ที่สุดใน Morocc เลยล่ะ";
			next;
			mes "[Thief Guide]";
			mes "เอาล่ะ สำหรับการทดสอบ คุณต้องไปขโมยเห็ดจากฟาร์มของเขา ไม่ต้องห่วง หมอนั่นสมควรโดนปล้นแล้ว";
			next;
			mes "[Thief Guide]";
			mes "คุณต้องเก็บเห็ดสองชนิด: ^0000FFOrange Net Mushrooms^000000 และ ^0000FFOrange Gooey Mushrooms^000000";
			next;
			mes "[Thief Guide]";
			mes "ระวังด้วยนะ เพราะมีมอนสเตอร์อยู่ที่ฟาร์มเพื่อเฝ้าเห็ด งานนี้ไม่ใช่เรื่องง่ายแน่";
			next;
			mes "[Thief Guide]";
			mes "เมื่อคุณกลับมาที่นี่หลังจากเก็บเห็ดแล้ว คุณจะได้รับการให้คะแนนตามเห็ดที่คุณเก็บมาได้";
			next;
			mes "[Thief Guide]";
			mes "Orange Net Mushroom แต่ละดอกจะได้ 3 คะแนน และคุณจะได้ 1 คะแนนสำหรับ Orange Gooey Mushroom แต่ละดอก คุณต้องได้คะแนนรวมทั้งหมด 25 คะแนนถึงจะผ่านการทดสอบ";
			next;
			mes "[Thief Guide]";
			mes "ออกไปข้างนอกแล้วตรงไปทางแผนที่ทิศตะวันออกของปิรามิด แล้วคุณจะเห็นเพื่อนของเราคนหนึ่งอยู่ระหว่างเสาสองต้น";
			next;
			mes "[Thief Guide]";
			mes "คุยกับเขา แล้วเขาจะพาคุณเข้าฟาร์มผ่านประตูลับ";
			next;
			mes "[Thief Guide]";
			mes "ในฟาร์มนั้น ฉันคิดว่าพิกัดของเขาคือ '^FF0000141, 125^000000' แค่พิมพ์ ^3355FF/where^000000 ในช่องแชทเพื่อตรวจสอบพิกัดปัจจุบันของคุณ";
		}
		next;
		mes "[Thief Guide]";
		mes "อย่าลืมวางแผนและเตรียมตัวให้พร้อมก่อนจะเข้าไปในฟาร์มเห็ดล่ะ เคลื่อนที่ให้เร็วที่สุดและพยายามอย่าให้โดนฆ่าตายล่ะ เข้าใจนะ?";
		close;
	}
	close;
}

moc_prydb1,42,133,2	script	Comrade	118,{
	if (BaseJob == Job_Thief) {
		mes "[Brad]";
		mes "พวกเรายังไม่มีกิจกรรมพิเศษอะไรในตอนนี้หรอกนะ ไว้มีข่าวสารอะไรค่อยแวะมาใหม่ละกัน ตกลงไหม?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Comrade]";
		mes "เอ่อ...";
		mes "คุณดูไม่เหมือน";
		mes "โจรเลยนะ";
		next;
		mes "[Comrade]";
		mes "นี่คุณมาทำบ้าอะไร";
		mes "ที่นี่กันแน่เนี่ย?";
		close;
	}
	if (job_thief_q == 0 || job_thief_q == 1) {
		mes "[Comrade]";
		mes "มีปัญหาอะไรหรือเปล่า? ถ้าคุณอยากจะเป็น Thief ก็ไปคุยกับแม่สาวที่อยู่ข้างๆ ฉันโน่น";
		close;
	}
	else if (job_thief_q == 2) {
		mes "[Comrade]";
		mes "คุณผ่านการสัมภาษณ์มาแล้วงั้นเหรอ?";
		mes "แล้วมัวรออะไรอยู่ล่ะ?";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[Comrade]";
		mes "อา ผู้แนะนำบอกฉันเรื่องของคุณแล้วล่ะ เอาละ ไหนขอฉันเช็คเห็ดของคุณหน่อย...";
		if (countitem(1069) == 0 && countitem(1070) == 0) {
			next;
			mes "[Comrade]";
			mes "อะไรกันวะเนี่ย...";
			mes "คุณไม่มีเห็ดติดตัวมาเลยสักดอก! กลับไปเก็บมาซะ ไม่อย่างนั้นคุณก็จะไม่ผ่านการทดสอบและไม่ได้เป็น Thief หรอกนะ!";
			close;
		}
		next;
		set .@thief_item1,countitem(1069) * 3;
		set .@thief_item2,countitem(1070);
		set .@total_thief,.@thief_item1 + .@thief_item2;
		set .@money_thief,((.@thief_item1 * 5) + (.@thief_item2 * 2)) + 200;
		mes "[Comrade]";
		if (countitem(1069) != 0) {
			mes "ก่อนอื่น ขอเช็ค Orange Net Mushrooms ที่คุณได้มาหน่อย";
			mes "หืม ได้มา " + countitem(1069) + " ดอกสินะ";
		}
		if (countitem(1070) != 0)
			mes "ทีนี้ฉันจะเช็ค Orange Gooey Mushrooms ของคุณ มีทั้งหมด " + countitem(1070) + " ดอกที่คุณเก็บมาได้";
		next;
		mes "[Comrade]";
		mes "งั้นคะแนนรวม";
		mes "ของคุณก็น่าจะ...";
		next;
		mes "[Comrade]";
		mes "อืมมม... " + .@total_thief + " องศา คูณด้วยความเร็วแสง หารด้วยอินทิกรัลของพายคูณความสูง บวกกับค่าสัมบูรณ์ของการเมือง...";
		next;
		mes "[Comrade]";
		mes "โอเค!";
		mes "ฉันได้คำตอบละ";
		next;
		mes "[Comrade]";
		if (.@total_thief > 25) {
			mes "คุณได้คะแนน";
			mes "มากกว่า 25 แต้มล่ะ!";
			mes "เจ๋งมาก!";
		}
		else if (.@total_thief == 25) {
			mes "ได้ 25 แต้มพอดีเป๊ะ!";
			mes "คุณทำได้แล้ว! โคตรแสบ!";
		}
		else {
			mes "คะแนนไม่ถึง 25 แต้มที่ต้องใช้เพื่อผ่านการทดสอบแน่นอน กลับออกไปเก็บเห็ดมาให้ฉันมากกว่านี้ซะ!";
			close;
		}
		next;
		mes "[Comrade]";
		mes strcharinfo(0) + "...";
		mes "คุณผ่านการทดสอบอาชีพ Thief อย่างเป็นทางการแล้ว ตอนนี้คุณเป็นพวกเดียวกับเราแล้วล่ะ";
		if (countitem(1069) != 0) delitem 1069,countitem(1069); //Mushroom_Of_Thief_1 (เห็ดของโจร 1)
		if (countitem(1070) != 0) delitem 1070,countitem(1070); //Mushroom_Of_Thief_2 (เห็ดของโจร 2)
		callfunc "Job_Change",Job_Thief;
		callfunc "F_ClearJobVar";
		completequest 1013;
		next;
		mes "[Comrade]";
		mes "ยินดีด้วยกับการได้เป็น Thief! จากนี้ไป จงเป็นตัวแทนที่มีเกียรติของสมาคมโจร";
		next;
		mes "[Comrade]";
		mes "ถ้าคุณนำความเสื่อมเสียมาสู่สมาคมของเรา คุณจะถูกกำจัดเสีย ฉันหวังว่าคุณจะสร้างความภาคภูมิใจให้กับพรรคพวกของเรานะ";
		next;
		mes "[Brad]";
		mes "*อะแฮ่ม* ยินดีต้อนรับสู่สมาคมนะ สหาย! ฉันชื่อ Brad เป็นคนดูแลฝ่ายทรัพยากรบุคคลที่นี่";
		next;
		mes "[Brad]";
		mes "นี่คือเงินอุดหนุนเล็กๆ น้อยๆ สำหรับมือใหม่อย่างคุณ จะเอาไปใช้ที่ไหนก็ตามใจล่ะ แล้วเจอกันนะ~";
		set Zeny, Zeny+.@money_thief;
		close;
	}
}

moc_ruins,141,125,3	script	Mr. Irrelevant	83,{
	if (BaseJob == Job_Thief) {
		mes "[Mr. Irrelevant]";
		mes "อา ฉันเห็นว่าตอนนี้คุณกลายเป็น Thief แล้วสินะ ฉันรู้อยู่แล้วล่ะว่าคุณต้องเข้าร่วมกับพวกเรา";
		next;
		mes "[Mr. Irrelevant]";
		mes "การขโมยของจากฟาร์มเห็ดมันกลายเป็นเรื่องง่ายเกินไปสำหรับคุณแล้วล่ะในตอนนี้ คุณควรจะเสริมสร้างทักษะและฝึกฝนวิชาชีพของเราให้เชี่ยวชาญนะ";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[Mr. Irrelevant]";
		switch(rand(4)) {
		case 1:
			mes "ได้ดื่มเครื่องดื่มแรงๆ สักแก้วก็คงดีนะ";
			close;
		case 2:
			mes "เอาเงินของคุณมาให้ฉันซะ";
			next;
			mes "[Mr. Irrelevant]";
			mes "ล้อเล่นน่ะ ตอนนี้ฉันอยู่นอกเวลาทำงาน";
			close;
		case 3:
			mes "แกเรียกใครว่าไอ้โรคจิตวะ?!?!";
			close;
		case 4:
			mes "ฉันไม่มีอะไรจะคุยกับคุณ จะรังเกียจไหมถ้าจะปล่อยให้ฉันอยู่คนเดียว?";
			close;
		default:
			mes "วันนี้ดูเหมือนจะเป็นวันที่ดีที่จะไปปิรามิดและออกล่ากับเพื่อนๆ ของฉันนะ";
			close;
		}
	}
	if (job_thief_q == 3) {
		mes "[Mr. Irrelevant]";
		mes "ฮ่าๆๆๆๆ~!";
		mes "คุณยัง";
		mes "สอบไม่ผ่านอีกเหรอ?";
		mes "เอาล่ะ ฉันจะปล่อยให้คุณเข้าไป...";
		close2;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 2) {
		mes "[Mr. Irrelevant]";
		mes "หืม...";
		mes "คุณมาเพื่อรับการทดสอบสินะ? ฉันดูจากตาคุณก็รู้แล้วว่าคุณรู้อะไรบางอย่าง";
		next;
		mes "[Mr. Irrelevant]";
		mes "ชื่อของคุณคือ " + strcharinfo(0) + " งั้นเหรอ? อา มีชื่ออยู่ในรายการพอดี เอาล่ะ ฉันจะปล่อยให้คุณเข้าไปในฟาร์มเห็ด (Mushroom Farm) แต่ฉันไม่รับประกันความปลอดภัยของคุณหรอกนะ...";
		close2;
		set job_thief_q,3;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 1) {
		mes "[Mr. Irrelevant]";
		mes "มีกลิ่นแปลกๆ โชยออกมาจาก... ตัวคุณ ทำไมถึงเป็นแบบนั้นกันนะ?";
		close;
	}
	mes "[Mr. Irrelevant]";
	mes "เฮ้ Novice! ทำไมไม่ลองมาเข้าร่วมกลุ่มกับสมาคมโจร (Thief Guild) ดูล่ะ? พวกเรายินดีต้อนรับพวกมือใหม่อย่างคุณเสมอที่จะมาร่วมอุดมการณ์อันเห็นแก่ตัวของพวกเรา";
	next;
	mes "[Mr. Irrelevant]";
	mes "คุณสามารถหาข้อมูลเพิ่มเติมได้ในห้องใต้ดินที่ชั้น B1 ของปิรามิดนะ";
	close;
}

// Monsters
//============================================================
job_thief1,0,0	monster	Orange Mushroom	1182,180
job_thief1,0,0	monster	Fabre	1184,50
job_thief1,0,0	monster	Chonchon	1183,50
job_thief1,0,0	monster	Spore	1014,30

//============================================================
// mapflag
//============================================================
