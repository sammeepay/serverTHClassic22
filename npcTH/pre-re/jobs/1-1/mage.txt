//===== rAthena Script ======================================= 
//= Mage Job Quest
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job quest for Mage classes
//===== Additional Comments: ================================= 
//= 1.0 Fully working.
//= 1.1 Fixed input ingridients bug [Lupus]
//= 1.2 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= 1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc, CHANGED NUMBERS TO CONSTANTS [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=     No longer uses function "F_ToHigh"
//= 1.7a Fixed a "If" in the quest. (bugreport:489) [Samuray22]
//= 1.8 Added Quest Log commands. [L0ne_W0lf]
//= 1.9 Fixed an issue with completequest by adding checkquest. [Kisuka]
//= 2.0 Moved "Dollshoi" and "Ponka-Hontas" NPCs. [Euphy]
//============================================================ 

geffen_in,164,124,4	script	Mage Guildsman	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Mage Guildsman]";
				mes "ว้าว ไม่เจอกันนานเลยนะ! แต่เดี๋ยวก่อน คุณน่าจะตายไปแล้วไม่ใช่เหรอ?";
				next;
				mes "[Mage Guildsman]";
				mes "อา... คุณต้องกลับมาเกิดใหม่แน่ๆ เอาล่ะ ฉันดีใจนะที่ได้คุณกลับมา";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Mage Guildsman]";
					mes "ฉันเสียใจด้วยนะ แต่ฉันคิดว่าคุณยังไม่พร้อมที่จะเรียนรู้เวทมนตร์ในตอนนี้ ทำไมคุณไม่กลับไปเรียนรู้ทักษะพื้นฐาน (Basic Skills) ให้จบก่อนล่ะ?";
					next;
					mes "[Mage Guildsman]";
					mes "ไม่ต้องรีบหรอกนะ ยิ่งคุณเรียนรู้มากเท่าไหร่ คุณก็จะยิ่งพร้อมสำหรับการเรียนเวทมนตร์อีกครั้งมากขึ้นเท่านั้น";
					close;
				}
				mes "[Mage Guildsman]";
				mes "เอาล่ะ ในเมื่อคุณเคยผ่านการทดสอบ Mage มาแล้วครั้งหนึ่ง ฉันจะไม่สงสัยในคุณสมบัติของคุณอีก คุณคงอยากได้ทักษะเวทมนตร์กลับคืนมาทันทีเลยใช่ไหมล่ะ?";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Mage_High;
				skill "MG_ENERGYCOAT",1,SKILL_PERM;
				mes "[Mage Guildsman]";
				mes "ว้าว ไม่รู้ทำไมนะ แต่คุณดูดีกว่าเมื่อก่อนเยอะเลย อย่างไรก็ตาม ฉันเชื่อว่าคุณจะทำหน้าที่เป็น Mage ได้ดียิ่งขึ้นกว่าเดิมแน่นอน";
				close;
			}
			else {
				mes "[Mage Guildsman]";
				mes "มีอะไรที่ฉันพอจะช่วยคุณได้อีกไหม? ถ้าไม่มี ทำไมคุณไม่ลองไปทดสอบทักษะของคุณดูล่ะ? โลกใบนี้กำลังรอคุณอยู่นะ~!";
				close;
			}
		}
		else {
			mes "[Mage Guildsman]";
			mes "อะไรกัน คุณสนใจสมาคม Mage งั้นเหรอ? ฉันไม่อยากจะบอกคุณแบบนี้หรอกนะ แต่คุณไม่ได้เหมาะกับที่นี่";
			next;
			mes "[Mage Guildsman]";
			mes "ฉันไม่แน่ใจว่าทำไมคุณยังยืนอยู่ตรงหน้าฉัน แต่ฉันบอกได้เลยว่าคุณไม่ได้ถูกกำหนดมาให้เป็น Mage";
			close;
		}
	}
	mes "[Mage Guildsman]";
	mes "โย่ มีอะไรเหรอ?";
	next;
	switch(select("ฉันอยากเป็น Mage ครับ:บอกเงื่อนไขการสมัครหน่อยครับ:ไม่มีอะไรเป็นพิเศษครับ")) {
	case 1:
		mes "[Mage Guildsman]";
		if (BaseJob == Job_Mage) {
			mes "เฮ้ นี่คุณยังไม่รู้ตัวอีกเหรอ? คุณเป็น Mage อยู่แล้วนะ เจ้าบ้า!";
			next;
			mes "[Mage Guildsman]";
			mes "สักวันหนึ่งคุณจะตระหนักถึงพลังในตัวคุณ เมื่อคุณสามารถเสกไฟออกมาได้ด้วยจิตใจของคุณเอง!";
			close;
		}
		if (BaseJob != Job_Novice) {
			mes "เฮ้~ ไม่เอาน่า เลิกเล่นได้แล้ว คุณจะเป็น Mage ไม่ได้หรอกนะเพราะคุณมีอาชีพอื่นอยู่แล้ว";
			close;
		}
		if (job_magician_q == 0) {
			mes "อยากเป็น Mage งั้นเหรอ...?";
			if (Sex == SEX_MALE)
				mes "เฮ้ ดูคุณสิ! คุณก็น่ารักดีนะเนี่ย~! แต่ไม่ใช่สเปกฉันหรอกนะ...";
			else {
				mes "โอ้โห คุณนี่เป็นสาวที่ฮอตจริงๆ เลยนะ~!";
				mes "ฉันชอบผู้หญิงแบบคุณจัง~";
			}
			next;
			mes "[Mage Guildsman]";
			mes "เอาล่ะ คุณบอกว่าอยากเป็น Mage ใช่ไหม? ถ้าอย่างนั้น ช่วยเซ็นใบสมัคร Mage ให้หน่อยนะ";
			next;
			if (select("ลงชื่อสมัคร:ยกเลิก") == 1) {
				mes "[Mage Guildsman]";
				mes "โอเค เซ็นตรงนั้นเลย โอ้ คุณสะกดคำเก่งมาก เรียบร้อย ชื่อของคุณคือ... " + strcharinfo(0) + ".";
				next;
				mes "[Mage Guildsman]";
				mes "คราวนี้ก็ได้เวลาที่";
				mes "ฉันจะมอบบททดสอบให้คุณแล้ว";
				switch(rand(0,3)) {
				case 1:
					mes "จงปรุง ^3355FFMixed Solution No. 1^000000";
					mes "แล้วนำมาให้ฉัน";
					set job_magician_q,1;
					break;
				case 2:
					mes "จงปรุง ^3355FFMixed Solution No. 2^000000";
					mes "แล้วนำมาให้ฉัน";
					set job_magician_q,2;
					break;
				case 3:
					mes "จงปรุง ^3355FFMixed Solution No. 3^000000";
					mes "แล้วนำมาให้ฉัน";
					set job_magician_q,3;
					break;
				default:
					mes "จงปรุง ^3355FFMixed Solution No. 4^000000";
					mes "แล้วนำมาให้ฉัน";
					set job_magician_q,4;
				}
				next;
				getitem 1092,1; //Empty_Cylinder (หลอดแก้วเปล่า)
				mes "[Mage Guildsman]";
				mes "คุณสามารถหาส่วนผสมที่จำเป็นได้จากหนังสือคู่มือภายในสมาคมนี้ ดังนั้นคุณควรตรวจสอบสิ่งที่ต้องใช้ก่อนออกไปล่ะ";
				next;
				mes "[Mage Guildsman]";
				mes "เมื่อคุณรวบรวมส่วนผสมครบแล้ว ให้ใช้เครื่องจักรกลางห้องเพื่อผสมน้ำยา ขอให้โชคดีนะ!";
				close;
			}
			mes "[Mage Guildsman]";
			mes "อะไรน้าาาา~! พอเพิ่งบอกฉันว่าอยากเป็น Mage เสร็จแล้วก็เปลี่ยนใจทันทีเลยเหรอ?! มีความแน่วแน่หน่อยสิ!";
			close;
		}
		mes "ว่าไง? พร้อมหรือยัง...?";
		if (!callfunc("F_CanChangeJob")) {
			mes "โอ้ แย่จัง คุณยังทำตามเงื่อนไขไม่ครบเลยนะ";
			next;
			mes "[Mage Guildsman]";
			mes "กลับไปเก็บ Job level ของ Novice ให้ถึงเลเวล 9 ก่อน และอย่าลืมว่าคุณต้องเรียนรู้ทักษะพื้นฐาน (Basic Skills) ทั้งหมดก่อนกลับมาหาฉันด้วย";
			close;
		}
		mes "บททดสอบของ " + strcharinfo(0) + " คือ...";
		if (job_magician_q == 1)
			mes "การทำ Mixed Solution No. 1";
		else if (job_magician_q == 2)
			mes "การทำ Mixed Solution No. 2";
		else if (job_magician_q == 3)
			mes "การทำ Mixed Solution No. 3";
		else
			mes "การทำ Mixed Solution No. 4";
		next;
		mes "[Mage Guildsman]";
		mes "โอเค เดี๋ยวฉันขอ";
		mes "ตรวจหน่อยว่าคุณทำน้ำยา";
		mes "ออกมาได้ถูกต้องแม่นยำหรือเปล่า...";
		next;
		mes "[Mage Guildsman]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "เฮ้ น้ำยาที่ฉันสั่ง";
			mes "อยู่ไหนล่ะ...? ฉันตรวจให้ไม่ได้หรอกนะถ้าคุณไม่เอามาให้ดู จริงไหม?";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "เดี๋ยวก่อน";
				mes "นี่มันไม่ใช่น้ำยา";
				mes "ที่ฉันสั่งนี่นา!";
				next;
				mes "[Mage Guildsman]";
				if (job_magician_q == 1)
					mes "คุณต้องทำ Mixed Solution No. 1 แล้วนำมาให้ฉันนะ คราวนี้กลับไปลองใหม่อีกครั้ง";
				else if (job_magician_q == 2)
					mes "คุณต้องทำ Mixed Solution No. 2 แล้วนำมาให้ฉันนะ คราวนี้กลับไปลองใหม่อีกครั้ง";
				else if (job_magician_q == 3)
					mes "คุณต้องทำ Mixed Solution No. 3 แล้วนำมาให้ฉันนะ คราวนี้กลับไปลองใหม่อีกครั้ง";
				else
					mes "คุณต้องทำ Mixed Solution No. 4 แล้วนำมาให้ฉันนะ คราวนี้กลับไปลองใหม่อีกครั้ง";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1 (น้ำยาทดสอบ 1)
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2 (น้ำยาทดสอบ 2)
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3 (น้ำยาทดสอบ 3)
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4 (น้ำยาทดสอบ 4)
				else delitem 1090,1; //Mage_Test_Etc (น้ำยาทดสอบอื่นๆ)
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		mes "หืมมม ฉันเห็นนะว่าคุณพยายามอย่างหนักเลย สำหรับการลองครั้งแรกของมือใหม่เนี่ย ถือว่าดีมากจริงๆ";
		mes "ทำได้ดีมาก!";
		next;
		mes "[Mage Guildsman]";
		mes "เอาล่ะ! ฉันยินดีที่จะบอกว่าคุณผ่านการทดสอบ Mage แล้ว ฉันจะเปลี่ยนอาชีพให้คุณเดี๋ยวนี้เลย!";
		next;
		mes "[Mage Guildsman]";
		mes "*อะแฮ่ม*";
		mes "ยินดีด้วยนะ!";
		mes "ตอนนี้คุณเป็น Mage แล้ว!";
		next;
		for ( .@quest_id = 1005; .@quest_id <= 1008; .@quest_id++ ) {
			if (isbegin_quest(.@quest_id) == 1)
				completequest .@quest_id;
		}
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		mes "[Mage Guildsman]";
		mes "'Welcome to My World~'";
		mes "เหะๆ ฉันแค่อยากลองพูดคำนี้ดูน่ะ คุณก็รู้ มันเป็นบทพูดจากหนังดังเรื่องหนึ่งเลยนะ~";
		next;
		mes "[Mage Guildsman]";
		mes "ในเมื่อตอนนี้คุณเป็น Mage เหมือนพวกเราแล้ว เรามาเป็นเพื่อนกันเถอะ โอเคไหม?";
		close;
	case 2:
		mes "[Mage Guildsman]";
		mes "อยากเป็น Mage งั้นเหรอ?";
		if (Sex == SEX_MALE)
			mes "สำหรับคนน่ารักอย่างคุณ ฉันยินดีที่จะอธิบายเงื่อนไขให้ฟังอย่างยิ่งเลยล่ะ!";
		else
			mes "ฉันยินดีที่จะอธิบายเงื่อนไขการสมัครให้สาวสวยอย่างคุณฟังจ้ะ!";
		next;
		mes "[Mage Guildsman]";
		mes "ก่อนอื่นเลย คุณต้องมี Job Level ของ Novice ถึงเลเวล 10 และเรียนรู้ทักษะพื้นฐาน (Basic Skills) ทั้งหมด จากนั้น คุณจะต้องผ่านการทดสอบของ Mage";
		next;
		if (job_magician_q != 0) {
			mes "[Mage Guildsman]";
			mes "บททดสอบของคุณคือการ";
			switch(job_magician_q) {
			case 1:
				mes "ปรุง";
				mes "^3355FFMixed Solution No. 1^000000";
				mes "แล้วนำมาส่งให้ฉัน";
				if (isbegin_quest(1005) == 0)
					setquest 1005;
				break;
			case 2:
				mes "ปรุง";
				mes "^3355FFMixed Solution No. 2^000000";
				mes "แล้วนำมาส่งให้ฉัน";
				if (isbegin_quest(1006) == 0)
					setquest 1006;
				break;
			case 3:
				mes "ปรุง";
				mes "^3355FFMixed Solution No. 3^000000";
				mes "แล้วนำมาส่งให้ฉัน";
				if (isbegin_quest(1007) == 0)
					setquest 1007;
				break;
			default:
				mes "ปรุง";
				mes "^3355FFMixed Solution No. 4^000000";
				mes "แล้วนำมาส่งให้ฉัน";
				if (isbegin_quest(1008) == 0)
					setquest 1008;
				break;
			}
			next;
			mes "[Mage Guildsman]";
			mes "คุณสามารถตรวจสอบส่วนผสมที่จำเป็นสำหรับการทำน้ำยาได้จากหนังสือคู่มือภายในสมาคมนี้นะ";
		}
		else {
			mes "[Mage Guildsman]";
			mes "คุณจะได้รับแจ้งว่าจะต้องทำ Mixed Solution ตัวไหน หลังจากที่คุณได้ลงชื่อในใบสมัครแล้ว";
		}
		next;
		mes "[Mage Guildsman]";
		mes "บอกฉันด้วยนะถ้าคุณพร้อมที่จะเป็น Mage แล้ว ตกลงไหม?";
		close;
	case 3:
		mes "[Mage Guildsman]";
		mes "ไม่มีอะไรเหรอ...?";
		close;
	}
}

geffen_in,164,112,4	script	Mixing Machine	111,{
	mes "[Mixing Machine]";
	mes "เครื่องจักรนี้เป็นทรัพย์สินของสมาคม Geffen Mage Guild และใช้สำหรับผสมน้ำยาเพื่อวัตถุประสงค์ทางเวทมนตร์เท่านั้น";
	next;
	if (select("ใช้งานเครื่องจักร:ยกเลิก") == 1) {
		mes "[Mixing Machine]";
		mes "โปรดเลือก";
		mes "ตัวทำละลาย (Solvent)";
		mes "สำหรับน้ำยานี้";
		next;
		switch(select("Payon Solution:Morocc Solution:ไม่ใช้ตัวทำละลาย")) {
		case 1:
			if (countitem(1089) == 0) {
				mes "[Mixing Machine]";
				mes "ข้อผิดพลาด";
				mes "ไม่พบไอเทมดังกล่าว";
				mes "โปรดตรวจสอบอีกครั้ง";
				mes "ระบบหยุดทำงาน";
				close;
			}
			set .@mixitem2,1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				mes "[Mixing Machine]";
				mes "ข้อผิดพลาด";
				mes "ไม่พบไอเทมดังกล่าว";
				mes "โปรดตรวจสอบอีกครั้ง";
				mes "ระบบหยุดทำงาน";
				close;
			}
			set .@mixitem2,2;
			break;
		case 3:
			set .@mixitem2,0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				mes "[Mixing Machine]";
				if (.@mixitem1_1 != 0) mes "Jellopy: " + .@mixitem1_1 + " ชิ้น";
				if (.@mixitem1_2 != 0) mes "Fluff: " + .@mixitem1_2 + " ชิ้น";
				if (.@mixitem1_3 != 0) mes "Milk: " + .@mixitem1_3 + " ชิ้น";
				if (.@mixitem2 == 0) mes "ตัวทำละลาย: ไม่มี";
				if (.@mixitem2 == 1) mes "ตัวทำละลาย: Payon Solution";
				if (.@mixitem2 == 2) mes "ตัวทำละลาย: Morocc Solution";
				next;
				mes "[Mixing Machine]";
				mes "โปรดเลือกว่าคุณต้องการเริ่มการผสม หรือต้องการกรอกจำนวนไอเทมที่จะผสมใหม่อีกครั้ง";
				next;
				switch(select("เริ่มการผสม:กรอกจำนวนไอเทมใหม่:รีเซ็ต (Reset)")) {
				case 1:
					mes "[Mixing Machine]";
					mes "โปรดใส่ไอเทมลงในภาชนะผสม ตรวจสอบให้แน่ใจว่าจำนวนไอเทมนั้นถูกต้อง";
					next;
					mes "[Mixing Machine]";
					mes "คุณไม่สามารถปรับเปลี่ยนหรือนำไอเทมคืนได้เมื่อใส่ลงในภาชนะผสมแล้ว";
					next;
					mes "[Mixing Machine]";
					mes "หากทุกอย่างถูกต้องแล้ว ให้กดปุ่ม 'Mix' เมื่อคุณพร้อม มิฉะนั้นให้กดปุ่ม 'Cancel'";
					next;
					if (select("กดปุ่ม 'Mix':กดปุ่ม 'Cancel'") == 1) {
						mes "[Mixing Machine]";
						mes "กำลังใส่ไอเทมลงในภาชนะผสม โปรดรอสักครู่";
						next;
						mes "[Mixing Machine]";
						if (countitem(909) < .@mixitem1_1) {
							mes "Jellopy ไม่เพียงพอ";
							mes "โปรดตรวจสอบอีกครั้ง";
							mes "ระบบหยุดทำงาน";
							close;
						}
						else if (countitem(914) < .@mixitem1_2) {
							mes "Fluff ไม่เพียงพอ";
							mes "โปรดตรวจสอบอีกครั้ง";
							mes "ระบบหยุดทำงาน";
							close;
						}
						else if (countitem(519) < .@mixitem1_3) {
							mes "Milk ไม่เพียงพอ";
							mes "โปรดตรวจสอบอีกครั้ง";
							mes "ระบบหยุดทำงาน";
							close;
						}
						else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								mes "ไม่พบน้ำยา Solution";
								mes "โปรดตรวจสอบอีกครั้ง";
								mes "ระบบหยุดทำงาน";
								close;
							}
						}
						if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
						if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Fluff
						if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Milk
						if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
						if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
						mes "เตรียมไอเทมเรียบร้อย";
						mes "ปิดฝาเครื่อง";
						set .@progress,3;
						next;
					}
					break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					mes "[Mixing Machine]";
					mes "รีเซ็ตเสร็จสมบูรณ์";
					mes "ต้องการเริ่มใหม่อีกครั้งหรือไม่?";
					next;
					if (select("ใช่:ไม่") == 1)
						break;
					mes "[Mixing Machine]";
					mes "ระบบหยุดทำงาน";
					mes "ขอบคุณ";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				mes "[Mixing Machine]";
				mes "ไม่พบรายการใดๆ";
				next;
			}
			mes "[Mixing Machine]";
			mes "เลือกไอเทมที่จะผสม";
			while(1) {
				switch(select("Jellopy:Fluff:Milk:พร้อมสำหรับผสมแล้ว")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "ข้อผิดพลาด: จำนวนไอเทมเกินขีดจำกัด โปรดกรอกค่าที่น้อยกว่า 10,000 แล้วลองใหม่อีกครั้ง";
							next;
						}
						else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "ข้อผิดพลาด: จำนวนไอเทมเกินขีดจำกัด โปรดกรอกค่าที่น้อยกว่า 10,000 แล้วลองใหม่อีกครั้ง";
							next;
						}
						else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "ข้อผิดพลาด: จำนวนไอเทมเกินขีดจำกัด โปรดกรอกค่าที่น้อยกว่า 10,000 แล้วลองใหม่อีกครั้ง";
							next;
						}
						else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[Mixing Machine]";
		mes "โปรดกรอก";
		mes "รหัสหมายเลข (Serial Number) ของ";
		mes "Magic Powder";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				mes "[Mixing Machine]";
				if (.@input == 0) {
					mes "คุณต้องการข้ามเมนูนี้ใช่หรือไม่?";
					next;
					if (select("ใช่:ไม่ใช่") == 1) {
						break;
					}
				}
				else {
					mes "รหัสหมายเลขไม่ถูกต้อง";
					mes "โปรดลองใหม่อีกครั้ง";
					next;
				}
			}
			else {
				mes "[Mixing Machine]";
				mes "รหัสหมายเลขคือ #" + .@input + ", ถูกต้องใช่หรือไม่?";
				next;
				if (select("ยืนยัน:ยกเลิก") == 1) {
					if (.@input == 8472)
						set .@magic_powder,1;
					else if (.@input == 3735)
						set .@magic_powder,2;
					else if (.@input == 2750)
						set .@magic_powder,3;
					else if (.@input == 5429)
						set .@magic_powder,4;
					else
						set .@magic_powder,5;
				}
				break;
			}
		}
		mes "[Mixing Machine]";
		mes "เลือก";
		mes "หินตัวเร่งปฏิกิริยา (Catalyst Stone)";
		next;
		switch(select("Yellow Gemstone:Red Gemstone:Blue Gemstone:1carat Diamond:ข้าม")) {
		case 1: set .@mixitem3,1; break;
		case 2: set .@mixitem3,2; break;
		case 3: set .@mixitem3,3; break;
		case 4: set .@mixitem3,4; break;
		case 5: break;
		}
		mes "[Mixing Machine]";
		mes "ตั้งค่าเรียบร้อย";
		mes "กำลังเริ่มต้น";
		mes "กระบวนการผสม";
		mes "โปรดรอสักครู่";
		next;
		mes "[Mixing Machine]";
		mes "- สุภาษิตประจำวัน -";
		switch(rand(1,5)) {
		case 1:
			mes "ตาต่อตา ฟันต่อฟัน: เมื่อคุณเอาสิ่งใดไปจากใคร คุณต้องทดแทนหรือชดใช้คืนในทางใดทางหนึ่ง";
			break;
		case 2:
			mes "ความน่าเชื่อถือคือสกุลเงินของมนุษย์: ความไว้วางใจที่แท้จริงนั้นมีมูลค่ามหาศาลจนมิอาจวัดได้";
			break;
		case 3:
			mes "ทำอย่างไรได้อย่างนั้น: ท้ายที่สุดแล้ว คุณจะได้รับการปฏิบัติในแบบเดียวกับที่คุณปฏิบัติต่อผู้อื่น";
			mes "มันหมายถึง 'เมื่อคุณทำร้ายผู้อื่น คุณจะถูกทำร้ายโดยเขาในสถานการณ์ที่หลีกเลี่ยงไม่ได้'";
			break;
		case 4:
			mes "เพื่อนบ้านที่ดี ดีกว่าพี่น้องที่อยู่ไกล: เมื่อคุณต้องการความช่วยเหลือ คุณสามารถพึ่งพาผู้ที่อยู่ใกล้ชิดคุณได้";
			break;
		default:
			mes "นกเผ่าพันธุ์เดียวกันมักรวมฝูงกัน: คุณสามารถดูเพื่อนของคนๆ หนึ่ง เพื่อเป็นเครื่องบ่งชี้ถึงนิสัยใจคอของเขาได้";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			mes "[Mixing Machine]";
			mes "Mage Test Solution No. 1";
			getitem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			mes "[Mixing Machine]";
			mes "Mage Test Solution No. 2";
			getitem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			mes "[Mixing Machine]";
			mes "Mage Test Solution No. 3";
			getitem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			mes "[Mixing Machine]";
			mes "Mage Test Solution No. 4";
			getitem 1087,1; //Mage_Test_4
			next;
		}
		else {
			mes "[Mixing Machine]";
			mes "เกิดข้อผิดพลาด";
			mes "ที่ไม่คาดคิดขึ้น";
			getitem 1090,1; //Mage_Test_Etc
			next;
		}
		mes "[Mixing Machine]";
		mes "การผสมเสร็จสิ้น";
		mes "ขอบคุณ";
		close;
	}
	close;
}

// The Morocc Solution and Payon Solution traders
// are also used in the Sage skill quests, and have
// been moved to npc/quests/mage_solution.txt.
//
// pay_arche,122,100,0	script	Dollshoi	88,{}
// moc_ruins,91,150,0	script	Ponka-Hontas	93,{}

geffen_in,177,112,4	script	Bookshelf	111,{
	mes "[Guide Book]";
	mes "หนังสือคู่มือเล่มนี้เป็นทรัพย์สินของสมาคม Geffen Mage Association โปรดหยิบจับด้วยความระมัดระวัง";
	next;
	switch(select("น้ำยา Solution No. 1.:น้ำยา Solution No. 2.:น้ำยา Solution No. 3.:น้ำยา Solution No. 4.:ปิด.")) {
	case 1:
		mes "[Mage Test Solution No. 1]";
		mes "* รายการส่วนผสม *";
		mes "Jellopy 2 ชิ้น";
		mes "Fluff 3 ชิ้น";
		mes "Milk 1 ขวด";
		next;
		mes "[Mage Test Solution No. 1]";
		mes "* ตัวทำละลาย (Solvent Agent) *";
		mes "Payon Solution";
		mes "สถานที่หา:";
		mes "บ่อน้ำพุเล็กๆ ใน Payon หมู่บ้านนักธนู";
		next;
		mes "[Mage Test Solution No. 1]";
		mes "* รหัสหมายเลขพลังเวทมนตร์ *";
		mes "8472";
		next;
		mes "[Mage Test Solution No. 1]";
		mes "* ตัวเร่งปฏิกิริยา (Catalyst) *";
		mes "Yellow Gemstone";
		mes "(มีให้เลือกใน";
		mes "เครื่องผสมน้ำยา)";
		close;
	case 2:
		mes "[Mage Test Solution No. 2]";
		mes "* รายการส่วนผสม *";
		mes "Jellopy 3 ชิ้น";
		mes "Fluff 1 ชิ้น";
		mes "Milk 1 ขวด";
		next;
		mes "[Mage Test Solution No. 2]";
		mes "* ตัวทำละลาย (Solvent Agent) *";
		mes "ไม่มี";
		next;
		mes "[Mage Test Solution No. 2]";
		mes "* รหัสหมายเลขพลังเวทมนตร์ *";
		mes "3735";
		next;
		mes "[Mage Test Solution No. 2]";
		mes "* ตัวเร่งปฏิกิริยา (Catalyst) *";
		mes "Red Gemstone";
		mes "(มีให้เลือกใน";
		mes "เครื่องผสมน้ำยา)";
		close;
	case 3:
		mes "[Mage Test Solution No. 3]";
		mes "* รายการส่วนผสม *";
		mes "Jellopy 6 ชิ้น";
		mes "Fluff 1 ชิ้น";
		next;
		mes "[Mage Test Solution No. 3]";
		mes "* ตัวทำละลาย (Solvent Agent) *";
		mes "Payon Solution";
		mes "สถานที่หา:";
		mes "บ่อน้ำพุเล็กๆ ใน Payon หมู่บ้านนักธนู";
		next;
		mes "[Mage Test Solution No. 3]";
		mes "* รหัสหมายเลขพลังเวทมนตร์ *";
		mes "2750";
		next;
		mes "[Mage Test Solution No. 3]";
		mes "* ตัวเร่งปฏิกิริยา (Catalyst) *";
		mes "Blue Gemstone";
		mes "(มีให้เลือกใน";
		mes "เครื่องผสมน้ำยา)";
		close;
	case 4:
		mes "[Mage Test Solution No. 4]";
		mes "* รายการส่วนผสม *";
		mes "Jellopy 2 ชิ้น";
		mes "Fluff 3 ชิ้น";
		next;
		mes "[Mage Test Solution No. 4]";
		mes "* ตัวทำละลาย (Solvent Agent) *";
		mes "Morocc Solution";
		mes "สถานที่หา:";
		mes "บ่อน้ำพุเล็กๆ ใกล้ทางเข้าพีระมิดใน Morocc";
		next;
		mes "[Mage Test Solution No. 4]";
		mes "* รหัสหมายเลขพลังเวทมนตร์ *";
		mes "5429";
		next;
		mes "[Mage Test Solution No. 4]";
		mes "* ตัวเร่งปฏิกิริยา (Catalyst) *";
		mes "1 carat Diamond";
		mes "(มีให้เลือกใน";
		mes "เครื่องผสมน้ำยา)";
		close;
	}
	close;
}
