//===== rAthena Script =======================================
//= Izlude Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Izlude
//===== Additional Comments: =================================
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

izlude,121,87,6	script	Guide#iz	105,{
	cutin "prt_soldier",2;
	mes "[Izlude Guide]";
	mes "ยินดีต้อนรับสู่ Izlude";
	mes "เมืองบริวารของ Prontera ค่ะ";
	mes "หากคุณต้องการคำแนะนำ";
	mes "รอบๆ Izlude เชิญสอบถาม";
	mes "ฉันได้ทุกเมื่อเลยนะคะ";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("แนะนำเมือง:ลบเครื่องหมายออกจากแผนที่ขนาดเล็ก:ประกาศ:ยกเลิก")) {
		case 1:
			mes "[Izlude Guide]";
			mes "กรุณาเลือก";
			mes "สถานที่จาก";
			mes "เมนูต่อไปนี้ค่ะ";
			if (.@compass_check == 0) {
				mes "คุณต้องการให้ฉัน";
				mes "ทำเครื่องหมายระบุตำแหน่ง";
				mes "ลงบนแผนที่ขนาดเล็ก (Mini-Map) ไหมคะ?";
				next;
				if (select("ใช่:ไม่") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000สมาคม Swordman^000000:หอประชุม Swordman:ลานประลอง (Arena):ท่าเรือ Izlude:ร้านขายอาวุธ:ร้านขายเครื่องมือ:ยกเลิก")) {
				case 1:
					mes "[Izlude Guide]";
					mes "สมาคม Swordman";
					mes "ตั้งอยู่บนเกาะทางทิศตะวันตกของ";
					mes "Izlude ค่ะ หากคุณกำลังคิดจะเปลี่ยนอาชีพ";
					mes "เป็น Swordman คุณควรจะลองไปเช็คดูนะคะ";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Izlude Guide]";
					mes "หอประชุม Swordman (Swordman Hall)";
					mes "ตั้งอยู่บนเกาะทางทิศตะวันออก";
					mes "ที่เชื่อมต่อกับ Izlude ค่ะ";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Izlude Guide]";
					mes "ลานประลอง (Arena)";
					mes "อันเลื่องชื่อของ Izlude ตั้งอยู่ที่";
					mes "สุดเขตทางทิศเหนือของ Izlude ค่ะ";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Izlude Guide]";
					mes "คุณสามารถพบ";
					mes "ท่าเรือได้ที่ส่วนตะวันออกเฉียงเหนือ";
					mes "ของ Izlude ค่ะ ที่นั่นคุณสามารถ";
					mes "โดยสารเรือซึ่งจะพาคุณไปยัง";
					mes "Alberta หรือเกาะ Byalan ได้ค่ะ";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Izlude Guide]";
					mes "คุณสามารถค้นหา";
					mes "ร้านขายอาวุธได้ง่ายๆ";
					mes "ที่ทางตะวันตกเฉียงเหนือของ Izlude ค่ะ";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Izlude Guide]";
					mes "ร้านขายเครื่องมือนั้น";
					mes "หาไม่ยากเลยค่ะ ซึ่งตั้งอยู่ที่";
					mes "ส่วนตะวันออกเฉียงเหนือของ Izlude";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Izlude Guide]";
					mes "กรุณาบอกให้ฉัน ''ลบเครื่องหมาย";
					mes "ออกจากแผนที่ขนาดเล็ก'' หากคุณ";
					mes "ไม่ต้องการให้มีเครื่องหมาย";
					mes "ระบุตำแหน่งแสดงผลอยู่บน";
					mes "แผนที่ขนาดเล็กของคุณอีกต่อไปค่ะ";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Izlude Guide]";
			mes "ความก้าวหน้าทางเวทมนตร์และ";
			mes "เทคโนโลยีช่วยให้เราสามารถ";
			mes "อัปเดตระบบข้อมูลของเราได้";
			mes "ทำให้สามารถทำเครื่องหมายระบุ";
			mes "ตำแหน่งบนแผนที่ขนาดเล็กของคุณ";
			mes "เพื่อให้การนำทางง่ายขึ้นค่ะ";
			next;
			mes "[Izlude Guide]";
			mes "แผนที่ขนาดเล็กของคุณตั้งอยู่";
			mes "ที่มุมขวาบนของหน้าจอค่ะ";
			mes "หากคุณมองไม่เห็นมัน ให้กดปุ่ม";
			mes "Ctrl + Tab หรือคลิกปุ่ม ''Map''";
			mes "ในหน้าต่างข้อมูลพื้นฐาน (Basic Info) ของคุณค่ะ";
			next;
			mes "[Izlude Guide]";
			mes "บนแผนที่ขนาดเล็กของคุณ";
			mes "คลิกที่สัญลักษณ์ ''+'' และ ''-''";
			mes "เพื่อซูมเข้าและซูมออก";
			mes "จากแผนที่ขนาดเล็กค่ะ เราหวังว่า";
			mes "คุณจะสนุกกับการเดินทางที่นี่ ในเมือง Izlude นะคะ";
			break;
		case 4:
			mes "[Izlude Guide]";
			mes "ตกลงค่ะ ถ้าอย่างนั้น";
			mes "เชิญมาหาฉันได้ตามสบายเลยนะคะ";
			mes "ถ้าคุณรู้สึกหลงทาง";
			mes "ใน Izlude เข้าใจไหมคะ?";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}
