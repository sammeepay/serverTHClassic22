//===== rAthena Script =======================================
//= Rachel Guide
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2b
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Rachel City guide
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Change the while loops a bit. [L0ne_W0lf]
//= 1.2 Deleted an Unnecessary "next;" [Samuray22]
//	-Thanks to $ephiroth.
//= 1.2a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.2b Deleted a unnecessary "end". [Samuray22]
//============================================================

rachel,138,146,5	script	Rachel Guide	934,{
	mes "[Rachel Guide]";
	mes "ยินดีต้อนรับสู่ Rachel เมืองหลวงแห่ง";
	mes "Arunafeltz สถานที่ซึ่งลมหายใจอันอบอุ่น";
	mes "ของเทพธิดา Freya เอื้อมไปถึงค่ะ";
	mes "หากนี่เป็นครั้งแรกของคุณ";
	mes "ที่ใช้บริการนำทาง ทำไม";
	mes "ไม่ลองเช็คเมนู ''ประกาศ'' ดูก่อนล่ะคะ?";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("แนะนำหมู่บ้าน:ลบเครื่องหมายออกจากแผนที่ขนาดเล็ก:ประกาศ:ยกเลิก")) {
		case 1:
			mes "[Rachel Guide]";
			mes "ฉันสามารถบอกตำแหน่งอาคารใดก็ได้ตราบเท่าที่มันอยู่ใน Rachel ค่ะ";
			mes "แล้วคุณต้องการจะไปที่ไหนคะ?";
			if (.@compass_check == 0) {
				mes "คุณต้องการให้ฉัน";
				mes "ทำเครื่องหมายระบุตำแหน่ง";
				mes "ลงบนแผนที่ขนาดเล็กไหมคะ?";
				next;
				if (select("ใช่:ไม่") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("วิหาร Cheshrumnir:โรงแรม:ร้านขายอาวุธ:ร้านขายเครื่องมือ:สนามบิน:ยกเลิก")) {
				case 1:
					mes "[Rachel Guide]";
					mes "Cheshrumnir เป็นพื้นที่ศักดิ์สิทธิ์ที่องค์สันตะปาปา (Pope) ผู้เป็นร่างสถิตของเทพธิดา Freya พำนักอยู่ค่ะ";
					mes "ใช้เส้นทางไปทางทิศเหนือเพื่อหาอาคารนั้นนะคะ";
					if (.@compass_check == 1) viewpoint 1,150,249,1,0xFF0000;
					break;
				case 2:
					mes "[Rachel Guide]";
					mes "คุณสามารถพักผ่อนจากความเหนื่อยล้าในการเดินทางได้ที่โรงแรมค่ะ";
					mes "อาคารทางซ้ายที่อยู่ถัดจากฉันไปคือโรงแรมของ Rachel ค่ะ";
					if (.@compass_check == 1) viewpoint 1,115,149,2,0xFF00FF;
					break;
				case 3:
					mes "[Rachel Guide]";
					mes "คุณต้องการตรวจสอบอาวุธที่มีขายใน Rachel หรือเปล่าคะ?";
					mes "ร้านขายอาวุธตั้งอยู่ใกล้กับประตูทางทิศตะวันตกค่ะ";
					if (.@compass_check == 1) viewpoint 1,42,87,3,0x99FFFF;
					break;
				case 4:
					mes "[Rachel Guide]";
					mes "ร้านขายเครื่องมือของ Rachel ขายยาโพชั่นคุณภาพดีที่สุดค่ะ";
					mes "ตั้งอยู่ใกล้กับประตูทางทิศตะวันตกค่ะ";
					if (.@compass_check == 1) viewpoint 1,83,78,4,0x0000FF;
					break;
				case 5:
					mes "[Rachel Guide]";
					mes "สนามบินตั้งอยู่ด้านนอกประตูทางทิศตะวันออกค่ะ";
					if (.@compass_check == 1) viewpoint 1,273,125,5,0x00FF00;
					break;
				case 6:
					mes "[Rachel Guide]";
					mes "หากคุณต้องการกำจัดเครื่องหมายระบุตำแหน่งทั้งหมดออกจากแผนที่ขนาดเล็กของคุณ";
					mes "แค่ถามฉันอีกครั้ง แล้วเลือกเมนู ''ลบเครื่องหมายออกจากแผนที่ขนาดเล็ก'' นะคะ";
					set .@loop2,0;
					break;
				}
			}
			break;

		case 2:
			viewpoint 2,150,249,1,0xFF0000;
			viewpoint 2,115,149,2,0xFF00FF;
			viewpoint 2,42,87,3,0x99FFFF;
			viewpoint 2,83,78,4,0x0000FF;
			viewpoint 2,273,125,5,0x00FF00;
			mes "[Rachel Guide]";
			mes "ตกลงค่ะ เครื่องหมายพวกนั้นหายไปแล้ว หากคุณมีสถานที่อื่นที่จะถามอีก ก็บอกฉันได้เลยนะคะ";
			break;
		case 3:
			mes "[Rachel Guide]";
			mes "เมื่อคุณกำลังใช้เมนู ''แนะนำหมู่บ้าน''";
			mes "ตำแหน่งของอาคารจะถูกทำเครื่องหมายไว้บนแผนที่ขนาดเล็กที่มุมขวาบนของหน้าจอคุณค่ะ";
			mes "หากคุณมองไม่เห็นแผนที่ขนาดเล็ก ให้ใช้คีย์ลัด ''ctrl+tab'' หรือกดปุ่ม ''Map'' บนหน้าต่างข้อมูลพื้นฐานของคุณ ตกลงไหมคะ?";
			mes "และคุณยังสามารถซูมออกแผนที่ขนาดเล็กได้โดยใช้ปุ่ม ''-'' ในกรณีที่คุณไม่สามารถดูแผนที่ทั้งหมดของหมู่บ้านได้ค่ะ";
			break;
		case 4:
			mes "[Rachel Guide]";
			mes "ขอให้มีช่วงเวลาการเดินทางที่แสนวิเศษ";
			mes "ใน Arunafeltz นะคะ";
			close2;
			set .@loop1,0;
			break;
		}
	}
	close;
}
