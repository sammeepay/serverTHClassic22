//===== rAthena Script =======================================
//= Veins Guide
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Veins City guide
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Added second Veins guide NPC. [L0ne_W0lf]
//= 1.2 Fixed a few small typos. [L0ne_W0lf]
//============================================================

veins,210,345,5	script	Veins Guide#1::ve_guide	934,{
	mes "[Veins Guide]";
	mes "เมืองกลางทะเลทราย Veins ยินดีต้อนรับเหล่านักผจญภัยที่มาพึ่งพิงเพื่อหลบหนีจากพายุทรายอันโหดร้ายค่ะ";
	mes "หากนี่เป็นครั้งแรกของคุณที่ใช้บริการนำทาง ทำไมไม่ลองเช็คที่เมนู...";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("แนะนำหมู่บ้าน:ลบเครื่องหมายออกจากแผนที่ขนาดเล็ก:ประกาศ:ยกเลิก")) {
		case 1:
			mes "[Veins Guide]";
			mes "ฉันสามารถบอกตำแหน่งอาคารใดก็ได้ตราบเท่าที่มันอยู่ใน Veins ค่ะ";
			mes "แล้วคุณต้องการจะไปที่ไหนคะ?";
			if (.@compass_check == 0) {
				mes "คุณต้องการให้ฉัน";
				mes "ทำเครื่องหมายระบุตำแหน่ง";
				mes "ลงบนแผนที่ขนาดเล็กไหมคะ?";
				next;
				if (select("ใช่:ไม่") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("วิหาร:โรงแรม:ร้านขายอาวุธ:ร้านขายเครื่องมือ:เรือเหาะ:โรงเตี๊ยม (Tavern):สถาบันวิจัยทางธรณีวิทยา:ยกเลิก")) {
				case 1:
					mes "[Veins Guide]";
					mes "วิหารของเราตั้งอยู่ทางทิศเหนือค่ะ";
					mes "และมักจะคลาคล่ำไปด้วยเหล่าผู้ศรัทธาที่จริงใจต่อเทพธิดา Freya เสมอ";
					if (.@compass_check == 1) viewpoint 1,196,258,1,0xFF0000;
					break;
				case 2:
					mes "[Veins Guide]";
					mes "คุณสามารถพักผ่อนจากความเหนื่อยล้าในการเดินทางได้ที่โรงแรมค่ะ";
					mes "อาคารทางซ้ายที่อยู่ถัดจากฉันไปคือโรงแรมของ Veins ค่ะ";
					if (.@compass_check == 1) viewpoint 1,128,266,2,0xFF00FF;
					break;
				case 3:
					mes "[Veins Guide]";
					mes "ใช่ค่ะ คุณควรจะปกป้องตัวเองจากอันตรายด้วยตัวคุณเองนะคะ";
					mes "เลือกซื้ออาวุธคุณภาพสูงในราคาที่เอื้อมถึงได้";
					mes "ร้านขายอาวุธของ Veins ตั้งอยู่ทางทิศตะวันตกค่ะ";
					if (.@compass_check == 1) viewpoint 1,150,175,3,0x99FFFF;
					break;
				case 4:
					mes "[Veins Guide]";
					mes "คุณจัดเตรียมสิ่งของที่จำเป็นสำหรับการผจญภัยเพียงพอหรือยังคะ?";
					mes "ถ้ายัง ฉันแนะนำให้คุณไปเช็คดูว่าร้านค้าใจกลางเมือง Veins มีอะไรให้คุณได้บ้างค่ะ";
					if (.@compass_check == 1) viewpoint 1,230,161,4,0x0000FF;
					break;
				case 5:
					mes "[Veins Guide]";
					mes "โปรดทราบว่าที่ Veins ให้บริการเฉพาะเรือเหาะขนส่งสินค้าเท่านั้นนะคะ";
					if (.@compass_check == 1) viewpoint 1,273,285,5,0x00FF00;
					break;
				case 6:
					mes "[Veins Guide]";
					mes "หากคุณอยากทำความรู้จักกับ";
					mes "ชาวเมือง ฉันแนะนำให้คุณ";
					mes "ลองไปดื่มสักหน่อยที่โรงเตี๊ยม";
					mes "ทางทิศตะวันตกค่ะ";
					if (.@compass_check == 1) viewpoint 1,150,217,6,0x00FF00;
					break;
				case 7:
					mes "[Veins Guide]";
					mes "คุณสนใจที่จะศึกษาด้านธรณีวิทยาไหมคะ?";
					mes "ถ้าสนใจ คุณควรจะไปลองเช็คดูที่";
					mes "สถาบันวิจัยทางธรณีวิทยาที่อยู่บน";
					mes "ชั้น 2 ของร้านขายอาวุธนะคะ";
					if (.@compass_check == 1) viewpoint 1,150,175,7,0x00FF00;
					break;
				case 8:
					mes "[Veins Guide]";
					mes "หากคุณต้องการกำจัดเครื่องหมายระบุตำแหน่งทั้งหมดออกจากแผนที่ขนาดเล็กของคุณ";
					mes "แค่ถามฉันอีกครั้ง แล้วเลือกเมนู 'ลบเครื่องหมายออกจากแผนที่ขนาดเล็ก' นะคะ";
					set .@loop2,0;
					break;
				}
			}
			break;

		case 2:
			viewpoint 2,196,258,1,0xFF0000;
			viewpoint 2,128,266,2,0xFF00FF;
			viewpoint 2,150,175,3,0x99FFFF;
			viewpoint 2,230,161,4,0x0000FF;
			viewpoint 2,273,285,5,0x00FF00;
			viewpoint 2,150,217,6,0x00FF00;
			viewpoint 2,150,175,7,0x00FF00;
			mes "[Veins Guide]";
			mes "ตกลงค่ะ เครื่องหมายพวกนั้นหายไปแล้ว หากคุณมีสถานที่อื่นที่จะถามอีก ก็บอกฉันได้เลยนะคะ";
			mes "ขอให้มีความสุขกับการพักอยู่ที่ Veins ค่ะ";
			break;
		case 3:
			mes "[Veins Guide]";
			mes "เมื่อคุณกำลังใช้เมนู ''แนะนำหมู่บ้าน''";
			mes "ตำแหน่งของอาคารจะถูกทำเครื่องหมายไว้บนแผนที่ขนาดเล็กที่มุมขวาบนของหน้าจอคุณค่ะ";
			mes "หากคุณมองไม่เห็นแผนที่ขนาดเล็ก ให้ใช้คีย์ลัด ''ctrl+tab'' หรือกดปุ่ม ''Map'' บนหน้าต่างข้อมูลพื้นฐานของคุณ ตกลงไหมคะ?";
			mes "และคุณยังสามารถซูมออกแผนที่ขนาดเล็กได้โดยใช้ปุ่ม ''-'' ในกรณีที่คุณไม่สามารถดูแผนที่ทั้งหมดของหมู่บ้านได้ค่ะ";
			break;
		case 4:
			mes "[Veins Guide]";
			mes "ขอให้มีความสุขกับการพักอยู่ที่ Veins ค่ะ";
			close2;
			set .@loop1,0;
			break;
		}
	}
	close;
}

veins,189,101,5	duplicate(ve_guide)	Veins Guide#2	934
