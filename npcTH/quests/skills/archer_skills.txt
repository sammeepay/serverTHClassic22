//===== rAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Quest for skills: Arrow Crafting, Arrow Repel
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.6 Updated to latest available official file.
//=     also fixed Item issue [bugreport:5607]. [Masao]
//= 1.7 Fixed JobLevel requirements. [Euphy]
//============================================================

moc_ruins,118,99,5	script	Roberto	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_MAKINGARROW") == 1) {
			mes "[Roberto]";
			mes "โอ้ คุณมาจากบ้านเกิดของข้านี่นา!";
			mes "ยินดีที่ได้พบคุณนะ!";
			mes "คุณเป็นอย่างไรบ้าง?";
			mes "อ๊ะ! ลูกธนูนั้น!";
			mes "คุณทำมันขึ้นมาเองใช่ไหมล่ะ!";
			next;
			mes "[Roberto]";
			mes "ฮ่าฮ่า...!";
			mes "คุณคิดว่ามันดีกว่าเดิมมากเลยใช่ไหม?";
			mes "ฮ่าฮ่า... อย่างไรก็ตาม ข้าดีใจนะ";
			mes "นานๆ ทีก็กลับมาพร้อมกับ";
			mes "ข่าวคราวจากที่บ้านบ้างล่ะ";
			mes "ถ้าอย่างนั้น บ๊ายบาย~";
			close;
		}
		else if (JobLevel >= 30 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[Roberto]";
			mes "เอ๊ะ!";
			mes " ";
			mes "คุณคือ...";
			next;
			if ((countitem(907) > 19) && (countitem(921) > 6) && (countitem(906) > 40) && (countitem(1019) > 12) && (countitem(501) > 0)) {
				mes "[Roberto]";
				mes "คุณนำพวกมันมาแล้ว!";
				mes "ขอบคุณมากจริงๆ";
				mes "ถ้าอย่างนั้น ตามที่ข้าสัญญาไว้ ข้าจะสอนทักษะให้แก่คุณ";
				next;
				delitem 907,20; //Resin
				delitem 921,7; //Mushroom_Spore
				delitem 906,41; //Pointed_Scale
				delitem 1019,13; //Wooden_Block
				delitem 501,1; //Red_Potion
				skill "AC_MAKINGARROW",1,SKILL_PERM;
				mes "[Roberto]";
				mes "ตอนนี้ไม่ต้องกังวลเรื่องลูกธนูแล้วล่ะ";
				mes "โอ้ แล้วคุณบังเอิญได้เห็น";
				mes "คนที่ชื่อ Jason ใน Payon บ้างไหม?";
				mes "ระวังตัวด้วยนะ เขาเป็นคนที่";
				mes "ดุร้ายเอาเรื่องเลยล่ะ";
				next;
				mes "[Roberto]";
				mes "คุณแค่ต้องระวัง Jason ใน Payon ไว้ให้ดี";
				mes "จำไว้ล่ะ";
				mes "ถ้าอย่างนั้น บ๊ายบาย~ ขอบคุณสำหรับของขวัญนะ~";
				close;
			}
			mes "[Roberto]";
			mes "นักธนูใน Morocc งั้นเหรอ!?";
			mes "ยินดีที่ได้พบนะ! ไม่นึกเลยว่าจะได้เจอเพื่อน";
			mes "นักธนูในที่แบบนี้! *ฟุดฟิด ฟุดฟิด*!";
			mes "ข้ามาที่ Morocc ตัวคนเดียว..";
			mes "แต่ข้ายังเป็นหน้าใหม่ และความกดดันนั้น... แงงงง~";
			mes "ข้าเหงามากเลยล่ะ~";
			next;
			switch (select("มันคงจะลำบากน่าดู ไม่เป็นไรนะ จงมีความเชื่อมั่นเข้าไว้:จงทนทุกข์ต่อไปเถอะ")) {
			case 1:
				mes "[Roberto]";
				mes "ใช่ ขอบคุณนะ...";
				mes "คุณเองก็คงจะลำบาก";
				mes "ในที่แบบนี้เหมือนกันสินะ";
				mes "มันหาลูกธนูยากใช่ไหมล่ะ?";
				mes "นั่นคือสาเหตุที่ข้าทำมันขึ้นมาเอง";
				next;
				select("เอ๊ะ จริงเหรอ?!");
				mes "[Roberto]";
				mes "ใช่! ข้าสะสมไอเทมต่างๆ";
				mes "และใช้พวกมันทำลูกธนูขึ้นมา";
				mes "มันเป็นทักษะที่มีประโยชน์ที่ช่วยให้ข้า";
				mes "รอดชีวิตอยู่ตัวคนเดียวในโลกที่โหดร้ายนี้ได้";
				mes "ถ้าคุณต้องการ ข้าสอนให้ได้นะ";
				next;
				select("นั่นคงจะวิเศษมากเลย");
				mes "[Roberto]";
				mes "แต่ว่า.. ข้าทำให้ฟรีๆ ไม่ได้หรอก";
				mes "ไม่มีอะไรฟรีในโลกนี้หรอกนะ~";
				mes "อืม... เอาแบบนี้ดีไหม?";
				mes "คุณนำสิ่งที่ข้าขอมาให้ข้า";
				mes "แล้วข้าจะสอนทักษะให้แก่คุณ";
				next;
				mes "[Roberto]";
				mes "ข้าเหงามากตั้งแต่จากบ้านเกิดมา";
				mes "ข้าอยากจะรักษาโรคคิดถึงบ้าน";
				mes "ด้วยสิ่งของจากที่นั่น";
				mes "นำ Resin 20 อันจากต้นไม้ใน";
				mes "ป่า Payon และ Red Potion 1 ขวด";
				mes "ที่มีขายในร้านค้ามาให้ข้าที";
				next;
				mes "[Roberto]";
				mes "นอกจากนี้ก็มี Trunk 13 อันจากพวก Willow ที่";
				mes "อาศัยอยู่ใกล้ป่า Payon,";
				mes "Pointed Scale 41 อัน,";
				mes "Mushroom Spore 7 อัน";
				mes "ถ้าคุณนำทั้งหมดนี้มาให้ข้าได้";
				next;
				mes "[Roberto]";
				mes "ข้าจะสอนทักษะให้แก่คุณ";
				mes "ถ้าอย่างนั้น.. ข้าจะรอนะ";
				mes "สำหรับข่าวคราวจากบ้านเกิดของเรา";
				close;
			case 2:
				mes "[Roberto]";
				mes "...เฮ้ "+((Sex == SEX_MALE)?"พ่อหนุ่ม":"แม่สาวน้อย")+"";
				mes "...ระวังตัวตอนกลางคืนด้วยล่ะ";
				close;
			}
		}
		mes "[Roberto]";
		mes "หืม... คุณ";
		mes "มีอะไรจะพูดงั้นเหรอ?";
		mes "ข้าไม่มีอะไรจะคุยด้วยหรอก";
		mes "ระดับที่แตกต่างกันเกินไป";
		mes "ทำให้บทสนทนาขาดช่วง";
		close;
	}
	mes "[?]";
	mes "เอ่อ... เพิ่งเคยเห็นนักธนูหรือไง?";
	mes "ไปในที่ที่คุณจะไปเถอะ";
	mes "ข้าคุยเฉพาะกับนักธนูระดับสูงเท่านั้น";
	mes "ไม่อย่างนั้นข้าไม่ยอมปริปากหรอก!";
	close;
}

payon,103,63,3	script	Jason	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_CHARGEARROW") == 1) {
			mes "[Jason]";
			mes "เอ๊ะ เราเจอกันอีกแล้วนะ";
			mes "เอออออ แปลกจัง";
			mes "เมื่อไหร่ก็ตามที่ข้าเจอใครอีกครั้ง";
			mes "ข้าจะเริ่มอุทาน เอ๊ะ-เอ่อ บ่อยมาก";
			mes "เอ้อ... ยังไงก็ยินดีที่ได้พบคุณอีกครั้งนะ";
			mes "เอ่อ... อย่ามาอีกเลยนะ เอ้อ...";
			close;
		}
		else if (JobLevel >= 35 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[Jason]";
			mes "บ้าจริง... บาดแผลของข้าไม่ยอมหายสักที";
			mes "เฮ้อ.. ข้าประมาทเกินไป... ";
			mes "จนต้องมาเป็นแบบนี้.. อึก...";
			mes "แต่ถึงอย่างนั้น การทำร้ายข้าขนาดนี้";
			mes "สร้างบาดแผลให้ข้ามากมายขนาดนี้...";
			next;
			mes "ข้าควรจะทำอย่างไรกับ Roberto ดีนะ";
			mes "อืม... อ๊ะ!";
			mes "คุณน่ะ? มาอยู่ตรงนั้นนานแค่ไหนแล้ว?";
			mes "อืม... ระดับสูงมากเลยนะ";
			mes "คนอย่างคุณต้อง";
			mes "รู้วิธีการใช้ Arrow Repel แน่นอน";
			next;
			switch (select("มันคืออะไรเหรอ?:สอนข้าที")) {
			case 1:
				mes "[Jason]";
				mes "...คุณล้อเล่นใช่ไหม?";
				mes "โอ้คุณพระ คุณไม่รู้จักแม้กระทั่ง";
				mes "Arrow Repel ในระดับขนาดนี้เนี่ยนะ?";
				mes "คุณนี่เป็นคนที่แปลกจริงๆ";
				next;
				mes "[Jason]";
				mes "(Jason กำลังตกอยู่ในภวังค์)";
				next;
				mes "[Jason]";
				mes "เอาล่ะ ก็ได้ ข้าจะสอนให้ว่า";
				mes "Arrow Repel คืออะไร";
				next;
				mes "[Jason]";
				mes "Arrow Repel คือทักษะที่ช่วยให้คุณ";
				mes "ผลักคู่ต่อสู้ออกไปได้ทันทีที่คุณโจมตี";
				mes "คุณสามารถใช้มันได้เมื่อคุณเล็งไปที่";
				mes "เป้าหมายอย่างแม่นยำ แต่ไม่เหมือนกับเวทมนตร์นะ";
				mes "มันจะไม่ทำให้ผลลัพธ์ของเวทมนตร์หายไป";
				next;
				mes "[Jason]";
				mes "มันมีประโยชน์มากสำหรับนักธนู";
				mes "ที่อ่อนแอในการต่อสู้ระยะประชิด";
				mes "ถ้าคุณอยากจะเรียนรู้";
				mes "ก็กลับมาหาข้าอีกครั้ง";
				mes "มันมีวัตถุดิบที่จำเป็นบางอย่างอยู่";
				next;
				mes "[Jason]";
				mes "อย่างแรก เพราะคุณต้องดัดแปลงธนู";
				mes "ให้นำ Crossbow ที่คุณไม่ใช้มาหนึ่งอัน";
				mes "Tentacle 10 อัน, Bill of Bird 10 อัน,";
				mes "Yoyo Tail 3 อัน.. พวกนี้มันยืดหยุ่นมากเลยล่ะ";
				mes "นอกจากนี้ก็ Emerald 2 เม็ด และสุดท้ายที่ขาดไม่ได้...";
				mes "Banana Juice 36 ขวดที่ข้าโปรดปราน!";
				next;
				mes "[Jason]";
				mes "......อะแฮ่ม!";
				mes "ถ้าคุณนำของทั้งหมดนี้มาได้";
				mes "ข้าจะสอน Arrow Repel ให้แก่คุณ";
				mes "ถ้าอย่างนั้น ไว้พบกันใหม่";
				mes "(ข้าจะโกรธมากนะถ้าคุณไม่เอา Banana Juice มาด้วย)";
				close;
			case 2:
				if ((countitem(721) > 1) && (countitem(942) > 2) && (countitem(962) > 9) && (countitem(925) > 9) && (countitem(532) > 35)) {
					mes "[Jason]";
					mes "โอเค! สมบูรณ์แบบ!";
					mes "ข้าจะสอนทักษะที่มีชื่อเสียง";
					mes "ระดับประเทศอย่าง Arrow Repel ให้แก่คุณ!";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill "AC_CHARGEARROW",1,SKILL_PERM;
					mes "[Jason]";
					mes "โอ้ มันทำงานได้ดีกว่าที่ข้าคาดไว้เสียอีก!";
					mes "ไม่จำเป็นต้องดัดแปลงธนูเลยล่ะ!";
					mes "คุณนำธนูนี้คืนไปได้เลย~";
					mes "และขอให้สนุกกับการใช้ทักษะใหม่ที่คุณ";
					mes "เพิ่งสืบทอดไปในทุ่งกว้างและดันเจี้ยนนะ!";
					mes "เฮ~เฮะ~!";
					close;
				}
				mes "[Jason]";
				mes "อืม... น่าเสียดาย";
				mes "คุณยังขาดของบางอย่างไปนะ";
				mes "ย้ำอีกครั้ง คุณต้องใช้ Emerald 2 เม็ด,";
				mes "Yoyo Tail 3 อัน, Tentacle 10 อัน,";
				mes "Bill of Bird 10 อัน และสุดท้ายที่";
				mes "ขาดไม่ได้คือ Banana juice 36 ขวด!";
				next;
				mes "[Jason]";
				mes "ตรวจสอบให้แน่ใจว่าคุณมีครบทั้งหมดแล้วค่อยมาใหม่ล่ะ!";
				close;
			}
		}
		mes "[Jason]";
		mes "โอ้... คุณเป็นนักธนูสินะ";
		mes "ถ้าคุณพยายามอีกสักหน่อย";
		mes "คุณจะมีชื่อเสียงที่โด่งดัง";
		mes "ในฐานะนักธนูที่ยอดเยี่ยมแน่นอน!";
		mes "พยายามเข้านะ!";
		close;
	}
	mes "[?]";
	mes "ชีวิตต้องการอะไรจาก";
	mes "ชายหนุ่มที่โดดเดี่ยวอย่างข้ากันนะ?";
	close;

OnTouch_:
	mes "[???]";
	mes "เอ้ออออออ...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 Roberto message text is based off RO npc.  Jason message
//=     text is custom from old version.
//=     All items are from official quests though.[kobra_k88]
//= 1.0a Now using functions found in "Global_Functions.txt"
//=     for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================
