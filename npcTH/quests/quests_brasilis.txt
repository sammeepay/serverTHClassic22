//===== rAthena Script =======================================
//= Brasilis Quests
//===== Description: =========================================
//= [Official Conversion]
//= Lost Puppies (Repeatable, 24 hours.)
//= Suspicious Beach (Repeatable, 24 hours. iRO/cRO version.)
//= Guarana Candy Quest
//= Brasilis Water Lily Quest
//= Brasilis Dungeon Access Quest
//= Iara (Buff reward. Repeatable, 24 hours.)
//===== Changelog: ===========================================
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Hydra Ball (12408). [og2]
//= 1.2 Optimization. [Euphy]
//= 1.2a Added 'consumeitem' command. [Euphy]
//= 1.3 Added VIP features and fixed some EXP values. [Euphy]
//= 1.3a Clean-up. [Capuche]
//= 1.4 Added pre-renewal differences [Daegaladh]
//============================================================

// Lost Puppies :: dogdog.sc
//============================================================
brasilis,297,307,5	script	Angelo#br	1_M_04,{
	if (BaseLevel < 40) {
		mes "[Angelo]";
		mes "สัตว์เลี้ยงออกไปนอกหมู่บ้านหมดแล้ว~!!";
		mes "ตายจริง... ข้าควรทำยังไงดี... ?";
		close;
	}
	if (checkquest(9032,PLAYTIME) == 2) {
		erasequest 9032;
	}
	if (checkquest(9032,PLAYTIME) == 0) {
		mes "[Angelo]";
		mes "วันนี้ยังไม่จบลงเลย";
		mes "เจ้าสามารถช่วยได้แค่วันละครั้งเท่านั้นนะ ฮิฮิ";
		close;
	}
	if (checkquest(9030) == 1) {
		mes "[Angelo]";
		mes "สัตว์เลี้ยงของข้าอยู่ในทุ่งข้างนอกหมู่บ้าน";
		mes "ทำไมพวกมันถึงหนีไปนะ? ได้โปรดช่วยตามหาพวกมันที";
		close;
	}
	if (checkquest(9031) == 1) {
		mes "[Angelo]";
		mes "โอ้ ขอบคุณมาก เจ้าพบลูกสุนัขทั้ง 3 ตัวแล้ว";
		mes "ขอบใจเจ้าจริงๆ";
		mes "ข้าหวังว่าสิ่งนี้จะมีประโยชน์กับเจ้านะ โฮ่โฮ่";
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 75000,0;
		else
			getexp 50000,0;
		erasequest 9031;
		setquest 9032;
		specialeffect2 EF_ASSUMPTIO;
		if (checkre(0)) {
			consumeitem 607; //Yggdrasilberry
			.@rand = rand(1,10);
			if (.@rand > 4 && .@rand < 9) getitem 504,2; //White_Potion
			else if (.@rand > 8) getitem 608,1; //Seed_Of_Yggdrasil
		}
		close;
	}
	mes "[Angelo]";
	mes "เจ้าเป็นนักผจญภัยงั้นรึ? เจ้ามาได้ถูกจังหวะพอดีเลย";
	mes "พวกลูกสุนัขเริ่มหายตัวไป";
	mes "และมีคนบอกว่าเห็นพวกมันอยู่ที่ทุ่งข้างนอกหมู่บ้านน่ะ....";
	next;
	mes "[Angelo]";
	mes "มันค่อนข้างยากและอันตรายที่จะตามหาพวกมัน";
	mes "เจ้าต้องตามหา ^0000FFลูกสุนัข 3 ตัว^000000 นะ";
	setquest 9030;
	close;

OnInit:
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "Angelo#br::OnGo";
	end;

OnGo:
	emotion ET_SURPRISE;
	initnpctimer;
	end;
}

-	script	Puppy#bra	-1,{
	mes "[Puppy]";
	mes "โฮ่ง! โฮ่ง โฮ่ง!";
	if (checkquest(9030) == 1) {
		if (brazil_kid < 3) {
			next;
			brazil_kid++;
			mes "[" + strcharinfo(0) + "]";
			if (brazil_kid == 3) {
				mes "เยี่ยม ข้าพบลูกสุนัขครบทั้ง 3 ตัวแล้ว";
				mes "ตอนนี้ข้าต้องไปบอก Angelo";
				brazil_kid = 0;
				erasequest 9030;
				setquest 9031;
			}
			else {
				mes "อา... ไหนใครเป็นเด็กดีนะ?";
				mes "โอเค ตัวอื่นๆ อยู่ที่ไหนกันอีกล่ะ?";
			}
			if (compare(strnpcinfo(2),"1")) setarray .@i[0],2,3;
			else if (compare(strnpcinfo(2),"2")) setarray .@i[0],1,3;
			else setarray .@i[0],1,2;
			donpcevent "Puppy#"+charat(strnpcinfo(2),0)+.@i[rand(2)]+"::OnEnable";
			hideonnpc strnpcinfo(0);
		}
	}
	close;

OnInit:
	if (!compare(strnpcinfo(2),"1")) hideonnpc strnpcinfo(0);
	end;

OnEnable:
	hideoffnpc strnpcinfo(0);
	end;

OnDisable:
	hideonnpc strnpcinfo(0);
	end;
}

bra_fild01,98,96,3	duplicate(Puppy#bra)	Puppy#a1	4_DOG01
bra_fild01,59,116,5	duplicate(Puppy#bra)	Puppy#a2	4_DOG01
bra_fild01,62,142,3	duplicate(Puppy#bra)	Puppy#a3	4_DOG01
bra_fild01,80,163,3	duplicate(Puppy#bra)	Puppy#b1	4_DOG01
bra_fild01,73,210,3	duplicate(Puppy#bra)	Puppy#b2	4_DOG01
bra_fild01,80,210,3	duplicate(Puppy#bra)	Puppy#b3	4_DOG01
bra_fild01,38,235,3	duplicate(Puppy#bra)	Puppy#c1	4_DOG01
bra_fild01,307,64,3	duplicate(Puppy#bra)	Puppy#c2	4_DOG01
bra_fild01,260,60,3	duplicate(Puppy#bra)	Puppy#c3	4_DOG01
bra_fild01,234,101,3	duplicate(Puppy#bra)	Puppy#d1	4_DOG01
bra_fild01,200,84,3	duplicate(Puppy#bra)	Puppy#d2	4_DOG01
bra_fild01,176,63,5	duplicate(Puppy#bra)	Puppy#d3	4_DOG01

// Suspicious Beach :: Suspicious_Beach.sc
//============================================================
brasilis,192,133,6	script	Lucia#brasilis	4_F_BRZ_WOMAN,{
	/* -------------- Hydra Ball -------------------------
	if (countitem(12408) > 0) {
		delitem 12408,1; //Leaf_Cat_Ball
		getitem 12408,1; //Leaf_Cat_Ball
	}
	else if (countitem(6221) > 0) {
		delitem 6221,1; //Mystic_Leaf_Cat_Ball
		getitem 6221,1; //Mystic_Leaf_Cat_Ball
	}
	--------------------------------------------------- */
	if (BaseLevel < 40) {
		mes "[Lucia]";
		mes "สวัสดี";
		mes "ข้ากำลังกังวลเรื่อง ^FF0000Strange Hydra^000000 ที่อยู่บน";
		mes "ชายหาดทางทิศใต้น่ะ";
		mes "ข้าหวังว่าจะมีนักผจญภัยที่มีประสบการณ์";
		mes "มาช่วยเราบ้าง";
		emotion ET_OHNO;
		close;
	}
	.@nQState1 = checkquest(9028);
	.@nQState2 = checkquest(9029);
	if (.@nQState1 == -1) {
		mes "[Lucia]";
		mes "สวัสดี";
		mes "เจ้ามาที่นี่เพื่อล่า ^FF0000Strange Hydra^000000 งั้นหรือ?";
		next;
		switch(select("ใช่ครับ/ค่ะ.:ไม่ครับ/ค่ะ.:^006400เกิดอะไรขึ้นที่นี่หรือ?^000000")) {
		case 1:
			setquest 9028;
			getitem 12408,1; //Hydra_Ball
			mes "[Lucia]";
			mes "นี่ รับ ^006400Hydra Ball^000000 นี่ไปสิ";
			mes "ใช้มันเพื่อจับ ^FF0000Strange Hydra^8B4513.^000000";
			mes "ข้าหวังว่าเจ้าจะทำสำเร็จนะ!";
			close;
		case 2:
			mes "[Lucia]";
			mes "อา ข้าคงเข้าใจผิดไป";
			mes "แล้วเจอกันนะ";
			close;
		case 3:
		L_WhatHappen:
			mes "[Lucia]";
			mes "อยู่มาวันหนึ่งเจ้า ^FF0000Strange Hydra^000000";
			mes "ก็โผล่มาและล้อมเมืองนี้ไว้";
			mes "เราไม่แน่ใจว่าอะไรดึงดูดพวกมันมา แต่บางคนบอกว่าเป็นเพราะพวกเจ้านักผจญภัยนั่นแหละ";
			next;
			mes "[Lucia]";
			mes "ไม่ว่าจะอย่างไร เพื่อจะควบคุมเจ้า ^FF0000Strange Hydra^000000";
			mes "เจ้าต้องใช้อุปกรณ์ที่ออกแบบมาเป็นพิเศษที่เรียกว่า ^8B4513Hydra Ball^000000";
			next;
			mes "[Lucia]";
			mes "ถ้าเจ้ายังมี ^006400Hydra Ball^000000 อยู่";
			mes "กรุณาใช้มันกับ ^FF0000Strange Hydra^000000 ที่";
			mes "เจ้าหาพบได้ตามชายหาดนะ";
			mes "ถ้าเจ้าโชคดี อุปกรณ์นั้นจะทำงานได้อย่างสมบูรณ์";
			next;
			mes "[Lucia]";
			mes "ข้าหวังว่าจะมีนักผจญภัยจำนวนมาก";
			mes "อาสามาทำงานนี้";
			mes " ";
			mes "ข้าเกลียด Hydra จริงๆ!";
			emotion ET_CRY;
			close;
		}
	}
	else if ((.@nQState1 == 0) || (.@nQState1 == 1)) {
		if (countitem(6221) > 0) {
			mes "[Lucia]";
			mes "สวัสดี เจ้าทำได้จริงๆ ด้วย!";
			if (checkweight(11502,3)) {
				//mes "I don't have enough ^006400Hydra Ball^000000s to give you."; //Poorly translated by iRO?
				mes "ข้าหวังว่าเจ้าจะกลับมา";
				mes "ช่วยเหลือข้าอีกนะ";
				mes "ขอให้เป็นวันที่ดีจ้ะ~!";
				delitem 6221,1; //Mystic_Leaf_Cat_Ball
				completequest 9028;
				//recall_completequest 9029;
				if (.@nQState2 > -1) erasequest 9029;
				setquest 9029;
				if (checkre(0))
					consumeitem 607; //Yggdrasilberry
				consumeitem 12070; //Luk_Dish05
				consumeitem 12055; //Vit_Dish05
				consumeitem 12065; //Dex_Dish05
				if (checkre(0))
					getitem 11502,3; //Light_Blue_Pot
				else
					getitem 505,5; //Blue_Potion
				close;
			}
			else {
				mes " ";
				mes "ข้าอยากจะมอบรางวัลให้เจ้าอยู่หรอกนะ";
				mes "แต่กระเป๋าของเจ้ามันเต็มแล้ว";
				mes "กรุณาไปทำให้มีที่ว่างแล้วกลับมาใหม่นะ!";
				close;
			}
		}
		else {
			mes "[Lucia]";
			if (countitem(12408) < 1) {
				mes "เจ้าต้องการ ^006400Hydra Ball^000000 อีกลูกงั้นหรือ?";
				mes "ข้าจะให้เจ้าอีกหนึ่งลูก";
				getitem 12408,1; //Leaf_Cat_Ball
				close;
			}
			else {
				mes "มีปัญหาอะไรหรือเปล่า?";
				next;
				switch(select("เปล่าครับ/ค่ะ.:^006400ช่วยเล่าสิ่งที่เกิดขึ้นให้ฟังอีกรอบที^000000")) {
				case 1:
					mes "[Lucia]";
					mes "ตกลง ฝากด้วยนะจ้ะ";
					close;
				case 2:
					goto L_WhatHappen;
				}
			}
		}
	}
	else {
		mes "[Lucia]";
		mes "โอ้ ^0000FF"+strcharinfo(0)+"^000000 เจ้ากลับมาแล้ว";
		.@nCheckTime = checkquest(9029,PLAYTIME);
		if ((.@nCheckTime == 0) || (.@nCheckTime == 1)) {
			mes "ข้าซึ้งใจในความช่วยเหลือของเจ้ามาก";
			mes "เราแจก ^006400Hydra Ball^000000 หนึ่งลูก ^006400ในทุกๆ 24 ชั่วโมง^000000";
			mes "กรุณากลับมาในเวลาที่เหมาะสมนะจ้ะ";
			close;
		}
		else {
			//recall_completequest 9028;
			if (.@nQState1 > -1) erasequest 9028;
			completequest 9029;
			mes "เจ้ามาที่นี่เพื่อล่า ^FF0000Strange Hydra^000000 งั้นหรือ?";
			next;
			switch(select("ใช่ครับ/ค่ะ.:ไม่ครับ/ค่ะ.:^006400เกิดอะไรขึ้นที่นี่หรือ?^000000")) {
			case 1:
				setquest 9028;
				getitem 12408,1; //Leaf_Cat_Ball
				mes "[Lucia]";
				mes "นี่ รับ ^006400Hydra Ball^000000 นี่ไปสิ";
				mes "ใช้มันเพื่อจับ ^FF0000Strange Hydra^8B4513.^000000";
				mes "ข้าหวังว่าเจ้าจะทำสำเร็จนะ!";
				close;
			case 2:
				mes "[Lucia]";
				mes "อา ข้าคงเข้าใจผิดไป";
				mes "แล้วเจอกันนะ";
				close;
			case 3:
				goto L_WhatHappen;
			}
		}
	}
	end;

OnInit:
	initnpctimer;
	end;

OnTimer7000:
	emotion ET_SURPRISE;
	stopnpctimer;
	initnpctimer;
	end;
}

// Guarana Quest :: brazil_tre.sc
//============================================================
brasilis,187,162,5	script	Candy Maker	4_M_BRZ_MAN1,{
	if (!checkweight(1201,1)) {
		mes "- เจ้าไม่สามารถเริ่มเควสได้ กรุณาลดน้ำหนักในช่องเก็บของของเจ้าก่อน -";
		close;
	}
	if (brazil_gua == 0) {
		mes "[Candy Maker]";
		mes "นี่ เจ้าเคยรู้จักผลไม้ที่ชื่อว่า ^FF0000Guarana^000000 ไหม?";
		next;
		mes "[Candy Maker]";
		mes "Guarana เป็นเบอร์รี่ที่พิเศษมากที่ขึ้นในพื้นที่เฉพาะเท่านั้น มันช่วยคลายความเหนื่อยล้าทางร่างกาย และเพิ่มพลังให้แก่ร่างกาย อีกทั้งยังช่วยล้างของเสียออกจากร่างกายได้ด้วยนะ";
		next;
		mes "[Candy Maker]";
		mes "ข้าเคยขายลูกอมที่ทำมาจากมันเมื่อสมัยก่อน";
		mes "ข้าได้รับรางวัลจากการประกวด <สุดยอดผลิตภัณฑ์ประจำปี> อยู่บ่อยๆ เลยล่ะ วันคืนเหล่านั้นมันช่างดีจริงๆ";
		next;
		mes "[Candy Maker]";
		mes "ตั้งแต่นั้นมา ผลผลิตของมันก็ลดลงและราคาก็สูงขึ้นมาก จนตอนนี้ส่วนผสมลูกอมต้องเปลี่ยนเป็นมะพร้าวหรือผลไม้เมืองร้อนอื่นๆ แทน ข้าคิดถึงลูกอม Guarana จริงๆ";
		next;
		switch(select("ข้าจะชิมลูกอม Guarana นี้ได้ยังไงบ้าง?:จบการสนทนา")) {
		case 1:
			mes "[Candy Maker]";
			mes "หืม? สต็อกเก่าของข้าขายไปจนเกลี้ยงแล้วล่ะ";
			next;
			mes "[Candy Maker]";
			mes "แต่ถ้าเจ้าสามารถหา Guarana มาได้ ข้าจะทำให้เจ้าเอง";
			next;
			switch(select("ข้าจะหา Guarana ได้ที่ไหน?:จบการสนทนา")) {
			case 1:
				mes "[Candy Maker]";
				mes "เจ้าจะไปหา Guarana งั้นรึ?? โหหหห~";
				next;
				mes "[Candy Maker]";
				mes "เจ้าหาได้แน่รึ?";
				mes "มันน่าจะแพงมากเลยนะ";
				mes "การค้าขายไม่ใช่แนวข้าซะด้วยสิ ขอนึกก่อนนะ";
				next;
				mes "[Candy Maker]";
				mes "เดี๋ยวข้าจะแนะนำเจ้าให้รู้จักกับใครบางคนที่ข้าเคยทำธุรกิจ Guarana ด้วย";
				mes "เขาน่าจะยังวนเวียนกับเรื่องนี้อยู่";
				next;
				mes "[Candy Maker]";
				mes "เขาชื่อ Cherto";
				mes "ถ้าเจ้าหาเขาไม่เจอในเมือง ก็ลองไปดูที่พิพิธภัณฑ์นะ";
				mes "เขาเป็นคนชอบอวดตัว ดังนั้นเขาเลยชอบเก็กท่าใหญ่โต";
				mes "เขาน่าจะเดินเตร่อยู่ในพิพิธภัณฑ์เพื่อพยายามโชว์ออฟใส่ใครสักคนชัวร์ๆ";
				brazil_gua = 1;
				setquest 2192;
				close;
			case 2:
				mes "[Candy Maker]";
				mes "เจ้าไม่อยากลองชิมลูกอม Guarana หน่อยรึ?";
				close;
			}
		case 2:
			mes "[Candy Maker]";
			mes "วันคืนเหล่านั้นมันช่างดีจริงๆ...";
			close;
		}
	}
	else if (brazil_gua == 1) {
		mes "[Candy Maker]";
		mes "ถ้าเจ้าอยากได้ Guarana ไปหา Cherto ซะ";
		mes "บางทีเขาอาจจะอยู่ที่พิพิธภัณฑ์";
		close;
	}
	else if (brazil_gua == 10) {
		if (!countitem(6237)) {
			mes "- Guarana ที่ข้ามีหายไปซะแล้ว -";
			close;
		}
		delitem 6237,1; //Guarana_Fruit
		mes "[Candy Maker]";
		mes "เจ้าได้ Guarana มาแล้วงั้นรึ?";
		next;
		mes "- เจ้ามอบ Guarana ให้แก่เขา -";
		next;
		mes "[Candy Maker]";
		mes "ว้าว! เจ้ามีความสามารถพิเศษจริงๆ";
		mes "นี่เป็นผลที่ดีที่สุดเท่าที่ข้าเคยเห็นมาเลย เจ๋งมาก~!";
		next;
		mes "[Candy Maker]";
		mes "ดีล่ะ มาเริ่มทำลูกอมกันเถอะ~!";
		mes "ไม่ได้เจอกันนานเลยนะ ลูกอม Guarana สุดวิเศษของข้า...";
		next;
		mes "- แฮ่ก แฮ่ก แฮ่ก แฮ่ก แฮ่ก แฮ่ก -";
		mes "- แฮ่ก แฮ่ก แฮ่ก แฮ่ก แฮ่ก แฮ่ก -";
		next;
		mes "[Candy Maker]";
		mes "ดูนี่สิ! นี่แหละลูกอม Guarana ยอดนิยม";
		mes "ลองลิ้มรสชาติที่น่าทึ่งของมันดูสิ เฮ้~ ใจเย็นๆ นะ ฮ่าๆๆ!!";
		brazil_gua = 11;
		completequest 2200;
		getitem 12414,1; //Guarana_Candy
		if (checkre(0)) {
			if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
				getexp 105000,15000;
			else
				getexp 70000,10000;
		}
		close;
	}
	else if (brazil_gua == 11) {
		mes "[Candy Maker]";
		mes "ลูกอม Guarana นั่นคือผลงานชิ้นเอกที่โดดเด่นที่สุดในชีวิตข้าอย่างแน่นอน!";
		next;
		mes "[Candy Maker]";
		mes "ตั้งแต่ที่เจ้าช่วยข้า ผลผลิต Guarana ก็เพิ่มขึ้นอย่างต่อเนื่อง";
		mes "ดังนั้น ข้าเลยกลับมาทำลูกอม Guarana ได้เป็นปกติแล้วล่ะ";
		next;
		mes "[Candy Maker]";
		mes "ว่าไงล่ะ? อยากจะซื้อสักหน่อยไหม?";
		mes "ราคาเม็ดละ 4000 zeny นะ";
		next;
		switch(select("ซื้อลูกอม Guarana.:ยกเลิก")) {
		case 1:
			if (Zeny > 3999) {
				mes "[Candy Maker]";
				mes "นี่จ้ะ ลูกอม Guarana แสนอร่อย";
				Zeny = Zeny-4000;
				getitem 12414,1; //Guarana_Candy
				close;
			}
			else {
				mes "[Candy Maker]";
				mes "อะไรกัน? เจ้าควรจะบอกก่อนสิถ้าไม่มีเงิน!";
				mes "ต่อให้เจ้าจะจน ข้าก็ให้สิ่งนี้ฟรีๆ ไม่ได้หรอกนะ";
				close;
			}
		case 2:
			mes "[Candy Maker]";
			mes "บางครั้งบางคนก็ไม่ค่อยชอบมันนัก เพราะมันมีฤทธิ์กระตุ้นประสาทน่ะ";
			close;
		}
		close;
	}
	else {
		mes "[Candy Maker]";
		mes "ลูกอม Guarana นั่นคือผลงานชิ้นเอกที่โดดเด่นที่สุดในชีวิตข้าอย่างแน่นอน!";
		close;
	}
}

bra_in01,95,179,3	script	Cherto	4_M_BRZ_MAN2,{
	if (brazil_gua <= 1) {
		mes "[Cherto]";
		mes "หืม... เฮ้ พ่อหนุ่ม เจ้ามาจากข้างนอกสินะ ใช่ไหมล่ะ?";
		next;
		mes "[Cherto]";
		mes "Cherto ดูออกตั้งแต่วันแรกที่เจอเลยล่ะ เจ้าหลอก Cherto ไม่ได้หรอก";
		mes "Cherto มีสายตาเฉียบคมดั่งพญาอินทรี! ฮ่าๆๆ!";
		next;
		mes "[Cherto]";
		mes "โอเค โอเค ใช่ ใช่ ข้าเข้าใจแล้ว!";
		next;
		mes "[Cherto]";
		mes "จะว่าไป เจ้ามาถึง Brasilis แล้วแต่ไม่รู้จะทำอะไรล่ะสิ?";
		mes "ข้าพูดถูกไหม?";
		mes "เจ้าไม่รู้หรอกว่าเจ้าโชคดีแค่ไหนที่ได้พบผู้ช่วยที่เหมาะสมอย่างข้า";
		next;
		mes "[Cherto]";
		mes "จากสีหน้าของเจ้า Cherto เดาได้ว่าเจ้าคงอยากจะพูดว่า ''ท่านคือยอดฝีมือ!'' ใช่ไหมล่ะ?";
		mes "ข้า Cherto ผู้นี้ สามารถอ่านและมองทุกอย่างออกในทันที! มันเขียนอยู่บนหน้าเจ้าหมดแล้ว!";
		next;
		if (brazil_gua == 0) {
			mes "[Cherto]";
			mes "Cherto อยากจะอยู่ตรงนี้และอธิบายทุกอย่างให้เจ้าฟังนะ แต่ข้าเป็นคนที่มีธุระยุ่งมากเลยล่ะ";
			close;
		}
		else {// brazil_gua == 1
			mes "[Cherto]";
			mes "ถ้าเจ้ามีเรื่องที่สงสัยจะถาม Cherto ล่ะก็ ข้าจะกรุณาตอบให้เอง";
			next;
			select("Guarana?");
			mes "[Cherto]";
			mes "อะไรนะ? เจ้าอยากตามหา Guarana งั้นรึ?";
			next;
			mes "[Cherto]";
			mes "Guarana นั้นเติบโตเฉพาะในพื้นที่นี้เท่านั้น มันมีเนื้อในที่อ่อนนุ่มและถูกปกคลุมด้วยขนบางๆ";
			mes "มันอาจจะดูแปลกไปนิด แต่ดอกของมันใหญ่และส่งกลิ่นหอมสวยงามมาก";
			next;
			mes "[Cherto]";
			mes "นานมาแล้ว Guarana ถูกใช้เพื่อรักษาโรคและดับกระหาย แต่เมื่อไม่นานมานี้มันเริ่มเป็นที่นิยมในการฟื้นฟูพลังกายและเพิ่มการไหลเวียนของโลหิต";
			next;
			mes "[Cherto]";
			mes "ถึงแม้มันจะมีสรรพคุณที่ยอดเยี่ยม แต่ Cherto เสียใจที่จะต้องบอกเจ้าว่า เราหาพวกมันไม่ได้อีกแล้ว";
			next;
			select("อะไรนะะะ??");
			mes "[Cherto]";
			mes "ช่วงหลังมานี้ ผล Guarana ไม่เติบโตที่นี่อีกเลย";
			next;
			mes "[Cherto]";
			mes "ต่อให้ Cherto จะหาเจอสักผลหนึ่ง มันก็จะเน่าเสียอย่างรวดเร็ว";
			next;
			mes "[Cherto]";
			mes "ถ้าเพียงแต่มันไม่เกิดขึ้น!";
			next;
			select("ท่านกำลังพูดถึงเรื่องอะไร?");
			mes "[Cherto]";
			mes "เงียบหน่อย!!!!!!!!!!!!!!!!";
			mes "เรื่องนี้ถูกสั่งห้ามพูด! ใครบางคนอาจจะกำลังแอบฟังบทสนทนาของเราอยู่...";
			next;
			mes "[Cherto]";
			mes "ถ้า Cherto บอกเจ้าไป เจ้าอาจจะพาเราไปลำบากได้ แต่เจ้าดูเหมือนอยากจะรู้จริงๆ งั้นข้าจะบอกเคล็ดลับให้";
			mes "เข้ามาใกล้ๆ สิ Cherto จะกระซิบให้ฟัง จะได้ไม่มีใครแอบได้ยิน";
			brazil_gua = 2;
			close;
		}
	}
	else if (brazil_gua == 2) {
		mes "[Cherto]";
		mes "เด็กชาย Guarana ได้ถือกำเนิดขึ้น";
		next;
		select("เด็ก Guarana งั้นรึ?");
		mes "[Cherto]";
		mes "เคยมีผู้หญิงคนหนึ่งที่เป็นผู้เชี่ยวชาญด้านพฤกษศาสตร์";
		mes "เธอเป็นที่ชื่นชอบของสิ่งมีชีวิตทั้งหลายมาก";
		next;
		mes "[Cherto]";
		mes "ในช่วงเวลาที่ลูกของเธอเกิดมา เธอได้เริ่มทำฟาร์ม Guarana แต่ด้วยเหตุผลบางอย่าง พี่ชายของเธอเกิดความอิจฉาจึงทำลายฟาร์มนั้นทิ้งและหายตัวไป";
		mes "มันเป็นเรื่องราวประเภทนั้นแหละ...";
		next;
		mes "[Cherto]";
		mes "เราไม่แน่ใจว่าเด็กคนนั้นเกิดมาลืมตาดูโลกหรือไม่ แต่ตั้งแต่นั้นมา Guarana ทั้งหมดใน Brasilis ก็อันตรธานหายไป";
		next;
		mes "[Cherto]";
		mes "ใครกันคือเด็ก Guarana?";
		mes "Pedro ที่มีชื่อเสียงว่าเป็นคนละโมบ?";
		mes "Meto ที่ไม่สามารถทนเห็นผลไม้พวกนั้นได้?";
		mes "Hovenue ที่ดูเศร้าหมอง?";
		mes "พวกเขาอาจจะรู้ก็ได้นะ~!";
		next;
		mes "[Cherto]";
		mes "แล้วเจ้าล่ะ?";
		mes "ใครคือเด็ก Guarana?";
		mes "เจ้าจะหาคำตอบได้ไหม? โฮ่โฮ่โฮ่โฮ่~";
		brazil_gua = 3;
		changequest 2192,2193;
		close;
	}
	else if (brazil_gua == 3) {
		mes "[Cherto]";
		mes "เจ้าจะหาเด็ก Guarana เจอไหมนะ?";
		mes "อาจจะเจอ? หรืออาจจะไม่?";
		close;
	}
	else if (brazil_gua == 4) {
		mes "[Cherto]";
		mes "เจ้าหาเด็ก Guarana เจอหรือยัง?";
		next;
		mes "- ข้าเล่าให้ Cherto ฟังเรื่องเด็กที่ส่งเสียงเหมือนสัตว์ -";
		next;
		mes "[Cherto]";
		mes "โหหห นี่มันเหลือเชื่อจริงๆ";
		mes "เด็กคนนั้นอาจจะเป็นเด็ก Guarana ก็ได้ แน่นอนล่ะ...";
		mes "ตามตำนานเล่าว่าเด็กคนนั้นสามารถสื่อสารกับพวกสัตว์ได้";
		next;
		mes "[Cherto]";
		mes "ถ้าเขาสามารถส่งเสียงร้องของสัตว์ได้ พวกเขาก็อาจจะสนทนากันได้!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "แล้วตอนนี้ ข้าควรทำยังไงต่อ?";
		mes "ถ้าเขาเป็นเด็กในตำนานจริงๆ มีวิธีไหนที่จะทำให้ Guarana กลับมาเติบโตได้อีกไหม?";
		next;
		mes "[Cherto]";
		mes "ฮ่าๆๆ!! Cherto เคยบอกเจ้าว่ายังไงล่ะ?";
		mes "Cherto ผู้นี้รู้ทุกอย่าง~!!";
		mes "ข้าคิดขั้นตอนต่อไป";
		mes "ไว้เรียบร้อยแล้ว";
		next;
		mes "[Cherto]";
		mes "ใน Brasilis นี้มีจอมเวทผู้เชี่ยวชาญอยู่คนหนึ่ง";
		mes "เขาชื่อ Paje";
		mes "จงนำจดหมายนี้ไปให้เขา";
		mes "เขาจะแสดงวิธีแก้ปัญหาให้แก่เจ้าและเด็กคนนั้นเอง";
		brazil_gua = 5;
		changequest 2194,2195;
		close;
	}
	else {
		mes "[Cherto]";
		mes "โฮ่โฮ่ จั๊กจี้~จั๊กจี้~~~~!!!";
		close;
	}
}

brasilis,203,64,3	script	Strange Kid#bra	4_M_KID1,{
	if (brazil_gua < 3) {
		mes "[Strange Kid]";
		mes "................";
		close;
	}
	else if (brazil_gua == 3) {
		mes "[Strange Kid]";
		mes "................";
		next;
		if(select("ลองคุยดู.:แกล้งเดินผ่านไป.") == 2) {
			mes "[Strange Kid]";
			mes "................";
			close;
		}
		mes "ข้าควรจะพูดอะไรกับเขาดีนะ?";
		next;
		while(1) {
			switch(select("เจ้าชื่ออะไร?:เจ้าอายุเท่าไหร่?:เจ้ากำลังทำอะไรอยู่?:จบการสนทนา")) {
			case 1:
				mes "[Strange Kid]";
				mes "กาาาาาาาโอ้~";
				mes "กรรรรรรรร - กาาาน-";
				next;
				break;
			case 2:
				mes "[Strange Kid]";
				mes "บูวววววโว้วววว-";
				mes "บูวววววโว้วววว- -";
				next;
				break;
			case 3:
				mes "[Strange Kid]";
				mes "แช็มเบอร์ พ็อท เบร้ แช็มเบอร์ พ็อท เบร้ แช็มเบอร์ พ็อท เบร้ -";
				mes "เบี๊ยยยเบี๊ยยยย -";
				next;
				break;
			case 4:
				mes "[Strange Kid]";
				mes "กาาาาาาาโอ้~";
				mes "กรรรรรรรร - กาาาน-";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "เขาส่งเสียงประหลาดเหมือนพวกสัตว์เลย";
				mes "ข้าควรไปขอคำแนะนำจาก Cherto ดีไหมนะ?";
				brazil_gua = 4;
				changequest 2193,2194;
				close;
			}
		}
	}
	else if (brazil_gua == 4) {
		mes "["+strcharinfo(0)+"]";
		mes "เขาส่งเสียงประหลาดเหมือนพวกสัตว์เลย";
		mes "ข้าควรไปขอคำแนะนำจาก Cherto ดีไหมนะ?";
		close;
	}
	else if ((brazil_gua > 4) && (brazil_gua < 9)) {
		mes "[Strange Kid]";
		mes "อา...? อา.....?";
		close;
	}
	else if (brazil_gua == 9) {
		mes "[Strange Kid]";
		mes "อา... อะฮ้า.....";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าไม่มีเรื่องเล่าหรอกนะ แต่มีเพื่อนๆ มากมายกำลังรอเจ้าอยู่ข้างนอกน่ะ";
		next;
		mes "- เจ้ามอบขนนก, เนื้อสด และกิ่งองุ่นให้แก่เด็กน้อย -";
		next;
		mes "[Strange Kid]";
		mes "อา.............";
		next;
		mes "[Strange Kid]";
		mes "อึน, อื้อ....";
		mes "มอออ... ม่ามี้.....";
		next;
		mes "[Strange Kid]";
		mes "อา..........";
		mes "นก....";
		mes "ลิ้... ลิ้... ลิง......";
		mes "บู๊, บูวววว...........";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "หมางั้นเหรอ?!";
		mes "คิกคิก ใช่แล้วล่ะ เพื่อนๆ มากมายอยากจะพบเจ้านะ";
		mes "อย่าได้เหงาอีกต่อไปเลย และจงมีความสุขกับเพื่อนๆ ของเจ้านะ";
		next;
		mes "[Strange Kid]";
		mes "อา....เหะ...เหะ....";
		next;
		mes "- เขาเริ่มยิ้มออกมาบางๆ และหัวเราะ -";
		next;
		mes "[Strange Kid]";
		mes "ชะ......ใช่.......";
		mes "ขะ... ขอบ......ขอบ......คุณ.........นะ.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เอาล่ะ ช่างเป็นยิ้มที่น่ารักจริงๆ!";
		mes "จงเป็นเด็กที่มีความสุขตลอดไปนะ";
		next;
		mes "[Strange Kid]";
		mes "อื้อ......";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อะไรเหรอ?";
		next;
		mes "[Strange Kid]";
		mes "นี่..........";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เจ้าต้องการจะพูดอะไรเหรอ?";
		next;
		mes "- เจ้าขยับเข้าไปใกล้และทำท่าระมัดระวัง -";
		next;
		emotion ET_CHUP;
		mes "(จู๊บ~)";
		next;
		mes "- เด็กน้อยหัวเราะเบาๆ อีกครั้ง แล้ววางบางอย่างลงในมือเจ้า -";
		next;
		mes "- มันคือเบอร์รี่สดที่มีสีแดงและแข็ง -";
		next;
		mes "[Strange Kid]";
		mes "กัว... รา.......... นา...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อุ๊ย ผล Guarana งั้นเหรอ?";
		mes "อา! ขอบใจเจ้ามากนะ!";
		emotion ET_CHUPCHUP, playerattached();
		emotion ET_SMILE;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ถ้าข้ามีสิ่งนี้ ข้าก็สามารถทำลูกอม Guarana ได้แล้ว";
		mes "ข้าไปตามหา Candy Maker ดีกว่า!";
		brazil_gua = 10;
		changequest 2199,2200;
		getitem 6237,1; //Guarana_Fruit
		close;
	}
	else if (brazil_gua == 10) {
		if (!countitem(6237)) {
			mes "[Strange Kid]";
			mes "เหะ.........";
			getitem 6237,1; //Guarana_Fruit
			close;
		}
	}
	else {
		mes "- เด็กน้อยกำลังยิ้ม -";
		close;
	}
}

brasilis,56,224,7	script	Mage Paje#bra	4_M_BIBI,{
	mes "[Mage Paje]";
	mes "โอม มะลึกกึ๊กกึ๋ย~";
	specialeffect EF_POISONHIT,AREA,"Poring#bra";
	setarray .@display[0],800,876,909;
	setnpcdisplay "Poring#bra",.@display[rand(3)];
	if (brazil_gua != 5) close;
	next;
	mes "[Mage Paje]";
	mes "โอ้โฮ~!";
	mes "ข้ามีแขกมาเยือนล่ะ";
	mes "ดีใจที่ได้พบเจ้านะ";
	mes "ข้าคือจอมเวท Paje";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "สวัสดีครับ/ค่ะ คุณ Cherto บอกให้ข้ามาหาท่าน";
	next;
	mes "[Mage Paje]";
	mes "อืม.. คุณ Cherto งั้นรึ? เกิดอะไรขึ้นล่ะ?";
	mes "เจ้ามาที่นี่เพื่อถามเกี่ยวกับข่าวลือแปลกๆ มากมายล่ะสิ?";
	next;
	mes "- เจ้ายื่นจดหมายให้แก่ Paje-";
	next;
	mes "[Mage Paje]";
	mes "โอ้โฮ~";
	mes "อืม ตายจริง.. เรื่องมันเป็นแบบนี้นี่เอง";
	next;
	mes "[Mage Paje]";
	mes "ข้าไม่สามารถช่วยเจ้าได้โดยตรงหรอกนะ";
	mes "แต่ข้าจะมอบเวทมนตร์ง่ายๆ ให้ เพื่อที่เจ้าจะได้หาทางออกได้ด้วยตัวเอง";
	next;
	select("มันคือเวทมนตร์ประเภทไหนหรือ?");
	mes "[Mage Paje]";
	mes "มันคือเวทมนตร์ที่จะทำให้เจ้าดูเหมือนสัตว์ในสายตาของสัตว์ตัวอื่นๆ ยังไงล่ะ เจ๋งใช่ไหมล่ะ?";
	next;
	mes "[Mage Paje]";
	mes "เอาล่ะ~ ข้าจะมอบเวทมนตร์ให้เจ้า";
	mes "สัตว์ส่วนใหญ่จะประสาทสัมผัสไวมาก พวกมันอาจจะรู้ตัวได้ จงไปตามหา Toucan ในทุ่งที่ดูเหมือนจะไม่ทันสังเกตเห็นมนตรานี้ เจ้าจะรู้ได้เองเมื่อลองคุยกับมัน";
	next;
	mes "[Mage Paje]";
	mes "ขอให้โชคดีนะ~!";
	brazil_gua = 6;
	changequest 2195,2196;
	specialeffect2 EF_ASSUMPTIO;
	close;
}

brasilis,59,226,3	script	Poring#bra	4_PORING,{}

bra_fild01,75,83,5	script	Toucan#bra	2073,2,2,{
OnTouch:
	if (brazil_gua == 6) {
		mes "[Toucan]";
		mes "แว้กซ์!";
		mes "ข้าไม่เคยเห็นเจ้ามาก่อนเลย";
		mes "แว้กซ์!";
		next;
		mes "[Toucan]";
		mes "มันคือจุดกึ่งกลางระหว่างทวีปใหม่และทวีปเก่า... ข้ารู้ว่าข้าไม่เคยเห็นเจ้ามาก่อน แต่เจ้าดูคุ้นเคยอย่างประหลาด เหมือนผู้หญิงที่กำลังเต้นระบำแซมบ้าเลย ";
		next;
		select("ท่านกำลังพูดถึงเรื่องอะไร?");
		mes "[Toucan]";
		mes "ข้าสัมผัสได้ถึงพลังบางอย่างที่คล้ายกับเด็ก Guarana แว้กซ์!";
		next;
		mes "[Toucan]";
		mes "เด็กคนนั้นต้องผ่านช่วงเวลาที่โดดเดี่ยวมาก แว้กซ์! บางทีเจ้าอาจจะเป็นเพื่อนของเขา? แว้กซ์!!";
		next;
		select("ยังไม่ใช่ตอนนี้... แต่ข้าอยากเป็นเพื่อนกับเขา");
		mes "[Toucan]";
		mes "เด็กที่ได้รับการดูแลจากหญิงสาว Guarana ก็เป็นเพื่อนกับพวกสัตว์เช่นกัน แว้กซ์!";
		next;
		mes "[Toucan]";
		mes "ข้าอยากจะมอบสัญลักษณ์ตัวแทนจาก Toucan ให้แก่เด็กคนนั้น แว้กซ์!";
		next;
		mes "[Toucan]";
		mes "หากเจ้าต้องการคลายความเหงาให้เขา เจ้าจะช่วยข้าได้ไหม?";
		next;
		select("แน่นอน!");
		mes "[Toucan]";
		mes "นี่คือขนนกของข้า";
		mes "ส่งมันไปให้เด็กคนนั้นที";
		mes "พวกเราจะรักษาคำสัญญาแห่งมิตรภาพระหว่างเด็ก Guarana และ Toucan ตลอดไป แว้กซ์!";
		next;
		mes "- เจ้าหยิบขนนกมาจาก Toucan - ";
		next;
		mes "[Toucan]";
		mes "น่าจะมีผู้อื่นแถวนี้ที่เป็นเหมือนข้า";
		mes "ทำไมเจ้าไม่ลองไปหา Jaguar ดูล่ะ แว้กซ์!";
		next;
		mes "[Toucan]";
		mes "ข้าจะมอบคำอวยพรจาก Toucan ให้แก่เจ้า";
		next;
		mes "[Toucan]";
		mes "บินไป บินไปให้ไกล แว้กซ์ซซซ--!";
		brazil_gua = 7;
		changequest 2196,2197;
		specialeffect2 EF_SEISMICWEAPON;
		close2;
		warp "bra_fild01",68,146;
		end;
	}
	else {
		mes "[Toucan]";
		mes "แว้กซ์~! แว้กซ์~!";
		close;
	}
	end;
}

bra_fild01,34,184,5	script	Jaguar#bra	2072,2,2,{
OnTouch_:
	if (brazil_gua == 7) {
		mes "[Jaguar]";
		mes "โฮวว..โฮวว.....";
		next;
		mes "[Jaguar]";
		mes "ได้กลิ่น! กลิ่นนี้มันมาจากมนุษย์!";
		mes "ที่ไหนสักแห่ง มีมนุษย์อยู่!";
		mes "ข้าเจอตัวแล้ว เจ้าคือคนคนนั้น!!!";
		specialeffect EF_HIT1,AREA,"Jaguar#bra";
		emotion ET_HUK, playerattached();
		next;
		mes "[Jaguar]";
		mes "อย่าได้กลัวไปเลยมนุษย์";
		mes "ข้าไม่มีพละกำลังพอที่จะล่ามนุษย์แล้ว ข้าแค่เพียงรอคอยเวลาให้ชีวิตจบสิ้นลงในป่าแห่งนี้";
		next;
		mes "[Jaguar]";
		mes "จะว่าไป เจ้าสามารถพูดคุยกับข้าได้ เจ้าคือเด็ก Guarana งั้นรึ?";
		next;
		select("ใช่หรือ? ม...ไม่......ความจริงคือ....");
		mes "[Jaguar]";
		mes "ลูกชายของหญิงสาว Guarana กลายมาเป็นเพื่อนของพวกเราด้วยเช่นกัน";
		mes "พวกเขาดูแลทุกชีวิตอย่างล้ำค่า";
		mes "ข้าหวังว่าเจ้าจะเป็นเหมือนนาง";
		next;
		mes "[Jaguar]";
		mes "เสียงนกร้องจิ๊บๆ บอกข่าวข้า";
		mes "ลูกชายของหญิงสาว Guarana มีหัวใจที่บอบช้ำ";
		mes "พวกพี่ชายของนางทำให้เขาโดดเดี่ยว เจ้าไม่คิดอย่างนั้นรึ?";
		next;
		mes "[Jaguar]";
		mes "นี่คือเนื้อสดที่ข้าเพิ่งล่ามาได้เมื่อไม่กี่วันก่อน";
		mes "รับไปแล้วนำไปมอบให้เด็กที่น่าสงสารคนนั้นที";
		next;
		mes "[Jaguar]";
		mes "ข้ามอบสิ่งเล็กๆ นี้ให้เจ้าได้ ดังนั้นอย่าลืมมันล่ะ";
		mes "ป่าแห่งนี้จะต้อนรับเจ้าเสมอไม่ว่าเมื่อไหร่!";
		next;
		mes "- เจ้าได้รับเนื้อสดจาก Jaguar -";
		next;
		mes "[Jaguar]";
		mes "Monkey ผู้ที่ชอบยุ่งเรื่องชาวบ้านไปทั่ว อยากจะพบเจ้า";
		next;
		mes "[Jaguar]";
		mes "ข้าจะมอบคำอวยพรชั้นสูงของ Jaguar ให้แก่เจ้า";
		mes "จงไปหาเจ้าลิงโดยพริ้วไหวไปตามสายลมดั่งผึ้ง";
		mes "แล้วพบกันใหม่นะเพื่อนข้า!";
		brazil_gua = 8;
		changequest 2197,2198;
		close2;
		consumeitem 12016; //Speed_Up_Potion
		end;
	}
	else {
		mes "[Jaguar]";
		mes "กรรรรรร....";
		close;
	}
	end;
}

bra_fild01,245,53,3	script	#Monkeybra	1057,{}

bra_fild01,245,52,3	script	Monkey#bra	HIDDEN_NPC,{
	if (brazil_gua == 8) {
		mes "[Monkey]";
		mes "อะไรกัน??!!";
		mes "พวกเราไม่ต้อนรับมนุษย์? ออกไปซะ~!!";
		next;
		mes "[Monkey]";
		mes "เดี๋ยวๆ.... กลิ่นนั่น!!";
		mes "ข้าได้กลิ่น Jaguar จากตัวเจ้า เจ้าเป็นใครกัน?";
		mes "ตายจริง หรือจะมี Jaguar ที่ไม่มีขนและมีรูปร่างประหลาดแบบนี้กันนะ!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "........................";
		mes "นี่ท่านจะบอกว่าข้าดูเหมือนสัตว์งั้นรึ?!?!";
		next;
		mes "[Monkey]";
		mes "อ้าว? เจ้าไม่ใช่ Jaguar หรอกรึ?";
		next;
		mes "[Monkey]";
		mes "อ๋อออ Jaguar ส่งเจ้ามาหาข้าล่ะสิ?? เจี๊ยกๆ";
		mes "แต่เจ้าดูไม่เหมือนเด็ก Guarana เลยนะ";
		next;
		select("ข้ามาที่นี่เพื่อช่วยเขา");
		mes "[Monkey]";
		mes "ข้าได้ยินมาว่าเด็ก Guarana กลายเป็นเด็กขี้เหงาไปแล้ว จริงหรือเปล่า?";
		mes "พวกเราคือผู้เชี่ยวชาญด้านกายกรรม เด็กคนนั้นจะชอบไหมนะ?? เจี๊ยกๆๆ!";
		next;
		mes "[Monkey]";
		mes "มอบกิ่งองุ่นนี้ให้เด็ก Guarana ที";
		mes "พวกเราจะทำให้เขาสนุกได้ทั้งคืนทุกครั้งที่เขามาหาเราเลย!! เจี๊ยกๆๆ!";
		next;
		mes "- เจ้าได้รับกิ่งองุ่นจาก Monkey -";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เยี่ยม~ ได้เวลากลับไปหาเด็กคนนั้นแล้ว~!!";
		brazil_gua = 9;
		changequest 2198,2199;
		close;
	}
	else {
		mes "[Monkey]";
		mes "เจี๊ยกๆๆ!";
		close;
	}
}

// Water Lily Quest :: brazil_tre.sc
//============================================================
brasilis,203,286,3	script	Botanist Karmen#bra	4_F_HUWOMAN,{
	if (brazil_regia == 0) {
		mes "[Karmen]";
		mes "ภูมิอากาศของ Brasilis นั้นพิเศษมาก";
		mes "สภาพอากาศนี้เปิดโอกาสให้มีการศึกษาทางพฤกษศาสตร์กรณีพิเศษที่แตกต่างจากภูมิภาคอื่นๆ ในโลก";
		next;
		mes "[Karmen]";
		mes "พืชพรรณที่นี่มีโครงสร้างที่แข็งแรง ชัดเจน และมีสีสันสดใส";
		mes "ที่นี่พืชมีขนาดใหญ่มากจนเราสัมผัสได้ถึงตัวตนของพวกมัน";
		next;
		mes "[Karmen]";
		mes "หนึ่งในนั้นคือ Water Lily ซึ่งเป็นพืชที่งดงามและมีเอกลักษณ์มาก";
		mes "ดอกไม้ชนิดนี้ค่อนข้างอ่อนไหว ดังนั้นมันจึงไม่เบ่งบานไปทั่วทุกที่";
		next;
		if(select("น่าสนใจครับ/ค่ะ.:จบการสนทนา") == 2) {
			mes "[Karmen]";
			mes "ข้าเดาว่าเจ้าคงไม่สนใจเรื่องพฤกษศาสตร์สินะ";
			close;
		}
		mes "[Karmen]";
		mes "มันไม่ได้ปรากฏให้เห็นได้ง่ายๆ และเป็นดอกไม้ที่ลึกลับแม้แต่กับคนท้องถิ่น ดังนั้นชาว Brasilis จึงเชื่อว่าใครก็ตามที่พบมันจะได้รับโชคลาภอันยิ่งใหญ่";
		next;
		mes "[Karmen]";
		mes "ในฐานะนักพฤกษศาสตร์ ข้าวนเวียนอยู่แถวนี้เพื่อตามหาดอกไม้แห่งโชคลาภนี้ แต่ก็ตามที่คาดไว้ มันยังไม่ยอมปรากฏตัวออกมาเลย";
		next;
		mes "[Karmen]";
		mes "ข้าเชื่อว่าด้วยความพยายามที่มากพอ ดอกไม้ชนิดนี้จะแสดงความงดงามอันโชติช่วงให้ข้าเห็น";
		next;
		mes "[Karmen]";
		mes "อา ถ้าเจ้าสนใจเรื่องราวของ Water Lily มากกว่านี้ ลองไปหาคนชื่อ Marta ดูสิ";
		mes "นางเป็นคนฉลาดและรู้เรื่องราวมากมายที่นี่ใน Brasilis";
		brazil_regia = 1;
		setquest 2201;
		close;
	}
	else if (brazil_regia == 1) {
		mes "[Karmen]";
		mes "อา ถ้าเจ้าสนใจเรื่องราวของ Water Lily มากกว่านี้ ลองไปหาคนชื่อ Marta ดูสิ";
		mes "นางเป็นคนฉลาดและรู้เรื่องราวมากมายที่นี่ใน Brasilis";
		close;
	}
	else if (brazil_regia == 9) {
		mes "- เจ้าแสดงดอกบัวให้ Karmen ดูและเล่าเรื่องราวที่เกิดขึ้นให้ฟัง -";
		next;
		mes "[Karmen]";
		mes "ว้าว!! เจ้าได้รับประสบการณ์ที่ดีจริงๆ";
		mes "งั้นนนน~ Water lily ก็อาศัยอยู่ในส่วนลึกของ brasilis สินะ?";
		mes "ข้าจะลองไปหาดูอีกครั้งด้วยตัวเอง ข้าจะไม่ยอมแพ้เด็ดขาด!!";
		next;
		mes "[Karmen]";
		mes "ข้าซึ้งใจจริงๆ ที่ได้พบเจ้า";
		mes "Water lily ต้องเป็นดอกไม้แห่งโชคลาภจริงๆ แน่เลย ฮ่าๆๆ";
		brazil_regia = 10;
		completequest 2207;
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 75000, checkre(0)?15000:0;
		else
			getexp 50000, checkre(0)?10000:0;
		close;
	}
	else {
		mes "[Karmen]";
		mes "สภาพอากาศนี้เปิดโอกาสให้มีการศึกษาทางพฤกษศาสตร์กรณีพิเศษที่แตกต่างจากภูมิภาคอื่นๆ ในโลก";
		next;
		mes "[Karmen]";
		mes "พืชพรรณที่นี่มีโครงสร้างที่แข็งแรง ชัดเจน และมีสีสันสดใส";
		mes "ที่นี่พืชมีขนาดใหญ่มากจนเราสัมผัสได้ถึงตัวตนของพวกมัน";
		next;
		mes "[Karmen]";
		mes "มันคือความฝันของนักพฤกษศาสตร์เลยล่ะ";
		close;
	}
}

bra_in01,142,27,5	script	Marta#bra	4_F_BRZ_INDOLD,{
	if (brazil_regia == 1) {
		mes "[Brasilis Boy]";
		mes "คุณยาย! คนคนนั้นมีกลิ่นแปลกๆ ด้วยล่ะ";
		next;
		mes "[Marta]";
		mes "คนคนนี้ไม่ได้มาจากแถวนี้จ้ะ";
		mes "ทักทายแขกของเราหน่อยสิ";
		next;
		mes "[Brasilis Boy]";
		mes "เฮ้~ หวัดดี!!";
		mes "ข้าชื่อ Kaka!!";
		mes "เจ้าชื่ออะไรเหรอ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าชื่อ "+strcharinfo(0)+"";
		next;
		mes "[Brasilis Boy]";
		mes "คนนอกนี่ชื่อแปลกจัง!";
		mes "ไม่เป็นไรหรอก! ถ้าเราคุยกันไปเรื่อยๆ เดี๋ยวเราก็เป็นเพื่อนกันเอง! สู้ๆ นะ!";
		next;
		mes "[Marta]";
		mes "ฮิฮิ...";
		mes "เอาล่ะ พ่อคนแปลกหน้า เจ้ามาที่นี่ทำไมกันล่ะ~?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าได้ยินมาว่าท่านรู้เรื่องราวมากมาย นั่นเป็นเรื่องจริงหรือเปล่า?";
		next;
		mes "[Kaka]";
		mes "วู้วว! เจ้ารู้ได้ยังไงว่ายายข้ารู้เรื่องเยอะ สุดยอดเลย~?";
		mes "ยายข้าฉลาดและใจดีมาก ข้าเลยได้ฟังเรื่องต่างๆ เยอะเลยล่ะ";
		next;
		mes "[Marta]";
		mes "ฮิฮิ Kaka มักจะฟังเรื่องเล่ามากมายทุกคืน เขาชอบเรื่องของยายมากเลยล่ะ";
		mes "Kaka ทำให้ยายมีความสุขเสมอเพราะเขามักจะถามเรื่องที่น่าสงสัยเยอะแยะ นั่นแหละคือความสุขที่บริสุทธิ์";
		next;
		mes "[Marta]";
		mes "เอาล่ะ Kaka ทำไมไม่ชวนแขกของเราเข้าร่วมการพบปะเล็กๆ ของเราในวันนี้ดูล่ะ?";
		next;
		mes "[Kaka]";
		mes "ตกลงครับยาย~!!";
		next;
		mes "[Marta]";
		mes "นี่~ เจ้ามีเรื่องพิเศษเรื่องไหนที่อยากจะฟังเป็นพิเศษไหมล่ะ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เรื่องเกี่ยวกับ Water lily ที่ลึกลับน่ะครับ/ค่ะ";
		next;
		mes "[Marta]";
		mes "Water lily....";
		mes "มันเป็นเรื่องตั้งแต่นาน นานมาแล้ว";
		next;
		mes "[Marta]";
		mes "ก่อนที่ Brasilis จะถูกสร้างขึ้นเสียอีก";
		mes "มีเผ่าหนึ่งที่อาศัยอยู่กับน้ำตกยักษ์และป่าดงดิบเยี่ยงมิตรแท้ พวกเขาใช้เวลาทั้งหมดไปกับธรรมชาติ";
		next;
		mes "[Marta]";
		mes "หนึ่งในหัวหน้าเผ่ามีลูกสาวที่แสนสวยชื่อว่า 'Naia'";
		next;
		mes "[Marta]";
		mes "Naia ชอบฟังเรื่องเล่าเหมือนกับ Kaka เลย แม่ของนางจึงเล่าเรื่องเกี่ยวกับธรรมชาติและเทพเจ้าให้นางฟังทุกคืน";
		next;
		mes "[Kaka]";
		mes "โหววว นางเหมือนข้าเลย!";
		mes "นางต้องสวยมากแน่ๆ... ฮิฮิ";
		next;
		mes "[Marta]";
		mes "ตามที่แม่ของนางเล่าไว้...";
		mes "หากพระจันทร์บนท้องฟ้าเกิดรักใคร่หญิงสาวบนโลกมนุษย์เข้า เขาจะเปลี่ยนนางให้กลายเป็นดวงดาวเพื่อให้พวกเขาได้อยู่ด้วยกันตลอดไป";
		next;
		mes "[Marta]";
		mes "หลังจากที่ Naia ได้ยินเรื่องนี้ นางก็ไปหาพ่อเพื่อถามว่ามันเป็นเรื่องจริงหรือไม่";
		next;
		mes "[Kaka]";
		mes "แล้วเขาว่ายังไงเหรอ?";
		next;
		mes "[Marta]";
		mes "^3131FF'ลูกรัก Naia เอ๋ย พระจันทร์คือชายที่กล้าหาญที่สุดคนหนึ่ง แต่เขาไม่สามารถมีเจ้าสาวได้อีกต่อไปแล้ว ดังนั้นเจ้าจึงเป็นดวงดาวไม่ได้... พ่อเสียใจด้วยนะ~.'^000000";
		next;
		mes "[Kaka]";
		mes "Naia อยากจะเป็นเจ้าสาวของชายคนนั้นเหรอ?";
		next;
		mes "[Marta]";
		mes "ใช่แล้ว Kaka ลองจินตนาการดูสิว่าพระจันทร์นั้นสวยงามและลึกลับเพียงใด นั่นแหละคืออุดมคติของเหล่าหญิงสาวเลย";
		next;
		mes "[Kaka]";
		mes "แต่พระจันทร์ไม่พบมนุษย์เพื่อมาเป็นภรรยาอีกต่อไปแล้วเหรอ? แล้วเกิดอะไรขึ้นกับ Naia ต่อล่ะ?";
		next;
		mes "[Marta]";
		mes "Naia เป็นเด็กดีจริงๆ";
		next;
		mes "[Marta]";
		mes "แม้ว่าพ่อแม่จะพยายามห้ามปรามนาง แต่นางก็ยังคงเข้าป่าไปเพื่อพบพระจันทร์ในทุกๆ คืน";
		mes "น่าเศร้าที่แม้จะพยายามเพียงใด พระจันทร์ก็ไม่แสดงปฏิกิริยาใดๆ ต่อตัวนางเลย";
		next;
		mes "[Marta]";
		mes "วันหนึ่ง นางขึ้นไปบนยอดเขาเพื่อให้ได้เข้าใกล้เขามากขึ้น นางตัดสินใจพักผ่อนครู่หนึ่งแถวๆ ริมทะเลสาบ";
		next;
		mes "[Marta]";
		mes "นั่นคือตอนที่.. Naia ได้เห็นมัน";
		mes "มันคือพระจันทร์ที่ทอแสงระยิบระยับอยู่เหนือทะเลสาบที่พลิ้วไหวอย่างแผ่วเบา";
		next;
		mes "[Kaka]";
		mes "ข้ารู้แล้ว มันก็แค่แสงพระจันทร์ที่สะท้อนบนน้ำ ใช่ไหมล่ะ?!";
		next;
		mes "[Marta]";
		mes "ใช่แล้วจ้ะ แต่สำหรับนาง ภาพนั้นทำให้นางกระโดดลงไปในทะเลสาบโดยไม่ลังเลและจมน้ำสิ้นใจ";
		next;
		mes "[Kaka]";
		mes "โอ้ ไม่นะ";
		next;
		mes "[Marta]";
		mes "พระจันทร์เองก็เฝ้ามองนางจากบนท้องฟ้าเช่นกัน";
		mes "เขาเสียใจและสงสารนางมาก เขาจึงตัดสินใจเปลี่ยนนางให้กลายเป็นดอกไม้ที่แสนสวยเพื่อขอบคุณในรักแท้ของนาง";
		next;
		mes "[Marta]";
		mes "นั่นคือเรื่องราวของดอกไม้ลึกลับที่ผู้คนเรียกว่า Brasilis Water Flower";
		mes "ดอก Naia นี้จะเป็นสีขาวนวลในตอนกลางวัน แต่ในตอนกลางคืนจะเปลี่ยนเป็นสีแดงเนื่องจากสายใยรักที่มีต่อดวงจันทร์";
		next;
		mes "[Kaka]";
		mes "ช่างน่าเศร้าแต่ก็งดงามจริงๆ!";
		next;
		mes "[Marta]";
		mes "แล้วเจ้าล่ะ พ่อคนแปลกหน้า?";
		mes "เจ้าสนุกกับเรื่องนี้ไหม?";
		mes "ถ้าเจ้าอยากฟังเรื่องอื่นอีก ก็มาหาข้าได้นะ";
		mes "ถ้าเจ้าไม่ถือสาที่จะเล่นกับหลานชายข้าสัก ~ นิดล่ะก็นะ ฮิฮิ";
		brazil_regia = 2;
		close;
	}
	else if (brazil_regia > 1) {
		mes "[Kaka]";
		mes "คุณยายของข้ากำลังเหนื่อยนิดหน่อยจากการทำธุระของเผ่า!";
		mes "เจ้าช่วยกลับมาวันหลังได้ไหม?";
		close;
	}
	else {
		mes "[Marta]";
		mes "เจ้าไม่ใช่คนที่นี่";
		mes "ข้าสัมผัสได้ถึงกลิ่นอายของดินที่แปลกประหลาด";
		next;
		mes "[Marta]";
		mes "แต่ดวงตาของเจ้านั้นทอประกายด้วยความเข้มแข็ง";
		mes "จริงๆ แล้ว เจ้ากำลังแผ่ซ่านจิตวิญญาณและความมุ่งมั่นออกมาจากทั่วร่างกายเลย";
		next;
		mes "[Marta]";
		mes "หากเจ้าพยายามต่อไป เจ้าจะเป็นบุคคลที่ยิ่งใหญ่ในสักวันหนึ่ง";
		close;
	}
}
bra_in01,145,27,3	duplicate(Marta#bra)	Brasilis Boy#bra	472

brasilis,270,145,5	script	Brasilis Girl#bra	4_F_BRZ_INDIAN,5,5,{
	if (!checkweight(1201,1)) {
		mes "- เดี๋ยวก่อน!! -";
		mes "- เจ้ามีไอเทมเยอะเกินไป -";
		mes "- เจ้าจึงไม่สามารถรับไอเทมเพิ่มได้อีก -";
		mes "- ทำตัวให้เบาลงหน่อย -";
		mes "- แล้วค่อยลองใหม่อีกครั้ง -";
		close;
	}
	if (brazil_regia == 2) {
		mes "[เสียงจากที่ไกลๆ]";
		mes "Jasira!!!";
		mes "เจ้าจะไปไหนอีกแล้วรึ?!!";
		mes "กลับมานะ ได้โปรด!!";
		next;
		mes "[Brasilis Girl]";
		mes "แม่จ๋า หนูต้องออกไปข้างนอก!!";
		next;
		mes "[เสียงจากที่ไกลๆ]";
		mes "ไม่มีทาง~!! เจ้าไม่ควรไป!!";
		next;
		mes "[Brasilis Girl]";
		mes "โธ่เอ๋ย.. วันนี้ก็ล้มเหลวอีกจนได้";
		next;
		mes "[Brasilis Girl]";
		mes "......";
		mes "มีอะไรเหรอ? ทำไมมองข้าแบบนั้นล่ะ?";
		mes "ข้าไม่อยากเป็นเด็กโชว์หรอกนะ!! ออกไปเลย!!";
		next;
		if(select("ไม่มีอะไร ขอโทษทีนะ.:มีเรื่องอะไรเกิดขึ้นเหรอ?") == 1) {
			mes "[Brasilis Girl]";
			mes "ข้าเศร้ามากเลย!!!";
			close;
		}
		mes "[Brasilis Girl]";
		mes "มันไม่ใช่ธุระของเจ้านะ";
		mes "เจ้าก็แค่คนนอก!";
		next;
		if(select("เสียมารยาทจังนะ!:ก็แค่พยายามจะช่วย") == 1) {
			mes "[Brasilis Girl]";
			mes "ข้าจะเสียมารยาทแล้วมันเกี่ยวอะไรกับเจ้าล่ะ??!!";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "ข้าเขารู้ว่าข้าเป็นแค่คนที่ผ่านมา แต่ข้าอาจจะช่วยเจ้าได้นะ เจ้าคิดว่ายังไงล่ะ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "การพบกันครั้งนี้อาจเป็นมากกว่าแค่เรื่องบังเอิญก็ได้นะ";
		next;
		mes "[Brasilis Girl]";
		mes "......................";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "หืมม บอกชื่อเจ้าหน่อยได้ไหม?";
		next;
		mes "[Brasilis Girl]";
		mes "จา...";
		mes "Jasira";
		mes "ข้าชื่อ Jasira";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ชื่อเพราะจังนะ~";
		mes "Jasira เกิดอะไรขึ้นเหรอ?";
		next;
		mes "[Jasira]";
		mes ".............";
		next;
		mes "[Jasira]";
		mes "ข้าต้องไปพบ 'Jasi' แต่ข้าออกไปไม่ได้....";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Jasi คือใครเหรอ?";
		mes "คนรัก.... ของเจ้าเหรอ?";
		next;
		emotion ET_HUK;
		mes "[Jasira]";
		mes "ค...น...รัก...คนรักเหรอ??!!";
		mes "ไม่มีทางอะ~";
		next;
		mes "[Jasira]";
		mes "ถ้าเขาเป็นคนรักของข้า มันก็คงจะดี... แต่...";
		next;
		mes "[Jasira]";
		mes "Jasi คือ......";
		mes "ดวงจันทร์ที่แสนยิ่งใหญ่";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ดวงจันทร์เหรอ?";
		mes "หรือว่า... เจ้ากำลังพูดถึงดวงจันทร์จากในเรื่องเล่า?";
		next;
		mes "[Jasira]";
		mes "ใช่แล้ว!";
		mes "Jasi ที่รักคือดวงจันทร์จากบนท้องฟ้า!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ทำไมเจ้าถึงคิดแบบนั้นล่ะ?";
		next;
		mes "[Jasira]";
		mes "เพราะ Jasi งดงามมาก และสิ่งที่สำคัญที่สุดคือเขากำลังดูแล Water lily ใน Brasilis อยู่";
		next;
		emotion ET_HUK, playerattached();
		mes "["+strcharinfo(0)+"]";
		mes "Brasilis water lily งั้นเหรอ??!!";
		mes "นั่นคือดอกไม้ที่มีเอกลักษณ์มากไม่ใช่เหรอ?";
		next;
		mes "[Jasira]";
		mes "ถูกต้อง มันเป็นดอกไม้ที่ลึกลับและหาได้ยากมาก";
		mes "แต่รอบๆ ตัว Jasi น่ะ มี Water lily อยู่เต็มไปหมดเลย";
		mes "นั่นแหละข้าถึงเชื่อว่า Jasi คือดวงจันทร์";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Jasi อยู่ที่ไหนเหรอ?";
		next;
		mes "[Jasira]";
		mes "เขาอยู่ในป่าลึก";
		mes "อย่างที่เจ้าเห็น ข้าอ่อนแอมาก ข้าเลยต้องอยู่แต่ในบ้าน แต่ครั้งหนึ่ง ข้าเคยแข็งแรงพอที่จะออกจากหมู่บ้านนี้ไป";
		next;
		mes "[Jasira]";
		mes "ข้าแค่เดินเตร่ไปในป่าแล้วก็ล้มลงที่ไหนสักแห่ง และนั่นคือที่ที่ข้าได้พบเขา";
		mes "เขาใจดีมาก เขาช่วยรักษาข้าและนำทางข้ากลับบ้าน";
		mes "นั่นเป็นช่วงเวลาที่วิเศษจริงๆ";
		next;
		mes "[Jasira]";
		mes "ตั้งแต่ข้ากลับมาบ้าน พ่อแม่ก็ทำโทษข้า";
		mes "ข้าเข้าใจว่าทำไมพวกเขาถึงเป็นห่วง แต่ข้าคิดถึง Jasi มากเลย!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ทำไมไม่ไปพบเขาหลังจากที่เจ้าหายดีแล้วล่ะ?";
		next;
		mes "[Jasira]";
		mes ".................";
		mes "ข้าอยากเจอเขาตอนนี้เลย...";
		next;
		if(select("ช่วย Jasira.:เมินนางซะ.") == 2) {
			mes "["+strcharinfo(0)+"]";
			mes "เสียใจด้วยนะข้าช่วยเจ้าไม่ได้ สู้ๆ ล่ะ!";
			next;
			mes "[Jasira]";
			mes "ร้องไห้........";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "Jasira ข้ามาที่นี่เพื่อตามหา Brasilis water lily";
		mes "เจ้าไม่คิดว่าโชคชะตาพาเรามาพบกันเหรอ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ถ้าเจ้าบอกข้าว่าจพบ Jasi ได้อย่างไร ข้าจะช่วยเจ้าเอง";
		next;
		mes "[Jasira]";
		mes "จริงเหรอ? แต่ข้าก็ไม่รู้เหมือนกันว่าจะไปที่นั่นได้ยังไง ข้าแค่เดินหลงไปตอนที่พบเขาน่ะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เจ้าจำอะไรไม่ได้เลยเหรอ?";
		mes "ถ้าเจ้ารู้อะไรบางอย่าง เจ้าต้องบอกข้ามานะ";
		next;
		mes "[Jasira]";
		mes "ให้ข้านึกดูนะ... ข้ากำลังเดินอยู่แถวน้ำตก แล้วก็ตกลงไปในน้ำ จากนั้นข้าก็ถูกดูดเข้าไปที่ไหนสักแห่ง";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ดีมาก ดีกว่าไม่รู้อะไรเลย! ข้าจะลองหาสถานที่ที่คล้ายๆ กันดู";
		next;
		mes "[Jasira]";
		mes "ข้าให้ข้อมูลเจ้าไปแล้ว งั้นเจ้าจะช่วยอะไรข้าหน่อยได้ไหม?";
		mes "มันง่ายมากเลย...";
		next;
		mes "[Jasira]";
		mes "ข้าอยากจะมอบผลไม้แสนอร่อยให้เขา";
		mes "ที่ที่ Jasi อยู่นั้นดูสบายดี แต่ข้าไม่เห็นอาหารแถวนั้นเลย... ดังนั้นถ้าเขาได้เห็นผลไม้แสนอร่อย เขาคงจะดีใจมาก!";
		next;
		mes "[Jasira]";
		mes "เอา Banana 10 อันไปให้เขา และบอกเขาด้วยว่าข้าคิดถึงเขามากจริงๆ";
		mes "ขอโทษที่เมินเจ้าก่อนหน้านี้ด้วยนะ ได้โปรดเถอะ มีแค่เจ้าเท่านั้นที่ช่วยข้าได้!";
		brazil_regia = 3;
		changequest 2201,2202;
		close;
	}
	else if ((brazil_regia == 3) || (brazil_regia == 4)) {
		mes "[Jasira]";
		mes "ถ้าเจ้าพบ Jasi เอา Banana 10 อันให้เขาด้วยนะ";
		mes "ให้ข้านึกดู... ข้ากำลังเดินอยู่แถวน้ำตก แล้วก็ตกลงไปในน้ำ จากนั้นข้าก็ถูกดูดเข้าไปที่ไหนสักแห่ง";
		next;
		mes "[Jasira]";
		mes "ถ้าเจ้าหาทางไม่เจอ ให้ขึ้นไปที่น้ำตกแล้วถามพวกเด็กๆ ในหมู่บ้าน Brasilis ดู";
		mes "ข้าได้ยินมาว่ามีเด็กคนหนึ่งเคยไปในที่แปลกๆ มาก่อน เขาอาจจะไปที่เดียวกับข้าก็ได้!";
		close;
	}
	else if (brazil_regia == 5) {
		mes "[Jasira]";
		mes "เจ้าได้พบ Jasi หรือยัง?";
		mes "เจ้าได้เล่าเรื่องของข้าให้เขาฟังไหม?";
		mes "เจ้าไม่ได้เล่าเหรอ? เอ๊ะ? ตาบื้อ! โธ่เอ๋ย~!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "นี่สาวน้อย~ เจ้านี่อารมณ์ร้อนจริงๆ นะ";
		mes "ข้าได้พบเขาแล้ว และข้าก็เล่าเรื่องของเจ้าให้เขาฟังแล้วด้วย!";
		next;
		mes "[Jasira]";
		mes "จริงเหรอ?";
		mes "เขาว่ายังไงบ้าง?";
		mes "เขาจำข้าได้ไหม?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เขาบอกว่าเจ้าเป็นเด็กที่มีจิตใจดีจริงๆ~";
		mes "ข้าบอกเขาไปว่าเจ้าจะพยายามไปพบเขาเมื่อร่างกายของเจ้าดีขึ้น";
		next;
		mes "[Jasira]";
		mes "เย้!!!!!";
		mes "ขอบคุณนะ! เจ้านี่พึ่งพาได้มากกว่าที่ข้าคิดไว้ซะอีก";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "จะว่าไป ข้ากำลังตามหาผลไม้ที่มีสีน้ำตาลและมีเปลือกแข็ง";
		mes "มันมีน้ำอยู่ข้างในและใช้เป็นแก้วดื่มน้ำได้น่ะ";
		next;
		mes "[Jasira]";
		mes "โธ่! เจ้าหมายถึง Coconut ใช่ไหมล่ะ?!";
		mes "พวกมันมีอยู่ทั่วไปที่นี่ใน Brasilis เลย";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ขอบใจนะ Jasira!";
		brazil_regia = 6;
		changequest 2204,2205;
		close;
	}
	else if ((brazil_regia == 6) || (brazil_regia == 7)) {
		mes "[Jasira]";
		mes "ข้าควรจะดูแลร่างกายให้แข็งแรงด้วยตัวเอง!";
		mes "ข้าจะนอนอยู่บนเตียงตลอดไปไม่ได้หรอก เจ้าไม่เห็นด้วยเหรอ?";
		close;
	}
	else if (brazil_regia == 8) {
		mes "[Jasira]";
		mes "เอ๊ะ? ทำไมเจ้ากลับมาล่ะ?";
		next;
		mes "- เจ้าบอกสิ่งที่ Jasi ฝากมาบอกเธอ -";
		next;
		mes "[Jasira]";
		mes "โอ้... จริงเหรอ?";
		mes "เขาพูดแบบนั้นจริงๆ เหรอ?";
		mes "ตายจริง! ตายจริง!!!";
		mes "กรี๊ดดดดด - !!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ต้องขอบคุณเจ้า ข้าถึงได้ดอกไม้มา";
		mes "ขอบคุณมากนะ!!";
		next;
		mes "[Jasira]";
		mes "ว้าววว มันช่างสวยงามเหลือเกิน";
		next;
		mes "[Jasira]";
		mes "อา... ข้าขอดูมันสักครู่ได้ไหม?";
		next;
		mes "[Jasira]";
		mes "เซอร์ไพรส์~!!";
		mes "ข้ากำลังทำหมวกใบนี้ในตอนที่เจ้าไม่อยู่ และตอนนี้มันก็เสร็จสมบูรณ์พร้อมกับดอก Water lily แล้ว!";
		next;
		mes "[Jasira]";
		mes "รู้น่า รู้น่า! ข้านี่มันเก่งที่สุด...";
		delitem 7553,1; //Lotus_Flower - untradable
		brazil_regia = 9;
		changequest 2206,2207;
		setarray .@card[0],4195,4177,4188; //Leaf_Cat_Card, Dryad_Card, Leib_Olmai_Card
		getitem2 5302,1,1,0,0,.@card[rand(3)],0,0,0; //Lotus_Flower_Hat
		close;
	}
	else if (brazil_regia > 8) {
		mes "[Jasira]";
		mes "ข้าแค่ต้องแข็งแรงขึ้นอีกนิด!";
		mes "ข้าจะนอนบนเตียงตลอดไปไม่ได้ Jasi ของข้ากำลังรอข้าอยู่~";
		close;
	}
	else {
		mes "[เสียงจากที่ไกลๆ]";
		mes "Jasira!!!";
		mes "เจ้าจะไปไหนอีกแล้วรึ?!!";
		mes "กลับมานะ ได้โปรด!!";
		next;
		mes "[Brasilis Girl]";
		mes "ได้โปรดเถอะแม่จ๋า~!";
		mes "ให้หนูไปเถอะนะ!";
		close;
	}
	end;

OnTouch:
	if (brazil_regia == 2)
		emotion ET_FRET;
	end;
}

bra_dun02,67,205,5	script	Recluse#bra	4_M_BRZ_JACI,3,3,{
	if (brazil_regia == 3) {
		mes "[Recluse]";
		mes "โอ้ ข้าไม่ได้เห็นคนอื่นมานานมากแล้ว";
		next;
		if(select("เดินต่อไป.:ท่านคือพระจันทร์รึเปล่า?") == 1) {
			mes "[Recluse]";
			mes "เจ้าไม่มีธุระอะไรกับข้าเป็นการเฉพาะสินะ";
			close;
		}
		mes "[Recluse]";
		mes "พระจันทร์รึ?";
		mes "ข้าชื่อ Jasi";
		mes "ครอบครัวของข้าทำงานดูแล Water lily จากรุ่นสู่รุ่น";
		next;
		mes "[Jasi]";
		mes "ตามปกติแล้ว Brasilis water lily นั้นขี้อายเกินกว่าจะปรากฏตัวต่อหน้าผู้คน ดังนั้นพวกมันจึงบานในสถานที่ที่หายากเท่านั้น ข้าเดาว่าพวกมันคงชอบที่นี่น่ะนะ นั่นเป็นเหตุผลที่ข้าอยู่ที่นี่มานานมากแล้ว";
		next;
		mes "[Jasi]";
		mes "ครอบครัวของข้าดูแล Water lily อย่างเงียบสงบเพื่อป้องกันอันตรายจากเงื้อมมือของมนุษย์หรือพวกมอนสเตอร์";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ท่านพอจะจำเด็กสาวที่ชื่อ Jasira ได้ไหม";
		next;
		mes "[Jasi]";
		mes "จา... สิ..........";
		mes "อ๋อ!! แม่สาวจอมลุกลี้ลุกลนคนนั้น ";
		mes "ตายจริง.. ข้าต้องเจอปัญหาเพราะแม่สาวคนนั้นแท้ๆ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ปัญหาเหรอ?";
		next;
		mes "[Jasi]";
		mes "วันหนึ่งมีหญิงสาวคนหนึ่งปรากฏตัวขึ้นพร้อมกับบาดแผลมากมาย ข้าเลยช่วยเหลือนางไว้ ทันใดนั้นนางก็พยายามจะเด็ด Water lily แล้วขอให้ข้ารับนางเป็นเมีย หรือไม่ก็เปลี่ยนนางให้เป็น Water lily ช่างเป็นคนบ้าอะไรอย่างนี้!";
		next;
		mes "[Jasi]";
		mes "ข้าแทบจะทำให้นางสงบลงไม่ได้ แล้วส่งนางกลับไปที่หมู่บ้าน";
		mes "ชีวิตของข้าคือ Water lily เหล่านั้น ดังนั้นข้าจึงไม่อยากให้มีเรื่องน่าอายอะไรเกิดขึ้น";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "นี่คือของขวัญจาก Jasira เพื่อกล่าวขอโทษสำหรับตอนนั้นครับ/ค่ะ";
		mes "นางเสียใจจริงๆ ที่ไม่สามารถมาที่นี่ได้ด้วยตัวเองเนื่องจากความยากลำบากส่วนตัว";
		next;
		if (countitem(513) < 10) {
			mes "[Jasi]";
			mes "เจ้ากำลังพูดเรื่องอะไรน่ะ?";
			next;
			mes "- จริงด้วย ข้าลืมหยิบ Banana มา 10 อัน -";
			close;
		}
		mes "[Jasi]";
		mes "อา! Bananas! ว้าว ไม่ได้เห็นมานานแล้วนะเนี่ย นางก็ค่อนข้างใส่ใจเหมือนกันนะเนี่ย ใช่ไหม?";
		next;
		mes "[Jasi]";
		mes "เอาล่ะ นั่นคือธุระทั้งหมดที่เจ้ามีกับข้าใช่ไหม?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เอ่อ ความจริงข้ากำลังพยายามตามหา water lily แล้วก็บังเอิญมาพบท่านเข้า Jasira เล่าเรื่องเศร้าของนางให้ฟัง นั่นเลยเป็นสิ่งที่นำข้ามาที่นี่ครับ/ค่ะ";
		next;
		mes "[Jasi]";
		mes "ข้าเข้าใจละ";
		mes "อย่างที่เจ้าเห็น มี Brasilis water lily อยู่มากมายรอบๆ นี้";
		mes "ตราบเท่าที่เจ้ามั่นใจว่าจะไม่ทำลายพวกมัน เจ้าจะชื่นชมพวกมันได้ตามที่ต้องการเลย";
		brazil_regia = 4;
		changequest 2202,2203;
		close;
	}
	else if (brazil_regia == 4) {
		mes "[Jasi]";
		mes "เจ้าเพลิดเพลินกับ water lily หรือยังล่ะ?";
		close;
	}
	else if (brazil_regia == 5) {
		mes "[Jasi]";
		mes "ข้าลืมไปแล้วว่าผลไม้นั่นชื่ออะไร...";
		close;
	}
	else if (brazil_regia == 6) {
		if (countitem(11515) < 5) {
			mes "[Jasi]";
			mes "ข้าลืมไปแล้วว่าผลไม้นั่นชื่ออะไร...";
			close;
		}
		else {
			mes "[Jasi]";
			mes "เจ้าหาผลไม้นั่นเจอแล้วรึ?";
			mes "โอ้ ใช่แล้ว นี่คือ....?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "มันถูกเรียกว่า 'Coconut' (มะพร้าว) ครับ/ค่ะ";
			next;
			mes "[Jasi]";
			mes "อ้อ! COCONUT!!";
			mes "ข้าจำได้แล้ว ขอบใจเจ้ามาก ข้าจำไม่ได้แล้วว่าครั้งสุดท้ายที่ได้กินผลไม้นี้คือเมื่อไหร่";
			next;
			mes "[Jasi]";
			mes "ข้าคิดว่าข้าควรรักษาสัญญา";
			mes "เจ้าเอา Water lily ไปได้หนึ่งดอก";
			next;
			mes "[Jasi]";
			mes "ข้าหวังว่า Brasilis water lily จะเข้าใจข้านะ";
			mes "เจ้าควรจะรีบหยิบดอกไม้ไปในตอนที่มีโอกาสล่ะ~";
			next;
			mes "[Jasi]";
			mes "โอ้ เจ้าช่วยบอกอะไรแม่สาว Jasira นั่นแทนข้าหน่อยได้ไหม?";
			mes "บอกนางว่าข้าไม่ใช่พระจันทร์จากในเรื่องเล่าหรอกนะ แต่ข้าอยากจะเป็นพระจันทร์ที่ส่องแสงเพื่อรอนางเพียงผู้เดียว";
			delitem 11515,5; //Coconut
			brazil_regia = 7;
			changequest 2205,2206;
			close;
		}
	}
	else {
		mes "[Jasi]";
		mes "ดอกไม้ที่บานจาก Water lily วันนี้ช่างวิเศษจริงๆ";
		close;
	}
	end;

OnTouchNPC:
	unitwarp 0,"this",67,215;
	end;
}

bra_dun02,71,200,3	script	Water lily#bra	HIDDEN_NPC,{
	if (brazil_regia == 4) {
		mes "ดอก Water lily ที่ไม่ธรรมดากำลังเบ่งบานอยู่ที่นี่ เจ้าไม่สามารถละสายตาจากมันได้เลย โดยที่รู้ดีว่ามีน้อยคนนักที่จะได้เห็นดอกไม้ชนิดนี้บาน";
		next;
		if(select("เด็ดดอกไม้.:จ้องมองต่อไป") == 2) {
			mes "- เจ้าไม่สามารถละสายตาจากความงามของมันได้เลย -";
			close;
		}
		mes "[Jasi]";
		mes "เอ๊ะ! เจ้ากำลังทำอะไรน่ะ??!!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "มีคนที่ต้องการดอกไม้นี้จริงๆ ข้าขอเอาไปแค่ดอกเดียวได้ไหม?";
		next;
		mes "[Jasi]";
		mes "อย่างที่ข้าบอกไปก่อนหน้านี้ ข้าคือผู้พิทักษ์ Water lily เหล่านี้";
		mes "ข้าไม่สามารถยืนดูเฉยๆ ในขณะที่เจ้าเด็ดดอกไม้ไปจากมันแม้เพียงดอกเดียว";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "นี่ท่าน~ ข้าเอาผลไม้แสนอร่อยเหล่านี้มาให้ท่านด้วย... ลองดูสิ! มันคือกล้วยสดๆ ที่สุกกำลังดีเลยนะ";
		next;
		if (countitem(513) < 10) {
			mes "[Jasi]";
			mes "เจ้ากำลังพูดเรื่องอะไรน่ะ?";
			next;
			mes "- จริงด้วย ข้าลืมหยิบ Banana มา 10 อัน -";
			close;
		}
		mes "[Jasi]";
		mes "อืม... ไม่ได้กินผลไม้นี้มานานมากแล้วนะเนี่ย";
		next;
		mes "[Jasi]";
		mes "ข้าจะขอลองชิมสักลูกละกัน แค่นั้นแหละ";
		next;
		mes "- แจ๊บๆ -";
		mes "- ง่ำๆๆๆๆ -";
		specialeffect EF_POTION7,AREA,"Recluse#bra";
		next;
		mes "[Jasi]";
		mes "โอ้ รสชาตินี้มัน... คือ!";
		mes "ข้าจำได้ว่าแม่เคยเอามันไปอบเป็นขนมปังแสนอร่อย!";
		next;
		mes "[Jasi]";
		mes "มันทำให้ข้าคิดถึงวัยเด็กของข้าจริงๆ";
		next;
		emotion ET_HUK, getnpcid(0, "Recluse#bra");
		mes "[Jasi]";
		mes "โฮโฮ!!!!";
		mes "ข้าอยู่ที่นี่มานานเท่าที่ข้าจะจำความได้...";
		mes "ข้าไม่มีเวลาแม้แต่จะทำเรื่องง่ายๆ อย่างการกินผลไม้แสนอร่อยเลย";
		next;
		mes "[Jasi]";
		mes "มันเป็นกล้วยที่อร่อยมากจริงๆ";
		mes "แต่กฎก็คือกฎ!";
		mes "ข้าต้องทำตามหน้าที่ของข้า";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ได้โปรดเถอะ! ข้าต้องการแค่ดอกเดียวเองนะ~ ข้าต้องทำยังไงท่านถึงจะยอม?";
		next;
		mes "[Jasi]";
		mes "กฎก็คือกฎ เจ้าต้องการอะไรจากข้าล่ะ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "กล้วยนั่นไม่ได้ทำให้ท่านนึกถึงวัยเด็กหรอกเหรอ? มีอะไรที่ข้าพอจะหามาให้ท่านได้อีกไหม?";
		next;
		mes "[Jasi]";
		mes "พอเจ้าพูดขึ้นมา มันก็มีผลไม้อยู่อย่างหนึ่งที่ข้าคิดถึงมาก";
		mes "มันเป็นของโปรดของข้าตอนเด็กๆ แต่ข้าจำไม่ได้แล้วว่ามันเรียกว่าอะไร";
		next;
		mes "[Jasi]";
		mes "มันมีสีน้ำตาลและมีเปลือกแข็งหุ้มอยู่ ข้างในมีน้ำ และเจ้าสามารถใช้มันเป็นถ้วยได้หลังจากกินเนื้อข้างในเสร็จแล้ว";
		mes "เจ้ารู้ไหมว่ามันคืออะไร?";
		next;
		mes "[Jasi]";
		mes "ถ้าเจ้าหาของสิ่งนั้นมาให้ข้า 5 อัน ข้าจะลองพิจารณาข้อเสนอของเจ้าดูอีกที";
		delitem 513,10; //Banana
		brazil_regia = 5;
		changequest 2203,2204;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "โอเค งั้นข้าต้องไปหาผลไม้เปลือกแข็งมา 5 อัน";
		mes "อืม มันคืออะไรกันนะ?";
		close;
	}
	else if (brazil_regia == 5) {
		mes "[Jasi]";
		mes "มันเป็นของโปรดของข้าตอนเด็กๆ แต่ข้าจำไม่ได้แล้วว่ามันเรียกว่าอะไร";
		next;
		mes "[Jasi]";
		mes "มันมีสีน้ำตาลและมีเปลือกแข็งหุ้มอยู่ ข้างในมีน้ำ และเจ้าสามารถใช้มันเป็นถ้วยได้หลังจากกินเนื้อข้างในเสร็จแล้ว";
		mes "เจ้ารู้ไหมว่ามันคืออะไร?";
		close;
	}
	else if (brazil_regia == 7) {
		if (!checkweight(1201,1)) {
			mes "- เดี๋ยวสักครู่!! -";
			mes "- เจ้ามีไอเทมมากเกินไป -";
			mes "- ดังนั้นเจ้าจึงไม่สามารถรับไอเทมเพิ่มได้ -";
			mes "- ทำตัวให้เบาลงหน่อย -";
			mes "- แล้วลองใหม่อีกครั้ง -";
			close;
		}
		mes "- เจ้าหยิบดอก Water lily ที่แสนสวยขึ้นมาด้วยความระมัดระวัง -";
		brazil_regia = 8;
		getitem 7553,1; //Lotus_Flower
		close;
	}
}

// Dungeon Access Quest :: brazil_tre.sc
//============================================================
brasilis,185,246,5	script	Pedro#bra	1_M_SIZ,{
	if (brazil_ghost == 0) {
		OnTalk:
		mes "[Pedro]";
		mes "ว้าว มันเป็นรูปปั้นที่ยอดเยี่ยมจริงๆ!";
		next;
		mes "[Mariana]";
		mes "ใช่ไหมล่ะ?";
		mes "รูปปั้นนี้เรียกว่าอนุสาวรีย์ Verass";
		next;
		mes "[Mariana]";
		mes "นานมาแล้วมีนักผจญภัยที่กล้าหาญมากชื่อว่า Verass ด้วยความทุ่มเทในการสำรวจของเขา ทำให้ Brasilis สามารถพัฒนาจนกลายเป็นเมืองที่ยิ่งใหญ่แบบนี้ได้";
		next;
		mes "[Pedro]";
		mes "เจ๋งไปเลย!!";
		mes "ข้าอยากเป็นลูกผู้ชายตัวจริงเหมือน Verass จัง";
		next;
		mes "[Mariana]";
		mes "Pedro เจ้าสามารถเป็นอะไรก็ได้ที่เจ้าต้องการนะ";
		next;
		mes "[Pedro]";
		mes "Mariana ฉลาดจังเลยนะว่าไหม? ฮิฮิ";
		next;
		mes "[Fabio]";
		mes "โอ้วววว! เจ้ารักนางล่ะสิ ชัวร์เลย!";
		next;
		mes "[Daniel]";
		mes "วู้ววววว Pedro กับ Mari นั่งอยู่บนต้นไม้!";
		next;
		mes "[Fabio]";
		mes "วู้ววว กำลัง จ-ู-บ-กั-น~!!!";
		next;
		mes "[Daniel]";
		mes "แบร่ แบร่ แบร่!";
		next;
		mes "[Fabio]";
		mes "ฮ่าๆๆๆๆๆ";
		next;
		mes "[Pedro]";
		mes "หยุดทำตัวเหมือนเด็กทารกได้แล้ว!";
		next;
		mes "[Mariana]";
		mes "พวกนายนี่นะ~!";
		next;
		mes "[Daniel]";
		mes "จ้าๆ...";
		mes "เฮ้พวกนาย ได้ยินเรื่องที่เกิดขึ้นเมื่อไม่กี่วันก่อนไหม?";
		next;
		mes "[Mariana]";
		mes "อ๋อ ใช่~ ฉันได้ยินมาว่ามีเรื่องน่ากลัวจริงๆ เกิดขึ้นด้วยล่ะ";
		next;
		mes "[Fabio]";
		mes "อา ใช่ นั่นเป็นเหตุผลที่ Mariana กลัวจนไม่กล้าเข้าห้องน้ำตั้ง 3 วัน จนตัวเหม็นไปหมดเลยไงล่ะ อึ๋ยยย~";
		next;
		mes "[Daniel]";
		mes "ฮ่า ฮ่า ฮ่า! Mari ตัวเหม็น!";
		next;
		mes "[Mariana]";
		mes "ฉันเกลียดพวกนายจัง~!";
		mes "เลิกปล่อยข่าวลือเกี่ยวกับฉันได้แล้ว ฉันไม่ได้กลัวห้องน้ำซะหน่อย";
		mes "Pedro นายคิดว่าฉันตัวเหม็นเหรอ?";
		next;
		mes "[Pedro]";
		mes "เอ๊ะ? หือ?";
		mes "ม......ไม่มีทาง... ไม่หรอก";
		mes "เฮ้พวกนาย~ ทำดีกับนางหน่อยสิ~";
		next;
		mes "[Fabio]";
		mes "เจี๊ยกๆๆๆๆๆๆๆ";
		next;
		mes "[Daniel]";
		mes "คิกๆๆๆๆๆๆ";
		next;
		if(select("เดินผ่านไป.:ถามเรื่องข่าวลือ") == 1) {
			mes "[Fabio]";
			mes "Mariana~ ตัวเหม็น~ แบร่ แบร่~";
			next;
			mes "[Daniel]";
			mes "โธ่เอ๊ย นายนั่นแหละตัวเหม็นเหมือนกันแหละ~! แบร่ แบร่~";
			close;
		}
		mes "[Fabio]";
		mes "นายยังไม่ได้ยินเหรอ?";
		mes "เรื่องผีในพิพิธภัณฑ์ศิลปะไง";
		next;
		mes "[Daniel]";
		mes "โอ้ววววววว! น่ากลัวจังเลย~~~!";
		next;
		select("ช่วยเล่าให้ฟังมากกว่านี้หน่อยได้ไหม?");
		mes "[Fabio]";
		mes "เมื่อสองสามวันก่อนพวกเราไปทัศนศึกษาที่พิพิธภัณฑ์ศิลปะกับโรงเรียน";
		mes "นายก็รู้ ไม่มีอะไรพิเศษ ก็แค่การออกไปนอกสถานที่ธรรมดาๆ";
		next;
		mes "[Fabio]";
		mes "พิพิธภัณฑ์มันน่าเบื่อ ฉันกับเพื่อนบางคนเลยแอบหนีออกมาจากกลุ่ม~!";
		next;
		mes "[Fabio]";
		mes "นั่นแหละคือตอนที่เราได้ยินเสียงกรีดร้องดังก้องไปทั่วทั้งพิพิธภัณฑ์";
		next;
		mes "[Daniel]";
		mes "กริ๊ดดดดดดดดด!!";
		mes "กี้สสสสสสสสส!";
		mes "แค็กๆ..";
		next;
		mes "[Mariana]";
		mes "ฉันก็ได้ยินเสียงกรีดร้องเหมือนกัน...";
		mes "พวกนายนี่ชอบส่งเสียงดังในที่ที่ไม่ควรทำอยู่เรื่อยเลย";
		next;
		mes "[Pedro]";
		mes "แล้วเราควรจะทำอะไรล่ะ? ถ้าเราไม่ทำ เดี๋ยวคนอื่นก็ทำอยู่ดีนั่นแหละ";
		next;
		emotion ET_FRET, getnpcid(0, "Mariana#bra");
		mes "[Mariana]";
		mes "เฮ้อ~ บางทีพวกผู้ชายนี่ก็น่าหงุดหงิดจริงๆ";
		next;
		select("แล้วหลังจากนั้นเกิดอะไรขึ้น?");
		mes "[Fabio]";
		mes "Daniel กับฉันเดาว่าต้องมีอะไรแปลกๆ เกิดขึ้นแน่ๆ เราเลยวิ่งไปทางที่คิดว่าเป็นต้นตอของเสียงกรีดร้อง";
		next;
		mes "[Fabio]";
		mes "มันมาจากในห้องน้ำ";
		mes "เด็กบางคนกลัวมากจนเริ่มกรีดร้องตามแล้วก็หลับตาปี๋เลยล่ะ เรื่องมันชักจะแย่แล้ว";
		next;
		mes "[Daniel]";
		mes "ฉันว่านายนั่นแหละที่ฉี่หรืออึราดกางเกง กลิ่นงี้โชยมาเลย";
		next;
		mes "[Fabio]";
		mes "ไม่มีทาง~ แม่นายนั่นแหละฉี่ราด~ แบร่!";
		next;
		mes "[Daniel]";
		mes "ไม่จริง~ นายนั่นแหละ~ ฉี่ราด~";
		next;
		mes "[Fabio]";
		mes "เอาเถอะ สรุปก็คือ นั่นแหละคือที่มาของข่าวลือเรื่องผีในพิพิธภัณฑ์";
		next;
		mes "[Daniel]";
		mes "คนขี้จุ๊ เรื่องผีไม่มีจริงหรอก~";
		next;
		select("สรุปแล้วมันคือผีเหรอ?");
		mes "[Fabio]";
		mes "ฉันจะไปรู้ได้ยังไงล่ะ?";
		mes "ไม่มีใครพูดได้เต็มปากว่าเห็น และไม่มีใครอยากโดนคุณครูลงโทษด้วย";
		next;
		mes "[Pedro]";
		mes "ฉันได้ยินมาว่าถ้าท่องคาถาพิเศษ ผีจะออกมาล่ะ";
		next;
		mes "[Daniel]";
		mes "หยุดสอดเรื่องชาวบ้านได้แล้ว Pedro";
		next;
		mes "[Fabio]";
		mes "ใช่ นายพูดเรื่องอะไรน่ะ Pedro?";
		mes "แล้วนายเห็นผีหรือเปล่าล่ะ?";
		next;
		mes "[Pedro]";
		mes "ไ... ไม่ ฉันกลัวผีจะตาย";
		mes "แต่เพื่อนฉันบอกว่าพวกเขาเห็น และพวกเขาไม่ใช่คนขี้โกหกด้วย";
		next;
		select("มีใครบอกคาถาให้นายไหม?");
		mes "[Pedro]";
		mes "ฉันได้ยินมันมาในรูปแบบของบทเพลง";
		mes "คาถาพิเศษนั่นก็คือ...";
		next;
		mes "[Pedro]";
		mes "'^3131FFคุณแม่ขา ประตูมันเปิดไม่ได้!^000000'";
		mes "'^FF0000จงหมุนกุญแจตามจำนวนสีของรุ้งกินน้ำ^000000'";
		mes "'^3131FFคุณแม่ขา น้ำมันกำลังท่วม!^000000'";
		mes "'^FF0000หากดวงจันทร์หายไป 3 ครั้ง ก็อย่าได้กังวลไปเลย^000000'";
		mes "'^3131FFคุณแม่ขา ความแห้งแล้งมาเยือนแล้ว!^000000'";
		mes "'^FF0000ไม่ต้องห่วงนะ น้ำตกจะช่วยเยียวยามันเอง^000000'";
		mes "'^3131FFคุณแม่ขา เพื่อนๆ ของหนูอยู่ที่ไหน?^000000'";
		mes "'^FF0000เพื่อนทั้ง 7 ของเจ้ากำลังหลับใหล ถึงเวลาปลุกพวกเขาแล้ว^000000'";
		mes "'^3131FFคุณแม่ขา แม่อยู่ที่ไหน?^000000'";
		next;
		mes "[Mariana]";
		mes "อืม ฟังดูเหมือนปริศนาคำทายเลยนะ";
		next;
		select("อยากช่วยฉันตามหาผีตัวนี้ไหม?");
		mes "[Pedro]";
		mes "ตัวใครตัวมันเถอะเพื่อน~";
		next;
		mes "[Mariana]";
		mes "ฉันไม่ชอบเรื่องน่ากลัวนะ!";
		next;
		mes "[Fabio]";
		mes "เหอะ ฉันไม่อยากเชื่อเลยว่านายจะเชื่อเรื่องแบบนั้น";
		next;
		mes "[Daniel]";
		mes "ฉันจะทำตามที่ Fabio ทำ เหมือนเดิมทุกครั้งนั่นแหละ!";
		next;
		mes "[Fabio]";
		mes "บางทีนายอาจจะแค่กลัวก็ได้นะ...";
		brazil_ghost = 1;
		setquest 2208;
		close;
	}
	else if (brazil_ghost == 1) {
		mes "[Pedro]";
		mes "นายอยากฟังคาถาอีกรอบไหม?";
		next;
		mes "[Pedro]";
		mes "'^3131FFคุณแม่ขา ประตูมันเปิดไม่ได้!^000000'";
		mes "'^FF0000จงหมุนกุญแจตามจำนวนสีของรุ้งกินน้ำ^000000'";
		mes "'^3131FFคุณแม่ขา น้ำมันกำลังท่วม!^000000'";
		mes "'^FF0000หากดวงจันทร์หายไป 3 ครั้ง ก็อย่าได้กังวลไปเลย^000000'";
		mes "'^3131FFคุณแม่ขา ความแห้งแล้งมาเยือนแล้ว!^000000'";
		mes "'^FF0000ไม่ต้องห่วงนะ น้ำตกจะช่วยเยียวยามันเอง^000000'";
		mes "'^3131FFคุณแม่ขา เพื่อนๆ ของหนูอยู่ที่ไหน?^000000'";
		mes "'^FF0000เพื่อนทั้ง 7 ของเจ้ากำลังหลับใหล ถึงเวลาปลุกพวกเขาแล้ว^000000'";
		mes "'^3131FFคุณแม่ขา แม่อยู่ที่ไหน?^000000'";
		close;
	}
	else {
		mes "[Pedro]";
		mes "ฉันสงสัยจังว่าต้องทำยังไงถึงจะมีรูปปั้นของตัวเองบ้างนะ?";
		close;
	}
}

brasilis,187,244,1	script	Mariana#bra	1_F_MARIA,{
	if (brazil_ghost == 0) {
		doevent "Pedro#bra::OnTalk";
		end;
	}
	else if (brazil_ghost == 1) {
		mes "[Mariana]";
		mes "พวกนายเลิกคุยเรื่องผีกันได้ไหม?";
		mes "ฉันขนลุกไปหมดทั้งตัวแล้วเนี่ย";
		close;
	}
	else {
		mes "[Mariana]";
		mes "ทำไม Fabio กับ Daniel ชอบมาตอแยพวกเราอยู่เรื่อยเลยนะ?";
		close;
	}
}

brasilis,181,250,5	script	Fabio#bra	4_M_KID1,{
	if (brazil_ghost == 0) {
		doevent "Pedro#bra::OnTalk";
		end;
	}
	else if (brazil_ghost == 1) {
		mes "[Fabio]";
		mes "นายยังจะเสียเวลากับเรื่องผีนั่นอยู่อีกเหรอ?";
		close;
	}
	else {
		mes "[Fabio]";
		mes "Mariana อยากดูอะไรเจ๋งๆ ไหม?";
		next;
		mes "[Mariana]";
		mes "กรี๊ดดดดดดดดด!! แมลง!! เอาพวกมันออกไปนะ!";
		close;
	}
}

brasilis,180,249,5	script	Daniel#bra	4_M_KID1,{
	if (brazil_ghost == 0) {
		doevent "Pedro#bra::OnTalk";
		end;
	}
	else if (brazil_ghost == 1) {
		mes "[Daniel]";
		mes "แบร่ แบร่ แบร่~";
		close;
	}
	else {
		mes "[Daniel]";
		mes "คิกๆ นี่ไง~ ฉันเจอแมลงเพิ่มแล้ว~";
		close;
	}
}

bra_in01,149,184,3	script	Door#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- มีลูกกุญแจเสียบอยู่ในประตูที่ถูกล็อค -";
		next;
		switch(select("หมุนกุญแจ.:ไม่ต้องสนใจ")) {
		case 1:
			mes "เจ้าเริ่มท่องคาถาบรรทัดแรก";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother the door won't open!";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าเจ้าจะพูดอะไรผิดไปนะ";
				close;
			}
			else {
				if (brazil_ghost == 2) {
					mes "[เสียงสะอื้น]";
					mes "'^FF0000จงหมุนกุญแจตามจำนวนสีของรุ้งกินน้ำ^000000'";
					next;
					switch(select("เปิดประตู:เคาะประตู.:หมุนกุญแจ.:ดึงกุญแจออก")) {
					case 1:
						mes "ประตูถูกล็อคอยู่";
						mes "ไม่มีอะไรเกิดขึ้น";
						close;
					case 2:
						mes "ควรจะลองเคาะกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						mes "เจ้าเคาะประตูไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 3:
						mes "ควรจะหมุนกุญแจกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						if (.@input == 7) {
							mes "เจ้าหมุนกุญแจไป 7 ครั้ง";
							next;
							mes "คลิก! คลิก! คลิก!";
							mes "คลิก! คลิก! คลิก!";
							mes "คลิก...!";
							next;
							mes "[เสียงจากที่ไกลๆ]";
							mes "^FF0000คิก... ฮิฮิฮิฮิ!!!^000000";
							mes "เจ้าได้ยินเสียงน้ำไหล";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางห้องน้ำ";
							brazil_ghost = 3;
							changequest 2208,60351;
							close;
						}
						else {
							mes "เจ้าหมุนกุญแจไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					case 4:
						mes "ควรจะเสียบกุญแจเข้าที่ประตูกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						mes "เจ้าเสียบกุญแจไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					}
				}
				else {
					mes "Mother the door won't open!";
					close;
				}
			}
		case 2:
			mes "เจ้าไม่ได้ทำอะไร";
			close;
		}
	}
	else {
		mes "- มีลูกกุญแจเสียบอยู่ในประตูที่ถูกล็อค -";
		close;
	}
}

bra_in01,144,187,3	script	Toilet#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- ดูเหมือนส้วมธรรมดาๆ -";
		next;
		if (brazil_ghost > 6) {
			switch(select("กดชักโครก.:ไม่ต้องทำอะไร")) {
			case 1:
				mes "หลังจากกดชักโครก ทันใดนั้นเจ้าก็รู้สึกเวียนหัวและถูกพัดพาไปยังที่ไหนสักแห่ง";
				specialeffect2 EF_WATERFALL_SMALL_T2_90;
				close2;
				warp "bra_in01",206,102;
				end;
			case 2:
				mes "น้ำในส้วมดูน่าขยะแขยงจัง";
				close;
			}
		}
		switch(select("ใช้ส้วม:ไม่ต้องสนใจ")) {
		case 1:
			mes "- คาถาบรรทัดที่สองนั่นว่ายังไงนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother the water is flooding!";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าเจ้าจะพูดอะไรผิดไปนะ";
				close;
			}
			else {
				if (brazil_ghost == 3) {
					mes "[เสียงสะอื้น]";
					mes "^FF0000หากดวงจันทร์หายไป 3 ครั้ง... ก็อย่าได้กังวลไปเลย.....^000000";
					next;
					switch(select("กดชักโครก.:ปิดฝาส้วม")) {
					case 1:
						mes "ควรจะกดชักโครกกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						if (.@input == 3) {
							mes "เจ้ากดชักโครกไป 3 ครั้ง";
							next;
							mes "ซ่าาาาาาาาาา!";
							mes "ซ่าาาาาาาาาาาา!";
							mes "ซ่าาาาาาาาาาาาาาาาาาา!";
							next;
							mes "[เสียงจากที่ไกลๆ]";
							mes "^FF0000คิก... ฮิฮิฮิฮิ!!!^000000";
							mes "จู่ๆ ก็มีเสียงน้ำไหลออกมาจากอ่างล้างหน้า";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางก๊อกน้ำ";
							brazil_ghost = 4;
							changequest 60351,60352;
							close;
						}
						else {
							mes "เจ้ากดชักโครกไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					case 2:
						mes "เจ้าปิดฝาส้วม";
						mes "ดูเหมือนไม่มีอะไรเกิดขึ้น";
						close;
					}
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "เจ้าไม่ได้ทำอะไร";
			close;
		}
	}
	else {
		mes "- ดูเหมือนส้วมธรรมดาๆ -";
		close;
	}
}

bra_in01,134,189,3	script	Faucet#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- ดูเหมือนก๊อกน้ำธรรมดาๆ -";
		next;
		switch(select("ตรวจสอบ.:ไม่ต้องสนใจ")) {
		case 1:
			mes "- คาถาบรรทัดต่อไปว่ายังไงนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother the drought has started!";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าเจ้าจะพูดอะไรผิดไปนะ";
				close;
			}
			else {
				if (brazil_ghost == 4) {
					mes "[เสียงสะอื้น]";
					mes "^FF0000ไม่ต้องห่วงนะ... น้ำตกจะช่วยเยียวยามันเอง....^000000";
					next;
					switch(select("เคาะที่ก๊อกน้ำ.:เปิดน้ำ")) {
					case 1:
						mes "เจ้าจะเคาะก๊อกน้ำกี่ครั้ง?";
						input .@input,0,999;
						next;
						mes "เจ้าเคาะก๊อกน้ำไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 2:
						mes "ควรจะเปิดน้ำกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						if (.@input == 1) {
							mes "เจ้าเปิดก๊อกน้ำหนึ่งครั้ง";
							next;
							mes "ซ่าาาาาาาาา-";
							next;
							mes "[เสียงจากที่ไกลๆ]";
							mes "^FF0000คิก... ฮิฮิฮิฮิ!!!^000000";
							mes "เจ้าเห็นพรมขยับ";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางพรม";
							brazil_ghost = 5;
							changequest 60352,60353;
							close;
						}
						else {
							mes "เจ้าเปิดก๊อกน้ำไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					}
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "เจ้าไม่ได้ทำอะไร";
			close;
		}
	}
	else {
		mes "- ดูเหมือนก๊อกน้ำธรรมดาๆ -";
		close;
	}
}

bra_in01,138,184,3	script	Carpet#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- พรมที่มีลวดลายวิจิตรบรรจง -";
		next;
		switch(select("ตรวจสอบ.:ไม่ต้องสนใจ")) {
		case 1:
			mes "- คาถาบรรทัดต่อไปว่ายังไงนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Mother where are my friends?";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าเจ้าจะพูดอะไรผิดไปนะ";
				close;
			}
			else {
				if (brazil_ghost == 5) {
					mes "[เสียงสะอื้น]";
					mes "^FF0000เพื่อนทั้ง 7 ของเจ้า....กำลัง...หลับใหล... ถึงเวลา....ปลุกพวกเขาแล้ว........^000000";
					next;
					switch(select("กระโดดบนพรม.:นอนบนพรม.:สะบัดพรม")) {
					case 1:
						mes "ควรจะกระโดดกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						mes "เจ้ากระโดดบนพรมไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 2:
						mes "ควรจะนอนบนพรมกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						mes "เจ้านอนบนพรมไป "+.@input+" ครั้ง";
						next;
						mes "แต่ไม่มีอะไรเกิดขึ้น";
						close;
					case 3:
						mes "ควรจะสะบัดพรมกี่ครั้งดีนะ?";
						input .@input,0,999;
						next;
						if (.@input == 7) {
							mes "เจ้าสะบัดพรมไป 7 ครั้ง";
							next;
							mes "- สะบัด~ สะบัด~ สะบัด~ สะบัด~ สะบัด~ สะบัด~ สะบัด~ -";
							next;
							mes "[เสียงจากที่ไกลๆ]";
							mes "^FF0000คิก... ฮิฮิฮิฮิ!!!^000000";
							next;
							specialeffect2 EF_VENOMDUST;
							mes "ได้ยินเสียงหัวเราะเบาๆ ดังมาจากทางกระจก";
							brazil_ghost = 6;
							changequest 60353,60354;
							close;
						}
						else {
							mes "เจ้าสะบัดพรมไป "+.@input+" ครั้ง";
							next;
							mes "แต่ไม่มีอะไรเกิดขึ้น";
							close;
						}
					}
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "เจ้าไม่ได้ทำอะไร";
			close;
		}
	}
	else {
		mes "- พรมที่มีลวดลายวิจิตรบรรจง -";
		close;
	}
}

bra_in01,151,180,3	script	Mirror#bra	CLEAR_NPC,{
	if (brazil_ghost > 0) {
		mes "- เจ้าเห็นกระจกที่สะอาดบานหนึ่ง ไม่มีแม้แต่รอยขีดข่วนหรือฝุ่นละออง -";
		next;
		switch(select("ตรวจสอบ.:ไม่ต้องสนใจ")) {
		case 1:
			mes "- คาถาบรรทัดต่อไปว่ายังไงนะ? -";
			input .@input$;
			next;
			mes "["+strcharinfo(0)+"]";
			mes .@input$;
			next;
			.@braspell$ = "Where are you mom?";
			.@chkspell = compare(.@braspell$,.@input$);
			if (!.@chkspell) {
				mes "ดูเหมือนว่าเจ้าจะพูดอะไรผิดไปนะ";
				close;
			}
			else {
				if (brazil_ghost == 6) {
					mes "[เสียงจากที่ไกลๆ]";
					mes "^FF0000คิก! ฮิ! ฮิ! ฮิ! ฮิ!^000000";
					next;
					mes "[เสียงจากที่ไกลๆ]";
					mes "^FF0000คิก! ฮิ! ฮิ! ฮิ! ฮิ!^000000";
					mes "^FF0000คิก! ฮิ! ฮิ! ฮิ! ฮิ!^000000";
					next;
					mes "[เสียงจากที่ไกลๆ]";
					mes "ข้างหลังเจ้า...";
					enablenpc "Ghost#bra";
					next;
					emotion ET_HUK, playerattached();
					mes "["+strcharinfo(0)+"]";
					mes "เรื่องเล่าเกี่ยวกับผีนั่นเป็นเรื่องจริงเหรอเนี่ย~!";
					next;
					mes "[Ghost]";
					mes "^FF0000ลูกของแม่....^000000";
					next;
					mes "[Ghost]";
					mes "^FF0000ข้ามองไม่เห็น.... ตาของข้า....^000000";
					mes "^FF0000มันเกิดอะไรขึ้น....?^000000";
					next;
					mes "- เจ้าสูดหายใจเข้าลึกๆ แล้วมองไปที่ Ghost และสังเกตเห็นว่ามันมีผ้าปิดตาอยู่ -";
					next;
					mes "[Ghost]";
					mes "^FF0000ตาของข้าถูกรัดแน่นเหลือเกิน... เจ้าช่วยเอาสิ่งนี้ออกให้หน่อยได้ไหม?^000000";
					next;
					mes "เจ้าค่อยๆ ก้าวเข้าไปหาผีอย่างระมัดระวัง";
					next;
					mes "ใบหน้าของเขาถูกปกคลุมไปด้วยฝุ่นและมีการบิดเบี้ยวของใบหน้าที่ดูประหลาด";
					next;
					mes "[Ghost]";
					mes "^FF0000มาสิ ช่วยแม่หน่อย.....^000000";
					next;
					switch(select("ถอดผ้าพันแผลที่ตาออก.:วิ่งหนี~")) {
					case 1:
						while(1) {
							.@cpudice = rand(1,6);
							.@pcdice = rand(1,6);
							if (.@cpudice != .@pcdice) {
								emotion (ET_OTL+.@cpudice), getnpcid(0, "Ghost#bra");
								emotion (ET_OTL+.@pcdice), playerattached();
								break;
							}
						}
						if (.@cpudice > .@pcdice) {
							specialeffect2 EF_DEVIL;
							mes "[Ghost]";
							mes "^FF0000ออกไปนะ!^000000";
							brazil_ghost = 1;
							changequest 60354,2208;
							percentheal -50,-50;
							close2;
							disablenpc "Ghost#bra";
							warp "bra_in01",12,183;
							end;
						}
						else {
							emotion ET_STARE, playerattached();
							mes "[Ghost]";
							mes "^FF0000อ๊าาา!^000000";
							mes "Ghost หายตัวเข้าไปในโถส้วม";
							brazil_ghost = 7;
							changequest 60354,60355;
							close2;
							disablenpc "Ghost#bra";
							warp "bra_in01",206,100;
							end;
						}
					case 2:
						mes "เจ้าวิ่งหนีออกมาจากผี";
						close2;
						brazil_ghost = 1;
						changequest 60354,2208;
						warp "bra_in01",12,183;
						disablenpc "Ghost#bra";
						end;
					}
					close;
				}
				else {
					mes "ไม่มีอะไรเกิดขึ้น";
					close;
				}
			}
		case 2:
			mes "เจ้าไม่ได้ทำอะไร";
			close;
		}
	}
	else {
		mes "- เจ้าเห็นกระจกที่สะอาดบานหนึ่ง ไม่มีแม้แต่รอยขีดข่วนหรือฝุ่นละออง -";
		close;
	}
}

bra_in01,136,180,5	script	Ghost#bra	1867,{
	end;

OnInit:
	disablenpc "Ghost#bra";
	end;
}

bra_in01,7,181,5	script	Curator#bra	4_M_BRZ_MAN2,{
	if (BaseLevel < 40) {
		mes "[Curator]";
		mes "ขออภัยด้วย แต่ตอนนี้บริเวณนี้กำลังอยู่ในระหว่างการปรับปรุง";
		close;
	}
	if ((brazil_ghost > 0) && (brazil_ghost < 7)) {
		if (countitem(11515) > 0) {
			mes "[Curator]";
			mes "มีอะไรให้ผมช่วยไหมครับ?";
			next;
			select("ฉันต้องการใช้ห้องน้ำ");
			mes "[Curator]";
			mes "ขออภัยครับ ตอนนี้เรากำลังปรับปรุงภายในอยู่ ดังนั้นจึงปิดให้บริการ";
			mes "กรุณาไปใช้ห้องน้ำอีกฝั่งแทนนะครับ";
			next;
			switch(select("แต่ฉันลืมของไว้ข้างในน่ะ.:ยอมแพ้")) {
			case 1:
				mes "[Curator]";
				mes "แย่จังเลยนะครับ";
				mes "แต่ผู้จัดการสั่งผมไว้ว่าห้ามให้ใครเข้าห้องน้ำนี้เด็ดขาด ดังนั้นผมต้องทำตามคำสั่งครับ";
				next;
				select("วันนี้อากาศร้อนจังเลยนะ!");
				mes "[Curator]";
				mes "ที่ Brasilis อากาศร้อนเสมอแหละครับ แต่วันนี้มันร้อนผิดปกติจริงๆ";
				mes "บางทีผมอาจจะต้องการน้ำมะพร้าวสักหน่อยเพื่อให้ร่างกายเย็นลงบ้าง";
				next;
				mes "เจ้ามอบ Coconut ให้กับ Curator";
				next;
				mes "[Curator]";
				mes "โอ้ จริงเหรอครับ ให้ผมได้จริงๆ ใช่ไหม?";
				mes "ขอบคุณมากครับ!";
				next;
				mes "[Curator]";
				mes "ถือว่าเป็นการตอบแทนน้ำใจกันนะครับ?";
				mes "ตกลง ผมจะยอมให้คุณเข้าไปแค่ครั้งเดียวเท่านั้นนะ";
				next;
				mes "Curator มองไปรอบๆ อย่างใจเย็นแล้วเปิดประตูให้";
				delitem 11515,1; //Coconut
				brazil_ghost = 2;
				close2;
				warp "bra_in01",138,176;
				end;
			case 2:
				mes "เจ้ายอมแพ้ที่จะเข้าไป";
				close;
			}
		}
		else {
			mes "[Curator]";
			mes "มีอะไรให้ผมช่วยไหมครับ?";
			next;
			select("ฉันต้องการใช้ห้องน้ำ");
			mes "[Curator]";
			mes "ขออภัยครับ ตอนนี้เรากำลังปรับปรุงภายในอยู่ ดังนั้นจึงปิดให้บริการ";
			mes "กรุณาไปใช้ห้องน้ำอีกฝั่งแทนนะครับ";
			next;
			switch(select("แต่ฉันลืมของไว้ข้างในน่ะ.:ยอมแพ้")) {
			case 1:
				mes "[Curator]";
				mes "แย่จังเลยนะครับ";
				mes "แต่ผู้จัดการสั่งผมไว้ว่าห้ามให้ใครเข้าห้องน้ำนี้เด็ดขาด ดังนั้นผมต้องทำตามคำสั่งครับ";
				next;
				select("วันนี้อากาศร้อนจังเลยนะ!");
				mes "[Curator]";
				mes "ที่ Brasilis อากาศร้อนเสมอแหละครับ แต่วันนี้มันร้อนผิดปกติจริงๆ";
				mes "บางทีผมอาจจะต้องการน้ำมะพร้าวสักหน่อยเพื่อให้ร่างกายเย็นลงบ้าง";
				close;
			case 2:
				mes "เจ้ายอมแพ้ที่จะเข้าไป";
				close;
			}
		}
	}
	else if (brazil_ghost > 6) {
		mes "[Curator]";
		mes "เฮ้ ขอบคุณสำหรับ Coconut เมื่อกี้นะครับ มันช่วยให้ผมเย็นลงได้เยอะเลย";
		close;
	}
	else {
		mes "[Curator]";
		mes "ผมคิดไปเองหรือเปล่านะ? หรือว่าวันนี้อากาศมันร้อนกว่าที่เคยเป็นมากันแน่!";
		close;
	}
}

bra_in01,12,185,0	script	inbathroom#bra	WARPNPC,1,1,{
OnTouch_:
	if (brazil_ghost > 6)
		warp "bra_in01",138,176;
	else {
		mes "ทางเข้าถูกปิดกั้นอยู่";
		close;
	}
	end;
}

bra_in01,138,174,0	warp	outbathroom#bra	1,1,bra_in01,12,183
bra_in01,206,98,0	warp	tobath#bra	1,1,bra_in01,138,176

bra_in01,206,188,1	script	Open Manhole#todunbra	CLEAR_NPC,{
	if (brazil_ghost == 7) {
		enablenpc "Ghost#bra_end";
		mes "[Ghost]";
		mes "ข้าคือวิญญาณที่ตายในขณะที่ร่อนเร่พเนจรอยู่ในป่าเมื่อหลายปีก่อน";
		next;
		mes "[Ghost]";
		mes "ข้าพบท่อส่งน้ำในป่าและเดินตามเสียงของผู้ชายคนหนึ่งมาจนถึงจุดนี้";
		next;
		mes "[Ghost]";
		mes "นั่นคือที่ที่ข้าทำตาข้างหนึ่งบาดเจ็บในขณะที่เดินไปรอบๆ ในความมืด";
		next;
		mes "[Ghost]";
		mes "ข้าหลงทางอยู่ในท่อระบายน้ำเหล่านี้เป็นเวลาหลายวัน จนกระทั่งพบว่าปลายสายนี้เชื่อมต่อกับโถส้วมในพิพิธภัณฑ์ ข้าตะโกนเรียกขอความช่วยเหลือตลอดกาล แต่ไม่มีใครตอบรับเสียงเรียกของข้าเลย";
		next;
		mes "[Ghost]";
		mes "ในที่สุดเจ้าก็ตอบรับเสียงเรียกของข้า แต่มันก็สายเกินไปเสียแล้ว ขอบคุณที่อย่างน้อยก็ยังสืบหาความจริง จนถึงทุกวันนี้ยังไม่เคยมีใครใส่ใจเลยสักคน";
		next;
		mes "[Ghost]";
		mes "มีสิ่งมีชีวิตที่อันตรายมากมายอยู่ที่ปลายท่อระบายน้ำนี้";
		mes "แต่เจ้าดูเป็นคนกล้าหาญนะ ข้าพนันได้เลยว่าเจ้าคงไม่กังวลเรื่องสัตว์ประหลาดพวกนั้นอยู่แล้วล่ะ";
		next;
		mes "[Ghost]";
		mes "ข้าเดาว่าตอนนี้ข้าคงไปสู่สุคติได้เสียที";
		mes "ขอบคุณนะสหาย";
		brazil_ghost = 8;
		//completequest 2208;
		completequest 60355;
		if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
			getexp 135000,0;
		else
			getexp 90000,0;
		disablenpc "Ghost#bra_end";
		close;
	}
	warp "bra_dun01",87,47;
	end;
}


bra_dun01,87,43,1	script	Pipe#bra	CLEAR_NPC,{
	warp "bra_in01",206,185;
	end;
}

bra_fild01,323,136,1	script	Pipe#brafild	CLEAR_NPC,{
	if (brazil_ghost > 6) {
		mes "เจ้าเห็นท่อที่เป็นสนิม ดูเหมือนมันจะเชื่อมโยงไปที่ไหนสักแห่งใต้ป่า";
		next;
		switch(select("ตรวจสอบ.:ไม่ต้องสนใจ")) {
		case 1:
			mes "เจ้าว่ายผ่านช่องว่างในท่อและถูกพัดพาไปโดยกระแสน้ำที่เชี่ยวกรากทันที";
			close2;
			warp "bra_in01",206,182;
			end;
		case 2:
			mes "มันอาจจะอันตราย ข้าไม่ควรวู่วามดีกว่า";
			close;
		}
	}
	else {
		mes "เจ้าเห็นท่อที่เป็นสนิม ดูเหมือนมันจะเชื่อมโยงไปที่ไหนสักแห่งใต้ป่า";
		close;
	}
}

bra_in01,206,190,3	script	Ghost#bra_end	1867,{
	end;

OnInit:
	disablenpc "Ghost#bra_end";
	end;
}

// Iara :: iara.sc
//============================================================
brasilis,315,334,5	script	Shaman#nk	4_F_BRZ_INDOLD,{
	.@iara_re = checkquest(4135,PLAYTIME);
	if ((.@iara_re == 0) || (.@iara_re == 1)) {
		mes "[Anori]";
		mes "ข้ายังเตรียมตัวอยู่";
		mes "ข้ายังไม่ต้องการความช่วยเหลือจากเจ้าในตอนนี้";
		mes "กรุณากลับมาใหม่ภายหลัง...";
		close;
	}
	else {
		if (checkquest(4135) > 1) erasequest 4135;
		.@iara_q = checkquest(4133);
		if ((.@iara_q == 0) || (.@iara_q == 1)) {
			mes "[Anori]";
			mes "ในการยับยั้ง Iara ";
			mes "ที่ล่อลวงคนในเผ่า";
			mes "เราจำเป็นต้องมีน้ำยาชำระล้าง...";
			next;
			mes "[Anori]";
			mes "เจ้านำวัตถุดิบ";
			mes "สำหรับปรุงน้ำยาชำระล้างมาหรือยัง?";
			next;
			if ((countitem(950) > 19) && (countitem(7172) > 9) && (countitem(1054) > 2)) {
				mes "[Anori]";
				mes "อืม... ดูเหมือนจะใช้ได้นะ";
				mes "ข้าจะปรุงยาที่ช่วย";
				mes "ทำให้อำนาจของ Iara อ่อนแอลงให้";
				next;
				mes "[Anori]";
				mes "ไหนดูซิ บดไอ้นี่...";
				mes "แล้วก็ผสมไอ้นั่นลงไป...";
				mes "จากนั้นก็ใส่เวทมนตร์ลงไปสักหน่อย...";
				next;
				setquest 4135;
				erasequest 4133;
				setquest 4134;
				completequest 4134;
				delitem 950,20; //Heart_Of_Mermaid
				delitem 7172,10; //Leopard_Talon
				delitem 1054,3; //Lip_Of_Ancient_Fish
				getitem 11517,2; //Puri_Potion
				mes "[Anori]";
				mes "เอ้า เสร็จเรียบร้อยแล้ว";
				mes "รับนี่ไปสิ";
				mes "มันจะทำให้ Iara หยุด";
				mes "บำเพ็ญตบะที่ถ้ำ";
				mes "ไปได้สักพักหนึ่ง";
				next;
				mes "[Anori]";
				mes "ได้โปรดช่วยยับยั้ง Iara ที่กำลังคุกคามความปลอดภัยของเผ่าด้วย";
				close;
			}
			else {
				mes "[Anori]";
				mes "เจ้ายยังนำวัตถุดิบมาไม่ครบเลยนะ";
				mes "เราไม่สามารถปรุงน้ำยาชำระล้างด้วยของเพียงเท่านี้ได้หรอก";
				close;
			}
		}
		else {
			if (BaseLevel < 40) {
				mes "[Anori]";
				mes "อา... เราต้องการนักผจญภัยที่แข็งแกร่ง";
				mes "เผ่าของเรากำลังเผชิญกับการคุกคามครั้งใหญ่";
				close;
			}
			.@re_q = checkquest(4134);
			if (.@re_q == 2) {
				mes "[Anori]";
				mes "เจ้าคือ...";
				mes "นักผจญภัยที่เคยมาขอ";
				mes "น้ำยาชำระล้างนี่นา...";
				next;
				mes "[Anori]";
				mes "อาจจะเป็นเพราะน้ำยาชำระล้างนั่น...";
				mes "หลังจากนั้น Iara ก็หยุดล่อลวงคนในเผ่า แต่ผลของมันอยู่ได้ไม่นานนัก";
				next;
				mes "[Anori]";
				mes "เจ้าช่วยไปหาวัตถุดิบ";
				mes "แบบเดิมมาอีกครั้งได้ไหม...";
				mes "ข้าต้องการพลังของเจ้า";
				next;
				switch(select("ไม่ดีกว่า.:ตกลง ข้าจะทำ")) {
				case 1:
					mes "[Anori]";
					mes "เรื่องนี้ อา...";
					mes "มันไม่มีทางอื่นแล้วสินะ";
					close;
				case 2:
					mes "[Anori]";
					mes "เจ้าช่างกล้าหาญจริงๆ!";
					mes "ข้า ในนามของเผ่า";
					mes "ขอขอบใจเจ้ามาก";
					next;
					mes "[Anori]";
					mes "วัตถุดิบก็เหมือนเดิมนั่นแหละ";
					mes "ถ้าเจ้าไปหา^ff0000 Heart of Mermaid 20 อัน,";
					mes "Leopard Talon 10 อัน และ";
					mes "Ancient Lip 3 อัน^000000 มาได้,";
					mes "ข้าจะปรุงยาที่ชำระล้างวิญญาณชั่วร้ายให้";
					mes "โดยใช้สูตรลับที่สืบทอดกันมาในเผ่า";
					next;
					mes "[Anori]";
					mes "โชคชะตาของเผ่าขึ้นอยู่กับเจ้าแล้ว";
					mes "ได้โปรดรีบไปหาวัตถุดิบมาโดยเร็ว";
					mes "ข้าจะเตรียมการทำ";
					mes "น้ำยาชำระล้างรออยู่ที่นี่";
					setquest 4133;
					close;
				}
			}
			else {
				mes "[Anori]";
				mes "มีบางคนที่ข้าไม่เคยเห็นหน้าค่าตาแถวนี้เลยนะ";
				mes "มันเป็นลางที่ดี...";
				next;
				mes "[Anori]";
				mes "นี่เจ้า...";
				mes "ช่วยฟังเรื่องราวของข้าสักครู่ได้ไหม";
				mes "ตอนนี้มีเหตุฉุกเฉินเกิดขึ้นในเผ่าของเรา";
				next;
				if(select("ไม่.:ตกลง") == 1) {
					mes "[Anori]";
					mes "เจ้าช่างเป็นคนไร้น้ำใจจริงๆ...";
					mes "เจ้าดูไม่ใช่พวกที่ชอบช่วยเหลือคนอื่นเลยนะ";
					mes "ไปตามทางของเจ้าเถอะ";
					close;
				}
				mes "[Anori]";
				mes "ขอบใจนะ ข้าได้พบคนใจดีเข้าแล้ว";
				mes "นี่เป็นความลับของเผ่าเราที่";
				mes "เจ้าห้ามบอกใครเด็ดขาด";
				next;
				mes "[Anori]";
				mes "ช่วงนี้ชายหนุ่มใน";
				mes "เผ่ากำลังหายตัวไป";
				mes "ทั้งเผ่าของเรากำลังถูกคุกคาม";
				next;
				mes "[Anori]";
				mes "มันน่าจะเป็นฝีมือของแม่มดที่ชื่อว่า Iara";
				mes "นางเป็นพรายน้ำที่คอยล่อลวงหัวใจของชายหนุ่มในเผ่าอยู่ที่ถ้ำหลังน้ำตก";
				next;
				mes "[Anori]";
				mes "ข้าจะหยุดชายหนุ่มพวกนั้นได้ยังไง?";
				mes "แต่ข้าค้นพบวิธีปรุงน้ำยาชำระล้างเพื่อแก้คำสาปจากเวทมนตร์ของ Iara แล้ว";
				next;
				mes "[Anori]";
				mes "น้ำยานี้ถูกสืบทอดมาหลายชั่วอายุคนในเผ่าของเรา";
				mes "น้ำยาชำระล้างนี้มีพลังในการปัดเป่าวิญญาณชั่วร้าย";
				next;
				mes "[Anori]";
				mes "ถ้าเจ้าสามารถไปหาวัตถุดิบมาให้ได้";
				mes "ข้าจะปรุง";
				mes "น้ำยาชำระล้างให้เจ้า";
				mes "เจ้าจะช่วยข้าทำเรื่องนี้ได้ไหม?";
				next;
				switch(select("ไม่.:ได้สิ ข้าทำได้")) {
				case 1:
					mes "[Anori]";
					mes "หืม...";
					mes "ถ้าอย่างนั้นก็ตามใจ";
					mes "ถ้าเจ้าเปลี่ยนใจก็กลับมาหาข้าได้อีกนะ";
					next;
					mes "[Anori]";
					mes "เจ้าไม่ควรเอาเรื่องที่ได้ยิน";
					mes "ไปบอกใครนะ";
					mes "มันค่อนข้างน่าอายน่ะ...";
					close;
				case 2:
					mes "[Anori]";
					mes "โอ้! เจ้าคือผู้ช่วยชีวิต";
					mes "เผ่าเราจริงๆ";
					mes "ในนามของเผ่า ข้าขอขอบใจเจ้ามาก";
					next;
					mes "[Anori]";
					mes "เอาล่ะ สิ่งที่เราต้องการคือสิ่งนี้";
					mes "พวกมันเป็นของที่เจ้าหาได้จากแถวนี้แหละ";
					mes "จดไว้ให้ดีล่ะ";
					next;
					mes "[Anori]";
					mes "^ff0000 Heart of Mermaid 20 อัน";
					mes "Leopard Talon 10 อัน";
					mes "Ancient Lip 3 อัน^000000";
					mes "นั่นคือวัตถุดิบทั้งหมดที่จำเป็น";
					next;
					mes "[Anori]";
					mes "เมื่อเจ้าได้ครบแล้ว ข้าจะปรุงยาที่ช่วยชำระล้างวิญญาณชั่วร้ายด้วยสูตรลับที่สืบทอดกันมาในเผ่าให้";
					setquest 4133;
					next;
					mes "[Anori]";
					mes "ชะตากรรมของเผ่าอยู่ในกำมือเจ้าแล้ว";
					mes "ข้าหวังว่าเจ้าจะรีบดำเนินการ";
					mes "แม้ในขณะนี้ ชายในหมู่บ้านก็กำลังถูกล่อลวงและหายตัวไปเรื่อยๆ...";
					close;
				}
			}
		}
	}
}

bra_dun02,157,74,5	script	Iara#nk	4_F_BRZ_WOMAN,2,2,{
	if (countitem(11517) > 0) {
		mes "[" + strcharinfo(0) + "]";
		mes "ฉันควรใช้ Purification Potion ไหม?";
		next;
		switch(select("ใช่.:ไม่")) {
		case 1:
			specialeffect EF_MAPPILLAR;
			mes "[Iara]";
			mes "อา... แสงนี่มัน...";
			mes "รู้สึกเหมือนความคิดชั่วร้ายถูกชำระล้าง";
			mes "จากส่วนลึกในหัวใจของข้าเลย";
			next;
			mes "[Iara]";
			mes "ในที่สุดข้าก็สามารถลืมคำสาปที่ข้าสาปตัวเองไว้ตอนที่จมน้ำตายได้เสียที";
			next;
			mes "[Iara]";
			mes "เจ้าคิดว่าข้าจะสามารถเกิดใหม่เป็นพรายน้ำที่ใจดีได้ไหม?";
			next;
			specialeffect EF_GHOST;
			mes "[Iara]";
			mes "อา... ขอบคุณที่ช่วยให้ข้าได้สติกลับมาสักพักหนึ่ง";
			mes "แต่... ข้าคิดว่าคำสาปมันอยู่กับข้ามานานเกินไป";
			mes "รีบไปจากข้าซะเถอะ";
			delitem 11517,1; //Puri_Potion
			if (checkre(0)) {
				percentheal 100,100;
				sc_start SC_INCFLEE,3600000,20;
				sc_start SC_INCCRI,3600000,10;
			}
			else
				sc_start SC_INCCRI,3600000,7;
			consumeitem 12043; //Str_Dish03
			consumeitem 12063; //Dex_Dish03
			consumeitem 12058; //Agi_Dish03
			consumeitem 12053; //Vit_Dish03
			consumeitem 12048; //Int_Dish03
			consumeitem 12068; //Luk_Dish03
			next;
			mes "[Iara]";
			mes "อ๊าาาา~...";
			specialeffect EF_DEVIL;
			next;
			mes "[Iara]";
			mes "คำสาปมันรุนแรงเกินกว่าที่ข้าจะสะกดมันไว้ได้";
			mes "รีบไปตอนนี้ในขณะที่เจ้ายังปลอดภัยเถอะ";
			close;
		case 2:
			mes "[" + strcharinfo(0) + "]";
			mes "(ฉันว่าฉันควรจะเลิกสนใจเธอดีกว่า)";
			close;
		}
	}
	else {
		mes "[Iara]";
		mes "อ๊าาาาาาาาาาาาาาาาาาาาาาห์";
		mes "อี๊อีอีอีอีอีอีอีอีอีอีอีอีอีอีอีห์";
		mes "โอ้ววววววววววววววววววววววห์";
		if (rand(1,2) == 1)
			sc_start SC_CURSE,60000,0;
		else
			sc_start SC_CONFUSION,60000,0;
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "อึ๊ก! เสียงประหลาดนี่มันอะไรกัน?";
		close;
	}
	end;

OnTouch:
	if (countitem(11517) < 1) {
		mes "[Iara]";
		mes "อ๊าาาาาาาาาาาาาาาาาาาาาาห์";
		mes "อี๊อีอีอีอีอีอีอีอีอีอีอีอีอีอีอีห์";
		mes "โอ้ววววววววววววววววววววววห์";
		if (rand(1,2) == 1)
			sc_start SC_CURSE,60000,0;
		else
			sc_start SC_CONFUSION,60000,0;
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "อึ๊ก! เสียงประหลาดนี่มันอะไรกัน?";
		close;
	}
	end;
}

bra_fild01,188,301,5	script	Native Warrior#nk	4_M_BRZ_INDIAN,{
	mes "[Native Warrior]";
	mes "อา... ใบหน้าที่ข้าไม่เคยลืมเลือนแม้ในความฝัน";
	next;
	mes "[Native Warrior]";
	mes "เมื่อไหร่นางจะออกมาจากน้ำตกอีกครั้งกันนะ...?";
	next;
	if ((checkquest(4133) >= 0) || (checkquest(4134) >= 0)) {
		mes "[" + strcharinfo(0) + "]";
		mes "(จะ... เจ้านี่เป็นอะไรของเขา?)";
	}
	else {
		mes "[" + strcharinfo(0) + "]";
		mes "(ชายคนนี้คงไม่มีทาง";
		mes "หลุดพ้นจากคำสาปของ Iara ได้เลยสินะ...)";
	}
	close;
}
