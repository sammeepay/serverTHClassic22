//===== rAthena Script ======================================= 
//= Quest NPCs related to Niflheim
//===== By: ================================================== 
//= The rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project; 7.15 +
//===== Description: ========================================= 
//= [Official Conversion]
//= Piano Key Ouest
//= - Escape from Niflheim
//= - Variables in use: nif_esc, MISC_QUEST (Bit 32)
//= Message Delivery Quest
//= - Deliver a message to Erious' departed wife.
//= - Variables in use: nif_revive, MISC_QUEST (Bit 16384)
//===== Additional Comments: ================================= 
//= 1.0 Initial release, moved Book of Devil and Piano quest
//=     from npc/cities/niflheim.txt [Evera]
//= 1.1 Optimized Book of Devil quest. used bit-wise var,
//=     Optimized Piano Quest - now uses 1 variable [Lupus]
//= 1.2 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//= 1.3 Moved the Book of Devil quest to custom folder. [L0ne_W0lf]
//=     Updated to Aegis 10.3 Standed, added Message Delivery Quest.
//=     Rescripted Piano Quest. Prior quest progress will be lost.
//= 1.4 Fixed NPCs using ID 111 when it shoudl have been 139. [L0ne_W0lf]
//= 1.5 Swapped NPC IDs back from last change (139 back to 111). [L0ne_W0lf]
//=     Clicking will do nothing, you have to still "touch" the npc. 
//= 1.6 Added questlog support. [Euphy]
//============================================================ 

// Piano Quest :: nif_ryu
//============================================================
niflheim,224,243,3	script	Alreg#nif	795,{
	if (nif_piano) set nif_piano,0;
	mes "[Alreg]";
	mes "ข้าชอบเนื้อ มุเหะเหะ";
	mes "เจ้าน่ะ! ดูน่ากินชะมัด";
	next;
	mes "[Alreg]";
	mes "ข้าอยากจะชิมเจ้าเหลือเกิน";
	next;
	mes "^FF3355*เคี้ยว* เคี้ยว*^000000";
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		percentheal -60,0;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "ไม่นะะะะะะะ~!";
		next;
		mes "[Alreg]";
		mes "นานมากแล้วที่ข้าไม่ได้กินเนื้อ";
		mes "ช่าง... ช่างดีเหลือเกินเจ้ารสชาติ";
		mes "ดีมาก Alreg จะให้สิ่งนี้กับเจ้า";
		mes "เป็นการตอบแทน ของขวัญไงล่ะ!";
		mes "*เอิ๊ก*";
		set nif_esc,rand(1,2);
		getitem 7184,1; //Piano_Keyboard
		close;
	}
	percentheal -30,0;
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "โอ๊ย~! นี่เจ้า";
	mes "พยายามจะทำอะไรน่ะ จะฆ่ากันหรือไง?!";
	next;
	mes "[Alreg]";
	mes "หืม? โอ๊ะ ไม่ๆๆ";
	mes "ข้าแค่พยายามจะกินเจ้าเฉยๆ";
	close;
}

nif_in,105,81,3	script	Crayu#nif	794,{
	mes "[Crayu]";
	mes "ผู้ที่ยังมีชีวิตอยู่...";
	mes "จงฟังเพลงนี้สิ...";
	mes "มันถูกขับขานโดยผู้คนมากมายมา";
	mes "เนิ่นนาน แต่ไม่มีใครรู้เลยว่า";
	mes "มันถูกแต่งขึ้นเมื่อไหร่หรือใครเป็นคนเขียน";
	next;
	mes "[Crayu]";
	mes "^FF00001. Mountain sunset to the west (ขุนเขาอาทิตย์อัสดงทางทิศประจิม)^000000";
	mes "^FF00002. Where the purple dusk falls (ณ ที่ซึ่งยามสนธยาสีม่วงย่างกราย)^000000";
	mes "^FF00003. Surrounded by beautiful melody (รายล้อมด้วยท่วงทำนองอันแสนหวาน)^000000";
	mes "^FF00004. You become the key that ignores its master (เจ้ากลายเป็นคีย์ที่เมินเฉยต่อผู้เป็นนาย)^000000";
	next;
	if (nif_esc == 1 || nif_esc == 4) {
		mes "[Crayu]";
		mes "โอเค รอเดี๋ยวนะ! นี่คือบททดสอบสำหรับเจ้า!";
		switch(rand(1,4)) {
		case 1:
			set .@songline$,"Mountain sunset to the west";
			mes "จงเขียนเนื้อร้องบรรทัดแรกของเพลงนี้";
			break;
		case 2:
			set .@songline$,"Where the purple dusk falls";
			mes "จงเขียนเนื้อร้องบรรทัดที่สองของเพลงนี้";
			break;
		case 3:
			set .@songline$,"Surrounded by beautiful melody";
			mes "จงเขียนเนื้อร้องบรรทัดที่สามของเพลงนี้";
			break;
		case 4:
			set .@songline$,"You become the key that ignores its master";
			mes "จงเขียนเนื้อร้องบรรทัดที่สี่ของเพลงนี้";
			break;
		}
		next;
		input .@input$;
		if (.@input$ == .@songline$) {
			mes "[Crayu]";
			mes "ยอดเยี่ยม...!";
			mes "แสดงว่าเจ้าตั้งใจฟังอยู่สินะ~!";
			mes "นั่นทำให้เจ้ากลายเป็นหนึ่งใน";
			mes "แขกคนโปรดของฉันเลยล่ะ เอา";
			mes "รางวัลเล็กๆ น้อยๆ นี่ไปสิ";
			next;
			if (nif_esc == 1) set nif_esc,3;
			else set nif_esc,6;
			getitem 7184,1; //Piano_Keyboard
			mes "[Crayu]";
			mes "สุดท้ายนี้ ฉันหวังว่าเจ้าจะแสดง";
			mes "ความเคารพต่อ Bard คนอื่นๆ เหมือน";
			mes "อย่างที่เจ้าทำให้กับฉันนะ";
			close;
		}
		else {
			mes "[Crayu]";
			mes "*เฮ้อ*... ถ้าเพียงแค่เจ้า";
			mes "ตั้งใจฟังฉันสักนิด มันคงจะ";
			mes "ตอบคำถามได้ง่ายมากทีเดียว";
			mes "ลองจำเนื้อร้องให้ดี";
			mes "แล้วมาลองใหม่อีกครั้งนะ!";
			close;
		}
	}
	next;
	mes "[Crayu]";
	mes "ฉันสงสัยจังว่าเนื้อเพลงนี้หมายความว่าอย่างไร...";
	mes "มันดูค่อนข้างลึกซึ้งทีเดียว...";
	mes "ฉันมีความรู้สึกว่าเพลงนี้";
	mes "มีความลับบางอย่างซ่อนอยู่";
	close;
}

nif_in,31,20,3	script	Kuzkahina#nif	794,{
	if (nif_esc == 2 || nif_esc == 3) {
		mes "[Kuzkahina]";
		mes "ฉันไม่เข้าใจเลยว่า";
		mes "ทำไมฉันถึงทำ";
		mes "เงินจากร้านนี้ไม่ได้เลย...";
		mes "แม้แต่ตอนที่ฉันยังมีชีวิตอยู่";
		mes "ฉันก็ไม่เคยหาเงิน";
		mes "จากธุรกิจของฉันได้เลยสักนิด...";
		next;
		mes "[Kuzkahina]";
		mes "นี่เจ้า! ทำตัวให้เป็นประโยชน์หน่อย";
		mes "แล้วเอาไอ้นี่ไปทิ้งข้างนอกตอนที่เจ้าออกไปด้วยนะ";
		set nif_esc,nif_esc+2;
		getitem 7184,1; //Piano_Keyboard
		next;
		mes "[Kuzkahina]";
		mes "ให้ตายเถอะ ร้านนี้มันเป็นอะไรกันแน่?";
		mes "ฉันลงเอยด้วยการจ้างพนักงานที่ไร้ประโยชน์แบบนี้ได้ยังไงกันนะ...?";
		mes "*บ่นพึมพำๆ*";
		mes "*บ่นพึมพำๆ*";
		close;
	}
	mes "[Kuzkahina]";
	mes "ฉันไม่เข้าใจเลยว่า";
	mes "ทำไมฉันถึงทำ";
	mes "เงินจากร้านนี้ไม่ได้เลย...";
	mes "แม้แต่ตอนที่ฉันยังมีชีวิตอยู่";
	mes "ฉันก็ไม่เคยหาเงิน";
	mes "จากธุรกิจของฉันได้เลยสักนิด...";
	close;
}

niflheim,208,103,1	script	#Graveyard1	111,1,1,{
	end;

OnTouch_:
	if (nif_esc == 5 || nif_esc == 7 || nif_esc == 8 || nif_esc == 10) {
		mes "^3355FFคุณพบบางอย่าง";
		mes "กึ่งถูกฝังอยู่ใกล้ๆ กับหลุมศพ";
		mes "มันดูมีรูปร่างสมบูรณ์ดีนะ^000000";
		next;
		mes "^3355FFคุณต้องการจะหยิบมันขึ้นมาไหม?^000000";
		if (select("ใช่:ไม่") == 1) {
			if (nif_esc < 10)
				set nif_esc,nif_esc+2;
			else if (nif_esc == 10)
				set nif_esc,11;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

niflheim,169,71,1	script	#Graveyard2	111,1,1,{
	end;

OnTouch_:
	if (nif_esc == 6 || nif_esc == 9) {
		mes "^3355FFคุณพบบางอย่าง";
		mes "กึ่งถูกฝังอยู่ใกล้ๆ กับหลุมศพ";
		mes "มันดูมีรูปร่างสมบูรณ์ดีนะ^000000";
		next;
		mes "^3355FFคุณต้องการจะหยิบมันขึ้นมาไหม?^000000";
		if (select("ใช่:ไม่") == 1) {
			set nif_esc,nif_esc+2;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

nif_in,115,181,0	script	#Piano	111,1,1,{
	end;

OnTouch_:
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		mes "^3355FFคุณเห็นเปียโนขนาดใหญ่และหนักมาก";
		mes "คุณมีความรู้สึกว่า";
		mes "เสียงดนตรีของมันคงจะไพเราะโอ่อ่ามากทีเดียว^000000";
		next;
		mes "^3355FFเมื่อคุณตรวจสอบเปียโนตัวนั้น";
		mes "คุณพบว่ามีคีย์เปียโน 7 คีย์";
		mes "หายไปจาก";
		mes "ตรงกลางของคีย์บอร์ด";
		mes "ถ้าคุณมีคีย์ครบทั้งหมด";
		mes "คุณคงจะสามารถบรรเลงเพลงได้...^000000";
		close;
	}
	if (nif_esc == 11 && countitem(7184) > 5) {
		set nif_esc,12;
		delitem 7184,6; //Piano_Keyboard
		mes "^3355FFคุณหยิบคีย์เปียโนที่คุณพบ";
		mes "แล้วใส่พวกมันลงไปใน";
		mes "ช่องว่างที่ว่างเปล่าแต่ละช่อง^000000";
		next;
		mes "^3355FFแต่แล้วคุณก็ตระหนักได้ว่า";
		mes "คุณยังขาดคีย์สุดท้าย";
		mes "สำหรับช่องว่างตรงฝั่งซ้ายสุด";
		mes "ของคีย์บอร์ด...^000000";
		close;
	}
	end;
}

-	script	Piano3	HIDDEN_NPC,{
	end;

OnTouch_:
	if (nif_esc == 12 || nif_esc == 13 || (MISC_QUEST & 32)) {
		mes "^3355FFในขณะที่คุณก้มลงไปยัง";
		mes "ช่องว่างคีย์อันสุดท้าย";
		mes "เพื่อมองมันให้ชัดๆ...^000000";
		next;
		soundeffect "complete.wav",0;
		next;
		if (nif_esc == 12) {
			set MISC_QUEST,MISC_QUEST | 32;
			set nif_piano, 0;
		}
		mes "^3355FFคุณเริ่มรู้สึกวิงเวียนและ";
		mes "ร่างกายของคุณรู้สึกเหมือนกับว่า";
		mes "กำลังลอยอยู่ในอากาศ จากนั้น";
		mes "ภาพตรงหน้าก็เริ่มพร่ามัว...^000000";
		close2;
		warp "nif_in",179,163;
		end;
	}
	end;
}

nif_in,188,168,3	script	Witch#nif	792,{
	mes "[Kirkena]";
	mes "หือ? ...เจ้ายังไม่ตายสินะ?";
	mes "ฉันไม่แน่ใจว่าพวกเจ้ามาที่นี่";
	mes "กันได้อย่างไรมากมายขนาดนี้ และฉัน";
	mes "ไม่รู้หรอกว่าเจ้ามาที่นี่เพื่ออะไร";
	mes "แต่ที่แห่งนี้มันอันตรายสำหรับ";
	mes "คนที่มีชีวิตอยู่";
	next;
	mes "[Kirkena]";
	mes "ฉันจะส่งเจ้ากลับไป";
	mes "ข้างนอกเมืองนี้";
	mes "ถ้าเป็นไปได้ เจ้า";
	mes "อย่ากลับมาที่";
	mes "ที่แห่งนี้อีกจะดีกว่านะ";
	close2;
	warp "umbala",138,208;
	end;
}

// Message Delivery Quest :: nif_npc_lackee
//============================================================
prt_in,161,20,3	script	Erious#nif	92,{
	if (MISC_QUEST & 16384) {
		mes "[Erious]";
		mes "ฉันขอให้เจ้าปลอดภัยจากอันตราย";
		mes "ในการเดินทางนะ นักเดินทาง";
		close;
	}
	if (nif_revive == 0) {
		mes "[Erious]";
		mes "*ซิก* ซิก*";
		mes "*ร้องไห้* แงงงง...!";
		mes "Elly ทำไมเธอถึงทิ้งฉัน";
		mes "ไปแบบนี้ล่ะ...";
		emotion ET_CRY;
		next;
		if (select("ปลอบโยนเขา:เมินเฉยเขา") == 1) {
			mes "[Erious]";
			mes "อา ขอบคุณในความเมตตาของเจ้านะ";
			mes "*ซิก* ภรรยาของฉันเสียชีวิตในขณะที่ฉัน";
			mes "ออกเดินทาง ฉันเสียใจเหลือเกินที่";
			mes "ฉันไม่ได้อยู่กับเธอเมื่อ";
			mes "วาระสุดท้ายมาถึง...";
			next;
			mes "[Erious]";
			mes "ฉันอยากจะคุยกับ Elly ของฉันจริงๆ";
			mes "แม้ว่าฉันจะรู้ว่ามันสายไปแล้วก็ตาม";
			mes "*ร้องไห้* แต่... มันยังมีความหวังสุดท้าย";
			mes "ที่ฉันมีอยู่ โอกาสสุดท้ายจริงๆ";
			mes "ของฉัน...";
			next;
			mes "[Erious]";
			mes "ฉันได้รับคำบอกเล่าว่าที่ไหนสักแห่ง";
			mes "ในโลกนี้ มีเมืองแห่งหนึ่งอยู่";
			mes "ที่ซึ่งคนตายอาศัยอยู่ ฉันอาจจะ";
			mes "ได้พบกับภรรยาของฉันที่นั่น แต่";
			mes "ฉันทิ้งลูกๆ ไว้ที่นี่ลำพัง";
			mes "ไม่ได้จริงๆ *ร้องไห้*";
			next;
			if (select("ฉันสามารถส่งข้อความของคุณไปให้เธอได้นะ:ฉันเสียใจด้วยที่ได้ยินแบบนั้น") == 1) {
				mes "[Erious]";
				mes "โอ้ เจ้าทำได้งั้นเหรอ?";
				mes "ขอบคุณมากสำหรับ";
				mes "ความเอื้อเฟื้อของเจ้า ถ้าเจ้า";
				mes "ทำให้ฉันได้ ฉันจะเป็นหนี้บุญคุณ";
				mes "เจ้าอย่างมหาศาลเลยล่ะ";
				next;
				mes "[Erious]";
				mes "เจ้าคือนักเดินทางใช่ไหม?";
				mes "ถ้าการเดินทางของเจ้านำพาเจ้าไปยัง";
				mes "เมืองแห่งคนตายนั้น และถ้าเจ้า";
				mes "บังเอิญได้พบเธอเข้าจริงๆ...";
				next;
				mes "[Erious]";
				mes "ได้โปรดส่งข้อความจากฉันไปหา Elly ที";
				mes "บอกเธอว่าเรื่องที่เกิดขึ้นเมื่อ";
				mes "หนึ่งเดือนก่อนเป็นความผิดของฉันเอง";
				mes "ขอบคุณสำหรับความช่วยเหลือนะ";
				mes "และโปรดดูแลตัวเองด้วย";
				set nif_revive,1;
				setquest 11038;
				close;
			}
			mes "[Erious]";
			mes "*ร้องไห้* ไม่มีทางเลยเหรอที่ฉัน";
			mes "จะได้เจอเธออีกครั้ง? ถ้าฉันได้";
			mes "คุยกับเธออย่างน้อยอีกสักครั้งเดียว";
			mes "แม้ว่าจะเป็นครั้งสุดท้ายก็ตาม";
			mes "ฉันยอมทำทุกอย่างเลยจริงๆ...";
			close;
		}
		mes "[Erious]";
		mes "*ร้องไห้* Eeeeelllllyyyy~~~~!";
		mes "ฉันไม่เคยคิดเลยว่านั่นจะเป็น";
		mes "ครั้งสุดท้ายที่เราจะได้เห็นหน้า";
		mes "กันและกัน...";
		mes "*ซิก*...";
		close;
	}
	else if (nif_revive == 1) {
		mes "[Erious]";
		mes "สถานที่ที่ฉันได้รับคำบอกเล่ามา...";
		mes "เมืองแห่งคนตาย...";
		mes "จากที่ฉันจำได้ มันตั้งอยู่";
		mes "ใกล้ๆ กับจุดพักผ่อนบางแห่ง";
		next;
		mes "[Erious]";
		mes "ทั้งวันทั้งคืน มันเต็มไปด้วย";
		mes "เสียงกรีดร้องที่ไม่มีวันจบสิ้นของ";
		mes "ความทุกข์ทรมานและสิ้นหวัง ฉันหวังว่า";
		mes "ข้อมูลนี้จะเป็นประโยชน์ในการค้นหาของเจ้านะ";
		close;
	}
	else if (nif_revive == 5 || nif_revive == 6) {
		mes "[Erious]";
		mes "โอ้พระเจ้า! เจ้าเจอเมืองนั้นจริงๆ";
		mes "งั้นเหรอ? ฉันซาบซึ้งใจจริงๆ ที่";
		mes "เจ้าอดทนต่อความยากลำบากทั้งหมดนี้";
		mes "เพื่อฉัน... เอิ่ม... แล้วสุดท้ายล่ะ...";
		mes "เจ้าได้พบ Elly ของฉันไหม?";
		next;
		if (select("ได้พบครับ/ค่ะ:เสียใจด้วย ฉันหาไม่เจอ") == 1) {
			if (nif_revive == 6 && countitem(934) > 0) {
				mes "[Erious]";
				mes "เจ้าพูดจริงเหรอ? เธอพูดว่ายังไงบ้าง?";
				mes "เธอคนนั้นบอกว่าเธอจะยกโทษให้ฉันไหม?";
				next;
				select("ครับ/ค่ะ และเธอมอบสิ่งนี้ให้มาด้วย...");
				delitem 934,1; //Mementos
				mes "[Erious]";
				mes "อา... ในกล่องนี้มีของขวัญ";
				mes "ที่ฉันเคยมอบให้เธอตอนที่เราเริ่ม";
				mes "คบกัน เธอมีอาการป่วย";
				mes "รุนแรงตั้งแต่ก่อนที่เราจะ";
				mes "พบกันเสียอีก ฉันเลยสัญญากับเธอว่า";
				mes "ฉันจะรักษาโรคของเธอให้หาย...";
				next;
				mes "[Erious]";
				mes "นั่นคือเหตุผลที่ฉันออกเดินทาง...";
				mes "เพื่อหาวิธีรักษาหรือตัวยา";
				mes "มาให้เธอ... แต่ความพยายามของฉัน";
				mes "กลับกลายเป็นสิ่งที่เปล่าประโยชน์";
				mes "...................";
				mes ".........";
				next;
				mes "[Erious]";
				mes "อย่างไรก็ตาม ฉันขอบคุณใน";
				mes "ความเอื้อเฟื้อของเจ้ามาก ฉันอยากจะ";
				mes "ตอบแทนเจ้าสำหรับความวุ่นวาย";
				mes "ที่ฉันก่อขึ้น แต่ฉันมีเพียงแค่";
				mes "ยาที่ฉันหามาได้เพื่อรักษาโรค";
				mes "ของเธอในการเดินทางครั้งนี้เท่านั้น";
				next;
				mes "[Erious]";
				mes "ฉันหวังว่าเจ้าจะรับยานี้ไป";
				mes "เพื่อเป็นเครื่องหมายแสดงความขอบคุณจากฉัน";
				next;
				if (checkweight(610,26)) {
					set MISC_QUEST,MISC_QUEST | 16384;
					set nif_revive,0;
					completequest 11043;
					getitem 607,2; //Yggdrasilberry
					getitem 608,2; //Seed_Of_Yggdrasil
					getitem 610,10; //Leaf_Of_Yggdrasil
					mes "[Erious]";
					mes "ขอบคุณสำหรับทุกอย่างนะ";
					mes "ฉันขอให้เจ้าโชคดีและปลอดภัย";
					mes "ในการผจญภัยของเจ้า";
					close;
				}
				mes "[Erious]";
				mes "ฉันมีบางอย่างจะมอบให้เจ้า";
				mes "แต่ตอนนี้เจ้าถือของอยู่";
				mes "เยอะเกินไปที่จะรับมันได้";
				mes "ทำไมเจ้าไม่ลองเอา";
				mes "ของไปเก็บไว้ที่ไหนสักแห่งก่อนล่ะ?";
				close;
			}
			else if (nif_revive == 6 && countitem(934) < 1) {
				mes "[Erious]";
				mes "อืม... นี่มันแปลกนะ ฉันแน่ใจว่าเธอต้องมอบบางอย่างให้เจ้าเป็นหลักฐานว่าได้พบกันแน่ๆ มีอะไรที่เจ้าลืมเอามาให้ฉันหรือเปล่า?";
				close;
			}
			mes "[Erious]";
			mes "ฉันว่าเจ้าคงเข้าใจอะไรผิด";
			mes "บางอย่าง หรือไม่เจ้าก็กำลังพยายาม";
			mes "หลอกลวงชายผู้กำลังโศกเศร้าอยู่สินะ";
			close;
		}
		if (nif_revive == 5) {
			mes "[Erious]";
			mes "ฉันเข้าใจแล้ว... เจ้าหาเธอ";
			mes "ไม่พบสินะ... อย่างไรก็ตาม ฉันก็เข้าใจ";
			mes "ว่าการทำให้คำขอของฉันสำเร็จ";
			mes "มันอาจจะเป็นเรื่องที่เป็นไปไม่ได้...";
			next;
			mes "[Erious]";
			mes "เจ้าพยายามอย่างมากจริงๆ";
			mes "เพื่อช่วยฉัน ดังนั้นฉันอยากจะมอบ";
			mes "บางอย่างให้เพื่อเป็นเครื่องหมายแสดง";
			mes "ความขอบคุณจากฉัน";
			next;
			if (checkweight(610,10)) {
				set MISC_QUEST,MISC_QUEST | 16384;
				set nif_revive,0;
				completequest 11042;
				getitem 610,10; //Leaf_Of_Yggdrasil
				mes "[Erious]";
				mes "ขอบคุณสำหรับทุกอย่างนะ";
				mes "ฉันขอให้เจ้าโชคดีและปลอดภัย";
				mes "จากอันตรายในการเดินทางของเจ้า";
				close;
			}
			mes "[Erious]";
			mes "ฉันมีบางอย่างจะมอบให้เจ้า";
			mes "แต่ตอนนี้เจ้าถือของอยู่";
			mes "เยอะเกินไปที่จะรับมันได้";
			mes "ทำไมเจ้าไม่ลองเอา";
			mes "ของไปเก็บไว้ที่ไหนสักแห่งก่อนล่ะ?";
			close;
		}
		mes "[Erious]";
		mes "ฉันเข้าใจแล้ว...";
		mes "...เดี๋ยวนะ! นั่นมันไม่ใช่ของ";
		mes "ของภรรยาฉันนี่ Elly น่ะเหรอ? ทำไมเจ้า";
		mes "ถึงพยายามหลอกชายผู้เศร้าโศกแบบนี้ล่ะ...?";
		close;
	}
	end;
}

umbala,215,181,3	script	Billik	733,{
	if (nif_revive > 1 || (MISC_QUEST & 16384)) {
		mes "[Billik]";
		mes "อืม ไม่เจอกันนานเลยนะ";
		mes "ถ้ามีโอกาสในภายภาคหน้า";
		mes "ไว้เราค่อยไปเจอกันที่";
		mes "เมืองแห่งคนตายในภายหลังละกัน";
		mes "ฮ่าฮ่าฮ่าฮ่า~";
		emotion ET_BEST;
		close;
	}
	if (nif_revive == 1) {
		mes "[Billik]";
		mes "เมืองแห่งคนตาย...";
		mes "มันเป็นสถานที่ที่อันตรายมากนะ";
		mes "ทำไมเจ้าถึงอยากไปที่นั่นล่ะ?";
		next;
		if (select("ฉันต้องไปพบใครบางคนที่นั่น:มันเป็นส่วนหนึ่งของการผจญภัยของฉัน") == 1) {
			mes "[Billik]";
			mes "ใครบางคนที่เจ้าต้องพบที่นั่นงั้นเหรอ...";
			mes "อืม... ข้าเดาว่าเจ้าคงต้องการจะ";
			mes "ติดต่อกับผู้ล่วงลับสินะ...";
			mes "ข้าเห็นได้ว่าเจ้ามีความมุ่งมั่น";
			mes "ที่จะไปจริงๆ";
			next;
			mes "[Billik]";
			mes "*เฮ้อ* โอเค ถ้าเจ้าอยากไปจริงๆ";
			mes "เจ้าต้องไปตามหาชายที่ชื่อว่า";
			mes "'Feylin' มันสำคัญมากที่จะ";
			mes "บอกเขาว่าข้าเป็นคนแนะนำ";
			mes "เจ้าให้เขารู้จัก";
			next;
			mes "[Billik]";
			mes "เขาไม่ใช่คนที่จะไว้ใจใครง่ายๆ แต่เขา";
			mes "ชอบกุหลาบมาก ดังนั้นจงนำกุหลาบ";
			mes "ไปให้เขาด้วย วิธีนั้นเขาจะรู้";
			mes "แน่นอนว่าข้าเป็นคนส่งเจ้ามา";
			set nif_revive,2;
			changequest 11038,11039;
			next;
			mes "[Billik]";
			mes "เมืองแห่งคนตายตั้งอยู่ทาง";
			mes "ทิศเหนือของที่นี่ จำไว้นะว่ามัน";
			mes "อันตรายมากที่จะไปที่นั่น แม้แต่";
			mes "นักเดินทางผู้มีประสบการณ์สูงก็ตาม...";
			mes "ระวังตัวด้วยล่ะ";
			close;
		}
		mes "[Billik]";
		mes "ฮ่าฮ่า~ ข้าเดาว่าเจ้าคงชอบการผจญภัย";
		mes "พอๆ กับข้านั่นแหละ ถึงมันจะเป็น";
		mes "สถานที่ที่อันตรายมาก แต่เจ้าจะ";
		mes "ได้รับผลตอบแทนที่คุ้มค่าเมื่อเจ้า";
		mes "ไปถึงที่นั่น แต่จงระวังตัวด้วย";
		close;
	}
	else if (nif_revive == 0) {
		mes "[Billik]";
		mes "ลมอะไรพัดเจ้ามาที่นี่ล่ะ? ข้ากำลังยุ่งอยู่";
		mes "ไปซะถ้าเจ้าไม่มีธุระอะไร";
		close;
	}
}

niflheim,153,215,3	script	Feylin	794,{
	if (nif_revive == 2) {
		mes "[Feylin]";
		mes "ช่างน่าสงสารเหลือเกิน "+((Sex == SEX_MALE)?"พ่อหนุ่ม":"แม่สาวน้อย")+"...";
		mes "ต้องมาตายทั้งที่ยังอายุเพลินใจแบบนี้...";
		mes "ฉันรู้สึกเสียใจเสมอที่เห็น";
		mes "คนหนุ่มสาวต้องจากไป";
		next;
		mes "[Feylin]";
		mes "............??";
		mes "โอ้ ฉันเห็นแล้วว่าเจ้ายังไม่ตายนี่";
		mes "ใช่ไหม? มันเหลือเชื่อจริงๆ ที่";
		mes "ได้เห็นคนเป็นอีกคนหนึ่ง";
		mes "ในเมืองแห่งนี้";
		next;
		mes "[Feylin]";
		mes "เจ้ามาที่นี่เพื่อช่วยชีวิตใครสักคน";
		mes "หรือว่าเจ้ามีจุดประสงค์อื่นกันแน่?";
		mes "ถ้าเจ้ามาที่นี่เพื่อพบกับเพื่อนที่";
		mes "ล่วงลับไปแล้วละก็ เจ้าคง";
		mes "มาหาผิดคนแล้วล่ะ";
		next;
		mes "[Feylin]";
		mes "ไม่มีใครใน Niflheim ที่สามารถ";
		mes "บันดาลปาฏิหาริย์แบบนั้นได้หรอก...";
		next;
		mes "[Feylin]";
		mes "ฉันพอมองออกว่าเจ้าผ่านความยากลำบาก";
		mes "มามาก แต่น่าเสียดายที่เจ้ามาที่นี่";
		mes "โดยเปล่าประโยชน์ ฉันหวังว่าเจ้าจะ";
		mes "กลับไปในที่ที่เจ้าจากมาเสีย";
		mes "ก่อนที่มันจะสายเกินไปนะ";
		set nif_revive,3;
		changequest 11039,11040;
		close;
	}
	else if (nif_revive == 3) {
		mes "[Feylin]";
		mes "ฉันขอให้เจ้าออกจากเมืองนี้";
		mes "ไปทันที ฉันขอโทษจริงๆ";
		mes "แต่ฉันไม่รู้อะไรเลยเกี่ยวกับ";
		mes "สิ่งที่คุณกำลังถามถึง";
		emotion ET_FRET;
		next;
		if (select("แต่ว่า...:ฉันขอโทษ") == 1) {
			mes "[Feylin]";
			mes "เจ้าต้องรู้อะไรบางอย่างแน่ๆ...";
			mes "มิฉะนั้น เจ้าคงจะไม่";
			mes "ดื้อรั้นขนาดนี้ ฉันไม่แน่ใจว่า";
			mes "ใครเป็นคนบอกเจ้าว่าการติดต่อกับ";
			mes "คนตายนั้นเป็นไปได้...";
			next;
			mes "[Feylin]";
			mes "แต่ฉันไม่มีทางบอกเรื่องนั้นกับเจ้า";
			mes "แน่นอน อีกอย่าง การพูดถึง";
			mes "เรื่องพวกนั้นมันถือเป็นเรื่องต้องห้าม";
			set nif_revive,4;
			changequest 11040,11041;
			close;
		}
		mes "[Feylin]";
		mes "โปรดพิจารณาเรื่องการออกจาก";
		mes "ที่แห่งนี้ให้เร็วที่สุดเท่าที่เป็นไปได้";
		mes "ไม่มีใครควรจะอยู่ใน Niflheim";
		mes "นานเกินไปหรอก";
		close;
	}
	else if (nif_revive == 4) {
		if (countitem(748) > 0) {
			mes "[Feylin]";
			mes "หือ Billik คงจะส่งเจ้ามาสินะ";
			mes "ในเมื่อฉันเป็นหนี้บุญคุณเขาอยู่";
			mes "ฉันคิดว่าฉันจะช่วยเจ้าสักหน่อยละกัน";
			mes "แต่ขอเตือนไว้ก่อนนะ";
			mes "ราคาที่ฉันเรียกร้องอาจจะสูง";
			mes "กว่าที่เจ้าคาดไว้เยอะเลยล่ะ...";
			next;
			if (select("ฉันยินดีที่จะทำ:ฉันไม่คิดว่าฉันจะทำไหวนะ") == 1) {
				if (BaseLevel > 79) {
					mes "[Feylin]";
					mes "ตกลง แต่เจ้าต้องสัญญากับ";
					mes "ฉันก่อนนะว่าเจ้าจะไม่ให้คนอื่น";
					mes "รู้เรื่องที่ฉันกำลังจะบอก";
					mes "กับเจ้าต่อไปนี้";
					next;
					mes "[Feylin]";
					mes "วิชาคุณไสยนี้ต้องการเครื่องสังเวย";
					mes "ที่ยิ่งใหญ่ ไม่ต้องบอกก็รู้ว่ามีโอกาส";
					mes "ที่มนตรานี้จะล้มเหลวได้ ฉันยัง";
					mes "ต้องใช้ 10 Yggdrasil Leaf เพื่อร่าย";
					mes "มนต์นี้ เจ้ายังต้องการจะทำต่อไหม?";
					next;
					if (select("ตกลง:ไม่") == 1) {
						if (countitem(610) > 9) {
							mes "[Feylin]";
							mes "เข้าใจแล้ว งั้นเรามาเริ่มกันเลย";
							mes "วูมบาห์โซกะซาบาฮา!";
							mes "วูมบาห์ วูมบาห์ ฮูมบาบาห์!";
							mes "เยสสสส!";
							next;
							set .@nif_revive1,rand(1,100);
							if (.@nif_revive1 > 0) && (.@nif_revive1 < 88) {
								mes "[Feylin]";
								mes "...ฉันเสียใจด้วย";
								mes "ฉันอัญเชิญคนที่เจ้าปรารถนาจะพบ";
								mes "ไม่สำเร็จ ฉันต้องขอโทษเจ้าจริงๆ";
								mes "สำหรับผลลัพธ์ที่ออกมาเป็นแบบนี้";
								emotion ET_HUK;
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								set nif_revive,5;
								changequest 11041,11042;
								percentheal -99,0;
								next;
								mes "[Feylin]";
								mes "น่าเสียดายที่มันอาจจะเป็นเรื่อง";
								mes "ที่เป็นไปไม่ได้ที่จะอัญเชิญวิญญาณ";
								mes "ของบุคคลที่เจ้าปรารถนาจะสนทนาด้วย";
								mes "มาได้ในตอนนี้...";
								close;
							}
							else if (.@nif_revive1 > 87) && (.@nif_revive1 < 101) {
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								percentheal -50,0;
								mes "[Feylin]";
								mes "..........";
								mes "..........";
								next;
								mes "[Elly]";
								mes "..........";
								mes ".....อืม...";
								mes "...ฉันเพิ่งตายไปไม่ใช่เหรอ?";
								mes "เอ่อ ฉันรู้จักคุณหรือเปล่า?";
								next;
								select("ฉันนำข้อความจากสามีของคุณมาส่งให้ครับ/ค่ะ");
								mes "[Elly]";
								mes "โอ้... ฉันเข้าใจแล้ว... ฉันเสียใจที่ฉันไม่สามารถพบเขาได้ก่อนที่ฉันจะตาย...";
								mes "แล้วข้อความจากเขาที่มีถึงฉันคืออะไรเหรอ?";
								next;
								select("เขายกโทษให้คุณสำหรับเรื่องที่เกิดขึ้นเมื่อหนึ่งเดือนก่อน");
								mes "[Elly]";
								mes "*เฮ้อ* เขาเป็นสามีที่ดีจริงๆ เลยนะ...";
								mes "เขาช่างใส่ใจแม้กระทั่งเรื่องเล็กๆ น้อยๆ";
								mes "ทั้งที่จริงๆ แล้วฉันควรจะเป็นฝ่ายขอโทษ";
								mes "เขามากกว่าแท้ๆ...";
								next;
								mes "[Elly]";
								mes "ได้โปรดส่งกล่องนี้ให้เขาด้วย และบอก";
								mes "เขาว่าให้ลืมเรื่องของฉันไปซะ เขาควร";
								mes "จะใช้ชีวิตที่เหลืออยู่อย่างมีความสุข";
								mes "กับใครสักคน โอ้ แล้วก็ช่วยบอก";
								mes "ให้เขารู้ด้วยนะว่าฉันยกโทษให้เขาแล้ว";
								set nif_revive,6;
								changequest 11040,11043;
								getitem 934,1; //Mementos
								next;
								mes "[Elly]";
								mes "ฉันดีใจที่ได้ยินข่าวจากสามี";
								mes "ของฉันอีกสักครั้งหนึ่ง แต่ฉัน";
								mes "คิดว่าฉันต้องไปแล้วล่ะ";
								mes "ขอบคุณที่ลำบากเพื่อฉันนะ";
								mes "ลาก่อน...";
								next;
								mes "[Elly]";
								mes ".....................";
								mes "....................";
								next;
								mes "[Feylin]";
								mes "ฉันเดาว่าเจ้าคงทำสิ่งที่คุณ";
								mes "ต้องการได้สำเร็จแล้วล่ะนะ เอาละ";
								mes "ระวังตัวด้วยตอนที่เจ้ากลับไป...";
								close;
							}
						}
						else {
							mes "[Feylin]";
							mes "ถ้าเจ้าไม่มีไอเทมเหล่านั้น";
							mes "เราก็ไม่สามารถดำเนินการต่อได้";
							mes "จำไว้นะ ฉันต้องการ 10 Yggdrasil";
							mes "Leaf เพื่อทำมนต์บทนี้";
							mes "ได้โปรดนำมันมาให้เร็วที่สุดเท่าที่";
							mes "จะทำได้นะ";
							close;
						}
					}
					mes "[Feylin]";
					mes "ฉันหวังว่าเจ้าจะนำไอเทมมาให้";
					mes "ครบก่อน แต่... ฉันเองก็ไม่มี";
					mes "ความมั่นใจพอที่จะสัญญาได้ว่า";
					mes "มนต์นี้จะร่ายได้สำเร็จผลนะ";
					close;
				}
				mes "[Feylin]";
				mes "อืม... เจ้าดูเหมือนจะไม่มี";
				mes "พละกำลังเพียงพอที่จะต้านทาน";
				mes "การร่ายมนต์บทนี้ได้ ด้วยความ";
				mes "แข็งแกร่งในตอนนี้ มนต์นี้จะ";
				mes "ทำให้เจ้าถึงแก่ความตายได้เลย";
				mes "ฉันรับความเสี่ยงนั้นไม่ได้หรอก";
				close;
			}
			mes "[Feylin]";
			mes "เจ้าตัดสินใจได้ดีแล้วล่ะ";
			mes "มันไม่ใช่ความคิดที่ดีเลยที่จะทำ";
			mes "คุณไสยประเภทนี้ตั้งแต่แรก";
			mes "เพราะความเสี่ยงที่เกี่ยวข้องนั่นแหละ...";
			close;
		}
		mes "[Feylin]";
		mes "ไม่ว่าเจ้าจะถามฉันกี่ครั้งก็ตาม";
		mes "ฉันก็ช่วยเจ้าไม่ได้หรอก ได้โปรด";
		mes "ออกจากที่นี่ไปให้เร็วที่สุดเถอะ";
		close;
	}
	mes "[Feylin]";
	mes "อะไรที่ทำให้เจ้ามาที่เมืองที่";
	mes "อันตรายขนาดนี้กันนะ? ได้โปรดออกจาก";
	mes "ที่แห่งนี้ไปให้เร็วที่สุดเท่าที่จะทำได้เถอะ";
	close;
}
