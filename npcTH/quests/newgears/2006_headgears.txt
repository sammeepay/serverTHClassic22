//===== rAthena Script =======================================
//= 2006 Headgear Quests
//===== By: ==================================================
//= DiviniaRO members, cleaned by reddozen
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Censor Bar, Feather Beret, Valk. Helm, Hahoe Mask,
//= Mythical Lion Mask, Bride Mask, Magistrate Hat and Ayam
//===== Additional Comments: =================================
//= NPC id's and dialog are custom, please replace
//= 1.0a fixed typos, added missing names, more cleaned and
//=     standartized stuff a bit [Lupus]
//= 1.0b fixed Helm of Valkyries ingredients
//= 1.0c corrected item names according to item_db [Lupus]
//= 1.1 Full update. EuRO (?) dialogs, and positions. [L0ne_W0lf]
//= 1.2 Fixed bugs in the Hahoe/Lion/Bride Mask NPC. [L0ne_W0lf]
//=     Fixed some of the names to iRO.
//= 1.3 Fixed some color code bugs in Ghenirhemin. Thanks to yyCC. [L0ne_W0lf]
//= 1.3a Fixed wrong item names (due to copy/paste) [Lupus]
//= 1.4 Chungwolmang now checks if the last item set should be deleted. (bugreport:4719) [L0ne_W0lf]
//============================================================

// Censor Bar
//============================================================
rachel,91,273,8	script	?	930,{
	if (countitem(983) > 0 && countitem(7111) > 99 && countitem(938) > 98 && Zeny > 99999) {
		mes "[?]";
		mes "คิก คิก... นั่นคู่หูของข้าไม่ใช่รึ หืม?";
		mes "แล้ว เจ้าเอาทุกอย่างที่ข้าขอมาครบหรือเปล่าล่ะ?";
		mes "เยี่ยมเลย ตอนนี้ข้าสามารถสร้างไอเทมที่จะช่วยเจ้า";
		mes "ในการปกปิดตัวตนได้แล้ว! คิก คิก...";
		next;
		mes "- บึซซซ บึซซซ คลิิก คลิก -";
		mes "- คลำ คลำ คลำ คลำ -";
		mes "- บึซซซ บึซซซ คลิิก คลิก -";
		mes "- คลำ คลำ คลำ คลำ -";
		next;
		mes "[?]";
		mes "...เฮ้ อย่ามองข้ามไหล่ข้าสิ";
		mes "ข้าไม่อยากแบ่งปัน";
		mes "ความลับทางการค้าของข้ากับเจ้าหรอกนะ รู้ไหม?";
		next;
		mes "- บึซซซ บึซซซ คลิิก คลิก -";
		mes "- คลำ คลำ คลำ คลำ -";
		mes "- บึซซซ บึซซซ คลิิก คลิก -";
		mes "- คลำ คลำ คลำ คลำ -";
		next;
		delitem 983,1; //Black_Dyestuffs
		delitem 7111,100; //Smooth_Paper
		delitem 938,99; //Sticky_Mucus 
		set Zeny,Zeny-100000;
		getitem 5175,1; //Anonimity_Request
		mes "[?]";
		mes "ฟู่ว... เสร็จแล้วล่ะ ก็นะ มันทำไม่ยากเท่าไหร่ แต่...";
		mes "คิก คิก สิ่งที่สำคัญก็คือ";
		mes "ตอนนี้เจ้าสามารถซ่อนตัวตนได้แล้ว เอ้า รับนี่ไป";
		mes "หวังว่าหน้ากากนี้จะช่วยเจ้า";
		mes "ให้หลีกเลี่ยงการเผชิญหน้ากับศัตรูได้นะ อะฮ่าฮ่า!";
		close;
	}
	mes "[?]";
	mes "...ไม่มีทาง! อย่าบังอาจมาสืบเรื่องของข้าเชียวนะ!";
	mes "อย่าแม้แต่จะมาคุยกับข้า!";
	mes "ชู่ววว! อย่าให้ศัตรูรู้เชียวว่าข้าอยู่ที่ไหน!";
	next;
	mes "[?]";
	mes "เอ๋? เจ้าเป็นคนประเภทเดียวกับข้านี่นา";
	mes "ดูท่าเจ้ากำลังหนีจากบางอย่างมาสินะ";
	mes "และเจ้าอยากจะปกปิดตัวตนของเจ้า ข้าพูดถูกไหมล่ะ?";
	mes "ใช่... ข้าเดาว่าข้าพูดถูก... คิก คิก";
	next;
	mes "[?]";
	mes "ก็นะ ข้าควรจะใจดีกับสหายหน่อย";
	mes "ถึงข้าจะเปิดเผยตัวตนให้เจ้ารู้ไม่ได้";
	mes "แต่ข้าช่วยให้เจ้าซ่อนตัวจากพวกศัตรูได้อย่างปลอดภัยนะ";
	next;
	mes "[?]";
	mes "ไปเอา ^FF0000Black Dyestuffs 1 อัน^000000,";
	mes "^FF0000Slick Paper 100 อัน^000000, ^FF0000Sticky Mucus 99 อัน^000000,";
	mes "และ ^FF0000100,000 zeny^000000 มาให้ข้า";
	mes "แล้วข้าจะช่วยเจ้า คิก คิก";
	next;
	mes "[?]";
	mes "เจ้าไปได้แล้วล่ะ";
	mes "บางทีเราอาจจะช่วยเหลือกันและกันได้มากทีเดียว";
	mes "เมื่อไหร่ที่เจ้ารวบรวมวัตถุดิบครบแล้ว";
	mes "จงกลับมาโดยอย่าให้ใครรู้ล่ะ คิก คิก";
	close;
}

// Feather Beret
//============================================================
rachel,152,131,3	script	Sakjul	928,{
	if (countitem(5172) > 0 && countitem(7063) > 99 && countitem(982) > 0) {
		mes "[Sakjul]";
		mes "ยอดเยี่ยม เจ้าเอาทุกอย่างมาครบแล้ว";
		mes "เพื่อสร้าง ^FF0000Feather Beret^000000!";
		mes "เพื่อเป็นรางวัลให้กับความเหนื่อยยากของเจ้า";
		mes "ข้าจะดำเนินการสร้างหมวกใบนี้ให้ด้วยตัวเอง";
		next;
		mes "[Sakjul]";
		mes "มายืนข้างๆ ข้า แล้วจงเฝ้ามองกระบวนการสร้างอย่างสำรวม";
		mes "รักษาเกียรติของเจ้าด้วยการยืนตัวตรง";
		mes "และมองตรงไปข้างหน้า!";
		next;
		mes "[Sakjul]";
		mes "หากเจ้าไม่ทำตามที่ข้าเพิ่งบอกไป";
		mes "^FF0000การสร้างหมวกใบนี้อาจลงเอยด้วยความล้มเหลว!^000000";
		mes "และหากเจ้าล้มเหลวในการสร้างหมวก";
		mes "เจ้าจะสูญเสียวัตถุดิบทั้งหมดไป";
		mes "และข้าจะไม่รับผิดชอบต่อความผิดพลาดของเจ้าด้วย!^000000";
		next;
		mes "- ปัง ปัง -";
		mes "- ปัง ปัง -";
		mes "- ปัง ปัง -";
		mes "- ตู้ม -";
		next;
		mes "- ปัง ปัง -";
		mes "- ปัง ปัง -";
		mes "- ปัง ปัง -";
		mes "- ตู้ม -";
		next;
		set .@result,rand(1,10);
		if (.@result == 4) {
			delitem 5172,1; //Beret 
			delitem 7063,100; //Soft_Feather 
			delitem 982,1; //White_Dyestuffs
			mes "[Sakjul]";
			mes "ข้าสร้างมันสำเร็จนะ";
			mes "แต่ข้าปล่อยให้มันตกไปอยู่ในมือคนอื่นไม่ได้!";
			mes "หัวใจสำคัญของ Feather Beret คือ";
			mes "มุมที่แม่นยำของหมวกเบเร่ต์และขนนก";
			mes "แต่มุมของหมวกใบนี้มันเบี้ยวไปนิดหน่อย";
			mes "และคุณภาพมันต่ำเกินกว่าจะเป็นผลงานศิลปะของข้า!";
			next;
			mes "[Sakjul]";
			mes "ข้าเข้าใจว่าเจ้าคงรู้สึกโชคร้าย";
			mes "กับผลลัพธ์นี้ แต่อย่างไรก็ตาม ข้าขอเตือนเจ้าว่า";
			mes "อย่าเพิ่งผิดหวังนัก และจงพยายามอีกครั้ง!";
			mes "ไม่มีอะไรที่เป็นไปไม่ได้ในโลกนี้ เพราะงั้นเจ้าทำได้แน่!";
			next;
			mes "[Sakjul]";
			mes "เอาล่ะ ทำใจดีๆ ไว้!";
			mes "เจ้าเคยทำมาแล้วครั้งหนึ่ง";
			mes "เพราะงั้นเจ้าจะทำมันได้ง่ายขึ้นอีกครั้ง";
			mes "ถ้าเจ้าเข้าใจแล้วล่ะก็ ไป ไปรวบรวมวัตถุดิบมาใหม่ซะ!";
			close;
		}
		delitem 5172,1; //Beret 
		delitem 7063,100; //Soft_Feather 
		delitem 982,1; //White_Dyestuffs
		getitem 5170,1; //Feather_Beret 
		mes "[Sakjul]";
		mes "เยี่ยม ข้าทำสำเร็จแล้ว! ดูความเข้ากันที่สวยงามนี่สิ";
		mes "ระหว่างขนนกกับหมวกเบเร่ต์";
		mes "และเจ้าจะสัมผัสได้ถึงความพอดีในการจัดวาง";
		mes "ข้าต้องขอบอกเลยว่าหมวกใบนี้";
		mes "เป็นหนึ่งในสิ่งที่จำเป็นสำหรับชายหนุ่มในสมัยนี้เลยล่ะ";
		next;
		mes "[Sakjul]";
		mes "ในเมื่อข้าสร้างหมวกใบนี้ด้วยวัตถุดิบของเจ้า";
		mes "มันจึงเป็นของเจ้า";
		mes "จงดูแลหมวกใบนี้ให้ดี และจงเป็นคนที่มีความมั่นใจและยอดเยี่ยม";
		close;
	}
	mes "[Sakjul]";
	mes "ยืดตัวตรง และรักษาระดับความตื่นตัวไว้!";
	mes "ควบคุมตัวเองให้ดี และเคลื่อนไหวอย่างมีเกียรติ!";
	mes "นั่นแหละคือสิ่งที่ชายผู้น่าเลื่อมใสควรจะประพฤติตน!";
	mes "ชายหนุ่มสมัยนี้ช่างอ่อนแอและบอบบางเหลือเกิน";
	mes "เจ้าไม่เห็นด้วยกับข้าหรอกรึ นักผจญภัยหนุ่ม?";
	next;
	if (select("ครับผม!:ไม่ครับ") == 1) {
		mes "[Sakjul]";
		mes "อา! ข้าชอบคำตอบของเจ้า!";
		mes "ช่วงหลังมานี้ ข้าหาชายหนุ่มที่มีระเบียบวินัยแบบเจ้าได้ยากเหลือเกิน";
		mes "แต่น่าเสียดาย ที่แม้แต่เจ้าเองก็ยังไม่อาจมีวินัยเท่าที่ข้าคาดหวัง!";
		mes "อืม.... โอ้ ใช่แล้ว บางที ^FF0000Feather Beret^000000";
		mes "อาจจะช่วยให้เจ้าดูมีระเบียบวินัยมากขึ้นก็ได้นะ";
		next;
		mes "[Sakjul]";
		mes "ข้าอยากจะชมเชยทัศนคติของเจ้า";
		mes "ดังนั้น ข้ายินดีจะสร้าง Feather Beret ให้เจ้า";
		mes "แต่ต้องต่อเมื่อเจ้าเอาวัตถุดิบมาให้ข้าเท่านั้น เข้าใจนะ?";
		next;
		mes "[Sakjul]";
		mes "วัตถุดิบคือ ^FF0000Beret 1 ใบ^000000, ^FF0000Soft Feather 100 อัน^000000,";
		mes "และ ^FF0000White Dyestuffs 1 อัน^000000";
		mes "จำไว้ให้ดีล่ะ และเอามาให้";
		mes "ในจำนวนที่ถูกต้องด้วย!";
		next;
		mes "[Sakjul]";
		mes "จงขอบคุณข้าซะล่ะสำหรับข้อเสนอนี้";
		mes "เพราะหมวกใบนี้จะทำให้ลุคของเจ้าดูสมบูรณ์แบบ";
		mes "ดูสง่าและสง่างามยิ่งขึ้น!";
		mes "ถ้าเจ้าเข้าใจแล้วล่ะก็ ไป";
		mes "ไปรวบรวมวัตถุดิบมาให้เร็วที่สุดเท่าที่เจ้าจะทำได้!";
		close;
	}
	mes "[Sakjul]";
	mes "ข้าไม่ได้ยินเสียงเจ้าเลย! ข้าไม่รู้สึกถึงความมั่นใจจากน้ำเสียงของเจ้า!";
	mes "เจ้าจะใช้ชีวิตในโลกที่โหดร้ายนี้ด้วยทัศนคติที่อ่อนแอแบบนั้นได้ยังไง?";
	mes "ตั้งสติเดี๋ยวนี้!";
	close;
}

// Valkyrie Helm
//============================================================
hugel,147,103,3	script	Ghenirhemin	897,{
	if (moza_valkylie == 5) {
		set .@val2,rand(1,10);
		if (.@val2 == 3) {
			specialeffect EF_LIGHTSPHERE;
			mes "[Ghenirhemin]";
			mes "วัตถุดิบต่างๆ กำลังหลอมรวมเข้าด้วยกัน";
			next;
			mes "[Ghenirhemin]";
			mes "อืมม?! โอ้...โอ้?! นี่มัน...";
			mes "นี่มัน...?!";
			next;
			specialeffect EF_FORESTLIGHT2;
			mes "[Ghenirhemin]";
			mes "อา...อาฮ่าฮ่า เราทำสำเร็จแล้ว! เราทำสำเร็จแล้ว!";
			mes "Valkyre's Helm... เราได้สร้าง Valkyre's Helm ขึ้นมาใหม่แล้ว";
			mes "เกราะในตำนานของเทพเจ้าอันทรงเกียรติ!";
			mes "อา... นี่หมายความว่า...?";
			next;
			mes "[Ghenirhemin]";
			mes "เรากำลังเขียนประวัติศาสตร์หน้าใหม่!";
			mes "ใช่แล้ว เราเพิ่งจะได้เริ่มต้นเขียนประวัติศาสตร์หน้าใหม่";
			next;
			mes "[Ghenirhemin]";
			mes "ขอบคุณมากที่ให้โอกาสข้ามีส่วนร่วมในเรื่องนี้";
			mes "ข้ามั่นใจว่าบรรพบุรุษของข้าบนสวรรค์จะดีใจ";
			mes "ที่ได้เห็นความสำเร็จในการสร้าง Valkyre's Helm ขึ้นมาใหม่ในครั้งนี้";
			mes "ฮ่าฮ่าฮ่าฮ่า!";
			next;
			mes "[Ghenirhemin]";
			mes "โปรดรับหมวกใบนี้ไป";
			mes "ตามที่ข้าสัญญาไว้ มันเป็นของเจ้า";
			mes "จงภาคภูมิใจในตัวเอง และ";
			mes "อย่าทำให้หมวกใบนี้เสื่อมเสียด้วยการกระทำที่ชั่วร้าย";
			next;
			set moza_valkylie,0;
			getitem 5171,1; //Valkyrie_Helm
			mes "[Ghenirhemin]";
			mes "ขอพระเจ้าอวยพรนักผจญภัยผู้นี้";
			mes "ผู้ซึ่งได้เขียนประวัติศาสตร์หน้าใหม่";
			mes "ข้า Ghenirhemin ขอสวดอ้อนวอนต่อพระเจ้าเพื่อการเดินทางที่ปลอดภัยของเขา";
			mes "ผู้ซึ่งกำลังมุ่งหน้าไปสู่ประวัติศาสตร์หน้าใหม่";
			close;
		}
		specialeffect EF_LIGHTSPHERE;
		mes "[Ghenirhemin]";
		mes "วัตถุดิบต่างๆ กำลังหลอมรวมเข้าด้วยกัน";
		close;
	}
	else if (moza_valkylie == 4) {
		set .@val,rand(1,10);
		if (.@val == 7) {
			mes "[Ghenirhemin]";
			mes "............";
			next;
			specialeffect EF_LIGHTSPHERE;
			mes "[Ghenirhemin]";
			mes "โอ้...โอ้? พวกมัน.... พวกมันเพิ่งจะเริ่มหลอมรวมกันแล้ว!";
			mes "ดูแสงที่เจิดจ้านี่สิ!";
			mes "ข้าแทบ... ข้าไม่อยากจะเชื่อเลยว่า";
			mes "นี่คือสิ่งที่เกิดขึ้นใน Valhala!";
			next;
			set moza_valkylie,5;
			mes "[Ghenirhemin]";
			mes "ตอนนี้เราคือพยานแห่งความเมตตาของพระเจ้า... อา....";
			close;
		}
		mes "[Ghenirhemin]";
		mes "....รออีกสักหน่อยเถอะนะ";
		close;
	}
	else if (moza_valkylie == 3) {
		set moza_valkylie,4;
		mes "[Ghenirhemin]";
		mes "เจ้าจะหยุดกระวนกระวายได้หรือยัง?";
		mes "แค่ทำใจสบายๆ แล้วรอหน่อย จะได้ไหม?";
		mes "ในแง่หนึ่ง เรากำลังประกอบพิธีกรรมอันศักดิ์สิทธิ์อยู่";
		mes "และมันจะไม่เสร็จเร็วขึ้นหรอก";
		mes "เพียงเพราะเจ้าที่เป็นมนุษย์ต้องการให้มันเร็วขึ้นน่ะ";
		close;
	}
	else if (moza_valkylie == 2 && Zeny > 9999999 && countitem(4219) > 0 && countitem(4114) > 0 && countitem(4177) > 0 && countitem(4259) > 0 && countitem(4212) > 0 && countitem(4073) > 0 && countitem(4112) > 0 && countitem(4081) > 0 && countitem(4251) > 0 && countitem(4166) > 0 && countitem(7511) > 999 && countitem(7563) > 999) {
		mes "[Ghenirhemin]";
		mes "อืม... ข้าประทับใจมากที่เจ้านำทุกอย่างมาให้ข้าได้";
		mes "ทั้งที่มันยากลำบากในการรวบรวมขนาดนั้น มันยากมากเลยใช่ไหมล่ะ?";
		mes "ฮ่าฮ่า ข้าบอกได้จากการมองหน้าเจ้าแค่นั้นแหละ";
		mes "ทำได้ดีมากสหาย เจ้าทำได้ยอดเยี่ยมมาก";
		next;
		mes "[Ghenirhemin]";
		mes "เอาล่ะ ส่งของพวกนั้นมาให้ข้าสิ";
		mes "ถ้าข้าเอามันมารวมไว้ในที่เดียวกัน";
		mes "ข้ามั่นใจมากว่าพวกมันจะเริ่ม";
		mes "หลอมรวมเข้าด้วยกันเป็นหมวกด้วยพลังลึกลับของพวกมันเอง";
		next;
		if (select("มอบไอเทมให้เขา:ไม่มอบไอเทมให้เขา") == 1) {
			mes "[Ghenirhemin]";
			mes "ยอดเยี่ยม ตอนนี้สิ่งที่เราต้องทำ";
			mes "ก็แค่รอจนกว่าพวกมันจะหลอมรวมเป็นหนึ่งเดียวกันเสร็จสิ้น";
			next;
			delitem 4219,1; //Sageworm_Card
			delitem 4114,1; //Argiope_Card
			delitem 4177,1; //Dryad_Card
			delitem 4259,1; //Wooden_Golem_Card
			delitem 4212,1; //Bon_Gun_Card
			delitem 4073,1; //Pirate_Skel_Card
			delitem 4112,1; //Marduk_Card
			delitem 4081,1; //Hode_Card
			delitem 4251,1; //Elder_Card
			delitem 4166,1; //Nightmare_Terror_Card
			delitem 7511,1000; //Rune_Of_Darkness
			delitem 7563,1000; //Bloody_Rune
			set Zeny,Zeny-10000000;
			set moza_valkylie,3;
			mes "[Ghenirhemin]";
			mes "ตอนนี้ข้าตื่นเต้นไปหมด";
			mes "ด้วยความคาดหวังในผลลัพธ์ที่ดี ฮ่าฮ่าฮ่าฮ่า";
			close;
		}
		mes "[Ghenirhemin]";
		mes "?";
		close;
	}
	else if (moza_valkylie == 2) {
		mes "[Ghenirhemin]";
		mes "โปรดนำไอเทมเหล่านี้มาให้ข้า";
		mes "^FF0000Sage Worm Card 1 ใบ, Argiope Card 1 ใบ,";
		mes "^FF0000Dryad Card 1 ใบ, Wooden Golem Card 1 ใบ,";
		mes "^FF0000Bongun Card 1 ใบ, Pirate Skeleton Card 1 ใบ,";
		mes "^FF0000Marduk Card 1 ใบ, Hode Card 1 ใบ,";
		mes "^FF0000Elder Card 1 ใบ, Nightmare Terror Card 1 ใบ^000000";
		next;
		mes "[Ghenirhemin]";
		mes "^FF0000Rune of the Darkness 1,000 อัน,";
		mes "^FF0000Bloody Rune 1,000 อัน,";
		mes "^FF0000และราคาสำหรับหมวกคือ 10,000,000 zeny^000000";
		next;
		mes "[Ghenirhemin]";
		mes "มันเป็นราคาที่สมเหตุสมผลแน่นอน เมื่อพิจารณาว่า";
		mes "เจ้ากำลังจะได้ครอบครองพลังแห่งเทพเจ้า เจ้าไม่คิดงั้นเหรอ?";
		next;
		set moza_valkylie,2;
		mes "[Ghenirhemin]";
		mes "ไว้ค่อยคุยเรื่องนี้กันวันหลัง";
		mes "เมื่อเจ้าเตรียมเงินและไอเทมพร้อมแล้ว";
		mes "ตอนนี้ข้าเหนื่อยเหลือเกิน";
		mes "เพราะข้าไม่ได้พูดมากขนาดนี้มานานแล้วล่ะ";
		mes "ขอตัวก่อนนะ";
		close;
	}
	else if (moza_valkylie == 1) {
		mes "[Ghenirhemin]";
		mes "เจ้าคงจะค่อนข้างประหลาดใจในตอนนี้";
		mes "ใช่แล้ว ข้าเข้าใจได้แน่นอน";
		mes "หากเจ้าเดินทางมาหลายแห่ง";
		mes "และได้ยินเรื่องราวมามากมาย เจ้าคงจะรู้ว่านี่คืออะไร";
		mes "ใช่แล้ว มันคือ ^FF0000Valkyre's Helm^000000";
		next;
		mes "[Ghenirhemin]";
		mes "หมวกในตำนานที่อนุญาตให้สวมใส่เฉพาะชาย";
		mes "ผู้ซึ่งก้าวข้ามขีดจำกัดของตนเองมาได้เท่านั้น";
		mes "เกราะแห่งเทพเจ้าที่ได้รับพรจากพลังอันเหลือเชื่อ";
		mes "ตอนนี้เจ้าเห็นหรือยังว่าเรื่องราวของข้านั้นเป็นจริง?";
		next;
		mes "[Ghenirhemin]";
		mes "มันผ่านพ่วงเวลามานานหลายปีแล้ว ดังนั้น";
		mes "พลังของมันจึงอ่อนแอลง";
		mes "แต่เจ้าก็ยังคงสัมผัสได้ถึงความศักดิ์สิทธิ์ของหมวกใบนี้";
		mes "เจ้าไม่คิดอย่างนั้นรึ?";
		next;
		mes "[Ghenirhemin]";
		mes "ก็นะ... ข้าไม่ได้รับอนุญาตให้สวมหมวกใบนี้";
		mes "ดังนั้น หากข้าเจอใครสักคนที่คู่ควรกับหมวกใบนี้อย่างสมบูรณ์แบบ";
		mes "ข้าจะมอบมันให้เขาโดยไม่เสียดายเลยล่ะ";
		next;
		mes "[Ghenirhemin]";
		mes "แน่นอนว่า ข้าคงยกสิ่งที่มีค่าขนาดนี้";
		mes "ให้ใครไปเฉยๆ โดยไม่มีอะไรตอบแทนไม่ได้";
		mes "เจ้าก็รู้นี่นาว่ามันเป็นยังไง?";
		mes "ข้าอาจจะลองพิจารณามอบมันให้เจ้า";
		mes "เพราะดูเหมือนเจ้าจะร่ำรวยกว่าคนอื่น";
		mes "และข้าสัมผัสได้ถึงออร่าพิเศษจากตัวเจ้า";
		next;
		mes "[Ghenirhemin]";
		mes "แล้ว เจ้าว่ายังไงล่ะ?";
		mes "ข้าคิดว่าข้ามองคนไม่ผิดนะ";
		mes "ข้ารู้สึกว่าเจ้าจะสามารถสร้าง Valkyre's Helm นี้ขึ้นมาใหม่ได้อย่างสมบูรณ์";
		mes "รวมถึงใช้พลังของมันได้อย่างเต็มประสิทธิภาพด้วย";
		next;
		if (select("แน่นอน:ไม่ล่ะ ขอบคุณ") == 1) {
			mes "[Ghenirhemin]";
			mes "เจ้าเป็นคนหนุ่มที่มีความทะเยอทะยานอย่างที่ข้าคาดไว้";
			mes "มันเป็นข้อได้เปรียบที่ยิ่งใหญ่แน่นอน";
			mes "เอาล่ะ ข้าจะบอกสิ่งที่ข้ารู้ให้ฟังตั้งแต่ตอนนี้";
			mes "ดังนั้น ฟังให้ดีล่ะ";
			next;
			mes "[Ghenirhemin]";
			mes "อันดับแรก เราต้องฟื้นฟูพลังของหมวกใบนี้อย่างเต็มที่";
			mes "การจะทำแบบนั้นได้ เราต้องหาการ์ดมอนสเตอร์";
			mes "ที่ครอบครองพลังลึกลับเอาไว้";
			next;
			mes "[Ghenirhemin]";
			mes "แต่นั่นไม่ใช่ทั้งหมดนะ เจ้าก็รู้";
			mes "เราจะเอาการ์ดมอนสเตอร์พวกนั้นไปทากาวติด";
			mes "บนหมวกไม่ได้หรอก ดังนั้นเราต้องการพลัง";
			mes "ที่ทำให้เราหลอมรวมการ์ดและหมวกเข้าเป็นหนึ่งเดียวกันได้";
			next;
			mes "[Ghenirhemin]";
			mes "รูนโบราณนั้นสมบูรณ์แบบในการจัดหาพลังนั้น";
			mes "โดยเฉพาะอย่างยิ่งเราต้องการ Rune of the Darkness และ Bloody Rune";
			mes "บางที พวกมันอาจถูกเลือกเพราะพลังมืดที่ถูกสะกดไว้";
			mes "และพลังแห่งเลือดที่เหนียวแน่นน่ะรึ? นั่นก็แค่การเดาของข้าน่ะ ฮ่าฮ่าฮ่าฮ่า!";
			next;
			mes "[Ghenirhemin]";
			mes "เมื่อเราฟื้นฟูพลังของหมวกสำเร็จแล้ว";
			mes "เจ้าจะต้องซื้อหมวกใบนี้ไปจากข้า";
			mes "อย่างที่ข้าบอกก่อนหน้านี้ ข้าจะไม่";
			mes "ยกของที่มีค่าขนาดนี้ให้โดยไม่มีอะไรตอบแทน";
			mes "และมันยังเป็นการพิสูจน์คุณสมบัติของเจ้าในการเป็นเจ้าของหมวกใบนี้ด้วย";
			next;
			mes "[Ghenirhemin]";
			mes "โดยพื้นฐานแล้ว เจ้าต้องเอาของพวกนี้มาให้ข้า...";
			mes "อา มีของหลายอย่างเลยที่เจ้าต้องเอามา";
			mes "ข้าแนะนำให้เจ้าจด และจำมันเอาไว้";
			next;
			mes "โปรดนำไอเทมเหล่านี้มาให้ข้า";
			mes "^FF0000Sage Worm Card 1 ใบ, Argiope Card 1 ใบ,";
			mes "Dryad Card 1 ใบ, Wooden Golem Card 1 ใบ,";
			mes "Bongun Card 1 ใบ, Pirate Skeleton Card 1 ใบ,";
			mes "Marduk Card 1 ใบ, Hode Card 1 ใบ,";
			mes "Elder Card 1 ใบ, Nightmare Terror Card 1 ใบ^000000";
			next;
			mes "[Ghenirhemin]";
			mes "^FF0000Rune of the Darkness 1,000 อัน,";
			mes "Bloody Rune 1,000 อัน,";
			mes "และราคาหมวกคือ 10,000,000 zeny^000000";
			next;
			mes "[Ghenirhemin]";
			mes "มันเป็นราคาที่สมเหตุสมผลแน่นอน เมื่อพิจารณาว่า";
			mes "เจ้ากำลังจะได้ครอบครองพลังแห่งเทพเจ้า เจ้าไม่คิดงั้นเหรอ?";
			next;
			set moza_valkylie,2;
			mes "[Ghenirhemin]";
			mes "ไว้ค่อยคุยเรื่องนี้กันวันหลัง";
			mes "เมื่อเจ้าเตรียมเงินและไอเทมพร้อมแล้ว";
			mes "ตอนนี้ข้าเหนื่อยเหลือเกิน";
			mes "เพราะข้าไม่ได้พูดมากขนาดนี้มานานแล้วล่ะ";
			mes "ขอตัวก่อนนะ";
			close;
		}
		mes "[Ghenirhemin]";
		mes "ฮ่าฮ่า ข้าเข้าใจว่า";
		mes "มันไม่ใช่ตัวเลือกที่ตัดสินใจได้ง่ายๆ";
		mes "ใช้เวลาคิดหน่อยนะสหาย";
		mes "ถ้าเจ้ายังไม่อยากได้หมวกใบนี้ ข้าก็เคารพการตัดสินใจของเจ้า";
		close;
	}
	else if (Zeny > 9999999) {
		mes "[Ghenirhemin]";
		mes "อืม... เจ้าดูเหมือนนักเดินทางที่ยากจนนะ";
		mes "แต่ดูเหมือนเจ้าจะพกเงินจำนวนมหาศาลติดตัวมาด้วย";
		mes "ข้าเดาว่าคำกล่าวโบราณนั้นคงจะจริง:";
		mes "''อย่าตัดสินหนังสือเพียงแค่หน้าปก''";
		next;
		mes "[Ghenirhemin]";
		mes "เจ้าอาจจะเข้าใจเรื่องราวของข้า";
		mes "เจ้าอยากฟังเรื่องยาวของข้าไหมล่ะ?";
		mes "มันอาจจะน่าสนใจหรือไม่น่าสนใจสำหรับเจ้าก็ได้";
		mes "แต่ข้ารับรองได้ว่ามันจะไม่ทำให้เจ้าเบื่อแน่นอน";
		next;
		if (select("ฟัง:ไม่ฟัง") == 1) {
			mes "[Ghenirhemin]";
			mes "ดีล่ะ ให้ข้าแนะนำตัวหน่อย";
			mes "ข้าชื่อ Ghenirhemin";
			mes "ข้าก็ไม่ได้ต่างจากคนอื่นหรอก";
			mes "ถ้าจะพูดถึงข้อดีเพียงอย่างเดียวของข้า";
			mes "ข้าก็แค่มีเงินมากกว่าพวกเขานิดหน่อยน่ะ ฮ่าฮ่าฮ่า";
			next;
			mes "[Ghenirhemin]";
			mes "อย่างไรก็ตาม ข้าไม่ได้หาเงินพวกนี้มาเองหรอก";
			mes "แต่ข้าได้รับมรดกเป็นทรัพย์สินจากบรรพบุรุษ";
			mes "ผู้ซึ่งล้วนเป็นนักธุรกิจที่มีพรสวรรค์";
			mes "ข้าต้องขอบคุณเขาที่ส่งมอบทรัพย์สมบัติอันมหาศาลนี้ให้";
			next;
			mes "[Ghenirhemin]";
			mes "อันที่จริง ข้าละอายใจที่จะบอกเจ้าว่า";
			mes "ข้าไม่รู้แน่ชัดหรอกว่าบรรพบุรุษของข้าทำอาชีพอะไร";
			mes "ถึงได้สร้างความมั่งคั่งได้ขนาดนี้ บางทีบางคนอาจจะทำธุรกิจ";
			mes "บางคนอาจจะเป็นศิลปินที่ยิ่งใหญ่...";
			next;
			mes "[Ghenirhemin]";
			mes "ครอบครัวของเราแค่ได้รับพรมากพอที่จะมี";
			mes "บรรพบุรุษที่ยอดเยี่ยม ผู้ที่นำความมั่งคั่งมาสู่ตระกูล";
			mes "มันคงต้องใช้เวลาอย่างน้อยเป็นสัปดาห์หากจะให้ข้าลิสต์รายชื่อ";
			mes "บรรพบุรุษที่ประสบความสำเร็จของข้าทุกคน";
			next;
			mes "[Ghenirhemin]";
			mes "อย่างไรก็ตาม ข้าอยากจะเล่าเรื่องของบรรพบุรุษที่น่าทึ่งของข้าคนหนึ่ง";
			mes "ผู้ซึ่งได้กลายเป็นฮีโร่ของตระกูลเรา";
			next;
			mes "[Ghenirhemin]";
			mes "ก็นะ ความจริงข้าไม่รู้แน่ชัดหรอกว่า";
			mes "เขาทำอะไรมาบ้าง แต่ข้ารู้เพียงว่า";
			mes "เขานำเกียรติยศและชื่อเสียงมาสู่";
			mes "ครอบครัวเราเป็นครั้งแรก";
			mes "แต่อย่างไรก็ตาม เนื่องจากมันผ่านมานานมากแล้ว เราเลยไม่มี";
			mes "เอกสารใดๆ เกี่ยวกับเขาเลย ดังนั้น...";
			next;
			mes "[Ghenirhemin]";
			mes "แต่ข้าจำสิ่งหนึ่งเกี่ยวกับเขาได้แม่นยำเลยล่ะ";
			mes "เขาเป็นมนุษย์เพียงคนเดียวที่สามารถ";
			mes "เข้าใกล้เหล่าทวยเทพได้ และเขาได้แสดง";
			mes "พลังแห่งเทพของเขาเพื่อนำแสงสว่างมาสู่พวกเรา มวลมนุษย์";
			next;
			mes "[Ghenirhemin]";
			mes "ข้าไม่รู้ว่าเขาได้รับพลังแห่งเทพมาได้ยังไง";
			mes "หรือเขาเข้าใกล้เหล่าเทพได้ยังไง";
			mes "ข้าแค่จำสิ่งเหล่านี้ได้เพราะข้าถูกเล่าซ้ำแล้วซ้ำเล่า";
			mes "เกี่ยวกับเรื่องราวที่น่าทึ่งของเขาตั้งแต่ข้าเกิดมา...";
			next;
			mes "[Ghenirhemin]";
			mes "ข้าแสดงให้เจ้าดูได้นะว่าข้าไม่ได้กุเรื่องขึ้นมา";
			mes "ข้าบอกเจ้าไปแล้วว่าเราไม่มีเอกสารเกี่ยวกับเขาใช่ไหม?";
			mes "แต่อย่างไรก็ตาม มีบางอย่างที่ข้าแสดงให้เจ้าดูได้";
			mes "เอาล่ะ ดูนี่สิ";
			next;
			set moza_valkylie,1;
			mes "[Ghenirhemin]";
			mes "เห็นไหม? สิ่งนี้บอกเจ้าได้อย่างแน่นอนว่า";
			mes "ข้าไม่ได้เมคเรื่องขึ้นมาเอง!";
			mes "นี่คือสิ่งที่บรรพบุรุษของข้าเคยใช้อยู่เป็นประจำ!";
			close;
		}
		mes "[Ghenirhemin]";
		mes "น่าเสียดายจัง! แต่อย่างไรก็ตาม ข้าไม่อยากบังคับเจ้าให้";
		mes "ทำในสิ่งที่เจ้าไม่อยากทำหรอก ฮ่าฮ่าฮ่า";
		mes "แค่จำเอาไว้ เจ้าเพิ่งจะเลือกอีกเส้นทางหนึ่งให้กับชีวิตของเจ้าเอง";
		close;
	}
	mes "[Ghenirhemin]";
	mes "ทุกสิ่งล้วนมีหน้าที่และเหตุผลในการดำรงอยู่บนโลกใบนี้";
	mes "เจ้าและข้า เราถูกกำหนดมาให้พบกันในวันนี้";
	next;
	mes "[Ghenirhemin]";
	mes "ก็นะ... ถึงข้าจะบอกว่าทุกสิ่งล้วนมีหน้าที่";
	mes "แต่น่าเสียดายที่เจ้าไม่ได้น่าสนใจสำหรับข้าเลยสักนิด";
	mes "จะเรียกข้าว่าพวกหน้าเลือดก็ได้นะ";
	mes "แต่ข้าชอบคนรวยน่ะ";
	next;
	mes "[Ghenirhemin]";
	mes "ถ้าเจ้าพิสูจน์ให้ข้าเห็นได้ว่าเจ้าน่ะร่ำรวย";
	mes "ข้าอาจจะเปลี่ยนใจและหันมาสนใจในตัวเจ้าก็ได้นะ";
	mes "แต่ข้าจะเป็นคนตัดสินเองว่าจะสนใจเจ้าหรือไม่";
	mes "ก็ต่อเมื่อเจ้าแสดงเงินให้ข้าดู ฮ่าฮ่าฮ่าฮ่า!";
	close;
}
// Hahoe Mask / Mythical Lion Mask / Bride Mask
//============================================================
payon,135,320,3	script	Chungwolmang	97,{
	if (moza_tal == 2) {
		if (countitem(7015) > 19 && countitem(952) > 99 && countitem(1028) > 99) {
			//Hahoe_Mask: Bookclip_In_Memory, Cactus_Needle, Wild_Boar's_Mane
			callsub S_MakeMask,5176,7015,20,952,100,1028,100,0,0;
		}
		if (countitem(1048) > 499 && countitem(1053) > 1 && countitem(980) > 0) {
			//Guardian_Lion_Mask: Slender_Snake, Tooth_Of_Ancient_Fish Orange_Dyestuffs
			callsub S_MakeMask,5177,1048,500,1053,2,980,1,0,0;
		}
		if (countitem(1049) > 19 && countitem(1059) > 499 && countitem(1054) > 1 && countitem(1024) > 99) {
			//Bride_Mask: Skirt_Of_Virgin, Transparent_Cloth, Lip_Of_Ancient_Fish, Chinese_Ink
			callsub S_MakeMask,5169,1049,20,1059,500,1054,2,1024,100;
		}
		mes "[Chungwolmang]";
		mes "เอาล่ะๆ เจ้ายังรวบรวมวัตถุดิบไม่เสร็จอีกรึ?";
		mes "เจ้ารีบหน่อยจะดีกว่านะ เพราะข้าคอยนานไม่ได้หรอก!";
		mes "เดี๋ยวนะ แล้วเจ้าขอให้ข้าทำหน้ากากอันไหนให้กันแน่?";
		next;
		switch(select("Hahoe Mask:Bride Mask:Mythical Lion Mask")) {
		case 1:
			mes "[Chungwolmang]";
			mes "อ้อ ใช่ เจ้าต้องการ Hahoe Mask";
			mes "งั้นเจ้าต้องเอา... ^FF0000Bookclip in Memory 20 อัน^000000,";
			mes "^FF0000Cactus Needle 100 อัน^000000, และ";
			mes "^FF0000Mane 100 อัน^000000";
			break;
		case 2:
			mes "[Chungwolmang]";
			mes "อ้อ ใช่ เจ้าต้องการ Bride Mask";
			mes "งั้นเจ้าต้องเอา... ^FF0000Skirt of Virgin 20 อัน^000000,";
			mes "^FF0000Fabric 500 อัน^000000, ^FF0000Ancient Lips 2 อัน^000000,";
			mes "และ ^FF0000Squid Ink 100 อัน^000000";
			break;
		case 3:
			mes "[Chungwolmang]";
			mes "อ้อ ใช่ เจ้าต้องการ Mythical Lion Mask";
			mes "งั้นเจ้าต้องเอา... ^FF0000Horrendous Hair 500 อัน^000000,";
			mes "^FF0000Ancient Tooth 2 อัน^000000, และ";
			mes "^FF0000Orange Dyestuffs 1 อัน^000000";
			break;
		}
		mes "เอาวัตถุดิบมาในจำนวนที่ถูกต้อง แล้วข้าจะมอบหน้ากากให้ทันที";
		close;
	}
	if (countitem(7201) > 499) && (countitem(7200) > 9 && moza_tal == 1) {
		mes "[Chungwolmang]";
		mes "อา Four Leaf Clover ต้องนำโชคมาให้ข้าแน่ๆ";
		mes "เพราะเจ้ากลับมาเร็วกว่าที่ข้าคิดไว้ซะอีก! ว้าว...";
		mes "ขอบคุณมากนะ ตอนนี้ข้าคิดว่า";
		mes "โชคชะตาได้มาอยู่ข้างข้าแล้ว ฮ่าฮ่าฮ่า!";
		next;
		mes "[Chungwolmang]";
		mes "เอาล่ะ! ข้าจะตอบแทนเจ้าตามที่สัญญาไว้";
		mes "ข้าจะบอกความจริงทั้งหมด จริงๆ แล้ว";
		mes "ข้าคือช่างทำหน้ากากพื้นเมือง";
		mes "ข้าภูมิใจที่จะบอกเจ้าว่าไม่มีใครสู้ข้าได้";
		mes "ในเรื่องการทำหน้ากากพื้นเมืองหรอก! อะแฮ่ม!";
		next;
		mes "[Chungwolmang]";
		mes "ข้าเคยผ่านช่วงเวลาที่สภาพจิตใจไม่มั่นคง";
		mes "แถมยังโชคร้ายไปซะทุกเรื่องเพราะเหตุผลหลายๆ อย่าง";
		mes "แต่เรื่องนั้นมันเป็นอดีตไปแล้ว! ข้ากลับมาแล้ว ฮ่าฮ่าฮ่าฮ่า!";
		next;
		mes "[Chungwolmang]";
		mes "ข้ารู้สึกดี และข้ามีโชคกลับมาอยู่ข้างกายข้าแล้ว";
		mes "ข้าซาบซึ้งใจจริงๆ ที่เจ้าช่วยให้ข้ารู้สึกดีขึ้น";
		mes "เพื่อเป็นการตอบแทน ข้าจะทำหน้ากากพื้นเมืองให้เจ้าใบหนึ่ง! ฮ่าฮ่าฮ่า!";
		mes "เจ้าควรจะขอบคุณนะที่ข้าเสนอของขวัญที่ยิ่งใหญ่ขนาดนี้ให้";
		next;
		mes "[Chungwolmang]";
		mes "เจ้ารู้ไหมว่าเพราะอะไร? เพราะไม่มีใครให้ของ";
		mes "ที่มีค่าขนาดนี้แก่เจ้าได้นอกจากข้ายังไงล่ะ";
		mes "ยังไงก็ตาม ข้าทำหน้ากากได้ 3 แบบ:";
		mes "Hahoe Mask, Bride Mask และ Mythical Lion Mask";
		mes "เจ้าอยากได้อันไหนล่ะ? เลือกมาใบหนึ่ง";
		next;
		switch(select("Hahoe Mask:Bride Mask:Mythical Lion Mask")) {
		case 1:
			mes "[Chungwolmang]";
			mes "เยี่ยม เจ้าต้องการ Hahoe Mask งั้นรึ? ง่ายมาก!";
			mes "งั้นเจ้าต้องเอา... ^FF0000Bookclip in Memory 20 อัน^000000,";
			mes "^FF0000Cactus Needle 100 อัน^000000, และ";
			mes "^FF0000Mane 100 อัน^000000";
			mes "เอาวัตถุดิบมาในจำนวนที่ถูกต้อง แล้วข้าจะมอบหน้ากากให้ทันที";
			break;
		case 2:
			mes "[Chungwolmang]";
			mes "เยี่ยม เจ้าต้องการ Bride Mask งั้นรึ? ง่ายมาก!";
			mes "งั้นเจ้าต้องเอา... ^FF0000Skirt of Virgin 20 อัน^000000,";
			mes "^FF0000Fabric 500 อัน^000000, ^FF0000Ancient Lips 2 อัน^000000,";
			mes "และ ^FF0000Squid Ink 100 อัน^000000";
			mes "เอาวัตถุดิบมาในจำนวนที่ถูกต้อง แล้วข้าจะมอบหน้ากากให้ทันที";
			break;
		case 3:
			mes "[Chungwolmang]";
			mes "เยี่ยม เจ้าต้องการ Mythical Lion Mask งั้นรึ? ง่ายมาก!";
			mes "งั้นเจ้าต้องเอา... ^FF0000Horrendous Hair 500 อัน^000000,";
			mes "^FF0000Ancient Tooth 2 อัน^000000, และ";
			mes "^FF0000Orange Dyestuffs 1 อัน^000000";
			mes "เอาวัตถุดิบมาในจำนวนที่ถูกต้อง แล้วข้าจะมอบหน้ากากให้ทันที";
			break;
		}
		delitem 7201,500; //Log
		delitem 7200,10; //Flexible_String
		set moza_tal,2;
		next;
		mes "[Chungwolmang]";
		mes "...ทำไมจ้องข้าแบบนั้นล่ะ?";
		mes "อะไร เจ้าหวังว่าข้าจะทำหน้ากากให้ฟรีๆ อย่างนั้นรึ?";
		mes "ถ้าเจ้าคิดแบบนั้น เจ้าต้องคิดใหม่แบบนี้นะ";
		mes "เงื่อนไขการตอบแทนของข้าคือการใช้ทักษะของข้า";
		mes "และให้เจ้าได้ครอบครองหน้ากากอันล้ำค่าของข้า";
		mes "ไม่ใช่การทำหน้ากากให้เจ้าโดยไม่คิดค่าใช้จ่าย เข้าใจไหม?";
		next;
		mes "[Chungwolmang]";
		mes "อย่างน้อยข้าก็ไม่ได้คิดค่าบริการเจ้าสักหน่อย จริงไหม?";
		mes "แค่เอาวัตถุดิบมาให้ข้า ข้าจะทำให้ดีที่สุด";
		mes "เพื่อสร้างหน้ากากที่น่าเหลือเชื่อให้เจ้าเอง!";
		next;
		mes "[Chungwolmang]";
		mes "ในระหว่างนี้ ข้าจะเตรียม";
		mes "งานโดยใช้ Log และ Elastic Band พวกนี้";
		mes "เพราะงั้นรีบกลับมาให้เร็วที่สุดเท่าที่เจ้าจะทำได้นะ เข้าใจไหม?";
		close;
	}
	if (moza_tal == 1) {
		mes "[Chungwolmang]";
		mes "อืม... สิ่งที่ข้าขอเจ้าไปคือ";
		mes "^FF0000Log 500 อัน^000000 และ ^FF0000Elastic Band 10 อัน^000000";
		mes "ขอบคุณล่วงหน้าละกัน";
		close;
	}
	if (countitem(706) > 0) {
		mes "[Chungwolmang]";
		mes "อา...! เฮ้! ข้าบอกได้เลยว่ามีบางอย่างที่ไม่เหมือนใครในตัวเจ้า";
		mes "เจ้ามี Four Leaf Clover ติดตัวมาบ้างหรือเปล่า?";
		mes "เจ้าจะรังเกียจไหมถ้าข้าจะขอโคลเวอร์นั่นจากเจ้า?";
		next;
		mes "[Chungwolmang]";
		mes "ข้าไม่ได้ขอจากเจ้าฟรีๆ หรอกนะ";
		mes "ถึงข้าจะรับประกันไม่ได้";
		mes "แต่ข้าจะสามารถตอบแทนเจ้าได้ในเร็วๆ นี้แน่นอน";
		mes "งั้น ข้าขอรับมันไว้ได้ไหม?";
		next;
		if (select("ได้เลย:ไม่ได้หรอก") == 1) {
			mes "[Chungwolmang]";
			mes "โอ้! ขอบคุณมากเลยนะ!";
			mes "มูฮ่าฮ่าฮ่าฮ่าฮ่า! ตอนนี้ข้าจะกลายเป็นคนดวงดีแล้ว!";
			mes "ด้วยโคลเวอร์นี้ โชคลาภจะกลับมาอยู่ข้างข้าอีกครั้ง!";
			next;
			mes "[Chungwolmang]";
			mes "โอ้ ใช่! ข้าเกือบจะลืมไปเลย";
			mes "ข้าสัญญาว่าจะตอบแทนบุญคุณเจ้าใช่ไหม?";
			mes "ข้ารู้ว่ามันอาจจะฟังดูเห็นแก่ตัวไปหน่อย";
			mes "แต่ เฮ้ เจ้าช่วยข้าอีกสักเรื่องได้ไหม?";
			mes "ในเมื่อเจ้าช่วยข้าไปแล้วเรื่องหนึ่ง";
			mes "ข้าไม่คิดว่ามันจะ";
			mes "ยากเกินไปสำหรับเจ้าที่จะช่วยข้าอีกสักครั้งหรอกนะ?";
			mes "ได้โปรด แสดงความใจกว้างของเจ้าออกมาหน่อยเถอะ~";
			next;
			mes "[Chungwolmang]";
			mes "ข้าต้องการ... ^FF0000Log 500 อัน^000000 และ";
			mes "^FF0000Elastic Band 10 อัน^000000";
			mes "เจ้าจะใจดีเอามาให้ข้าได้ไหม?";
			mes "ความจริงคือ ข้าตอบแทนเจ้าไม่ได้หรอกถ้าข้าไม่มีของพวกนั้น";
			next;
			delitem 706,1; //Four_Leaf_Clover
			set moza_tal,1;
			mes "[Chungwolmang]";
			mes "เจ้าอยากให้ข้าตอบแทนเจ้าใช่ไหมล่ะ?";
			mes "เพราะฉะนั้น ได้โปรดเอาพวกมันมาให้ข้าทีเถอะนะ~";
			close;
		}
		mes "[Chungwolmang]";
		mes "ข้าเข้าใจ... ก็นะ มันไม่ใช่เรื่องง่ายที่จะยกของนำโชค";
		mes "ให้คนแปลกหน้าโดยไม่คิดหน้าคิดหลัง";
		mes "...ฮ่าฮ่า แต่นั่นก็ไม่ได้ทำให้ข้าหายหงุดหงิดหรอกนะ";
		mes "เจ้าบังอาจปฏิเสธคำขอของข้าเชียวรึ? เหอะ!";
		close;
	}
	mes "[Chungwolmang]";
	mes "อ้ากกกกก... ข้าต้องการ... ข้าต้องการ...";
	next;
	mes "[Chungwolmang]";
	mes "ข้าอยากเป็นคนโชคดี! ใครก็ได้ ช่วยเอาโชคมาให้ข้าที!";
	mes "ข้าไม่ได้ขอมากไปหรอก! ข้าขอแค่สิ่งเดียว!";
	next;
	mes "[Chungwolmang]";
	mes "ข้าต้องการ ^FF0000Four Leaf Clover^000000!";
	mes "ใครก็ได้ ช่วยเอา Four Leaf Clover มาให้ข้าที!";
	close;

S_MakeMask:
	.@item_id = getarg(0);
	mes "[Chungwolmang]";
	mes "โอ้ เจ้าเอาทุกอย่างมาครบแล้ว";
	mes "เพื่อสร้าง "+ getitemname(.@item_id) +". ยอดเยี่ยม";
	mes "โปรดมอบวัตถุดิบให้ข้า ข้าจะเริ่มสร้างหน้ากากให้ทันที";
	next;
	if (select("มอบไอเทมให้เขา:ไม่มอบไอเทมให้เขา") == 1) {
		mes "[Chungwolmang]";
		mes "เยี่ยม ข้าชอบท่าทางที่ไม่ลังเลของเจ้าจริงๆ!";
		mes "เอาล่ะ ข้าจะสร้างหน้ากากให้เร็วที่สุดเท่าที่ข้าจะทำได้!";
		next;
		mes "[Chungwolmang]";
		mes "- คลำ คลำ คลำ -";
		mes "- คลำ คลำ คลำ -";
		mes "- คลำ คลำ คลำ -";
		mes "- ปัง ปัง ปัง ปัง -";
		next;
		mes "[Chungwolmang]";
		mes "- คลำ คลำ คลำ -";
		mes "- คลำ คลำ คลำ -";
		mes "- คลำ คลำ คลำ -";
		mes "- ปัง ปัง ปัง ปัง -";
		next;
		delitem getarg(1),getarg(2);
		delitem getarg(3),getarg(4);
		delitem getarg(5),getarg(6);
		if (.@item_id == 5169) 
			delitem getarg(7),getarg(8);
		getitem .@item_id,1;
		set moza_tal,0;
		mes "[Chungwolmang]";
		mes "ว้าว มันง่ายเหมือนปอกกล้วยเข้าปากเลย!";
		mes "เจ้ารู้ไหม ข้าไม่ได้เป็นช่างฝีมือแค่ในนามหรอกนะ";
		mes "ฮ่าฮ่า เอ้า รับ "+ getitemname(.@item_id) +" ของเจ้าไป";
		mes "ข้าหวังว่าเจ้าจะสวมใส่มันด้วยความภาคภูมิใจนะ ฮ่าฮ่าฮ่า!";
		next;
		mes "[Chungwolmang]";
		mes "เอาล่ะ ข้าต้องพักผ่อนสักหน่อย";
		mes "จนกว่าจะมีลูกค้ารายต่อไป ฮ่าฮ่า บาย!";
		close;
	}
	mes "[Chungwolmang]";
	mes "เอ๋? เจ้าไม่อยากได้ "+ getitemname(.@item_id) +" งั้นรึ?";
	mes "งั้นก็ตามใจ... เจ้าคนขี้ขลาด";
	close;
}


// Magistrate Hat / Ayam
//============================================================
payon_in03,139,124,3	script	Han Garam	88,{
	if (moza_korea == 2 && countitem(954) > 299 && countitem(733) > 4 && countitem(975) > 0 && countitem(7166) > 49) {
		mes "[Han Garam]";
		mes "เจ้ากลับมาเร็วกว่าที่ข้าคาดไว้เสียอีก";
		mes "เยี่ยมเลย ข้าเองก็มีแรงกระตุ้นอย่างมากจากความจริงที่ว่า";
		mes "Ayam ของข้าจะได้เป็นของใครบางคนเช่นเจ้า";
		mes "ผู้ซึ่งมีความสามารถและน่าเชื่อถือ";
		next;
		mes "[Han Garam]";
		mes "ตกลง ถ้าอย่างนั้น ข้าจะทำหมวกให้เร็วที่สุดเท่าที่จะทำได้";
		mes "ทีนี้ ส่งวัตถุดิบทั้งหมดมาให้ข้า";
		next;
		if (select("มอบไอเทมให้เขา:ไม่มอบไอเทมให้เขา") == 1) {
			mes "[Han Garam]";
			mes "ยอดเยี่ยม! ข้าไม่จำเป็นต้องให้เทพมังกร";
			mes "มาประทับร่างเพื่อทำงานชิ้นนี้";
			mes "เพราะข้าสามารถทำมันด้วยตัวเองได้อย่างสมบูรณ์แบบ";
			mes "เพราะฉะนั้น รออยู่ตรงนั้นนะ ตกลงไหม?";
			next;
			mes "[Han Garam]";
			mes "ใช่... ถูกต้อง... อืม ฮึ่ม";
			next;
			mes "[Han Garam]";
			mes ".................";
			next;
			mes "- Han Garam เริ่มลงค้อน -";
			mes "- และประกอบวัตถุดิบเข้าด้วยกันโดยไม่พูดอะไรสักคำ -";
			next;
			mes "[Han Garam]";
			mes ".................";
			next;
			mes "[Han Garam]";
			mes "เฮ้อ มันค่อนข้างยากเหมือนกันนะ";
			mes "เพราะข้าไม่ได้ทำแบบนี้มานานพอสมควรแล้ว";
			next;
			mes "[Han Garam]";
			mes "ข้ายังรู้สึกดีที่ได้มีโอกาส";
			mes "ฝึกปรือฝีมือของข้า เจ้ารู้ไหม มันสนุกดีเหมือนกันนะ";
			mes "โปรดรับ Ayam นี้ไป";
			mes "ตามที่ข้าสัญญาไว้ นี่คือของขวัญจากข้าสำหรับเจ้า";
			next;
			delitem 954,300; //Shining_Scales
			delitem 733,5; //Crystal_Jewel___
			delitem 975,1; //Scarlet_Dyestuffs
			delitem 7166,50; //Soft_Silk_Cloth
			set moza_korea,0;
			getitem 5174,1; //A_yam
			mes "[Han Garam]";
			mes "ตอนนี้ข้าจำเป็นต้องพบกับเทพมังกรอีกครั้ง";
			mes "ถ้าข้าต้องการจะทำหมวกใบอื่นอีก... อืม...";
			mes "เอาเถอะ อย่างไรเสียครั้งนี้ข้าก็สามารถทำขึ้นมาได้ใบหนึ่งด้วยความช่วยเหลือของเจ้า";
			mes "ข้าเดาว่ามันคงจะเกิดขึ้นเมื่อถึงเวลาที่เหมาะสมเอง";
			next;
			mes "[Han Garam]";
			mes "ขอบคุณมากจริงๆ! โปรดดูแล Ayam ของข้าให้ดี";
			mes "และจงภูมิใจที่เจ้าได้เป็นเจ้าของ Ayam!";
			close;
		}
		mes "[Han Garam]";
		mes "...? อะไรนะ? เจ้าต้องการเวลามากกว่านี้เหรอ?";
		mes "เจ้าแค่ส่งมันมาให้ข้าตอนนี้เลยไม่ได้หรือไง?";
		close;
	}
	else if (moza_korea == 2 && countitem(954) > 299 && countitem(733) > 4 && countitem(983) > 0 && countitem(2221) > 0) {
		mes "[Han Garam]";
		mes "เจ้ากลับมาเร็วกว่าที่ข้าคาดไว้เสียอีก";
		mes "เยี่ยมเลย ข้าเองก็มีแรงกระตุ้นอย่างมากจากความจริงที่ว่า";
		mes "หมวก Magistrate Hat ของข้าจะได้เป็นของใครบางคนเช่นเจ้า";
		mes "ผู้ซึ่งมีความสามารถและน่าเชื่อถือ";
		next;
		mes "[Han Garam]";
		mes "ตกลง ถ้าอย่างนั้น ข้าจะทำหมวกให้เร็วที่สุดเท่าที่จะทำได้";
		mes "ทีนี้ ส่งวัตถุดิบทั้งหมดมาให้ข้า";
		next;
		if (select("มอบไอเทมให้เขา:ไม่มอบไอเทมให้เขา") == 1) {
			mes "[Han Garam]";
			mes "ยอดเยี่ยม! ข้าไม่จำเป็นต้องให้เทพมังกร";
			mes "มาประทับร่างเพื่อทำงานชิ้นนี้";
			mes "เพราะข้าสามารถทำมันด้วยตัวเองได้อย่างสมบูรณ์แบบ";
			mes "เพราะฉะนั้น รออยู่ตรงนั้นนะ ตกลงไหม?";
			next;
			mes "[Han Garam]";
			mes "ใช่... ถูกต้อง... อืม ฮึ่ม";
			next;
			mes "[Han Garam]";
			mes ".................";
			next;
			mes "- Han Garam เริ่มลงค้อน -";
			mes "- และประกอบวัตถุดิบเข้าด้วยกันโดยไม่พูดอะไรสักคำ -";
			next;
			mes "[Han Garam]";
			mes ".................";
			next;
			mes "[Han Garam]";
			mes "เฮ้อ มันค่อนข้างยากเหมือนกันนะ";
			mes "เพราะข้าไม่ได้ทำแบบนี้มานานพอสมควรแล้ว";
			next;
			mes "[Han Garam]";
			mes "ข้ายังรู้สึกดีที่ได้มีโอกาส";
			mes "ฝึกปรือฝีมือของข้า เจ้ารู้ไหม มันสนุกดีเหมือนกันนะ";
			mes "โปรดรับ Magistrate Hat นี้ไป";
			mes "ตามที่ข้าสัญญาไว้ นี่คือของขวัญจากข้าสำหรับเจ้า";
			next;
			delitem 954,300; //Shining_Scales
			delitem 733,5; //Crystal_Jewel___
			delitem 983,1; //Black_Dyestuffs
			delitem 2221,1; //Hat_
			set moza_korea,0;
			getitem 5173,1; //Judge_Hat
			mes "[Han Garam]";
			mes "ตอนนี้ข้าจำเป็นต้องพบกับเทพมังกรอีกครั้ง";
			mes "ถ้าข้าต้องการจะทำหมวกใบอื่นอีก... อืม...";
			mes "เอาเถอะ อย่างไรเสียครั้งนี้ข้าก็สามารถทำขึ้นมาได้ใบหนึ่งด้วยความช่วยเหลือของเจ้า";
			mes "ข้าเดาว่ามันคงจะเกิดขึ้นเมื่อถึงเวลาที่เหมาะสมเอง";
			next;
			mes "[Han Garam]";
			mes "ขอบคุณมากจริงๆ! โปรดดูแล Magistrate Hat ของข้าให้ดี";
			mes "และจงภูมิใจที่เจ้าได้เป็นเจ้าของ Magistrate Hat!";
			close;
		}
		mes "[Han Garam]";
		mes "...? อะไรนะ? เจ้าต้องการเวลามากกว่านี้เหรอ?";
		mes "เจ้าแค่ส่งมันมาให้ข้าตอนนี้เลยไม่ได้หรือไง?";
		close;
	}
	else if (moza_korea == 2) {
		mes "[Han Garam]";
		mes "ในการทำ Ayam ข้าต้องการ";
		mes "^FF0000300 Shining Scale^000000, ^FF00005 Cracked Diamond^000000,";
		mes "^FF00001 Scarlet Dyestuffs^000000, และ";
		mes "^FF000050 Soft Silk^000000!";
		next;
		mes "[Han Garam]";
		mes "ในการทำ Magistrate Hat ข้าต้องการ";
		mes "^FF0000300 Shining Scale^000000, ^FF00005 Cracked Diamond^000000,";
		mes "^FF00001 Black Dyestuffs^000000, และ ^FF00001 Slotted Hat^000000!";
		next;
		mes "[Han Garam]";
		mes "...นั่นคือสิ่งที่ข้าบอกไป";
		mes "ดังนั้น เลือกหมวกมา แล้วนำ";
		mes "วัตถุดิบมาให้ข้าโดยห้ามขาดเหลือสิ่งใด";
		next;
		mes "[Han Garam]";
		mes "อา~ นานแล้วนะตั้งแต่ครั้งล่าสุดที่ข้ารู้สึกถึงเทพมังกรในตัวข้า";
		mes "ข้ารู้สึกเหมือนว่าข้าได้เรียนรู้บางสิ่งที่เหนือธรรมชาติ";
		close;
	}
	else if (moza_korea == 1) {
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes "...*สั่นเทา สั่นเทา*...ท่าน...ท่านอยู่ที่นี่...ท่านอยู่ที่นี่แล้ว!";
		mes "*สั่นเทา สั่นเทา* ข้าสัมผัสได้";
		mes "ถึงเทพมังกรในตัวข้า! ว้าว!";
		mes "ท่านกำลังโบกสะบัดหาง และแสดงให้ข้าเห็นอนาคต!";
		mes "อ๊าาาาาาาาาาา!";
		next;
		specialeffect EF_LORD;
		mes "[Han Garam]";
		mes "ว๊าาาาาาาาา!";
		next;
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes "ใช่... ใช่ ข้าทำได้! ข้าสามารถทำ ^FF0000Ayam^000000";
		mes "และ ^FF0000Magistrate Hat^000000 ได้แล้วตอนนี้!";
		mes "โอ้ ข้าเห็นวิธีการทำพวกมันแล้ว";
		mes "ข้ารู้ว่าข้าต้องการอะไรเพื่อสร้างพวกมัน! ทุกอย่างอยู่ในนิมิตของข้า!";
		mes "ข้ารู้สึกเหมือนว่าข้ากำลังสร้างมันอยู่แล้วตอนนี้!";
		next;
		mes "[Han Garam]";
		mes "ในการทำ Ayam ข้าต้องการ";
		mes "^FF0000300 Shining Scale^000000, ^FF00005 Cracked Diamond^000000,";
		mes "^FF00001 Scarlet Dyestuffs^000000, และ";
		mes "^FF000050 Soft Silk^000000!";
		next;
		mes "[Han Garam]";
		mes "ในการทำ Magistrate Hat ข้าต้องการ";
		mes "^FF0000300 Shining Scale^000000, ^FF00005 Cracked Diamond^000000,";
		mes "^FF00001 Black Dyestuffs^000000, และ ^FF00001 Slotted Hat^000000!";
		next;
		mes "[Han Garam]";
		mes "ใครก็ตามที่นำสิ่งเหล่านี้มา ข้าจะสร้างหมวกให้เขา!";
		mes "อาาา~ เทพมังกร!";
		next;
		specialeffect EF_EXIT;
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes ".............";
		next;
		mes "[Han Garam]";
		mes "เฮ้อ... เทพมังกรจากไปแล้ว";
		mes "อย่างไรก็ตาม ข้ายังคงจำได้อย่างแม่นยำ";
		mes "ว่าข้าต้องการอะไร และต้องทำหมวกอย่างไร";
		mes "ดังนั้น เจ้าไม่ต้องกังวลเรื่องนั้นหรอก";
		mes "เจ้ารู้ไหม... พวกมันคือ Ayam และ Magistrate Hat ใช่ไหม?";
		next;
		mes "[Han Garam]";
		mes "เจ้าได้ยินแล้วใช่ไหมว่าข้าต้องการวัตถุดิบอะไร?";
		mes "เลือกหมวกมาใบหนึ่ง แล้วนำวัตถุดิบมาให้ข้าโดยห้ามขาดเหลือสิ่งใด";
		next;
		set moza_korea,2;
		mes "[Han Garam]";
		mes "ตกลง ถ้าอย่างนั้น ข้าจะรอเจ้ากลับมา";
		close;
	}
	if (countitem(7446) > 6 && countitem(7448) > 6 && countitem(7445) > 6 && countitem(7447) > 6) {
		mes "[Han Garam]";
		mes "ชื่อของข้าคือ Han Garam";
		mes "และข้าเป็นทายาทผู้ภาคภูมิใจของตระกูลที่มีชื่อเสียง... เอ๊ะ?";
		mes "เดี๋ยวก่อน ข้าสัมผัสได้ถึงบางสิ่งที่คุ้นเคยอย่างน่าประหลาดจากเจ้า...";
		mes "เฮ้ เจ้ามี Bijou สีละ 7 อันครบทั้ง 4 สีใช่ไหม?";
		next;
		mes "[Han Garam]";
		mes "...ว้าว! นี่มันบ้าไปแล้ว! เจ้ามีพวกมันจริงๆ ด้วยใช่ไหม?";
		mes "ว้าว เจ้าไปรวบรวมพวกมันทั้งหมดมาได้ยังไง? พวกมันหายากมากเลยนะ...";
		next;
		mes "[Han Garam]";
		mes "........อืม";
		next;
		mes "[Han Garam]";
		mes "เฮ้ ถ้าเจ้าไม่รังเกียจ ข้าขอพวกมันได้ไหม?";
		mes "ความจริงแล้ว ข้าเป็นช่างทำหมวกพื้นเมืองเพียงคนเดียว";
		mes "ในอาณาจักร Rune-Midgarts แห่งนี้";
		mes "อย่างไรก็ตาม ช่วงนี้ข้าประสบปัญหา";
		mes "และไม่สามารถจดจ่ออยู่กับธุรกิจของข้าได้";
		next;
		mes "[Han Garam]";
		mes "ปัญหาคือข้าไม่มี Bijou พวกนั้นเหลือแล้ว";
		mes "เจ้าก็น่าจะรู้ ลูกปัดหลากสีที่เจ้ามีอยู่นั่นไง";
		mes "ข้าจะสามารถเริ่มธุรกิจใหม่ได้อีกครั้ง";
		mes "ก็ต่อเมื่อข้ามีพวกมันเท่านั้น... *ถอนหายใจ*";
		next;
		mes "[Han Garam]";
		mes "เจ้าพอจะยกพวกมันให้ข้าได้ไหม?";
		mes "ข้าจะตอบแทนเจ้าด้วยหมวกพื้นเมืองที่ข้าภาคภูมิใจใบหนึ่ง";
		mes "ก็นะ ในเมื่อข้าเป็นคนเดียวที่สามารถทำมันได้";
		mes "ความจริงแล้ว เจ้าไม่ได้ทำธุรกิจที่ขาดทุนกับข้าหรอก เจ้าไม่คิดอย่างนั้นเหรอ?";
		next;
		mes "[Han Garam]";
		mes "ได้โปรดเถอะ? ข้ารับรองว่าข้าสามารถทำหมวกพื้นเมืองได้จริงๆ";
		next;
		if (select("มอบไอเทมให้เขา:ไม่มอบไอเทมให้เขา") == 1) {
			mes "[Han Garam]";
			mes "ว้าว ว้าว ขอบคุณมากจริงๆ!";
			mes "ตามที่สัญญาไว้ ข้าจะทำหมวกให้เจ้าเป็นการตอบแทน";
			mes "แต่ว่า เจ้าช่วยรอสักประเดี๋ยวได้ไหม?";
			mes "ในการทำหมวก ข้าต้องติดต่อกับเทพมังกรก่อน";
			next;
			delitem 7446,7; //Blue_Orb
			delitem 7448,7; //Yellow_Orb
			delitem 7445,7; //Green_Orb
			delitem 7447,7; //Red_Orb
			set moza_korea,1;
			mes "[Han Garam]";
			mes "...ตกลง ขอเวลาให้ข้าได้รวบรวมสมาธิสักหน่อย";
			close;
		}
		mes "[Han Garam]";
		mes "เชอะ นั่นก็เป็นการตัดสินใจของเจ้า";
		mes "แต่ถ้าเจ้าเปลี่ยนใจเมื่อไหร่ รีบบอกข้าทันทีเลยนะ";
		mes "เจ้าควรจะรู้ไว้นะว่า";
		mes "สิ่งเหล่านั้นมันคู่ควรกับคนอื่น ไม่ใช่เจ้า";
		close;
	}
	mes "[Han Garam]";
	mes "ข้าคือ Han Garam และเป็นทายาทของตระกูลที่เคยมีชื่อเสียง";
	mes "เจ้าอยากรู้ไหมว่าทำไมข้าถึงพูดว่า ''เคยมีชื่อเสียง''?";
	mes "นั่นเป็นเพราะท่านปู่ของข้าทำทรัพย์สมบัติของตระกูลสูญสิ้นไป";
	mes "และตอนนี้พวกเราก็เหมือนกับครอบครัวธรรมดาทั่วไปนั่นแหละ ฮ่าฮ่าฮ่าฮ่า!";
	next;
	mes "[Han Garam]";
	mes "ข้าไม่ได้รับมรดกอะไรเลยนอกจากทักษะการทำหมวกพื้นเมืองนี้";
	mes "ถึงอย่างนั้น มันก็เป็นมรดกที่ค่อนข้างพิเศษเชียวล่ะ";
	mes "เพราะข้าเป็นเพียงคนเดียวในอาณาจักร Rune-Midgarts";
	mes "ที่สามารถทำหมวกพื้นเมืองได้";
	next;
	mes "[Han Garam]";
	mes "น่าเสียดายที่ข้าไม่สามารถ";
	mes "ใช้ทักษะของข้าได้เพราะข้ายังไม่พบกับเทพมังกร...";
	mes "เอ๊ะ? ข้าหมายความว่ายังไงน่ะเหรอ?";
	mes "เจ้ารู้จักหน้าที่ของร่างทรงใช่ไหม?";
	mes "พวกเขารับวิญญาณเข้ามาในร่าง";
	mes "และสื่อสารกับพวกท่าน";
	next;
	mes "[Han Garam]";
	mes "ในกรณีของข้า เทพมังกรจะเข้ามาในร่างของข้า";
	mes "และมอบพละกำลังรวมถึงสติปัญญาให้แก่ข้า";
	mes "ตอนที่ตระกูลของข้ายังมั่งคั่ง";
	mes "พวกเรามีวัตถุที่ใช้ในการสวดอ้อนวอนเพียงพอ";
	mes "ที่จะอัญเชิญเทพมังกร";
	mes "แต่ตอนนี้ อย่างที่เจ้าเห็น";
	mes "พวกเราไม่มีกำลังพอจะซื้อของราคาแพงพวกนั้นได้";
	next;
	mes "[Han Garam]";
	mes "สรุปคือ ข้าอาจจะได้รับมรดกตกทอดของตระกูลมา";
	mes "แต่ข้าไม่สามารถใช้งานมันได้";
	mes "เพราะข้าไม่มีปัญญาซื้อวัตถุดิบสำหรับสวดอ้อนวอน";
	next;
	mes "[Han Garam]";
	mes "ในการอัญเชิญเทพมังกร";
	mes "ข้าต้องมี ^FF0000Bijou สีละ 7 อันครบทั้ง 4 สี^000000";
	mes "^FF0000ข้าต้องการสีละ 7 อัน สำหรับสี Blue, ";
	mes "^FF0000Yellow, Green และ Red^000000";
	mes "โดยพื้นฐานแล้วข้าต้องการ Bijou ทั้งหมด 28 อัน... อืม";
	next;
	mes "[Han Garam]";
	mes "อย่างที่ข้าบอกไปก่อนหน้านี้ ไม่เพียงแต่พวกมันจะมีราคาแพง";
	mes "แต่พวกมันยังหายากอีกด้วย...";
	mes "*ถอนหายใจ* ข้าเกรงว่าทักษะตกทอดของตระกูลข้า";
	mes "อาจจะต้องสิ้นสุดลงในรุ่นของข้าเสียแล้ว...";
	close;
}
