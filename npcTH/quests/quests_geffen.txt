//===== rAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By: ================================================== 
//= kobra_k88 & Samuray22
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project; 7.15 +
//===== Description: ========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "Welding Mask" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "names" with item id. Added missing [Lupus]
//= 1.4 Updated welding mask and headset quests. [L0ne_W0lf]
//= 1.5 Added Number Match Game. [Euphy]
//============================================================ 

// Blacksmith: 'Welding Mask' Quest
//============================================================
geffen_in,144,166,4	script	Blacksmith	63,{
	mes "[Blacksmith]";
	mes "คุณไม่เคยได้ยินชื่อเสียงของข้ามาก่อนรึ? โธ่เอ๋ย ข้านี่แหละคือช่างตีเหล็กระดับตำนานแห่งดินแดนนี้ ข้าใช้เวลากว่า 30 ปีในเปลวเพลิงที่ร้อนระอุและนองเลือดนี้ เพื่อดัดเหล็กกล้าด้วยเจตจำนงที่แข็งแกร่งดั่งเหล็กของข้า!";
	next;
	mes "[Blacksmith]";
	mes "แต่ว่า...";
	mes "ข้าเริ่มแก่ตัวลงและความสามารถของข้าก็เริ่มจางหายไปตามกาลเวลา ข้าจึงได้ประดิษฐ์บางอย่างขึ้นมาสำหรับเหล่าช่างตีเหล็กหน้าใหม่ที่มีไฟแรง ด้วยความหวังว่าพวกคุณจะสามารถเชี่ยวชาญในงานฝีมือของข้าได้เช่นกัน...";
	next;
	switch(select("มันคืออะไรเหรอ?:...")) {
	case 1:
		mes "[Blacksmith]";
		mes "พวกเราเหล่า Blacksmith ต้องจัดการกับเหล็กภายใต้ความร้อนที่สูงมากเสมอ และอากาศที่ร้อนระอุรวมถึงโลหะที่หลอมละลายนั้นมันเกินกว่าที่คนทั่วไปจะทนทานได้";
		next;
		mes "[Blacksmith]";
		mes "เพื่อเห็นแก่งานฝีมือของเรา ใบหน้าที่หล่อเหลาและดูแมนเข้มของเราจึงต้องตกอยู่ในความเสี่ยง...";
		next;
		mes "[Blacksmith]";
		mes "แต่ด้วยสิ่งมหัศจรรย์ทางเทคโนโลยีนี้ คุณไม่จำเป็นต้องกังวลอีกต่อไป! จงดูนี่สิ ^3355FFWelding Mask^000000 !!";
		next;
		mes "[Blacksmith]";
		mes "มันทำงานโดยการปกปิดใบหน้าของคุณด้วยแผ่นโลหะหนา บอกตามตรงว่ามันเป็นไอเทมที่ทุกคนควรจะต้องการ! ถ้าไม่ใช่สำหรับตัวคุณเอง มันก็น่าจะเป็นความคิดที่ดีที่จะมอบสิ่งนี้ให้เพื่อนที่กำลังศึกษาด้านการตีเหล็กอยู่";
		next;
		mes "[Blacksmith]";
		mes "แล้วว่าเป็นยังไงล่ะ? ถ้าคุณมอบ ^2244FF50 Steel^000000 และเงิน ^4422FFเพียง 2000 Zeny^000000 ให้ข้า ข้าจะทำมันให้คุณทันทีเลย~!!";
		next;
		switch(select("อืม... ก็ไม่เลว ตกลงครับ/ค่ะ:มันทำงานยังไงนะขออีกรอบ?")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[Blacksmith]";
				mes "อา ข้าเห็นแล้วว่าคุณเห็นคุณค่าในงานของ Blacksmith! ตกลง ข้าจะทำให้เดี๋ยวนี้เลย !!";
				next;
				delitem 999,50; //Steel
				set Zeny,Zeny-2000;
				getitem 2292,1; // Welding_Mask
				mes "[Blacksmith]";
				mes "ฮ่าๆๆ~! ใช้มันให้คุ้มค่าล่ะ!";
				close;
			}
			else {
				mes "[Blacksmith]";
				mes "หืมมม...";
				mes "คุณไม่มีไอเทมตามที่ข้าต้องการ";
				next;
				mes "[Blacksmith]";
				mes "ถ้าเป็น Blacksmith ทั่วไปอาจจะมองว่านี่เป็นการไม่ให้เกียรติกัน แต่ข้าจะยกโทษให้คุณละกัน แต่อย่าลืมสิ่งที่ข้าต้องการล่ะถ้าคุณอยากจะให้ข้าทำหน้ากากนี้ให้";
				close;
			}
		case 2:
			mes "[Blacksmith]";
			mes "เจ้า ^3355FFWelding Mask^000000 นี้จะปกป้องใบหน้าของคุณด้วยแผ่นโลหะหนา การประดิษฐ์นี้เป็นผลมาจากความเหนื่อยยากตลอด 30 ปีที่อยู่หน้าค้อนและทั่งตีเหล็ก";
			close;
		}
	case 2:
		mes "[Blacksmith]";
		mes "อา...!";
		mes "ข้าเดาว่านี่คงเป็นครั้งแรกที่คุณได้พบกับ Blacksmith ที่ยิ่งใหญ่ขนาดข้า จนคุณถึงกับพูดไม่ออกด้วยความเลื่อมใส ไม่ต้องรีบนะ ค่อยๆ ใช้เวลาแล้วพูดออกมาเมื่อคุณพร้อม";
		next;
		mes "^3355FFหมอนี่ดูท่าทางจะ";
		mes "หลงตัวเอง";
		mes "อยู่ไม่น้อยเลยนะ^000000";
		close;
	}
}

// Eric: 'Headset' Quest
//============================================================
geffen_in,30,71,4	script	Eric	83,{
	mes "[Eric]";
	mes "ได้โปรดฟัง";
	mes "เรื่องราวของ";
	mes "คุณปู่ผู้ล่วงลับ";
	mes "ของผมหน่อยนะครับ";
	next;
	switch(select("คุยเรื่องราว:ถามเรื่องไอเทมที่ต้องใช้:เริ่มการผลิต:ยกเลิก")) {
	case 1:
		mes "[Eric]";
		mes "คุณปู่ของผมที่จากไปเมื่อไม่กี่ปีที่แล้ว ท่านเป็นคนพิเศษสำหรับผมมาก ท่านอยู่กับผมเสมอและคอยดูแลผม ท่านดีกว่าพ่อของผมที่มักจะยุ่งอยู่กับการทำงานตลอดเวลาเสียอีก";
		next;
		mes "[Eric]";
		mes "สำหรับผม ห้องสมุดของคุณปู่เปรียบเสมือนดินแดนในจินตนาการที่ผมสามารถหลบหนีไปได้ ผมใช้เวลาส่วนใหญ่ของผมที่นั่นครับ";
		next;
		mes "[Eric]";
		mes "ผมได้เรียนรู้สิ่งต่างๆ มากมายจากหนังสือในห้องสมุดของท่าน คุณปู่สนใจในเรื่องการแปรธาตุและดนตรี และห้องสมุดของท่านก็เต็มไปด้วยหนังสือเก่าแก่ที่น่าพิศวงมากมาย";
		next;
		mes "[Eric]";
		mes "อยู่มาวันหนึ่ง...";
		mes "เหมือนเช่นเคย ผมเข้าไปในห้องสมุดและได้พบกับแผนผังที่น่าสนใจเข้า";
		next;
		mes "[Eric]";
		mes "มันแสดงให้เห็นเครื่องมือบางอย่างที่ดูเหมือน Ear Muffs ซึ่งเมื่อสวมไว้บนศีรษะ จะสามารถส่งผ่านเสียงดนตรีหรือเสียงกระซิบไปยังหูผ่านสายไฟเส้นเล็กๆ ได้";
		next;
		mes "[Eric]";
		mes "ผมเดาว่า...";
		mes "มันคืองานที่ยังทำไม่เสร็จของคุณปู่ ผมเคยไปพบ Alchemist สองสามคนเพื่อขอให้พวกเขาช่วยผลิตมันขึ้นมา แต่พวกเขาก็ปฏิเสธข้อเสนอของผม...";
		next;
		mes "[Eric]";
		mes "สุดท้าย ผมเลยคิดว่า 'ฉันต้องทำมันด้วยตัวเอง' ผมจึงค้นคว้าอยู่หลายปี และตอนนี้ผมเข้าใจวิธีทำสิ่งนั้นอย่างถ่องแท้แล้ว แต่ปัญหาคือ ผมไม่สามารถรวบรวมวัตถุดิบเพื่อสร้างมันได้เลย!";
		next;
		mes "[Eric]";
		mes "ถ้าผมได้สิ่งของเหล่านั้นมา ผมจะสร้างมันขึ้นมาทันที ถ้าคุณสนใจในไอเดียของผม คุณจะช่วยนำสิ่งของเหล่านั้นมาให้ผมได้ไหมครับ?";
		next;
		mes "[Eric]";
		mes "ทั้งหมดที่ผมต้องการทำคือสร้างสิ่งนี้ขึ้นมาเพื่อเป็นเกียรติแก่คุณปู่และสานต่องานที่ท่านเริ่มไว้ให้สำเร็จ ถ้าคุณอยากจะได้มันไว้เอง ผมก็ไม่รังเกียจหรอกครับ";
		close;
	case 2:
		mes "[Eric]";
		mes "40 ^3355FFSteel^000000";
		mes "1 ^3355FFOridecon^000000";
		mes "1 ^3355FFAlcohol^000000";
		mes "1 ^3355FFCoal^000000";
		mes "นี่คือสิ่งของที่ผมต้องการสำหรับผลงานชิ้นเอกของคุณปู่ครับ";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alchol
			delitem 1003,1; //Coal
			getitem 5001,1; //Headset
			mes "*ตอก! ตอก! ตอก!*";
			next;
			mes "[Eric]";
			mes "ขอบคุณครับ";
			mes "เพราะคุณแท้ๆ ผมถึงทำตามความปรารถนาของคุณปู่ได้สำเร็จ ท่านคงจะมีความสุขหากมองลงมาจากสวรรค์";
			next;
			mes "[Eric]";
			mes "อย่างที่ผมบอกไป ได้โปรดรับสิ่งนี้ไว้เถอะครับ ผมแค่ภูมิใจที่สามารถสร้างสิ่งนี้ขึ้นมาได้ด้วยตัวเอง";
			close;
		}
		else {
			mes "[Eric]";
			mes "ขอบคุณที่พยายามช่วยผมนะครับ แต่คุณยังนำไอเทมมาไม่ครบตามที่ผมต้องการ แต่ผมคิดว่าคุณคงจะหามันมาจนครบได้ในเร็วๆ นี้แหละครับ";
			close;
		}
	case 4:
		mes "[Eric]";
		mes "มะ-ไม่เหรอครับ?";
		mes "อืม...";
		mes "คุณคงจะมีเรื่อง";
		mes "ให้ต้องกังวลอยู่เหมือนกันสินะ";
		mes "ลาก่อนครับ";
		close;
	}
}

// Number Match Game :: gef_yagu
//============================================================
geffen,122,77,4	script	Nia#yagu	700,{
	mes "[Nia]";
	mes "สวัสดีค่ะ ฉันชื่อ Nia เป็น";
	mes "ลูกศิษย์คนที่ห้าของ Mawong";
	mes "เจ้าแห่งเกมการเล่น เอาล่ะค่ะ";
	mes "คุณสนใจจะเล่นเกมทายตัวเลข";
	mes "(Number Match) กับฉันไหมคะ?";
	next;
	switch(select("เกม Number Match คืออะไร?:ลองเล่นโหมดฝึกซ้อม:เริ่มเล่นจริง:ดูเจ้าของสถิติ:ยกเลิก")) {
	case 1:
		mes "[Nia]";
		mes "อา สำหรับจุดประสงค์ของ";
		mes "เกม Number Match คือการ";
		mes "ทายตัวเลขที่ฉันกำลังคิดอยู่ค่ะ";
		mes "แน่นอนว่ามีกฎบางอย่างที่จำกัด";
		mes "ตัวเลขที่ฉันสามารถเลือกได้";
		next;
		mes "[Nia]";
		mes "ตัวเลขทั้งหมดที่ฉันตั้งขึ้น";
		mes "ต้องมี 3 หลัก และฉันไม่สามารถ";
		mes "ใช้เลข 0 ในหลักใดก็ได้ รวมถึง";
		mes "ฉันไม่สามารถใช้ตัวเลขซ้ำกัน";
		mes "ในแต่ละหลักมากกว่าหนึ่งครั้งค่ะ";
		next;
		mes "[Nia]";
		mes "ยกตัวอย่างเช่น ฉันไม่สามารถ";
		mes "ใช้เลข 103, 112, 252, หรือ 701";
		mes "ตัวเลขเหล่านั้นอาจจะมีเลข 0";
		mes "หรือมีการใช้ตัวเลขซ้ำในหลักต่างๆ";
		mes "มากกว่าหนึ่งครั้ง เข้าใจหรือยังคะ?";
		next;
		mes "[Nia]";
		mes "เอาล่ะ คุณจะมีโอกาสทายแค่ 5 ครั้ง";
		mes "และหลังจากทายแต่ละครั้ง ฉันจะ";
		mes "บอกคุณว่ามีกี่หลักที่คุณทายถูก";
		mes "ตามด้วยจำนวนหลักที่ทายตัวเลขถูก";
		mes "และวางอยู่ในตำแหน่งที่ถูกต้องค่ะ";
		next;
		mes "[Nia]";
		mes "ตัวอย่างเช่น สมมติว่าตัวเลข";
		mes "ที่ฉันคิดไว้คือ 168";
		mes "ถ้าคุณทายครั้งแรกเป็น 678 แสดงว่าคุณทายตัวเลขถูกสองหลักคือ";
		mes "6 และ 8 แต่ว่ามีเพียงเลข 8 เท่านั้น";
		mes "ที่อยู่ในตำแหน่งที่ถูกต้องค่ะ";
		next;
		mes "[Nia]";
		mes "จริงสิ มันจะเป็นการดีนะถ้าคุณ";
		mes "ลองเล่นโหมดฝึกซ้อมก่อน";
		mes "จากนั้นพอเริ่มคล่องแล้ว ค่อย";
		mes "มาเล่นเกม Number Match ของจริง";
		mes "อย่าลืมว่าความเร็วมีผลต่อคะแนนด้วยนะ!";
		next;
		mes "[Nia]";
		mes "อ้อ ถ้าคุณบังเอิญทำสถิติ";
		mes "ใหม่ในเกม Number Match ได้";
		mes "ชื่อของคุณจะถูกบันทึกไว้ค่ะ";
		mes "อย่าพลาดโอกาสที่จะมีชื่อเสียง";
		mes "และอาจจะได้โชคลาภด้วยนะคะ~";
		close;
	case 2:
		mes "[Nia]";
		mes "เยี่ยมเลย มาลองเล่นโหมดฝึกซ้อม";
		mes "กันค่ะ โปรดลองทายตัวเลขของฉันดูนะ";
		mes "ถ้าต้องการยกเลิก ให้ใส่ตัวเลข";
		mes "ที่มากกว่า 1,000 ค่ะ";
		set .@practice,1;
		break;
	case 3:
		mes "[Nia]";
		mes "เยี่ยมเลย มาเล่น Number Match กัน!";
		mes "คุณจะมีโอกาส 5 ครั้งในการทายตัวเลขที่ฉันกำลังคิดอยู่ค่ะ";
		mes "ถ้าต้องการยกเลิก ให้ใส่ตัวเลข";
		mes "ที่มากกว่า 1,000 ค่ะ";
		set .@practice,0;
		break;
	case 4:
		set .@name_record$, $050908_minus1_yagu$;
		if (.@name_record$ == "") //custom
			set .@name_record$,"(null)";
		set .@score_record, $050908_minus1_yagu;
		set .@score_min, .@score_record / 60;
		set .@score_sec, .@score_record % 60;
		mes "[Nia]";
		mes "^ff0000" + .@name_record$ + "^000000 คือ";
		mes "เจ้าของสถิติเกม Number Match";
		mes "ด้วยเวลา";
		mes "^ff0000" + .@score_min + " นาที " + .@score_sec + " วินาที^000000 ค่ะ";
		close;
	case 5:
		mes "[Nia]";
		mes "ดูแลตัวเองด้วยนะคะ นักเดินทาง";
		mes "ถ้าคุณรู้สึกอยากเล่นเกม";
		mes "Number Match เมื่อไหร่ ก็แวะมา";
		mes "คุยกับฉันได้เสมอเลยนะ ตกลงไหม?";
		mes "แล้วเจอกันค่ะ~";
		close;
	}
	next;

	// Generate 3 digits of a number.
	while(1) {
		set .@yagu100, rand(1,9);
		set .@yagu10, rand(1,9);
		set .@yagu1, rand(1,9);
		if ((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1) && (.@yagu10 != .@yagu1)) {
			// Store the actual number (unofficial).
			set .@yagu, (100*.@yagu100) + (10*.@yagu10) + .@yagu1;
			break;
		}
	}

	// The official time calculation is based on ServerTime,
	// an int HHMMSS equivalent to atoi(gettimestr("%H%M%S",7)).
	// UNIX time is really a better way to do this.
	set .@start_time, gettimetick(2);

	while(1) {

		// Guess a number that meets all conditions.
		while(1) {
			if (input(.@input,100,999) != 0) {
				mes "[Nia]";
				mes "คุณได้ยกเลิกการเล่น";
				mes "เกม Number Match แล้วค่ะ";
				close;
			}
			set .@input100, .@input / 100;
			set .@input10, (.@input % 100) / 10;
			set .@input1, .@input % 10;
			if ((.@input100 > 0) && (.@input10 > 0) && (.@input1 > 0)) {
				if ((.@input100 != .@input10) && (.@input100 != .@input1) && (.@input10 != .@input1))
					break;
			}
			mes "[Nia]";
			mes "คุณใส่ตัวเลขหลักใดหลักหนึ่ง";
			mes "เป็นเลข 0 หรือใส่ตัวเลขซ้ำกัน";
			mes "ในแต่ละหลักมากกว่าหนึ่งครั้งค่ะ";
			mes "โปรดลองทายใหม่อีกครั้งนะคะ";
			next;
		}

		set .@retry, .@retry+1;
		mes "[Nia]";
		mes "^ff0000"+strcharinfo(0)+"^000000,";
		mes "คุณทายว่า ^0000ff"+.@input+"^000000";
		mes "รอสักครู่เพื่อให้ฉันคำนวณ";
		mes "ผลลัพธ์ของคุณนะคะ";
		next;

		// Correct digits in correct locations.
		set .@strike,0;
		if (.@yagu100 == .@input100)
			set .@strike, .@strike+1;
		if (.@yagu10 == .@input10)
			set .@strike, .@strike+1;
		if (.@yagu1 == .@input1)
			set .@strike, .@strike+1;

		// Correct digits in incorrect locations.
		set .@ball,0;
		if ((.@yagu100 == .@input10) || (.@yagu100 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu10 == .@input100) || (.@yagu10 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10))
			set .@ball, .@ball+1;

		if (.@strike == 3) {
			if (.@practice) {
				mes "[Nia]";
				mes "ใช่แล้วค่ะ ตัวเลขที่";
				mes "ฉันทายไว้คือ ^ff0000"+.@yagu+"^000000!";
				mes "ยินดีด้วยนะคะ คุณเพิ่งชนะ";
				mes "ในเกมโหมดฝึกซ้อมนี้! ตอนนี้";
				mes "คุณน่าจะพร้อมสำหรับของจริงแล้วนะ~";
				close;
			} else {

				// Calculate final time and check against the previous record.
				set .@end_time, gettimetick(2);
				if (.@end_time < .@start_time)
					set .@end_time, .@end_time + 3600;
				set .@total_time, .@end_time - .@start_time;
				set .@total_min, .@total_time / 60;
				set .@total_sec, .@total_time % 60;
				set .@name_record$, $050908_minus1_yagu$;
				if (.@name_record$ == "") //custom
					set .@name_record$,"(null)";
				set .@score_record, $050908_minus1_yagu;
				set .@score_min, .@score_record / 60;
				set .@score_sec, .@score_record % 60;

				if (.@score_record >= .@total_time || !.@score_record) { //custom (second condition)
					set $050908_minus1_yagu$, strcharinfo(0);
					set $050908_minus1_yagu, .@total_time;
					mes "[Nia]";
					mes "สุดยอด! คุณทายตัวเลข";
					mes "ของฉันถูก ซึ่งก็คือ";
					mes "^ff0000" + .@yagu + "^000000 คุณทำเวลาไป";
					mes "^ff0000" + .@total_min + " นาที " + .@total_sec + " วินาที^000000";
					mes "ซึ่งทำลายสถิติเดิมที่ทำไว้";
					mes "^ff0000" + .@score_min + " นาที " + .@score_sec + " วินาที^000000 ค่ะ";
					next;
					mes "[Nia]";
					mes "^ff0000" + .@name_record$ + "^000000";
					mes "เป็นคนทำสถิติเก่าไว้นั้น";
					mes "แต่ตอนนี้คุณคือคนใหม่ที่";
					mes "ทุกคนต้องมาโค่นในเกม";
					mes "Match Game นี้ ยินดีด้วยนะคะ";
					mes "สำหรับการสร้างสถิติใหม่!";
					close;
				}
				mes "[Nia]";
				mes "ทำได้ดีมากค่ะ คุณทายตัวเลข";
				mes "ของฉันถูก! คำตอบแน่นอนว่า";
				mes "คือ ^ff0000" + .@yagu + "^000000 คุณสามารถ";
				mes "ทายตัวเลขนี้ได้ถูกต้องในเวลา";
				mes "^ff0000" + .@total_min + " นาที " + .@total_sec + " วินาที^000000 ค่ะ";
				next;
				mes "[Nia]";
				mes "^ff0000" + .@name_record$ + "^000000 คือ";
				mes "เจ้าของสถิติ Number Match";
				mes "คนปัจจุบันด้วยเวลา";
				mes "^ff0000" + .@score_min + " นาที " + .@score_sec + " วินาที^000000 ค่ะ";
				mes "ขอให้โชคดี และหวังว่าคุณจะ";
				mes "ทำลายสถิตินี้ได้ในสักวันนะคะ~";
				close;
			}
		} else {
			if (.@practice) {
				mes "[Nia]";
				mes "ในคำทายของคุณมีตัวเลขที่ถูกอยู่ ^ff0000" + .@ball + "^000000 ตัว";
				mes "และมีตัวเลข ^ff0000" + .@strike + "^000000 หลักที่คุณทาย";
				mes "ตัวเลขได้ถูกต้องและวางอยู่";
				mes "ในตำแหน่งที่ถูกต้องค่ะ";
				mes "เอาล่ะ ลองใหม่อีกครั้งนะ~";
				next;
			} else {
				mes "[Nia]";
				mes "คำทายล่าสุดของคุณมีตัวเลขที่ถูก ^ff0000" + .@ball + "^000000 ตัว";
				mes "และมี ^ff0000" + .@strike + "^000000 หลักในคำทายของคุณ";
				mes "ที่มีตัวเลขถูกต้องและวางอยู่ใน";
				mes "ตำแหน่งหลักที่ถูกต้องค่ะ";
				switch(.@retry) {
					case 1: mes "คุณเหลือโอกาสทายอีกสี่ครั้งค่ะ"; break;
					case 2: mes "คุณเหลือโอกาสทายอีกสามครั้งค่ะ"; break;
					case 3: mes "คุณเหลือโอกาสทายอีกสองครั้งค่ะ"; break;
					case 4: mes "คุณเหลือโอกาสทายอีกแค่ครั้งเดียวแล้วนะคะ..."; break;
					case 5: mes "คำตอบคือ ^ff0000" + .@yagu + "^000000 ค่ะ"; close;
				}
				next;
			}
		}
	}
}
