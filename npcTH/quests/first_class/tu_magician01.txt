//===== rAthena Script =======================================
//= Magician Class Tutorial and Job Specific Quest
//===== By: ==================================================
//= Fix up by Jukka
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Magician training quest.
//===== Additional Comments: =================================
//= 1.0 Fully working
//= 1.1 Optimized [Lupus]
//= 1.2 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.3 Misc. updates. [L0ne_W0lf]
//= 1.4 Updated to match the official script. [Euphy]
//= 2.0 Replaced occurrence of PcName and clean-up. [Capuche]
//============================================================

// Sign
//============================================================
geffen,61,174,4	script	Sign#M	HIDDEN_NPC,{
	mes "- เปลี่ยนอาชีพ Mage -";
	close;
}

// New Mage Manager
//============================================================
geffen,67,180,4	script	New Mage Manager#M	8_F,{
	mes "[Mana]";
	if(Class == Job_Novice){
		mes "คุณยังไม่พร้อมสำหรับ";
		mes "การฝึกฝนใดๆ ที่ฉันมี";
		mes "ให้ในตอนนี้ คุณต้องไปฝึก";
		mes "เพิ่มเติมและเปลี่ยนอาชีพ";
		mes "เป็น Mage ก่อนนะ เข้าใจไหมจ๊ะ?";
		close;
	}
	if(BaseClass != Job_Mage || (eaclass()&EAJL_BABY)){
		mes "รู้ไหม บางครั้ง";
		mes "อาชีพอื่นๆ อาจจะดู";
		mes "ดีกว่านิดหน่อยในบางสถานการณ์";
		mes "การต่อสู้ แต่ขอให้ภูมิใจ";
		mes "ในตัวเองเสมอ ไม่ใช่ว่า";
		mes "ใครๆ ก็ทำอย่างที่คุณทำได้นะ~";
		close;
	}
	if(Class != Job_Mage && tu_magician01 < 7){
		mes "ถึงแม้ฉันจะมีการฝึกฝน";
		mes "เวทมนตร์ให้ แต่มันก็ไม่ค่อยมี";
		mes "ประโยชน์เท่าไหร่ถ้าคุณมีประสบการณ์";
		mes "ในการใช้เวทมนตร์อยู่แล้ว ฉันเสียใจด้วย";
		mes "แต่คุณต้องพึ่งพาตัวเอง";
		mes "ในการขัดเกลาทักษะของคุณแล้วล่ะ";
		close;
	}
	switch(tu_magician01){
	case 0:
		mes "อ๊ะ ยินดีต้อนรับจ้า~";
		mes "ฉันชื่อ Mana เป็น";
		mes "ครูฝึกที่ดูแลเหล่า";
		mes "สมาชิกใหม่ในสมาคม";
		mes "Mage Guild นั่นหมายความว่า";
		mes "ฉันต้องดูแลคุณยังไงล่ะ~";
		next;
		mes "[Mana]";
		mes "ฉันเพิ่งจะมาทำหน้าที่";
		mes "ตรงนี้ได้ไม่นานนัก";
		mes "แต่ฉันมาที่นี่เพื่อช่วยคุณถ้า";
		mes "คุณต้องการเรียนรู้เพิ่มเติม";
		mes "เกี่ยวกับพื้นฐานอาชีพ Mage";
		next;
		switch(select("ช่วยสอนฉันหน่อยได้ไหม?:ไว้โอกาสหน้าละกัน...")) {
		case 1:
			mes "[Mana]";
			mes "แน่นอนอยู่แล้ว!";
			mes "และไม่ต้องกังวลไปนะ";
			mes "ทุกคนต่างก็เคยเป็นมือใหม่";
			mes "กันทั้งนั้นแหละ";
			mes "คุณมีแต่จะเก่งขึ้นเท่านั้น!";
			next;
			break;
		case 2:
			mes "[Mana]";
			mes "อืม ฉันคิดว่า";
			mes "เดี๋ยวทุกอย่างก็คงจะดีเอง";
			mes "แต่ฉันหวังว่าจะได้";
			mes "พบคุณอีกครั้งนะ";
			mes "ถ้าคุณตัดสินใจได้แล้ว";
			mes "ว่าต้องการคำแนะนำบางอย่าง~";
			close;
		}
		mes "[Mana]";
		mes "เอาล่ะ เรามาเริ่มกันที่";
		mes "คำอธิบายทั่วไปเกี่ยวกับ";
		mes "อาชีพของเรากันดีไหม?";
		mes "Mage คืออะไรกันแน่?";
		next;
		mes "[Mana]";
		mes "แทนที่จะใช้พละกำลังทางกาย";
		mes "Mage จะใช้พลังแห่งธาตุ";
		mes "ในการต่อสู้ ธาตุเหล่านี้คือ";
		mes "^ff0000ไฟ^000000, ^5C4033ดิน^000000, ^93DB70ลม^000000, ^0000FFน้ำ^000000";
		mes "พวกเรายังสามารถใช้เวทมนตร์";
		mes "ที่เป็นธาตุ ^CFB53BGhost^000000 ได้อีกด้วย";
		next;
		mes "[Mana]";
		mes "ทีนี้ เมื่อศัตรูมี";
		mes "คุณสมบัติทางธาตุ มันจะ";
		mes "สามารถต้านทานธาตุนั้นๆ ได้ แต่จะแพ้ทางธาตุอื่นๆ";
		mes "นั่นคือเหตุผลว่าทำไมการรู้เรื่องคุณสมบัติธาตุถึงสำคัญมากสำหรับเรา";
		next;
		mes "[Mana]";
		mes "ฟังความสัมพันธ์ระหว่าง";
		mes "ธาตุเหล่านี้ให้ดีนะ";
		mes "^FF0000ไฟ^000000 จะแข็งแกร่งเมื่อสู้กับ ดิน";
		mes "แต่จะอ่อนแอเมื่อสู้กับ น้ำ";
		mes "^5C4033ดิน^000000 จะแข็งแกร่งเมื่อสู้กับ ลม";
		mes "แต่จะอ่อนแอเมื่อสู้กับ ไฟ";
		next;
		mes "[Mana]";
		mes "^0000FFน้ำ^000000 จะแข็งแกร่งเมื่อสู้กับ";
		mes "ไฟ แต่จะอ่อนแอเมื่อสู้กับ ลม";
		mes "สุดท้าย ^93DB70ลม^000000 จะแข็งแกร่งเมื่อสู้กับ";
		mes "น้ำ แต่จะอ่อนแอเมื่อสู้กับ ดิน";
		next;
		mes "[Mana]";
		mes "ฉันจำไม่ได้เลยว่า";
		mes "ครั้งสุดท้ายที่ฉัน";
		mes "พูดเยอะขนาดนี้ในครั้งเดียว";
		mes "คือเมื่อไหร่! ฉันเหนื่อยแล้วล่ะ ถึง";
		mes "เวลาพักแล้ว แต่เราจะมา";
		mes "ต่อเรื่องนี้กันทีหลังนะ ตกลงไหม?";
		tu_magician01 = 1;
		if (checkre(3))
			getexp BaseLevel*3,BaseLevel*2;
		else
			getexp BaseLevel*30,BaseLevel*15;
		specialeffect2 EF_HIT5;
		close;
	case 1:
		mes "คุณพร้อมหรือยัง";
		mes "ที่จะคุยเรื่องคุณสมบัติ";
		mes "ทางธาตุกันต่อ?";
		next;
		mes "[Mana]";
		mes "คุณควรจะรู้ว่า";
		mes "มอนสเตอร์แต่ละตัวมี";
		mes "คุณสมบัติทางธาตุ และ";
		mes "ยังมีระดับที่แตกต่างกัน";
		mes "ของธาตุเหล่านั้น มัน";
		mes "สำคัญมากที่จะต้องเข้าใจเรื่องนี้...";
		next;
		mes "[Mana]";
		mes "ให้ฉันอธิบาย";
		mes "รายละเอียดปลีกย่อยของ";
		mes "แต่ละธาตุให้คุณฟังดีกว่า";
		mes "แค่บอกฉันมาว่าธาตุไหน";
		mes "ที่คุณอยากเรียนรู้เพิ่มเติม";
		next;
		mes "[Mana]";
		mes "พูดอีกอย่างก็คือ แม้แต่ธาตุชนิดเดียวกัน";
		mes "ก็มีลักษณะที่แตกต่างกันซึ่งต้องนำมาพิจารณาด้วย";
		mes "ฉันจะอธิบายคุณสมบัติต่างๆ เดี๋ยวนี้ล่ะ";
		while((1)){
			next;
			switch(select("^ff0000ไฟ^000000", "^5C4033ดิน^000000", "^93DB70ลม^000000", "^0000FFน้ำ^000000", "^CFB53BGhost^000000", "เลิกคุย")){
			case 1:
				mes "[Mana]";
				mes "มอนสเตอร์ที่มีคุณสมบัติ ^ff0000ไฟ^000000";
				mes "มักจะมีการโจมตีที่รุนแรง";
				mes "เนื่องจากพวกเรา Mage มี";
				mes "ร่างกายที่อ่อนแอกว่า แม้แต่";
				mes "การโจมตีเพียงเล็กน้อยจากมอนสเตอร์";
				mes "เหล่านี้ก็สามารถสร้างความเสียหายหนักได้";
				next;
				mes "[Mana]";
				mes "ดังนั้นถ้าเราต้องสู้กับ";
				mes "มอนสเตอร์ที่มีคุณสมบัติ ^FF0000ไฟ^000000";
				mes "ด้วยตัวคนเดียว เราสามารถ";
				mes "ใช้ Frost Diver เพื่อ";
				mes "แช่แข็งพวกมันอยู่กับที่ได้";
				next;
				mes "[Mana]";
				mes "แต่ถ้าเราใช้สกิลธาตุ";
				mes "ไฟ กับมอนสเตอร์ธาตุ";
				mes "ไฟ เราจะสร้างความเสียหาย";
				mes "ได้น้อยมากหรือไม่ได้เลย ซึ่งนั่นไม่ดีแน่";
				next;
				mes "[Mana]";
				mes "เพื่อใช้อ้างอิง สกิลธาตุไฟ";
				mes "ของเราคือ Sight, Fire Bolt,";
				mes "Fire Ball และ Fire Wall";
				mes "โอ๊ะ ให้ฉันบอกอะไรเพิ่มเติม";
				mes "เกี่ยวกับ Fire Wall หน่อยนะ";
				next;
				mes "[Mana]";
				mes "Fire Wall นั้นมีความพิเศษตรงที่";
				mes "มันสามารถใช้ได้สูงสุดถึง";
				mes "3 ครั้งในหนึ่งหน้าจอ มอนสเตอร์";
				mes "ตัวใดที่วิ่งเข้ามาชนมันจะ";
				mes "ได้รับความเสียหาย และกระเด็น";
				mes "ออกไปในทิศทางตรงกันข้าม";
				next;
				mes "[Mana]";
				mes "แต่ถ้ามอนสเตอร์เผ่า Undead";
				mes "วิ่งชน Fire Wall มันจะ";
				mes "ได้รับบาดเจ็บเพียงเล็กน้อย และ";
				mes "Fire Wall จะไม่สามารถขวาง";
				mes "มันไว้ได้เสมอไป ดังนั้นระวังด้วยนะ!";
				.@eread_1 = 1;
				break;
			case 2:
				mes "[Mana]";
				mes "เอาล่ะ มอนสเตอร์ธาตุ ^5C4033ดิน^000000";
				mes "มักจะเล็งเป้าหมายได้";
				mes "แม่นยำ ดังนั้นแม้ว่า";
				mes "อัตราการหลบหลีกของคุณจะสูง";
				mes "คุณอาจจะพบว่ามันยากที่จะ";
				mes "หลบการโจมตีทั้งหมดของพวกมัน";
				next;
				mes "[Mana]";
				mes "เมื่อต้องรับมือกับมอนสเตอร์";
				mes "เหล่านี้ ให้ใช้เวทมนตร์ธาตุไฟ";
				mes "ปกติแล้ว Fire Wall จะค่อนข้าง";
				mes "ได้ผลดี เพราะมันจะหยุด";
				mes "มอนสเตอร์และสร้างความ";
				mes "เสียหายได้ดีพอสมควรเลย";
				next;
				mes "[Mana]";
				mes "มันยังมีประโยชน์ที่จะรู้ว่า";
				mes "เมื่อคุณใช้ Stone Curse";
				mes "ใส่มอนสเตอร์ มอนสเตอร์";
				mes "ตัวนั้นจะกลายเป็นธาตุ";
				mes "ดิน ในขณะที่ผลของเวทมนตร์";
				mes "ยังทำงานอยู่ เจ๋งใช่ไหมล่ะ?";
				next;
				mes "[Mana]";
				mes "ดังนั้นถ้าคุณใช้ Stone Curse";
				mes "กลยุทธ์ที่ดีคือการใช้สกิล";
				mes "ธาตุไฟตามไปทันที เช่น";
				mes "Fire Bolt และ Fire Ball เพื่อ";
				mes "ชิงความได้เปรียบจากธาตุดินของมัน";
				.@eread_2 = 1;
				break;
			case 3:
				mes "[Mana]";
				mes "ทีนี้ อย่าแปลกใจไปล่ะ";
				mes "ถ้าคุณโจมตีพลาดบ่อยๆ เมื่อคุณ";
				mes "โจมตีมอนสเตอร์ธาตุ ^93DB70ลม^000000";
				mes "พวกมันส่วนใหญ่สามารถหลบการ";
				mes "โจมตีทางกายภาพได้ดีมากเพราะ";
				mes "มีอัตราการหลบหลีกที่สูง";
				next;
				mes "[Mana]";
				mes "แต่เนื่องจากพวกเรา Mage";
				mes "ส่วนใหญ่โจมตีด้วยเวทมนตร์";
				mes "เราจึงไม่ต้องกังวลเรื่องนั้น";
				mes "มากนัก ถึงอย่างนั้น มอนสเตอร์";
				mes "ธาตุลมมักจะค่อนข้างเร็ว ดังนั้นระวังด้วย";
				next;
				mes "[Mana]";
				mes "เมื่อพูดถึงการโจมตี";
				mes "มอนสเตอร์ธาตุลม มันจะดีมาก";
				mes "ถ้าได้ใช้สกิลธาตุดิน แต่เนื่อง";
				mes "จากมีเพียง Wizard เท่านั้นที่ใช้";
				mes "ธาตุดินได้ เราจึงควรใช้สกิลธาตุไฟแทน";
				next;
				mes "[Mana]";
				mes "สกิลธาตุไฟอาจจะทำความ";
				mes "เสียหายได้ไม่มากนัก แต่มัน";
				mes "จะมีผลมากกว่าสกิลธาตุลมหรือน้ำ ซึ่งจะสร้างความเสียหายได้น้อยมากหรือไม่ทำความเสียหายเลยต่อมอนสเตอร์ธาตุลม";
				next;
				mes "[Mana]";
				mes "แค่บอกให้รู้นะ";
				mes "สกิลธาตุ ^93DB70ลม^000000 ที่พวกเรา";
				mes "Mage สามารถใช้ได้ก็คือ";
				mes "Lightning Bolt และ Thunder Storm";
				.@eread_3 = 1;
				break;
			case 4:
				mes "[Mana]";
				mes "มอนสเตอร์ธาตุ ^0000FFน้ำ^000000";
				mes "มักจะมีพลังชีวิตเยอะ";
				mes "ดังนั้นพวกมันจึงทนความเสียหาย";
				mes "ได้มากกว่ามอนสเตอร์ตัวอื่น ทำให้";
				mes "การต่อสู้กับพวกมันใช้เวลานานขึ้น";
				next;
				mes "[Mana]";
				mes "ทีนี้ จำที่ฉันบอกได้ไหมว่า";
				mes "ธาตุต่างๆ มีระดับที่ต่างกัน?";
				mes "มอนสเตอร์ธาตุน้ำจริงๆ แล้วจะอ่อนไหว";
				mes "ต่อธาตุที่แพ้ทางมากขึ้นเมื่อมีระดับธาตุที่สูงขึ้น";
				next;
				mes "[Mana]";
				mes "พูดง่ายๆ ก็คือ มอนสเตอร์";
				mes "ที่มีระดับธาตุน้ำสูงขึ้น";
				mes "จะยิ่งได้รับความเสียหาย";
				mes "รุนแรงขึ้นจากการโจมตีธาตุลม";
				next;
				mes "[Mana]";
				mes "สกิลธาตุน้ำที่มีอยู่ใน";
				mes "ความสามารถของ Mage";
				mes "ประกอบไปด้วย Cold Bolt และ";
				mes "Frost Diver สกิลเหล่านี้มี";
				mes "ประสิทธิภาพมากเมื่อสู้กับมอนสเตอร์ธาตุไฟ";
				next;
				mes "[Mana]";
				mes "Frost Diver นั้นพิเศษตรงที่";
				mes "มันมีโอกาสที่จะแช่แข็ง";
				mes "มอนสเตอร์ได้จริงๆ และในขณะ";
				mes "ที่มอนสเตอร์ถูกแช่แข็งอยู่ มันจะ";
				mes "ถูกนับว่าเป็นธาตุน้ำ น่าสนใจใช่ไหมล่ะ?";
				.@eread_4 = 1;
				break;
			case 5:
				mes "[Mana]";
				mes "ในปัจจุบัน มีมอนสเตอร์";
				mes "ธาตุ ^CFB53BGhost^000000 ไม่มากนัก";
				mes "แต่ถ้าคุณไปเจอเข้าล่ะก็";
				mes "ลองใช้สกิลธาตุ Ghost";
				mes "ของคุณดูสิ มันจะได้ผลดี";
				mes "แน่นอน";
				next;
				mes "[Mana]";
				mes "การโจมตีธาตุ Ghost ที่";
				mes "พวกเรา Mage ใช้ได้คือ";
				mes "Napalm Beat และ Soul Strike";
				mes "สกิลเหล่านี้มักจะใช้งานได้";
				mes "ค่อนข้างดี ยกเว้นกับมอนสเตอร์";
				mes "ธาตุ Holy ระดับสูง";
				next;
				mes "[Mana]";
				mes "สำหรับสกิล Safety Wall";
				mes "มันก็จัดเป็นสกิลธาตุ Ghost";
				mes "ชนิดหนึ่ง แต่มันจำแนกประเภทได้ยาก การร่าย Safety Wall จะสร้าง";
				mes "บาเรียที่ช่วยป้องกันการโจมตี";
				mes "ทางกายภาพในระยะประชิด";
				next;
				mes "[Mana]";
				mes "อย่างไรก็ตาม บาเรียนี้";
				mes "คุ้มครองได้แค่คนเดียว และไม่";
				mes "สามารถป้องกันการโจมตีระยะ";
				mes "ไกล หรือการโจมตีธาตุที่รุนแรง";
				mes "ได้ ดังนั้นต้องระวังให้มากๆ นะ!";
				.@eread_5 = 1;
				break;
			case 6:
				mes "[Mana]";
				mes "คุณแน่ใจนะว่าคุณฟังเรื่อง";
				mes "รายละเอียดปลีกย่อยของ";
				mes "คุณสมบัติธาตุเพียงพอแล้ว?";
				mes "เพราะถ้าคุณพร้อมแล้ว เราจะ";
				mes "ไปต่อที่เนื้อหาใหม่ให้คุณเรียนรู้กัน";
				next;
				if(select("เ-เดี๋ยวสิ ยังไม่เสร็จเลย!:ฉันพร้อมจะไปต่อแล้ว") == 2) {
					mes "[Mana]";
					mes "เอาล่ะ เราจะ";
					mes "ไปต่อที่หัวข้อถัดไป";
					mes "กันเลยนะ โฮ่ โฮ่ โฮ่~!";
					tu_magician01 = 2;
					specialeffect2 EF_HIT5;
					.@total = .@eread_1 + .@eread_2 + .@eread_3 + .@eread_4 + .@eread_5;
					if (.@total) getexp (.@total*150),(.@total*70);
					close;
				}
				break;
			}
		}
	case 2:
		mes "ฉันรู้ว่าแนวคิดเรื่อง";
		mes "คุณสมบัติธาตุอาจจะดู";
		mes "น่าสับสนในตอนนี้ แต่เมื่อ";
		mes "เวลาผ่านไป คุณจะคุ้นเคยกับ";
		mes "มันเอง ตอนนี้ได้เวลาคุยเรื่อง";
		mes "สกิลเวทมนตร์แบบเจาะลึกกันแล้ว~";
		next;
		mes "[Mana]";
		mes "ทีนี้ เวทมนตร์โจมตีขั้น";
		mes "พื้นฐานที่สุดคือ Fire Bolt,";
		mes "Cold Bolt, Lightning Bolt และ";
		mes "Napalm Beat การเรียนรู้สิ่งเหล่านี้";
		mes "จะช่วยให้ Mage สามารถเรียนรู้";
		mes "สกิลที่ขั้นสูงกว่าได้";
		specialeffect2 EF_NAPALMBEAT;
		next;
		mes "[Mana]";
		mes "การเพิ่มเลเวลของสกิล";
		mes "เหล่านี้ไม่เพียงแต่จะช่วยให้";
		mes "เข้าถึงเวทมนตร์อื่นๆ ได้ แต่";
		mes "พลังโจมตีของสกิลพื้นฐาน";
		mes "เหล่านั้นก็จะเพิ่มขึ้นด้วย";
		specialeffect2 EF_LIGHTBOLT;
		next;
		mes "[Mana]";
		mes "สกิลอื่นๆ ที่เป็นพื้นฐาน";
		mes "ของอาชีพ Mage ก็คือ";
		mes "Sight, Stone Curse และ";
		mes "Increase SP Recovery พวกมัน";
		mes "ไม่ใช่สกิลโจมตีโดยตรง แต่มีประโยชน์ในบางสถานการณ์";
		next;
		specialeffect2 EF_SIGHT;
		mes "[Mana]";
		mes "Sight ใช้สำหรับ";
		mes "เปิดเผยศัตรูที่กำลัง";
		mes "ซ่อนตัวอยู่ อย่าลืมล่ะว่า";
		mes "ระยะของสกิลนี้ไปได้ไม่ไกลนัก";
		next;
		specialeffect2 EF_STONECURSE;
		mes "[Mana]";
		mes "Stone Curse มีโอกาสที่จะ";
		mes "สาปเป้าหมาย ทำให้เป็นอัมพาต";
		mes "ชั่วคราว และเปลี่ยนให้กลายเป็น";
		mes "ธาตุ ^5C4033ดิน^000000 อย่างไรก็ตาม การ";
		mes "ใช้ Stone Curse แต่ละครั้ง";
		mes "ต้องใช้ 1 Red Gemstone";
		next;
		mes "[Mana]";
		mes "Increase SP Recovery เป็น";
		mes "สกิลติดตัวซึ่งช่วยให้คุณฟื้นฟู";
		mes " SP ได้มากขึ้นขณะที่นั่งพัก มันไม่ได้ส่งผลโดยตรงกับการต่อสู้ แต่การมี SP มากขึ้นย่อมดีกว่าเสมอ";
		next;
		mes "[Mana]";
		mes "เอาล่ะ ฉันรู้สึกว่าฉันได้";
		mes "อธิบายสกิลพื้นฐานไปมาก";
		mes "พอแล้ว ทีนี้ก่อนที่ฉันจะพูด";
		mes "ถึงสกิลขั้นสูงของเรา มีงาน";
		mes "ชิ้นหนึ่งที่ฉันอยากให้คุณทำ";
		mes "เพื่อเป็นการฝึกฝน";
		next;
		mes "[Mana]";
		mes "ถ้าคุณเดินไปทางทิศตะวันตก";
		mes "จากที่นี่ คุณจะพบสะพานใหญ่";
		mes "ซึ่งที่นั่นคุณจะได้พบกับ";
		mes "มอนสเตอร์ที่ชื่อว่า Pupa";
		mes "Pupa จะไม่ขยับไปไหนและไม่";
		mes "สามารถโจมตีโต้ตอบได้เลย";
		next;
		mes "[Mana]";
		mes "จงล่าเจ้า Pupa พวกนี้";
		mes "จนกว่าคุณจะรวบรวม";
		mes "Chrysalis ได้ 10 อัน";
		mes "แล้วนำมาให้ฉันนะ ตกลงไหม?";
		tu_magician01 = 3;
		getexp 400,200;
		specialeffect2 EF_HIT5;
		close;
	case 3:
	case 4:
		if (tu_magician01 == 3) {
			mes "อ๊ะ คุณกลับมาแล้วเหรอ?";
			mes "ไหนดูซิว่าคุณรวบรวม";
			mes "Chrysalis มาครบ 10 อันไหม";
			mes "จากการล่า Pupa พวกนั้น หืม...";
		}
		else {
			mes "เอาล่ะ...";
			mes "คุณหา Chrysalis";
			mes "ครบ 10 อันหรือยัง";
			mes "จากการล่า Pupa ตามที่";
			mes "ฉันบอก? หวังว่านะ...";
			mes "เพราะฉันกำลังจะตรวจสอบแล้ว~";
		}
		next;
		mes "[Mana]";
		if (countitem(915) < 10){// Chrysalis
			if (tu_magician01 == 3) {
				mes "โอ้ คุณยังมีมัน";
				mes "ไม่เพียงพอนะ ลองไป";
				mes "ทางทิศตะวันตกจากที่นี่";
				mes "แล้วล่า Pupa เพื่อหา";
				mes "Chrysalis เพิ่มนะ ตกลงไหม?";
				mes "ตอนนี้ ฉันจะช่วยรักษาให้ก่อน";
				tu_magician01 = 4;
				percentheal 100,100;
			}
			else {
				mes "โธ่ คุณต้องการมัน";
				mes "มากกว่านี้นะ ฉันรู้ว่า";
				mes "คุณทำได้... Pupa น่ะ";
				mes "เป็นมอนสเตอร์ที่ไม่มีทางสู้";
				mes "เลยนะนั่น!";
			}
		}
		else {
			mes "...เก้า, สิบ";
			mes "เยี่ยมมาก คุณผ่าน!";
			mes "ถึงตอนนี้ คุณคงเริ่มชิน";
			mes "กับการใช้สกิลแล้วใช่ไหมล่ะ?";
			mes "ยังไงก็เถอะ บอกฉันนะถ้าคุณพร้อมสำหรับบทเรียนต่อไป ตกลงไหม?";
			tu_magician01 = 5;
			getexp 400,200;
			specialeffect2 EF_HIT5;
		}
		close;
	case 5:
		mes "ทีนี้ฉันจะบอกคุณ";
		mes "ในสิ่งที่ฉันรู้เกี่ยวกับสกิล";
		mes "Mage ที่ขั้นสูงขึ้น";
		mes "คุณอยากให้ฉันอธิบาย";
		mes "สกิลไหนดีล่ะ?";
		while(1){
			next;
			switch(select("Soul Strike:Safety Wall:Fire Ball:Frost Diver:Thunderstorm:Fire Wall:Energy Coat:เลิกคุย")) {
			case 1:
				mes "[Mana]";
				mes "Soul Strike คือสกิลธาตุ ^CFB53BGhost^000000";
				mes "ที่คุณสามารถเรียนรู้ได้หลังจาก";
				mes "เพิ่มระดับสกิล Napalm Beat";
				mes "ไปจนถึงเลเวลที่กำหนด มัน";
				mes "จะโจมตีด้วยพลังวิญญาณ";
				mes "โบราณล่ะ~";
				next;
				specialeffect2 EF_SOULSTRIKE;
				next;
				mes "[Mana]";
				mes "เมื่อคุณฝึกฝนการใช้ Soul";
				mes "Strike และเพิ่มเลเวลของมัน";
				mes "มันจะแข็งแกร่งขึ้น และคุณจะ";
				mes "ร่ายมันได้รวดเร็วขึ้นมากเลย";
				.@skill_e1 = 1;
				break;
			case 2:
				mes "[Mana]";
				mes "คุณสามารถเรียนรู้ Safety Wall";
				mes "ได้หลังจากฝึกฝนการใช้งาน";
				mes "Napalm Beat และ Soul Strike";
				mes "Safety Wall จะช่วยป้องกันการ";
				mes "โจมตีระยะประชิดเกือบทั้งหมด";
				next;
				specialeffect2 EF_GLASSWALL;
				next;
				mes "[Mana]";
				mes "อย่างไรก็ตาม Safety Wall";
				mes "มีระยะเวลาคงอยู่และสามารถ";
				mes "ป้องกันการโจมตีได้ตามจำนวน";
				mes "ครั้งที่กำหนด ขึ้นอยู่กับเลเวล";
				mes "ของสกิล Safety Wall ที่คุณใช้";
				.@skill_e2 = 1;
				break;
			case 3:
				mes "[Mana]";
				mes "Fire Ball คือสกิลธาตุ ^FF0000ไฟ^000000";
				mes "ที่คุณสามารถเรียนรู้ได้";
				mes "หลังจากที่ได้ฝึกฝน";
				mes "การใช้งาน Fire Bolt";
				next;
				specialeffect2 EF_FIREBALL;
				next;
				mes "[Mana]";
				mes "Fire Ball สามารถสร้างความ";
				mes "เสียหายแก่ศัตรูได้หลายตัว";
				mes "ถ้าพวกมันอยู่ในระยะของสกิล";
				mes "จงใช้พลังทำลายล้างของ Fire Ball";
				mes "ให้เป็นประโยชน์นะ!";
				.@skill_e3 = 1;
				break;
			case 4:
				mes "[Mana]";
				mes "Frost Diver คือสกิลธาตุ ^0000FFน้ำ^000000";
				mes "ที่สามารถเรียนรู้ได้หลังจาก";
				mes "เพิ่มระดับเลเวลของสกิล";
				mes "Cold Bolt ไปจนถึงจุดหนึ่ง";
				specialeffect2 EF_FROSTDIVER;
				next;
				specialeffect2 EF_FROSTDIVER2;
				mes "[Mana]";
				mes "Frost Diver จะทำความเสียหาย";
				mes "ศัตรูและมีโอกาสที่จะแช่แข็ง";
				mes "พวกมันได้ด้วย เมื่อถูกแช่แข็ง";
				mes "มอนสเตอร์จะถูกนับว่าเป็น";
				mes "มอนสเตอร์ธาตุน้ำ";
				next;
				mes "[Mana]";
				mes "ทีนี้ เมื่อคุณเพิ่มระดับเลเวล";
				mes "ของสกิล Frost Diver พลัง";
				mes "โจมตีและโอกาสแช่แข็งก็จะ";
				mes "เพิ่มขึ้นด้วย ค่อนข้างอันตรายเลยใช่ไหมล่ะ? ^FFFFFFkamenriderblack^000000";
				.@skill_e4 = 1;
				break;
			case 5:
				mes "[Mana]";
				mes "Thunderstorm เป็นสกิลธาตุ";
				mes "^93DB70ลม^000000 ที่คุณสามารถ";
				mes "เรียนรู้ได้หลังจากเพิ่มระดับ";
				mes "เลเวลสกิล Lightning Bolt";
				mes "ไปจนถึงเลเวลที่กำหนด";
				next;
				specialeffect2 EF_THUNDERSTORM;
				mes "[Mana]";
				mes "Thunderstorm นั้นคล้ายกับ";
				mes "Fire Ball ตรงที่มันสามารถ";
				mes "โจมตีศัตรูหลายตัวได้ภายใน";
				mes "ระยะพื้นที่ที่กำหนด มันเป็น";
				mes "การโจมตีลงพื้นที่มีพลังสูงมาก";
				.@skill_e5 = 1;
				break;
			case 6:
				mes "[Mana]";
				mes "Fire Wall คือสกิลธาตุ ^FF0000ไฟ^000000";
				mes "ซึ่งสามารถเรียนรู้ได้หลังจาก";
				mes "ฝึกฝนการใช้ Fire Ball และ";
				mes "Sight ไม่เหมือนสกิลอื่น";
				mes "Fire Wall จะกำหนดพื้นที่เป้าหมาย";
				next;
				specialeffect2 EF_FIREWALL;
				mes "[Mana]";
				mes "ดังนั้นคุณสามารถวาง";
				mes "Fire Wall ไว้ในตำแหน่งที่";
				mes "คุณคิดว่าศัตรูจะเดินมาได้ และมัน";
				mes "จะมีประโยชน์อย่างยิ่งเวลาที่มี";
				mes "มอนสเตอร์วิ่งเข้ามาหาคุณ!";
				next;
				mes "[Mana]";
				mes "ดังนั้นถ้าคุณใช้ Fire Well";
				mes "อย่างมีกลยุทธ์และมี";
				mes "ประสิทธิภาพ มันจะเป็น";
				mes "สกิลที่ทรงพลังมากเลยล่ะ~";
				.@skill_e6 = 1;
				break;
			case 7:
				mes "[Mana]";
				mes "Energy Coat คือ สกิลที่ค่อนข้าง";
				mes "พิเศษที่คุณไม่สามารถเรียนรู้";
				mes "มันได้ด้วยตัวเอง คุณจะต้อง";
				mes "ไปขอให้ Blizardis ที่อยู่ใน";
				mes "Mage Guild สอนวิธีการ";
				mes "ใช้งานมันให้กับคุณ";
				next;
				specialeffect2 EF_ENERGYCOAT;
				next;
				mes "[Mana]";
				mes "ถ้าคุณตัดสินใจที่จะ";
				mes "เรียนรู้ Energy Coat ฉัน";
				mes "มั่นใจว่า Blizardis จะบอก";
				mes "ทุกสิ่งที่คุณจำเป็นต้องรู้ให้เอง";
				.@skill_e7 = 1;
				break;
			case 8:
				mes "[Mana]";
				mes "คุณเข้าใจทั้งหมดนั่น";
				mes "ไหม? ดีเลย เพราะตอนนี้";
				mes "ฉันมีภารกิจจะมอบให้คุณ";
				mes "ในนามของสมาคม Mage Guild";
				mes "เมื่อคุณพร้อมรับฟังรายละเอียด";
				mes "มาคุยกับฉันนะ ตกลงไหม?";
				next;
				mes "[Mana]";
				mes "โอ้ และโปรดรับนี่ไป";
				mes "มันเป็นของขวัญสำหรับ";
				mes "คุณเพื่อช่วยในการฝึกฝน";
				mes "ฉันหวังว่ามันจะมีประโยชน์";
				mes "ในตอนที่ต่อสู้นะ...";
				tu_magician01 = 6;
				getitem 1604,1; //Wand
				specialeffect2 EF_HIT5;
				.@total = .@skill_e1 + .@skill_e2 + .@skill_e3 + .@skill_e4 + .@skill_e5;
				if (.@total) getexp (.@total*300),(.@total*100);
				close;
			}
		}
		close;
	case 6:
		mes "โอ้ ดีเลย คุณกลับมาแล้ว";
		mes "ให้ฉันบอกเรื่องภารกิจ";
		mes "ของคุณนะ เมื่อไม่นานมานี้";
		mes "มีบางอย่างเกิดขึ้นใน Morocc";
		mes "และ Thief Guild กำลังขอ";
		mes "ความช่วยเหลือจากพวกเรา";
		next;
		mes "[Mana]";
		mes "มันไม่ใช่เรื่องบังคับ";
		mes "ที่คุณต้องทำ แต่คุณ";
		mes "จะได้รับผลตอบแทนสำหรับ";
		mes "ความพยายาม และพวกเรา";
		mes "จะให้ความสนับสนุนแก่คุณด้วย";
		mes "คุณคิดยังไงล่ะ?";
		next;
		switch(select("ฉันจะทำ:เสียใจด้วย แต่ว่า...")) {
		case 1:
			mes "[Mana]";
			mes "ยอดเยี่ยม! เอาล่ะ";
			mes "ฉันจะส่งคุณตรงไปที่";
			mes "Morocc เดี๋ยวนี้เลย เมื่อคุณ";
			mes "ไปถึงที่นั่น ให้ไปที่ด้านหน้า";
			mes "พีระมิด Morocc และคุย";
			mes "กับ Yierhan นะ ขอบใจจ้ะ~";
			next;
			mes "[Mana]";
			mes "โอ้ และเมื่อคุณ";
			mes "เสร็จธุระกับสิ่งที่ Yierhan";
			mes "มอบหมายแล้ว ให้กลับมา";
			mes "รายงานฉันด้วยนะ ตกลงไหม?";
			tu_magician01 = 7;
			emotion ET_SMILE;
			warp "morocc",182,286;
			close;
		case 2:
			mes "[Mana]";
			mes "อืมมม...?";
			mes "โอ้ ฉันเข้าใจแล้วล่ะ ถ้า";
			mes "คุณเกิดเปลี่ยนใจขึ้นมา";
			mes "ก็กลับมาถามฉันได้ใน";
			mes "ภายหลังนะ ตกลงไหม?";
			close;
		}
	case 7:
		mes "ฉันไม่รู้เหมือนกัน";
		mes "ว่างานแบบไหนที่";
		mes "Yierhan เตรียมไว้";
		mes "ให้คุณ แต่หวังว่ามัน";
		mes "จะไม่ยากจนเกินไปนะ";
		close;
	case 27:
		mes "Yierhan เพิ่งจะส่งข้อความ";
		mes "มาหาฉัน บอกว่าคุณทำ";
		mes "หน้าที่ได้ยอดเยี่ยมมากใน";
		mes "การทำภารกิจให้สำเร็จ";
		mes "เก่งมากเลย "+strcharinfo(0)+"~";
		next;
		mes "[Mana]";
		mes "ดูเหมือนว่าจะมีองค์กรลับ";
		mes "สองแห่งเข้ามาเกี่ยวข้องใน";
		mes "เรื่องสกปรกทั้งหมดนี้ ทำไม";
		mes "พวกนั้นถึงมาทำงานร่วมกันได้ล่ะ?";
		next;
		mes "[Mana]";
		mes "ยังไงก็เถอะ...";
		mes "นี่เป็นเรื่องค่อนข้างใหญ่";
		mes "ซึ่งฉันคิดว่าพวกเบื้องบน";
		mes "จะเป็นคนจัดการเอง ดังนั้น";
		mes "คุณยังไม่ต้องกังวลไปในตอนนี้";
		next;
		mes "[Mana]";
		mes "โปรดรับนี่ไว้เป็นรางวัล";
		mes "เล็กๆ น้อยๆ ที่ช่วยเหลือกิลด์";
		mes "ของเราทั้งคู่ ขอบคุณอีกครั้ง";
		mes "จริงๆ นะจ๊ะ~";
		tu_magician01 = 28;
		getitem 2321,1; //Silk_Robe
		Zeny = Zeny + 3000;
		getexp 3000,1000;
		close;
	default:
		if(tu_magician01 < 27){
			mes "เอาล่ะ...";
			mes "ดูเหมือนว่ามีบางอย่างกำลัง";
			mes "เกิดขึ้นในอาณาจักร Rune-Midgarts";
			mes "อย่างแน่นอน ไม่แปลกใจเลยที่";
			mes "กิลด์ต่างๆ ถึงได้ยุ่งกันขนาดนี้...";
			next;
			mes "[Mana]";
			mes "ยังไงก็เถอะ โปรด";
			mes "ตั้งใจทำงานต่อไปนะ";
			mes "แล้ว... เอิ่ม Yierhan";
			mes "สบายดีไหม?";
		} else {
			mes "คุณดูเหมือนจะมีประสบการณ์";
			mes "มากแล้วในตอนนี้ และไม่มี";
			mes "อะไรที่ฉันสามารถสอนคุณได้";
			mes "อีกแล้ว ต่อจากนี้มันขึ้นอยู่กับ";
			mes "คุณที่จะค้นหาวิธีเชี่ยวชาญ";
			mes "ในวิถีแห่งเวทมนตร์ด้วยตนเอง";
		}
		close;
	}
}
