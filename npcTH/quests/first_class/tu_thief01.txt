//===== rAthena Script =======================================
//= Thief Class Tutorial and Job Specific Quest
//===== By: ==================================================
//= Fix up by Jukka
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Thief training quest.
//===== Additional Comments: =================================
//= 1.0 Fully working
//= 1.1 optimized [Lupus]
//= 1.2 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.3 Fixed a few minor mistakes [Playtester]
//= 1.4 Misc. updates. [L0ne_W0lf]
//= 1.5 Added Pre-Renewal support. [Euphy]
//= 2.0 Fixed a wrong exp value & clean-up. [Capuche]
//============================================================

// Thief Trainer
//============================================================
moc_ruins,66,164,4	script	Thief Trainer#T	4_M_02,{
	mes "[Yierhan]";
	if(Class == Job_Novice){
		mes "เอ๋...?";
		mes "Novice เหรอ?";
		mes "กำลังคิดอยู่ล่ะสิ";
		mes "ว่าอาชีพไหนที่";
		mes "เธอจะเลือกเปลี่ยน...";
		mes "ฉันพูดถูกใช่ไหมล่ะ?";
		next;
		mes "[Yierhan]";
		mes "ฟังนะ ถ้าเธอตัดสินใจที่จะเป็น Thief--ซึ่งฉันขอบอกเลยว่าเป็น";
		mes "ทางเลือกที่ฉลาดมาก--ก็แวะมาคุยกับฉันนะ เดี๋ยวฉันจะสอนพื้นฐานให้เอง!";
		close;
	}
	else if (BaseClass == Job_Thief && Upper != 2) {
		if (Class != Job_Thief && tu_thief01 < 8) {
			mes "ว่าไงงงงง เธอมาทำ";
			mes "อะไรที่นี่ล่ะ? ไม่มี";
			mes "อะไรที่ฉันจะสอนเธอได้แล้ว!";
			mes "เธอเก่งเกินหน้าเกินตาฉันไปไกลแล้ว!";
			next;
			mes "[Yierhan]";
			mes "จริงๆ แล้ว ฉันว่า";
			mes "เธอมีคุณสมบัติพอ";
			mes "ที่จะสอนฉันได้แล้วนะ!";
			mes "มาเถอะ! ฉันอยากได้ท่าใหม่ๆ บ้าง!";
			close;
		}
		if(tu_thief01 == 0){
			mes "เฮ้ เพื่อน";
			mes "ฉันชื่อ Yierhan";
			mes "ฉันเป็นคน";
			mes "ที่ดูแลรับผิดชอบ";
			mes "การฝึกฝนเหล่า Thief หน้าใหม่น่ะ";
			next;
			switch(select("ฝึกฝน?:ฝึกฝน? ตอนนี้เลยเหรอ?")){
			case 1:
				mes "[Yierhan]";
				mes "ใช่ การฝึกฝนไง ฉันหมายความว่า เรื่องพวกนี้มันอาจจะเป็นสัญชาตญาณสำหรับบางคน แต่สำหรับคนอื่นอาจจะต้องการความช่วยเหลือสักหน่อย ดังนั้นนี่คือหนึ่งในเรื่องที่ทำไว้ 'เผื่อเหลือเผื่อขาด' น่ะ";
				next;
				break;
			case 2:
				mes "[Yierhan]";
				mes "ตอนนี้เลยเหรอ?";
				mes "ใช่ ตอนนี้เลย!";
				mes "แต่ถ้าเธอยังไม่พร้อมด้วยเหตุผลบางอย่าง ฉันคิดว่าฉันรอได้นะ";
				close;
			}
			mes "[Yierhan]";
			mes "เอาล่ะ อย่างแรกเลย Thief ใช้การโจมตีระยะประชิด เอ้อ... ส่วนใหญ่เราจะทำแบบนั้นล่ะนะ มีบางคนเหมือนกันที่ชอบใช้ธนูยิงระยะไกล แต่พวกเราทุกคนน่ะเก่งเรื่องความรวดเร็ว!";
			next;
			mes "[Yierhan]";
			mes "งั้นสำหรับการโจมตีระยะประชิด ค่าสถานะ (stat) ไหนที่ช่วยเพิ่มพลังโจมตีล่ะ? เอาน่า";
			mes "เธอควรจะรู้เรื่องนี้นะถ้าเธอไม่ได้โดดเรียนที่ Novice Training Grounds มาก่อน";
			next;
			switch(select("^6B8E23INT^000000:^2F4F2FSTR^000000:^23238EDEX^000000")){
			case 1:
				mes "[Yierhan]";
				mes "ว่าไงนะ? ^6B8E23INT^000000 มันส่งผลต่อพลังโจมตีเวทมนตร์ พลังป้องกันเวทมนตร์ และทักษะบางอย่าง Thief น่ะไม่ได้ยุ่งเกี่ยวกับเวทมนตร์เลยนะ!";
				next;
				mes "[Yierhan]";
				mes "เธอต้องโดดเรียนที่ Novice Training Grounds มาชัวร์! แต่ฉันก็ไม่โทษเธอหรอกนะ เอาเป็นว่า ค่า ^2F4F2FSTR^000000 ต่างหากที่เพิ่มพลังโจมตี เข้าใจไหม?";
				next;
				mes "[Yierhan]";
				mes "ดังนั้นถ้าเธอรู้สึกว่าพลังโจมตีที่ทำได้มันช่างเบาหวิว เธออาจจะต้องการแต้มสถานะเพิ่มที่ ^2F4F2FSTR^000000 ส่วนเธอจะเพิ่ม STR สูงแค่ไหนนั้นมันก็ขึ้นอยู่กับเธอเลย";
				getexp 200,100;
				break;
			case 2:
				mes "[Yierhan]";
				mes "ใช่ ถูกต้องแล้ว! ถ้าเธออยากเพิ่มพลังโจมตี เธอต้อง";
				mes "ใส่แต้มสถานะไปที่ ^2F4F2FSTR^000000 การเพิ่ม STR ยังช่วยเพิ่ม";
				mes "น้ำหนักบรรทุกสูงสุด (Max Weight Limit) ให้เธอด้วยนะ";
				next;
				mes "[Yierhan]";
				mes "ดังนั้นถ้าเธอรู้สึกว่าพลังโจมตีที่ทำได้มันช่างเบาหวิว เธออาจจะต้องการแต้มสถานะเพิ่มที่ ^2F4F2FSTR^000000 ส่วนเธอจะเพิ่ม STR สูงแค่ไหนนั้นมันก็ขึ้นอยู่กับเธอเลย";
				next;
				mes "[Yierhan]";
				mes "แน่นอนว่าค่า ^23238EDEX^000000 และ LUK ก็สามารถ";
				mes "เพิ่มพลังโจมตีได้เหมือนกัน แต่มันน้อยนิดมากเมื่อเทียบกับ STR ฉันขอย้ำนะ: ^660000น้อยนิดมาก^000000";
				getexp 400,200;
				break;
			case 3:
				mes "[Yierhan]";
				mes "ว่าไงนะ? ^23238EDEX^000000 มันส่งผลต่อ";
				mes "ความแม่นยำในการโจมตี ไม่ใช่พลังโจมตี! เอ้อ... เว้นแต่ว่าเธอจะใช้";
				mes "ธนูล่ะนะ ไม่อย่างนั้นมันจะเพิ่มพลังโจมตีให้เธอได้แค่จึ๋งเดียวเอง";
				next;
				mes "[Yierhan]";
				mes "เธอต้องโดดเรียนที่ Novice Training Grounds มาชัวร์! แต่ฉันก็ไม่โทษเธอหรอกนะ เอาเป็นว่า ค่า ^2F4F2FSTR^000000 ต่างหากที่เพิ่มพลังโจมตี เข้าใจไหม?";
				next;
				mes "[Yierhan]";
				mes "ดังนั้นถ้าเธอรู้สึกว่าพลังโจมตีที่ทำได้มันช่างเบาหวิว เธออาจจะต้องการแต้มสถานะเพิ่มที่ ^2F4F2FSTR^000000 ส่วนเธอจะเพิ่ม STR สูงแค่ไหนนั้นมันก็ขึ้นอยู่กับเธอเลย";
				getexp 200,100;
			}
			tu_thief01 = 1;
			specialeffect2 EF_HIT5;
			close;
		}
		else if(tu_thief01 == 1){
			mes "เอาล่ะ พอเรื่องค่าสถานะได้แล้ว";
			mes "รู้ไหมอะไร? ฉันว่าฉันจะคุยกับเธอเรื่องทักษะ (Skill) ที่พวกเรา Thief ใช้งานกันดีกว่า";
			next;
			mes "[Yierhan]";
			mes "งั้นก็ไปอัปเลเวลทักษะของเธอซะ เรียนรู้ทักษะใหม่ๆ บ้างถ้าเธอต้องการ แล้ว";
			mes "ค่อยกลับมาหาฉันที่นี่";
			tu_thief01 = 2;
			close;
		}
		else if(tu_thief01 == 2){
			mes "โอเค ไหนขอดูทักษะของเธอหน่อย เธอรู้ใช่ไหมว่าเธอต้องเปลี่ยนกลยุทธ์การต่อสู้ไปตามทักษะที่มี? ทักษะน่ะสำคัญพอๆ กับค่าสถานะเลยนะ!";
			next;
			.@chk_th_skill1 = getskilllv("TF_DOUBLE");
			.@chk_th_skill2 = getskilllv("TF_MISS");
			.@chk_th_skill3 = getskilllv("TF_STEAL");
			.@chk_th_skill4 = getskilllv("TF_HIDING");
			.@chk_th_skill5 = getskilllv("TF_POISON");
			.@chk_th_skill6 = getskilllv("TF_DETOXIFY");

			if(.@chk_th_skill1 == 0 && .@chk_th_skill2 == 0 && .@chk_th_skill3 == 0 && .@chk_th_skill4 == 0 && .@chk_th_skill5 == 0 && .@chk_th_skill6 == 0){
				mes "[Yierhan]";
				mes "เธอยังไม่ได้เรียนรู้ทักษะอะไรเลยเหรอ? เอาน่า ทักษะพวกนี้นี่แหละที่จะทำให้เธอแตกต่างจาก Novice และคนอื่นๆ!";
				close;
			}
			if(.@chk_th_skill1 > 0){
				mes "[Yierhan]";
				mes "อา เธอเรียนรู้";
				mes "Double Attack เลเวล "+.@chk_th_skill1+" แล้วสินะ";
				mes "เยี่ยม! ทักษะนี้จะทำให้เธอมีโอกาสโจมตีสองครั้งในการ";
				mes "โจมตีเพียงครั้งเดียว สุดยอดไปเลย!";
				next;
				mes "[Yierhan]";
				mes "มันเป็นทักษะติดตัว (Passive skill) ดังนั้นมันจะทำงานตลอดเวลาและไม่ต้องใช้ SP ในการใช้งาน ยิ่งเลเวล Double Attack สูงขึ้น โอกาสที่เธอจะโจมตีเบิ้ลก็ยิ่งมากขึ้น";
				next;
			}
			if(.@chk_th_skill2 > 0){
				mes "[Yierhan]";
				mes "ไหนดูซิ...";
				mes "Increase Dodge เลเวล "+.@chk_th_skill2+" เหรอ?";
				mes "นั่นจะช่วยเพิ่มอัตราการหลบหลีก (Flee Rate) หมายความว่าเธอจะมีโอกาสหลบการโจมตีจากศัตรูได้ดีขึ้น";
				next;
				mes "[Yierhan]";
				mes "เหมือนกับทักษะ Double Attack เลย Increase Dodge เป็นทักษะติดตัว มันจะไม่ใช้ SP และทำงานอยู่";
				mes "ตลอดเวลา ถ้าเธอไม่ชอบให้ร่างกายฟกช้ำ ทักษะนี้แหละคือคำตอบ";
				next;
			}
			if(.@chk_th_skill3 > 0){
				mes "[Yierhan]";
				mes "โห นี่เธอมี";
				mes "Steal เลเวล "+.@chk_th_skill3+" ด้วย~";
				mes "นี่แหละคือทักษะที่ทำให้ชื่ออาชีพของเรามีความหมาย! แต่เธอเอา";
				mes "ไปใช้กับคนอื่นไม่ได้หรอกนะ...";
				next;
				mes "[Yierhan]";
				mes "แต่เธอสามารถใช้ Steal เพื่อขโมยไอเทมจากมอนสเตอร์ได้ ถ้าเธอโชคดี เธออาจจะได้ไอเทมดีๆ ด้วยวิธีนั้น อ้อ แล้ว Steal ไม่ส่งผลต่ออัตราการดรอปไอเทมปกติของมอนสเตอร์นะ";
				next;
			}
			if(.@chk_th_skill4 > 0){
				mes "[Yierhan]";
				mes "เธอเรียนรู้";
				mes "Hiding เลเวล "+.@chk_th_skill4+" แล้วเหรอ?";
				mes "ไหนดูซิ เธอจะเรียนรู้ทักษะนี้ได้";
				mes "ก็ต่อเมื่อเรียนรู้ทักษะ Steal ถึงเลเวลที่กำหนดก่อนเท่านั้น";
				next;
				mes "[Yierhan]";
				mes "แน่นอนว่าเธอใช้";
				mes "ทักษะ Hiding เพื่อมุดลงดินในยามฉุกเฉิน อย่างตอนที่ถูกรุมล้อมด้วยศัตรูโหดๆ แต่ระวังด้วยล่ะ...";
				next;
				mes "[Yierhan]";
				mes "มอนสเตอร์บางตัวจะยังคงมองเห็นเธอได้ ไม่ว่าเธอจะซ่อนตัวดีแค่ไหนก็ตาม แถมยังมีมอนสเตอร์บางชนิดที่สามารถลากเธอออกมาจากที่ซ่อนได้ด้วย!";
				next;
			}
			if(.@chk_th_skill5 > 0){
				mes "[Yierhan]";
				mes "เอาล่ะ ฉันเห็นว่าเธอ";
				mes "รู้จัก Envenom เลเวล "+.@chk_th_skill5+" ด้วย";
				mes "เธอชอบทำตัวอันตราย";
				mes "ใช่ไหมล่ะ?";
				next;
				mes "[Yierhan]";
				mes "ทักษะการโจมตีนี้มีโอกาส";
				mes "ที่จะทำให้ศัตรูติดพิษในช่วงระยะเวลาหนึ่ง ขณะที่ติดพิษ ศัตรูจะเสีย HP อย่างต่อเนื่องและพลังป้องกันจะลดลง";
				next;
				mes "[Yierhan]";
				mes "เอ้อ... แต่จำไว้นะ";
				mes "ถ้ามอนสเตอร์มันแข็งแกร่งเกินไปสำหรับเธอ เธออาจจะทำให้มันติดพิษไม่ได้ เพราะงั้นอย่าหักโหมนักล่ะ";
				next;
			}
			if(.@chk_th_skill6 > 0){
				mes "[Yierhan]";
				mes "Detoxify เลเวล "+.@chk_th_skill6+"";
				mes "ถ้าเธออุตส่าห์เรียนรู้ทักษะนี้ เธอต้องเป็นพวกที่";
				mes "รอบคอบเอามากๆ เลยสินะ";
				next;
				mes "[Yierhan]";
				mes "เธอจะเรียนรู้ Detoxify ได้";
				mes "ก็ต่อเมื่อเรียนรู้ทักษะ Envenom มาก่อนเท่านั้น Detoxify จะช่วยล้างฤทธิ์พิษให้กับเป้าหมายได้";
				next;
			}
			mes "[Yierhan]";
			mes "เอาล่ะ ฉันคิดว่า";
			mes "ถ้าเธออยากรู้";
			mes "เกี่ยวกับทักษะอื่นๆ";
			mes "ฉันสามารถอธิบายให้ฟังคร่าวๆ ได้";
			tu_thief01 = 3;
			getexp BaseLevel*30,BaseLevel*15;
			specialeffect2 EF_HIT5;
			close;
		}
		else if(tu_thief01 == 3){
			mes "งั้น...";
			mes "มีทักษะไหน";
			mes "ที่เธออยากให้";
			mes "ฉันอธิบายไหม หรือว่า";
			mes "เรื่องพวกนี้เธอรู้";
			mes "หมดเปลือกอยู่แล้ว?";
			while(1){
				next;
				switch(select("Double Attack:Increase Dodge:Steal:Hiding:Envenom:Detoxify:ฉันรู้มากพอแล้ว")) {
				case 1:
					mes "[Yierhan]";
					mes "ก็ตามชื่อเลย";
					mes "Double Attack จะทำให้การโจมตีของเธอมีโอกาสเป็นการโจมตีสองครั้ง สองจังหวะในหนึ่งดาบ";
					next;
					mes "[Yierhan]";
					mes "มันเป็นทักษะติดตัว ดังนั้นมันจะทำงานตลอดเวลาและไม่ต้องใช้ SP ยิ่งเลเวลทักษะสูง โอกาสเบิ้ลก็ยิ่งเยอะ";
					.@read_d = 1;
					break;
				case 2:
					mes "[Yierhan]";
					mes "Increase Dodge";
					mes "จะช่วยเพิ่มค่า Flee Rate";
					mes "อย่างมากแบบที่อาชีพ";
					mes "อื่นๆ ไม่มีให้ ทำไมต้องยอม";
					mes "เจ็บตัวในเมื่อเธอหลบมันได้ล่ะ?";
					next;
					mes "[Yierhan]";
					mes "เหมือนกับ Double Attack ทักษะนี้เป็นทักษะติดตัว ไม่ใช้ SP และทำงานตลอดเวลา ถ้าไม่อยากตัวเขียวช้ำ ทักษะนี้แหละใช่เลย";
					.@read_f = 1;
					break;
				case 3:
					mes "[Yierhan]";
					mes "Steal เป็นทักษะกดใช้ที่มีโอกาสหยิบเอา";
					mes "ไอเทมฟรีๆ มาให้เธอ! แต่ใช้กับคนอื่นไม่ได้นะ";
					next;
					specialeffect2 EF_STEAL;
					mes "[Yierhan]";
					mes "แต่เธอใช้ Steal กับพวกมอนสเตอร์ได้! ถ้าโชคดีก็ได้ของดีมาครอง อ้อ แล้วมันไม่เกี่ยวกับการดรอปของปกตินะ";
					.@read_s = 1;
					break;
				case 4:
					mes "[Yierhan]";
					mes "Hiding เป็นทักษะกดใช้ที่เธอจะมุดหายลงไปใต้ดิน เธอจะซ่อนตัวได้ในระยะเวลาหนึ่งและเคลื่อนที่ไม่ได้ แต่บางทีมันก็ดีกว่าการถูกเจอตัวล่ะนะ!";
					next;
					mes "[Yierhan]";
					mes "แน่นอนว่าเธอใช้";
					mes "ทักษะ Hiding เพื่อมุดลงดินในยามฉุกเฉิน อย่างตอนที่ถูกรุมล้อมด้วยศัตรูโหดๆ แต่ระวังด้วยล่ะ...";
					next;
					mes "[Yierhan]";
					mes "มอนสเตอร์บางตัวจะยังคงมองเห็นเธอได้ ไม่ว่าเธอจะซ่อนตัวดีแค่ไหนก็ตาม แถมยังมีมอนสเตอร์บางชนิดที่สามารถลากเธอออกมาจากที่ซ่อนได้ด้วย!";
					.@read_p = 1;
					break;
				case 5:
					mes "[Yierhan]";
					mes "Envenom เป็นทักษะโจมตีที่ Thief ทุกคนควรต้องรู้ไว้ แต่นั่นก็แค่ความคิดของฉันนะ";
					next;
					mes "[Yierhan]";
					mes "ทักษะนี้มีโอกาส";
					mes "ทำให้ศัตรูติดพิษได้ชั่วระยะเวลาหนึ่ง ขณะติดพิษศัตรูจะเสีย HP เรื่อยๆ และพลังป้องกันจะลดลง";
					mes "จำไว้ให้ดีล่ะ";
					.@read_h = 1;
					break;
				case 6:
					mes "[Yierhan]";
					mes "ในเมื่อ Thief น่ะ";
					mes "ยุ่งเกี่ยวกับพิษค่อนข้างบ่อย";
					mes "เราก็ต้องมีวิธีที่จะ...";
					mes "จัดการกับยาพิษของ";
					mes "พวกเราเองสิ";
					next;
					mes "[Yierhan]";
					mes "เธอเรียนรู้ Detoxify ได้หลังเรียน Envenom ทักษะนี้จะช่วยล้างพิษออกจากเป้าหมายได้";
					.@read_r = 1;
					next;
					specialeffect2 EF_DETOXICATION;
					break;
				case 7:
					mes "[Yierhan]";
					mes "อา...";
					mes "ฉันเริ่มขี้เกียจอธิบายทักษะพวกนี้แล้วล่ะ งั้นเราขยับไป";
					mes "การฝึก Thief ขั้นต่อไปกันเถอะ...";
					next;
					mes "[Yierhan]";
					mes "ตอนนี้เธอคงเชี่ยวชาญเรื่องทักษะแล้ว ฉันอยากให้เธอไปอัปเลเวลทักษะแล้วค่อยกลับมา เข้าใจไหม?";
					tu_thief01 = 4;
					specialeffect2 EF_HIT5;
					.@total = .@read_d + .@read_f + .@read_s + .@read_p + .@read_h + .@read_r;
					if (.@total)
						getexp (.@total*300),(.@total*100);
					close;
				}
			}
		}
		else if(tu_thief01 == 4){
			mes "เอาล่ะ เราศึกษาเรื่องทักษะกันไปแล้ว และเธอก็คงไปซ้อมมาบ้างแล้วใช่ไหม? หวังว่าจะเป็นแบบนั้นนะ...";
			next;
			mes "[Yierhan]";
			mes "โอเค ตอนนี้ฉันมีการฝึกต่อสู้จริงมาให้เธอทำ นี่เป็นโอกาสที่เธอจะได้รู้ว่าทักษะไหนเหมาะกับสถานการณ์ไหนที่สุด";
			next;
			mes "[Yierhan]";
			mes "ไปหามาให้ฉัน";
			mes "10 ^ff0000Feather of Birds^000000";
			mes "เธอไปจัดการกับพวก Picky เพื่อเอามันมาได้เลย ไม่ยากหรอก อ้อ แล้วก็ใช้ไอ้ปีกนี่ตอนจะกลับมาหาฉันด้วยนะ";
			tu_thief01 = 5;
			savepoint "moc_ruins",80,164,1,1;
			getitem 602,1; //Wing_Of_Butterfly
			getexp 100,50;
			specialeffect2 EF_HIT5;
			close2;
			warp "moc_fild12",158,373;
			end;
		}
		else if(tu_thief01 == 5 || tu_thief01 == 6){
			if(countitem(916) < 10){
				mes "เฮ้ เธอยังไม่มี 10 ^ff0000Feather of Birds^000000 ที่ฉันขอเลยนี่! เธอต้องรู้จักเอาสิ่งที่เรียนมามาใช้นะ";
				mes "รีบไปทำเข้าล่ะ!";
				close2;
				warp "moc_fild07",203,38;
				end;
			}
			else {
				mes "เยี่ยมเลย...!";
				mes "ทำได้ดีมาก เพื่อน";
				mes "ในเมื่อเธอหาขนนกมาได้ครบแบบนี้ เธอคงตั้งใจจริงที่จะเป็น Thief ที่ดีสินะ";
				next;
				mes "[Yierhan]";
				mes "ฉันหวังว่าเธอจะ";
				mes "มุ่งมั่นแบบนี้ต่อไป";
				mes "เพื่อที่จะเก่งขึ้นเรื่อยๆ";
				mes "พยายามให้เต็มที่ล่ะ! อ้อ";
				mes "แล้วเธอมีคำถามอะไรไหม?";
				next;
				if(tu_thief01 == 6){
					switch(select("เกี่ยวกับร่องรอยนั่น...:ไม่มีครับ/ค่ะ")) {
					case 1:
						mes "[Yierhan]";
						mes "เธอรู้เรื่องแล้วสินะ?";
						mes "จริงๆ ฉันก็ไม่อยากจะ";
						mes "บอกเธอเท่าไหร่หรอกนะ";
						mes "เพราะเธอยังเป็น Thief";
						mes "มือใหม่หัดขับอยู่เลย แต่ว่า...";
						next;
						mes "[Yierhan]";
						mes "มันมีการ";
						mes "ต่อสู้เกิดขึ้นที่";
						mes "ทางตอนใต้";
						mes "ของเมืองนี้";
						next;
						mes "[Yierhan]";
						mes "เพราะมันเกิดขึ้นกลางดึก เลยมีคนรู้เรื่องนี้แค่ไม่กี่คน ส่วนฉันน่ะ อยู่ประชุมกิลด์จนดึกเลยได้เห็นมาด้วยความบังเอิญสุดๆ";
						next;
						mes "[Yierhan]";
						mes "ฉันกลับไปตรงที่เกิดเหตุแล้วก็เจอร่องรอยที่ชี้ว่าพวกที่สู้กันน่ะหนีไปทางใต้";
						next;
						mes "[Yierhan]";
						mes "จากการที่มีการใช้พิษในการ";
						mes "ต่อสู้ ฉันเลยเดาว่าน่าจะมี Assassin เข้ามาเกี่ยวด้วย แต่ก็ไม่ชัวร์นะ ถ้าเธออยากลองไปเช็คดู ก็ตามรอยการต่อสู้นั่นไป";
						next;
						mes "[Yierhan]";
						mes "เอ้อ... แต่ระวัง";
						mes "อย่าไปเข้าใกล้พิษนั่น";
						mes "เข้านะ! พิษพวกนั้น";
						mes "มันค่อนข้างแรงเลยล่ะ!";
						tu_thief01 = 8;
						getitem 1207,1; //Main_Gauche
						getexp 1000,500;
						specialeffect2 EF_HIT5;
						close;
					case 2:
						mes "[Yierhan]";
						mes "ดี!";
						mes "ฉันจะได้ไม่ต้องเหนื่อย!";
						mes "เอาล่ะ ครั้งหน้าที่เจอหน้ากัน เธอต้องเก่งขึ้นนะ อ้อ แล้วก็เอาของพวกนี้ไปซะ เห็นว่าเธอมีแววเป็นจอมโจรตัวจริง";
						tu_thief01 = 7;
						getitem 1207,1; //Main_Gauche
						getexp 500,200;
						specialeffect2 EF_HIT5;
						close;
					}
				}
				else {
					switch(select("ยินดีที่ได้รู้จักครับ/ค่ะ:ไม่มีครับ/ค่ะ")) {
					case 1:
						mes "[Yierhan]";
						mes "ใช่ การได้นั่งคุยกันมันก็สนุกดี สู้กับ";
						mes "มอนสเตอร์ต่อไปแบบที่เธอทำนะ";
						mes "แล้วเธอจะเป็น Thief ที่ยอดเยี่ยมในไม่ช้า";
						next;
						mes "[Yierhan]";
						mes "และเพราะฉันถูกชะตา";
						mes "กับเธอเหลือเกิน เจ้าหนู";
						mes "เอาไอ้นี่ไป";
						mes "รับไปสิ มันเป็นของเธอแล้ว!";
						getexp 500,200;
						break;
					case 2:
						mes "[Yierhan]";
						mes "ดี!";
						mes "ฉันจะได้ไม่ต้องเหนื่อย!";
						mes "เอาล่ะ ครั้งหน้าที่เจอหน้ากัน เธอต้องเก่งขึ้นนะ อ้อ แล้วก็เอาของพวกนี้ไปซะ เห็นว่าเธอมีแววเป็นจอมโจรตัวจริง";
						getexp 50,20;
						break;
					}
					tu_thief01 = 7;
					getitem 1207,1; //Main_Gauche
					specialeffect2 EF_HIT5;
					close;
				}
			}
		}
		else if(tu_thief01 == 7){
			mes "[Yierhan]";
			mes "รู้ไหม...";
			mes "มันมีการ";
			mes "ต่อสู้เกิดขึ้นที่";
			mes "ทางตอนใต้";
			mes "ของเมืองนี้";
			next;
			mes "[Yierhan]";
			mes "เพราะมันเกิดขึ้นกลางดึก เลยมีคนรู้เรื่องนี้แค่ไม่กี่คน ส่วนฉันน่ะ อยู่ประชุมกิลด์จนดึกเลยได้เห็นมาด้วยความบังเอิญสุดๆ";
			next;
			mes "[Yierhan]";
			mes "ฉันกลับไปตรงที่เกิดเหตุแล้วก็เจอร่องรอยที่ชี้ว่าพวกที่สู้กันน่ะหนีไปทางใต้";
			next;
			mes "[Yierhan]";
			mes "จากการที่มีการใช้พิษในการ";
			mes "ต่อสู้ ฉันเลยเดาว่าน่าจะมี Assassin เข้ามาเกี่ยวด้วย แต่ก็ไม่ชัวร์นะ ถ้าเธออยากลองไปเช็คดู ก็ตามรอยการต่อสู้นั่นไป";
			next;
			mes "[Yierhan]";
			mes "เอ้อ... แต่ระวัง";
			mes "อย่าไปเข้าใกล้พิษนั่น";
			mes "เข้านะ! พิษพวกนั้น";
			mes "มันค่อนข้างแรงเลยล่ะ!";
			next;
			mes "[Yierhan]";
			mes "เฮ้ เธออาจจะไปเจอ";
			mes "พิษเข้า เพราะงั้นจำไว้ว่า";
			mes "Green Herb และ Green Potion";
			mes "จะช่วยล้างพิษได้ อ้อ แล้วก็จำไว้ว่า Red Gemstone น่ะ";
			mes "สามารถเอาไปใช้โจมตีด้วยพิษได้นะ";
			next;
			mes "[Yierhan]";
			mes "เฮ้ ถ้าเธอ";
			mes "ตัดสินใจจะไปเช็ค";
			mes "ที่นั่นล่ะก็ ระวังตัว";
			mes "ให้ดีๆ ด้วยนะ เข้าใจไหม?";
			tu_thief01 = 8;
			getexp 200,100;
			specialeffect2 EF_HIT5;
			close;
		}
		else if(tu_thief01 == 8){
			mes "ว่าไง เพื่อน";
			mes "เธอโอเคดีไหม?";
			next;
			mes "[Yierhan]";
			mes "การต่อสู้กับสิ่งที่เรา";
			mes "ไม่รู้จักเลยน่ะมันเสี่ยงเสมอ ในเมื่อฉันได้ยินมาว่ามีคนนอกมาอวดอำนาจอยู่แถวนี้ เธอควรจะระวังตัวให้มากนะ";
			next;
			mes "[Yierhan]";
			mes "มันเป็นกฎที่ดีที่จะต้องระวังตัวเสมอในการผจญภัย คอยดูมอนสเตอร์ไว้ และคอย";
			mes "ดูผู้คนไว้ด้วย! เข้าใจไหม?";
			close;
		}
		else if(tu_thief01 < 26){
			mes "ฉันได้ยินมาว่ามี";
			mes "Assassin คนหนึ่ง";
			mes "ที่ไปทำภารกิจ";
			mes "แล้วก็ไม่เคยได้กลับมาอีกเลย";
			next;
			mes "[Yierhan]";
			mes "สิ่งที่แปลกที่สุดก็คือ Assassin Guild รายงานว่าภารกิจนั้นสำเร็จลุล่วงแล้ว!";
			mes "ฉันจำได้ว่าหมอนั่นน่ะ";
			mes "เก่งระดับแถวหน้าเลยล่ะ...";
			next;
			mes "[Yierhan]";
			mes "บ้าบอชะมัดว่าไหม?";
			mes "ยังไงก็ดูแล";
			mes "ตัวเองด้วยล่ะ";
			close;
		}
		else if(tu_thief01 == 26){
			mes "ไง ทำภารกิจ";
			mes "เสร็จหรือยังล่ะ?";
			mes "ฉันรู้ๆ ไอ้สิ่งที่เธอต้องทำน่ะมันค่อนข้างโหดเอาเรื่องอยู่";
			next;
			switch(select("ฉันยังสืบสวนอยู่ครับ/ค่ะ:ยังเลยครับ/ค่ะ:ใช่ ฉันทำเสร็จแล้ว")) {
			case 1:
				mes "[Yierhan]";
				mes "อา เข้าใจล่ะ ของแบบนี้มันต้องใช้เวลาหน่อยใช่ไหมล่ะ?";
				close;
			case 2:
				mes "[Yierhan]";
				mes "งั้นเหรอ...?";
				mes "ไม่เป็นไร";
				mes "ฉันว่าเรื่องพวกนี้มันต้องใช้ความอดทน และการใช้หัวคิด";
				mes "ซึ่งเป็นสิ่งที่ฉันน่ะห่วยแตกที่สุดเลย";
				close;
			case 3:
				mes "[Yierhan]";
				mes "เยี่ยมไปเลย...!";
				mes "เป็นไงบ้างล่ะ?";
				mes "เล่ามาให้ฉันฟังหน่อยซิว่า";
				mes "เธอไปเจออะไรมาบ้าง~";
				next;
				mes "^3355FFคุณอธิบายผลการสืบสวนให้ Yierhan ฟัง และเล่าเรื่องเศษผ้าที่คุณพบในภารกิจ^000000";
				next;
				mes "[Yierhan]";
				mes "เศษผ้าเหรอ?";
				mes "หืม โอเค ไหน";
				mes "ขอดูหน่อยซิ";
				next;
				mes "[Yierhan]";
				mes "โอ้โห!";
				mes "เธอทำได้ดีมาก";
				mes "นี่เป็นข้อมูลที่";
				mes "สำคัญมากเลยนะ!";
				next;
				mes "[Yierhan]";
				mes "เธอรู้ไหม ลวดลายบนผ้านี่มันเหมือนกับโค้ดลับที่พวก Assassin ใช้กัน และลวดลายเฉพาะตัวแบบนี้ดูเหมือนจะเป็นของพวก Assassin ระดับสูงด้วย";
				next;
				mes "[Yierhan]";
				mes "ฉันอาจจะพอรู้แล้วว่าเกิดอะไรขึ้นในคืนนั้นด้วยข้อมูลใหม่ที่เศษผ้านี้นำทางไป ขอบใจมากนะ!";
				next;
				mes "[Yierhan]";
				mes "หลังจากนี้";
				mes "พวกระดับสูงในกิลด์";
				mes "จะมารับช่วงต่อการสืบสวนเอง เธอทำหน้าที่ของตัวเองได้อย่างสมบูรณ์แบบแล้ว ถึงเวลาที่เธอต้องไปขัดเกลาทักษะของตัวเองต่อแล้วล่ะ";
				next;
				mes "[Yierhan]";
				mes "เฮ้ ของพวกนี้เป็นของเธอนะ คิดซะว่าเป็นรางวัลที่ช่วยพวกเราละกัน ดูแลตัวเองด้วยล่ะ~";
				tu_thief01 = 27;
				getitem 2307,1; //Mantle
				Zeny = Zeny + 5000;
				getexp 8000,3000;
				close;
			}
		}
		else {
			mes "[Yierhan]";
			mes "เฮ้...";
			mes "เธอมีความฝัน";
			mes "ใช่ไหมล่ะ? ฉันรู้";
			mes "มันอาจจะเป็นหัวข้อที่";
			mes "ลึกซึ้งไปหน่อยที่จู่ๆ";
			mes "ฉันก็พูดขึ้นมา แต่ว่า...";
			next;
			mes "[Yierhan]";
			mes "ฉันแค่ชอบบอกให้คนทำตามความฝันน่ะ เพราะงั้นทำมันซะ ชีวิตที่ไม่มีอะไรให้ตั้งตารอน่ะมันน่าเบื่อจะตาย เธอว่าไหม?";
			close;
		}
	}
	else if (BaseClass == Job_Mage && Upper != 2){
		if(tu_magician01 < 7){
			mes "[Yierhan]";
			mes "ว่าไง";
			mes "ฉันคือครูฝึก Thief แถวนี้ล่ะ แน่นอนว่าดูเหมือนเราจะไม่มีอะไรเหมือนกันเท่าไหร่ แต่...";
			next;
			mes "[Yierhan]";
			mes "จริงๆ แล้วฉันก็มีเพื่อนเป็นพวกใช้เวทมนตร์อยู่เหมือนกันนะ เรื่องลึกลับพวกนั้นน่ะมันเกินความเข้าใจฉันไปไกล แต่";
			mes "ฉันก็นับถือพวกเธอมากเลยล่ะ";
			close;
		}
		else if(tu_magician01 == 7){
			mes "หืม...?";
			mes "แปลกแฮะ ปกติจะมีแค่";
			mes "พวก Thief ที่มาป้วนเปี้ยนแถวนี้ คนอย่างเธอมาทำอะไร";
			mes "ที่นี่กันล่ะ? นอกจากว่า...";
			next;
			select("ฉันมาในนามของ 'Mana' น่ะ");
			mes "[Yierhan]";
			mes "อ้อ เธอคือคนที่ Mana ส่งมาช่วยสินะ! มาได้ถูกจังหวะพอดีเลย คือว่านะ เราเจออะไรแปลกๆ ที่ South Morocc น่ะ";
			next;
			mes "[Yierhan]";
			mes "หลักๆ คือเราเจอร่องรอยของพิษที่ใช้ในการต่อสู้ เรากะว่าจะไปสืบดูเอง แต่ดันยุ่งจนหัวหมุนด้วยงานอื่นๆ ทั้งหมดนี่น่ะ";
			next;
			mes "[Yierhan]";
			mes "นั่นคือเหตุผลที่เราต้องขอ";
			mes "ความช่วยเหลือจาก Mage Guild โชคดีที่ฉันเป็นเพื่อนซี้กับ Mana ก็เลย...";
			next;
			mes "[Yierhan]";
			mes "เอาล่ะ มุ่งหน้าไปทาง";
			mes "South Morocc เลย เพราะดูจะเป็นจุดเริ่มต้นการสืบสวนที่ดี เธอจะเห็นสิ่งที่เราเจออยู่ตรงด้านนอกประตู South Morocc เลย";
			tu_magician01 = 8;
			close;
		}
		else if(tu_magician01 < 26){
			mes "ฉันได้ยินมาว่ามี";
			mes "Assassin คนหนึ่ง";
			mes "ที่ไปทำภารกิจ";
			mes "แล้วก็ไม่เคยได้กลับมาอีกเลย";
			next;
			mes "[Yierhan]";
			mes "สิ่งที่แปลกที่สุดก็คือ Assassin Guild รายงานว่าภารกิจนั้นสำเร็จลุล่วงแล้ว!";
			mes "ฉันจำได้ว่าหมอนั่นน่ะ";
			mes "เก่งระดับแถวหน้าเลยล่ะ...";
			next;
			mes "[Yierhan]";
			mes "ระวังตัวด้วยนะ เพื่อน";
			mes "ดูเหมือนว่าการ";
			mes "สืบสวนของเธอ";
			mes "อาจจะเกี่ยวข้องกับ";
			mes "Assassin ลึกลับคนนั้น...";
			close;
		}
		else if(tu_magician01 == 26){
			mes "เป็นไงบ้าง";
			mes "การสืบสวน";
			mes "ไปถึงไหนแล้วล่ะ?";
			mes "ฉันมัวแต่ยุ่งจน";
			mes "ไม่ได้สนใจเรื่องนี้";
			mes "เลยแม้แต่นิดเดียว...";
			next;
			switch(select("ฉันยังสืบสวนอยู่ครับ/ค่ะ:ยังไม่เสร็จเลยครับ/ค่ะ...:โอ้ ฉันทำเสร็จแล้วล่ะ~")) {
			case 1:
				mes "[Yierhan]";
				mes "อา เข้าใจล่ะ ของแบบนี้มันต้องใช้เวลาหน่อยใช่ไหมล่ะ?";
				close;
			case 2:
				mes "[Yierhan]";
				mes "งั้นเหรอ...?";
				mes "ไม่เป็นไร";
				mes "ฉันว่าเรื่องพวกนี้มันต้องใช้ความอดทน และการใช้หัวคิด";
				mes "ซึ่งเป็นสิ่งที่ฉันน่ะห่วยแตกที่สุดเลย";
				close;
			case 3:
				mes "[Yierhan]";
				mes "เยี่ยมไปเลย...!";
				mes "เป็นไงบ้างล่ะ?";
				mes "เล่ามาให้ฉันฟังหน่อยซิว่า";
				mes "เธอไปเจออะไรมาบ้าง~";
				next;
				mes "^3355FFคุณอธิบายผลการสืบสวนให้ Yierhan ฟัง และเล่าเรื่องเศษผ้าที่คุณพบในภารกิจ^000000";
				next;
				mes "[Yierhan]";
				mes "เศษผ้าเหรอ?";
				mes "หืม โอเค ไหน";
				mes "ขอดูหน่อยซิ";
				next;
				mes "[Yierhan]";
				mes "โอ้โห!";
				mes "เธอทำได้ดีมาก";
				mes "นี่เป็นข้อมูลที่";
				mes "สำคัญมากเลยนะ!";
				next;
				mes "[Yierhan]";
				mes "เธอรู้ไหม ลวดลายบนผ้านี่มันเหมือนกับโค้ดลับที่พวก Assassin ใช้กัน และลวดลายเฉพาะตัวแบบนี้ดูเหมือนจะเป็นของพวก Assassin ระดับสูงด้วย";
				next;
				mes "[Yierhan]";
				mes "ฉันอาจจะพอรู้แล้วว่าเกิดอะไรขึ้นในคืนนั้นด้วยข้อมูลใหม่ที่เศษผ้านี้นำทางไป ขอบใจมากนะ!";
				next;
				mes "[Yierhan]";
				mes "หลังจากนี้";
				mes "พวกระดับสูงในกิลด์";
				mes "จะมารับช่วงต่อการสืบสวนเอง เธอทำหน้าที่ของตัวเองได้อย่างสมบูรณ์แบบแล้ว ถึงเวลาที่เธอต้องไปขัดเกลาทักษะของตัวเองต่อแล้วล่ะ";
				next;
				mes "[Yierhan]";
				mes "เฮ้ ของพวกนี้เป็นของเธอนะ คิดซะว่าเป็นรางวัลที่ช่วยพวกเราละกัน เดี๋ยวฉันจะไปบอก Mana ให้ว่าเธอทำได้ยอดเยี่ยมมาก ดูแลตัวเองด้วยล่ะ~";
				tu_magician01 = 27;
				Zeny = Zeny + 5000;
				getexp 5000,2000;
				close;
			}
		}
		else {
			mes "อย่างที่ฉันคิดไว้เลย";
			mes "มีมากกว่าหนึ่งกลุ่ม";
			mes "ที่เข้ามาเกี่ยวข้องกับเรื่องนี้";
			mes "หึ... อะไรบางอย่างที่เลวร้ายจริงๆ";
			mes "อาจจะเกิดขึ้นเร็วๆ นี้ก็ได้...";
			close;
		}
	}
	mes "บางคนคิดว่า";
	mes "ทะเลทรายน่ะมันเป็นแค่";
	mes "สถานที่ที่อันตรายและน่ารำคาญ";
	mes "ที่ไม่มีใครอยากจะ";
	mes "มาอยู่หรอก แต่ที่นี่น่ะ";
	mes "มีของเจ๋งๆ ตั้งหลายอย่างนะ";
	next;
	mes "[Yierhan]";
	mes "เรามีความร้อนระอุที่ชวนให้ใส่ชุดน้อยชิ้น เรามี... ตะบองเพชร เรามีทรายเยอะกว่าชายหาดอีก อืม... พายุทรายล่ะมั้ง?";
	close;
}
