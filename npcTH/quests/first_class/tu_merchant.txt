//===== rAthena Script =======================================
//= Merchant Class Tutorial and Job Specific Quest
//===== By: ==================================================
//= Fix up by Jukka
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Merchant training quest.
//===== Additional Comments: =================================
//= 1.0 Fully working
//= 1.1 Fixed bugs, optimized [Lupus]
//= 1.2 fixed Weight check
//= 1.2 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.3 Misc. updates. [L0ne_W0lf]
//= 2.0 Clean-up. [Capuche]
//============================================================

// Guarnien
//============================================================
alberta_in,70,51,5	script	Guarnien	4W_M_02,{
	mes "[Guarnien]";
	if(Class != Job_Merchant){
		mes "สวัสดีคนแปลกหน้า";
		mes "ฉันหวังว่าฉันจะสามารถ";
		mes "หยิบยื่นความช่วยเหลือ";
		mes "ในการผจญภัยของคุณได้...";
		next;
		mes "[Guarnien]";
		mes "อย่างไรก็ตาม สิ่งที่ฉันจะสอนนั้นมีประโยชน์ต่อ Merchant เท่านั้น การค้าขายและการทำเงินคือความเชี่ยวชาญของฉัน ถ้าคุณมีเพื่อนที่เป็น Merchant ก็ส่งพวกเขามาหาฉันนะ";
		if(Class == Job_Merchant_High){
			next;
			mes "[Guarnien]";
			mes "ตามหลักการแล้ว ใช่ คุณคือ Merchant แต่ฉันดูออกว่าใครคือผู้เชี่ยวชาญ คุณผ่านชีวิตการเป็น Merchant มาก่อนแล้ว ฉันบอกได้เลย!";
		}
		close;
	}
	switch(tu_merchant){
	case 17:
		mes "เราไม่สามารถแม้แต่จะ";
		mes "คาดเดาถึงจำนวนของ";
		mes "ตลาดและผู้คนมากมาย";
		mes "ข้างนอกนั่น ในโลกที่กว้าง";
		mes "แสนกว้างใหญ่ใบนี้ของเรา";
		next;
		mes "[Guarnien]";
		mes "พวกเขาทั้งหมดล้วนเป็นลูกค้าและหุ้นส่วนทางธุรกิจที่อาจเป็นไปได้ แน่นอนว่ามันขึ้นอยู่กับคุณที่จะไปพบพวกเขา";
		mes "ถ้าคุณมีความเมตตาอย่างจริงใจต่อทุกคนที่คุณพบเจอ คุณจะ";
		mes "ไม่มีปัญหาเลยล่ะ";
		close;
	case 15:
	case 16:
		.@chk_vend = getskilllv("MC_VENDING");
		if (tu_merchant == 15) {
			mes "ฉันเชื่อว่ามันถึงเวลา";
			mes "สำหรับการบรรยายครั้งสุดท้ายของฉัน";
			mes "ฉันจะพูดถึงทักษะที่สำคัญที่สุด";
			mes "ของ Merchant: ^871F78Vending^000000 โปรดฟังให้ดี";
			next;
			mes "[Guarnien]";
			mes "ทักษะ Vending เป็นทักษะประเภทกดใช้งาน (Active skill) ซึ่งช่วยให้คุณเปิดร้านค้าของตัวเองในจุดที่คุณยืนอยู่ Vending ช่วยให้คุณทำธุรกิจจริงๆ กับผู้คนคนอื่นได้";
			next;
			mes "[Guarnien]";
			mes "อย่างไรก็ตาม คุณสามารถขายได้เฉพาะไอเทมที่เก็บไว้ใน PushCart ของคุณเท่านั้น";
			mes "ดังนั้นคุณจึงจำเป็นต้องมีมัน ราคาสูงสุดที่คุณสามารถตั้งได้สำหรับ";
			mes "ไอเทมแต่ละชิ้นคือ 99,990,000 Zeny";
			next;
			mes "[Guarnien]";
			mes "โดยพื้นฐานแล้ว คุณต้องเรียนรู้ทักษะ Push Cart ก่อน ถึงจะสามารถเรียนทักษะ Vending ได้";
			next;
			mes "[Guarnien]";
			mes "ยิ่งเลเวลของทักษะ Vending สูงเท่าไหร่ คุณก็จะสามารถขายไอเทมได้มากขึ้นในคราวเดียว Vending จะสูงสุดที่เลเวล 10";
			next;
			mes "[Guarnien]";
			mes "Vending เลเวล 1 จะอนุญาตให้คุณ";
			mes "ขายไอเทมได้ทั้งหมด 3 ชิ้นในคราวเดียว ทุกครั้งที่คุณเพิ่มเลเวลทักษะ Vending จำนวนไอเทมทั้งหมดที่คุณสามารถขายได้จะ";
			mes "เพิ่มขึ้นเลเวลละหนึ่งชิ้น";
			next;
			mes "[Guarnien]";
			if((.@chk_vend > 3) && (.@chk_vend < 10)){
				mes "แต่ฉันเห็นว่าคุณคงจะรู้อยู่แล้วสินะ หืม? เอาล่ะ ในเมื่อคุณฉลาดหลักแหลมขนาดนี้ ฉันคิดว่าคุณสมควรได้รับรางวัลเล็กๆ น้อยๆ นี้~";
				getexp 186,60;
			} else if(.@chk_vend == 10){
				mes "ดูเหมือนฉันจะพูดจาขายหน้าตัวเองซะแล้ว ขอโทษทีนะ ฉันไม่ได้สังเกตเลยว่าคุณสำเร็จทักษะ Vending ไปแล้ว...";
				next;
				mes "[Guarnien]";
				mes "คุณเป็นนักเรียนที่";
				mes "ยอดเยี่ยมมาก!";
				mes "โปรดรับรางวัลเล็กๆ";
				mes "น้อยๆ นี้สำหรับความ";
				mes "พยายามทั้งหมดของคุณนะ~";
				getexp 199,69;
			} else {
				mes "สำหรับงานชิ้นสุดท้าย ฉันจะขอให้คุณเรียนรู้ทักษะ Vending จนถึงเลเวล 4 แน่นอนว่าเมื่อคุณทำได้ ฉันจะมอบรางวัลให้เล็กน้อย ฉันจะรอนะ พยายามเข้าล่ะ~";
				tu_merchant = 16;
				erasequest 8239;
				setquest 8240;
				close;
			}
			completequest 8239;
		}
		else {
			mes "เอาล่ะ...";
			mes "คุณได้เรียนรู้";
			mes "มากแค่ไหนแล้ว";
			mes "เกี่ยวกับ Vending?";
			next;
			mes "[Guarnien]";
			if((.@chk_vend > 3) && (.@chk_vend < 10)){
				mes "ยอดเยี่ยม!";
				mes "คุณเรียนรู้ด้วยตัวเองได้ดีมาก เอ้า รับรางวัลเล็กๆ น้อยๆ นี้ไป~";
				getexp 186,43;
			} else if(.@chk_vend == 10){
				mes "โอ้ ฉันเห็นว่าคุณจริงจังกับการเป็น Merchant มาก ฉันภูมิใจที่คุณฝึกฝนทักษะนี้จนสำเร็จ! เอ้า คุณสมควรได้รับรางวัลนี้!";
				getexp 199,69;
			} else {
				mes "อืม...";
				mes "ยังไม่ได้เรียน Vending เลเวล 4 สินะ? จำไว้ว่าการขายของได้นั้นเป็นหนึ่ง";
				mes "ในคุณลักษณะที่ชัดเจนของการเป็น Merchant เลยนะ!";
				close;
			}
			completequest 8240;
		}
		tu_merchant = 17;
		next;
		mes "[Guarnien]";
		mes "อา ตอนนี้คุณได้บรรลุพื้นฐานของอาชีพ Merchant แล้ว แค่หมั่นฝึกฝนหลักการเบื้องต้นอย่างการสำรวจตลาดเพื่อเช็คราคาไอเทม คุณก็น่าจะไปได้สวย";
		next;
		mes "[Guarnien]";
		mes "ถึงแม้ว่าทุกอย่างที่เราทำจะเป็นไปเพื่อเงิน แต่อย่าลืมความต้องการของลูกค้าและขายสินค้าของคุณในราคาที่ยุติธรรมเสมอ ไม่อย่างนั้นอาจไม่มีใครซื้อสินค้าของคุณเลยก็ได้!";
		next;
		mes "[Guarnien]";
		mes "เอาล่ะ ไม่มีอะไรที่ฉันจะสอนคุณได้อีกแล้วในตอนนี้ จงออกไปสู่โลกกว้างและสร้างชื่อในฐานะ Merchant เสียเถิด";
		next;
		mes "[Guarnien]";
		mes "และแวะมาหาฉัน";
		mes "ได้ทุกเมื่อที่คุณ";
		mes "ต้องการนะ";
		mes "โชคดีนะจ๊ะ~";
		close;
	case 13:
	case 14:
		.@chk_cart = getskilllv("MC_PUSHCART");
		if (tu_merchant == 13) {
			mes "ตอนนี้มาพูดถึงเรื่อง";
			mes "^871F78Push Cart^000000 ก่อนอื่น คุณจำเป็นต้องมีทักษะ Increase Weight Limit เลเวล 5 ก่อน ถึงจะสามารถเรียนทักษะ Push Cart ได้";
			next;
			mes "[Guarnien]";
			mes "Push Cart สามารถเรียนได้สูงสุดที่";
			mes "เลเวล 10 ทักษะนี้ช่วยให้คุณสามารถเข็นรถเข็นไปกับคุณได้ คุณสามารถบรรจุไอเทมที่แตกต่างกันได้สูงสุด 100 ชนิด และมีน้ำหนักรวมน้อยกว่า 8,000";
			next;
			mes "[Guarnien]";
			mes "อย่างที่คุณเห็น ทักษะนี้เป็นรูปแบบขั้นสูงของทักษะ Increase Weight Limit คุณสามารถเช่า PushCart ได้จากเจ้าหน้าที่ Kafra ส่วนใหญ่โดยเสียค่าธรรมเนียมเล็กน้อย";
			next;
			mes "[Guarnien]";
			mes "เมื่อคุณเช่ารถเข็นมาแล้ว คุณสามารถรักษามันไว้ได้ตราบเท่าที่คุณไม่ถอดมันออก ข้อเสียของการใช้ PushCart คือความเร็วในการเคลื่อนที่ของคุณจะลดลง";
			mes "เกือบครึ่งหนึ่งเลยล่ะ! น่ากลัวใช่ไหมล่ะ?";
			next;
			mes "[Guarnien]";
			mes "โชคดีที่เมื่อคุณเพิ่ม";
			mes "เลเวลของทักษะ Push Cart ความเร็วในการเคลื่อนที่ของคุณจะค่อยๆ กลับคืนมา ด้วย Push Cart เลเวล 10 รถเข็นจะไม่ทำให้คุณเดินช้า";
			mes "ลงเลยแม้แต่นิดเดียว!";
			next;
			erasequest 8237;
			.@chk_cart = getskilllv("MC_PUSHCART");
			mes "[Guarnien]";
			if(.@chk_cart > 3 && .@chk_cart < 10){
				mes "อา ฉันเห็นว่าคุณกำลังมุ่งเน้นไปที่การเรียน Push Cart แม้ว่าคุณอาจจะยังมีปัญหาเรื่องความเร็ว";
				mes "ในการเคลื่อนที่อยู่บ้าง แต่มันก็ไม่น่าจะเป็นเรื่องใหญ่นัก";
				mes "ทำได้ดีมาก~";
				getexp 162,52;
			} else if(.@chk_cart == 10){
				mes "มหัศจรรย์มาก! คุณสำเร็จ";
				mes "ทักษะ Push Cart เรียบร้อยแล้ว";
				mes "คุณสมควรได้รับรางวัลเล็กๆ น้อยๆ จริงๆ~";
				getexp 186,60;
			} else {
				mes "ตอนนี้ ทำไมคุณ";
				mes "ไม่ลองไปเรียนทักษะ";
				mes "Push Cart ให้ถึงเลเวล 4 ล่ะ? เมื่อ";
				mes "คุณทำได้แล้ว ฉันถึงจะไปต่อ";
				mes "ในการบรรยายครั้งหน้าได้";
				tu_merchant = 14;
				setquest 8238;
				close;
			}
			setquest 8239;
		}
		else {
			mes "เป็นยังไงบ้าง คุณได้";
			mes "เรียน Push Cart ถึง";
			mes "เลเวล 4 ตามที่ฉันบอกหรือยัง?";
			next;
			mes "[Guarnien]";
			if(.@chk_cart > 3 && .@chk_cart < 10){
				mes "ทำได้ดีมาก~";
				mes "คุณเป็นนักเรียนที่";
				mes "ให้ความร่วมมือดีมาก";
				mes "คุณสมควรได้รับรางวัล";
				mes "เล็กน้อยสำหรับความพยายามนะ~";
				getexp 162,52;
			} else if(.@chk_cart == 10){
				mes "ยอดเยี่ยม!";
				mes "คุณทำได้เกินกว่าที่สั่งเสียอีก โดยการสำเร็จทักษะ Push Cart เลย เก่งมาก!";
				getexp 186,60;
			} else {
				mes "ยังไม่ถึงสินะ?";
				mes "อย่าลืมว่าคุณต้องมี";
				mes "Increase Weight Limit เลเวล 5 ก่อนถึงจะเรียนทักษะ Push";
				mes "Cart ได้ หวังว่านั่นจะช่วยได้นะ";
				close;
			}
			erasequest 8238;
			setquest 8239;
		}
		tu_merchant = 15;
		next;
		mes "[Guarnien]";
		mes "ครั้งหน้า ฉันจะพูดถึงทักษะที่ทำให้ Merchant แตกต่างจากอาชีพอื่นๆ ทั้งหมด: ^871F78Vending^000000 อย่าพลาดล่ะ!";
		close;
	case 11:
	case 12:
		.@chk_over = getskilllv("MC_OVERCHARGE");
		if (tu_merchant == 11) {
			mes "อา พร้อมที่จะ";
			mes "เรียนแล้วล่ะสิ";
			mes "ใช่ไหม? เอาล่ะ";
			mes "ให้ฉันเล่าให้ฟัง";
			mes "เกี่ยวกับ ^871F78Over Charge^000000 นะ";
			next;
			mes "[Guarnien]";
			mes "เช่นเดียวกับ Discount, Over Charge เป็นทักษะติดตัว (Passive skill) ที่เรียนได้สูงสุดเลเวล 10 ดังนั้นมันจึงไม่ใช้ SP ในการใช้งาน";
			next;
			mes "[Guarnien]";
			mes "Over Charge ช่วยให้คุณขายไอเทมให้กับร้านค้า NPC ได้ในราคา Zeny ที่สูงขึ้น ยิ่งเลเวลทักษะ Over Charge สูงเท่าไหร่ คุณก็จะได้รับ Zeny จากการขายไอเทมให้ NPC มากขึ้นเท่านั้น";
			next;
			mes "[Guarnien]";
			erasequest 8235;
			.@chk_over = getskilllv("MC_OVERCHARGE");
			if(.@chk_over > 3 && .@chk_over < 10){
				mes "อา คุณเรียน Over Charge เลเวล 4 แล้ว สมบูรณ์แบบ! รับรางวัลเล็กๆ น้อยๆ ไปนะ~";
				getexp 126,27;
			} else if(.@chk_over == 10){
				mes "น่าทึ่งมาก! คุณสำเร็จทักษะ Over Charge แล้ว! คุณคงอยากได้รางวัลนี้มากเลยสินะ~";
				getexp 142,33;
			} else {
				mes "ส่วนตัวฉันคิดว่าคุณควรจะเรียนทักษะนี้ไว้อย่างแน่นอน! สำหรับงานชิ้นต่อไป จงเรียน Over Charge ให้ถึงเลเวล 4 มันไม่น่าจะยากเกินไปหรอกนะ";
				tu_merchant = 12;
				setquest 8236;
				next;
				mes "[Guarnien]";
				mes "เมื่อคุณทำเสร็จแล้ว";
				mes "เราค่อยมาคุยกัน";
				mes "ในเรื่องที่สูงขึ้นไป";
				mes "พยายามเข้าล่ะ~";
				close;
			}
			setquest 8237;
		}
		else {
			mes "เอาล่ะ...";
			mes "เป็นยังไงบ้าง";
			mes "กับการเรียนทักษะ";
			mes "^871F78Over Charge^000000 นั่น?";
			next;
			mes "[Guarnien]";
			if(.@chk_over > 3 && .@chk_over < 10){
				mes "งานดีมาก!";
				mes "ฉันบอกได้เลยว่า";
				mes "คุณได้ทำตามที่";
				mes "ฉันขอแล้ว โปรด";
				mes "รับรางวัลเล็กๆ น้อยๆ นี้ไปนะ~";
				getexp 126,27;
			} else if(.@chk_over == 10){
				mes "โห...";
				mes "คุณสำเร็จ";
				mes "Over Charge เลยเหรอ?";
				mes "น่าประทับใจมาก! คุณสมควรได้รับรางวัลเล็กน้อยสำหรับงานของคุณ!";
				getexp 142,33;
			} else {
				mes "หืม? คุณยังไม่ได้";
				mes "เรียน Over Charge";
				mes "ถึงเลเวล 4 อีกเหรอ?";
				mes "ไม่ต้องรีบก็ได้ แต่ฉันจะไม่บรรยายเรื่องอะไรต่อจนกว่าคุณจะทำงานเล็กๆ นี้ให้เสร็จนะ";
				close;
			}
			erasequest 8236;
			setquest 8237;
		}
		tu_merchant = 13;
		next;
		mes "[Guarnien]";
		mes "ครั้งหน้า ฉันจะพูดถึงเรื่อง";
		mes "ทักษะ Push Cart เตรียมตัวให้พร้อมสำหรับการบรรยายนี้ล่ะ เพราะ Push Cart เป็นหนึ่งในทักษะที่สำคัญมากสำหรับ Merchant";
		close;
	case 10:
		mes "ขอดูเจ้าพวก";
		mes "Red Potion นั่นหน่อย";
		mes "ทีนี้ คุณจ่ายเงิน";
		mes "ซื้อมันมาเท่าไหร่กัน...?";
		next;
		mes "[Guarnien]";
		mes "อา...!";
		mes "ดูเหมือนว่า...";
		mes "คุณโดนหลอกเข้าแล้ว!";
		mes "ต-แต่ไม่ต้องห่วงนะ";
		mes "ฉันจะไม่บอกใครหรอก";
		next;
		mes "[Guarnien]";
		mes "เอาเถอะ มันเกิดขึ้นได้แม้กระทั่งกับพวกเราที่เก่งที่สุด จงคิดซะว่านี่เป็นประสบการณ์การเรียนรู้ที่ประเมินค่าไม่ได้";
		next;
		mes "[Guarnien]";
		mes "เอาล่ะ ทำไมคุณไม่ลองไปที่ Prontera และพยายามซื้อ 10 Red Potion จาก NPC ในราคาที่ถูกที่สุดอีกครั้งดูล่ะ?";
		tu_merchant = rand(6,8);
		close;
	case 9:
		mes "ขอดูเจ้าพวก";
		mes "Red Potion นั่นหน่อย อา ดูเหมือนคุณจะสำรวจตลาดและซื้อมันมาได้ในราคาที่ถูกที่สุด! เก่งมาก!";
		tu_merchant = 11;
		changequest 8234,8235;
		getexp 112,22;
		next;
		mes "[Guarnien]";
		mes "เอาล่ะ ครั้งหน้า เราจะคุยกันถึงทักษะที่คุณจะเรียนได้หลังจากรู้จักวิธี Discount แล้ว:";
		mes "^871F78Over Charge^000000";
		close;
	case 6:
	case 7:
	case 8:
		mes "อืม...";
		mes "คุณจะพบ NPC บางคน";
		mes "ที่ขาย 10 Red Potion";
		mes "ใน Prontera งานของคุณ";
		mes "คือต้องซื้อโพชั่นพวกนั้น";
		mes "ในราคาที่ถูกที่สุด";
		next;
		mes "[Guarnien]";
		mes "อันดับแรก คุณคงต้องสำรวจราคาตลาดสำหรับ Red Potion ก่อน และจำไว้เสมอ ระวังพวกสิบแปดมงกุฎและพวกขี้โกงให้ดี!";
		close;
	case 5:
		mes "เอาล่ะ...";
		mes "ฉันลองคิดดูแล้ว และฉันมีความท้าทายเล็กๆ น้อยๆ มาให้คุณ ลองไปหา... 10 Red Potion มาให้ฉันซิ!";
		next;
		mes "[Guarnien]";
		mes "มันไม่ยากเกินไปหรอก แต่เงื่อนไขที่ฉันเพิ่มเข้าไปคือคุณต้องไปหาพ่อค้าที่ขาย Red Potion ในราคาที่ถูกที่สุดมาให้ได้";
		next;
		mes "[Guarnien]";
		mes "ดังนั้นอย่างแรก คุณต้องสำรวจราคาตลาดสำหรับ Red Potion ก่อน และระวังพวกต้มตุ๋นด้วยล่ะ! ถ้าราคาดูถูกจนเกินจริง มันมักจะเป็นเรื่องโกหกเสมอ";
		next;
		mes "[Guarnien]";
		mes "ฮ่า ฮ่า ฮ่า ให้ฉันระบุเป้าหมายให้มันสับสนน้อยลงหน่อยละกัน คุณต้องซื้อ 10 Red Potion จาก NPC ในเมือง Prontera";
		next;
		mes "[Guarnien]";
		mes "Prontera อยู่ไกลจากที่นี่หน่อยใช่ไหมล่ะ? เดี๋ยวฉันจะใช้ Kafra Warp นี่แหละ-- Mahnsoo มีไว้เยอะแยะด้วย";
		mes "เหตุผลบางอย่าง --เพื่อส่งคุณไปที่นั่นเดี๋ยวนี้เลย เตรียมตัวนะ...!";
		next;
		tu_merchant = rand(6,8);
		changequest 8233,8234;
		warp "prontera",155,46;
		end;
	case 3:
	case 4:
		.@chk_disc = getskilllv("MC_DISCOUNT");
		if (tu_merchant == 3) {
			mes "อา คุณกลับมาแล้ว!";
			mes "เมื่อกี้ ฉันกำลัง";
			mes "จะเล่าให้ฟังเรื่อง";
			mes "ทักษะ Discount ใช่ไหม?";
			next;
			mes "[Guarnien]";
			mes "ทักษะ ^871F78Discount^000000 ช่วยให้คุณซื้อไอเทมจากร้านค้า NPC ได้ในราคาที่ถูกลง มันเป็น ^871F78Passive Skill^000000 ซึ่งจะแสดงผลอยู่เสมอและไม่ใช้ SP";
			next;
			mes "[Guarnien]";
			mes "ทักษะ Discount นี้จะ";
			mes "สูงสุดที่ ^871F78เลเวล 10^000000 ยิ่งเลเวลทักษะสูงเท่าไหร่ คุณก็ยิ่งจ่ายเงินซื้อไอเทมที่ร้าน NPC น้อยลงเท่านั้น";
			next;
			mes "[Guarnien]";
			if(.@chk_disc > 3 && .@chk_disc < 10){
				mes "โอ้ งานดีนี่";
				mes "ฉันเห็นว่าคุณ";
				mes "เพิ่ม Discount ของคุณ";
				mes "เป็นเลเวล 4 แล้ว";
				mes "เอ้า รับรางวัล";
				mes "เล็กๆ น้อยๆ ไปนะ~";
				getexp 83,11;
			} else if(.@chk_disc == 10){
				mes "เหลือเชื่อเลย!";
				mes "คุณสำเร็จทักษะ Discount แล้ว! คุณเป็นนักเรียนที่ยอดเยี่ยมจริงๆ คุณสมควรได้รับรางวัลสักหน่อยนะ!";
				getexp 97,18;
			} else {
				mes "เอาล่ะ! ทีนี้ เพื่อขยับเข้าไปใกล้การเป็น Merchant ที่แท้จริง ฉันอยากให้คุณเรียนทักษะ Discount ให้ถึงเลเวล 4";
				next;
				mes "[Guarnien]";
				mes "มันคงจะดีกว่าถ้าคุณสามารถ";
				mes "ฝึกจนจบเลเวล 10 ได้ แต่คุณอาจจะสนใจแบ่งเวลาไปเรียนทักษะอื่นมากกว่า";
				next;
				mes "[Guarnien]";
				mes "แต่อย่างไรก็ตาม คุณไม่สามารถละทิ้งความจริงที่ว่าการต่อรองราคาเป็นทักษะที่จำเป็นสำหรับ Merchant! กลับมาหาฉันเมื่อคุณพร้อมนะ~";
				tu_merchant = 4;
				erasequest 8231;
				setquest 8232;
				close;
			}
			erasequest 8231;
			setquest 8233;
		}
		else {
			mes "เป็นยังไงบ้างกับการเรียนทักษะ Discount? อย่างที่ฉันพูดเสมอ ถ้าคุณต่อรองราคาไม่เป็น คุณก็เป็น Merchant ไม่ได้!";
			next;
			mes "[Guarnien]";
			if(.@chk_disc > 3 && .@chk_disc < 10){
				mes "อา คุณได้เรียนรู้";
				mes "วิธีใช้ทักษะ Discount";
				mes "ได้ดีพอแล้วล่ะ ดีมาก นี่คือรางวัลเล็กๆ สำหรับการทำงานหนักนะ~";
				getexp 83,11;
			} else if(.@chk_disc == 10){
				mes "เหลือเชื่อเลย!";
				mes "คุณสำเร็จทักษะ Discount แล้ว! คุณเป็นนักเรียนที่ยอดเยี่ยมจริงๆ คุณสมควรได้รับรางวัลสักหน่อยนะ!";
				getexp 97,18;
			} else {
				mes "ยังไม่ได้สินะ?";
				mes "เอาเถอะ คุณน่าจะบรรลุเป้าหมายนั้นได้ในไม่ช้า ยังไงมันก็เป็นหนึ่งในพื้นฐานของการเป็น Merchant อยู่แล้ว!";
				close;
			}
			erasequest 8232;
			setquest 8233;
		}
		tu_merchant = 5;
		next;
		mes "[Guarnien]";
		mes "เอาล่ะ บทเรียนต่อไปของฉันหวังว่ามันจะท้าทายคุณขึ้นมาอีกสักหน่อย กลับมาเมื่อคุณคิดว่าพร้อมแล้วล่ะ ตกลงไหม?";
		close;
	case 2:
		mes "อา คุณกลับมาเรียนเพิ่มแล้ว ไหนดูซิ ฉันกำลังจะเล่าเรื่องอะไรให้ฟังนะ... อ๋อ ใช่ Mammonite!";
		next;
		mes "[Guarnien]";
		mes "^871F78Mammonite^000000 เป็นทักษะที่ช่วยให้คุณสร้างความเสียหายอย่างหนักแก่เป้าหมายด้วย Zeny เงินคือพลังจริงๆ นั่นแหละ!";
		next;
		mes "[Guarnien]";
		mes "ข้อดีคือมันสร้างความเสียหายได้รุนแรงมากและใช้ SP เพียงเล็กน้อย อย่างไรก็ตาม ข้อเสียคือมันจะใช้ Zeny ของคุณด้วย!";
		next;
		mes "[Guarnien]";
		mes "ดังนั้นถ้าคุณใช้ทักษะนี้บ่อยเกินไป คุณอาจลงเอยด้วยการล้มละลายได้! ดังนั้นจงระวังให้มาก และเมื่อเลเวลของ Mammonite เพิ่มขึ้น พลังโจมตีและการใช้ Zeny ก็จะเพิ่มขึ้นตามไปด้วย";
		next;
		mes "[Guarnien]";
		.@chk_mam = getskilllv("MC_MAMMONITE");
		if(.@chk_mam > 3 && .@chk_mam < 10){
			mes "อา ฉันเห็นว่าคุณได้ลองใช้ทักษะนี้แล้ว คุณเสียเงินไปเท่าไหร่แล้วล่ะกับการใช้ Mammonite? หวังว่าคงไม่มากเกินไปนะ";
			next;
			mes "[Guarnien]";
			mes "ในเมื่อคุณยังเป็นมือใหม่ ฉันรู้สึกเสียใจจริงๆ ถ้าคุณต้องเสีย Zeny ไปฟรีๆ ทำไมไม่รับนี่ไปเป็นการชดเชยล่ะ?";
			tu_merchant = 3;
			erasequest 8230;
			setquest 8231;
			getexp 70,12;
			next;
			mes "[Guarnien]";
			mes "ตกลง ทีนี้ฉันคิดว่าเราพร้อมแล้วที่จะมาคุยกันเรื่องทักษะ ^871F78Discount^000000 กลับมาเมื่อคุณอยากให้ฉันเล่าเรื่องนี้เพิ่มเติมนะ ตกลงไหม?";
			close;
		} else {
			mes "ตกลง ทีนี้ฉันคิดว่าเราพร้อมแล้วที่จะมาคุยกันเรื่องทักษะ ^871F78Discount^000000 กลับมาเมื่อคุณอยากให้ฉันเล่าเรื่องนี้เพิ่มเติมนะ ตกลงไหม?";
			tu_merchant = 3;
			erasequest 8230;
			setquest 8231;
			close;
		}
	case 1:
		mes "เอาล่ะ...";
		mes "คุณได้เรียนทักษะ";
		mes "Increase Weight Limit";
		mes "ถึงเลเวล 4";
		mes "ตามที่ฉันขอหรือยัง?";
		next;
		mes "[Guarnien]";
		.@chk_soji = getskilllv("MC_INCCARRY");
		if(.@chk_soji > 3 && .@chk_soji < 10){
			mes "อา ฉันบอกได้เลยว่าคุณทำแล้ว ไม่เลว! ถึงตอนนี้คุณควรจะสามารถเรียนทักษะ ^871F78Discount^000000 ได้แล้ว แต่ฉันจะสอนเรื่องนั้นทีหลังนะ";
			next;
			mes "[Guarnien]";
			mes "ยังมีทักษะพื้นฐานอื่นๆ อีกสองสามอย่างที่คุณสามารถเรียนได้ทันที ซึ่งฉันจะพูดถึงก่อน อย่าเพิ่งรีบร้อนเลย~";
			next;
			mes "[Guarnien]";
			mes "อย่างแรก คือทักษะ ^871F78Item Appraisal^000000 ความสามารถนี้เมื่อเรียนแล้ว จะช่วยให้คุณ ^871F78ระบุไอเทมที่ไม่รู้จัก^000000 ได้";
			next;
			mes "[Guarnien]";
			mes "ทักษะนี้ทำงานเหมือนกับไอเทม ^871F78Magnifier^000000 ทุกประการ ดังนั้นคุณจึงไม่ต้องเสีย Zeny ซื้อพวกมัน และทักษะนี้จะสูงสุดที่ ^871F78เลเวล 1^000000";
			next;
			if(getskilllv("MC_IDENTIFY")){
				mes "[Guarnien]";
				mes "ฉันเห็นในแววตาของคุณว่าคุณเรียนรู้วิธีการประเมินไอเทมมาแล้ว ฮ่าฮ่า ถึงแม้การมีความรู้นั้นจะเป็นรางวัลในตัวมันเองอยู่แล้ว แต่ขอให้ฉันมอบอะไรพิเศษให้เพิ่มนะ!";
				tu_merchant = 2;
				erasequest 8229;
				setquest 8230;
				getexp 58,11;
				getitem 1351,1; //Battle_Axe
				next;
				mes "[Guarnien]";
				mes "เอาล่ะ ครั้งหน้า";
				mes "ฉันจะพูดถึงทักษะ Mammonite ตอนนี้ขอฉันพักจากการบรรยายอันเหน็ดเหนื่อยนี้สักครู่นะ ^666666*เฮ้อ!*^000000";
				close;
			}
		} else {
			mes "ไม่เหรอ? ไม่เป็นไร";
			mes "แต่ฉันไม่สามารถสอนอะไร";
			mes "คุณได้มากนักถ้าคุณยังไม่เข้าใจพื้นฐานง่ายๆ เหล่านี้ ดังนั้นรีบไปเรียนสกิลพวกนั้นซะนะ ตกลงไหม?";
			close;
		}
		mes "เอาล่ะ ครั้งหน้า";
		mes "ฉันจะพูดถึงทักษะ Mammonite ตอนนี้ขอฉันพักจากการบรรยายอันเหน็ดเหนื่อยนี้สักครู่นะ ^666666*เฮ้อ!*^000000";
		tu_merchant = 2;
		erasequest 8229;
		setquest 8230;
		close;
	}
	mes "คุณเพิ่งเริ่มเป็น Merchant ใช่ไหมล่ะ? คุณอยากจะเรียนรู้พื้นฐานของธุรกิจจากเพื่อนร่วมอาชีพที่มีประสบการณ์หน่อยไหม?";
	next;
	if (select("แน่นอน!:ฉันจะลุยด้วยตัวเอง!") == 1){
		if(getskilllv("MC_INCCARRY") < 4){
			tu_merchant = 1;
			setquest 8229;
			mes "[Guarnien]";
			mes callfunc("F_SexMes","ดีมากสาวน้อย~!","ดีมากไอ้หนู~!");
			mes "แต่สิ่งแรกที่ต้องทำ!";
			mes "คุณควรไปเรียนทักษะ";
			mes "^871F78Increase Weight Limit^000000 ซะก่อนนะ!";
			next;
			mes "[Guarnien]";
			mes "คุณจะเป็น Merchant จริงๆ ไม่ได้หรอกถ้าคุณยังแบกสินค้าไปขายไม่ได้ เมื่อคุณเพิ่มเลเวลของทักษะนี้ คุณจะสามารถพกพาไอเทมได้มากขึ้น";
			next;
			mes "[Guarnien]";
			mes "ทำไมคุณไม่อัปเกรดทักษะ";
			mes "Increase Weight Limit ให้ถึงเลเวล 4 ล่ะ? หลังจากนั้น เราค่อยมาคุยกันต่อเรื่องการเป็น Merchant ที่แท้จริง";
			close;
		} else {
			mes "[Guarnien]";
			mes "เยี่ยม...!";
			mes "อา และฉันเห็นว่าคุณเรียนทักษะ ^871F78Increase Weight Limit^000000 ถึงเลเวล 4 แล้ว คุณนี่เป็นคนกระตือรือร้นจริงๆ นะเนี่ย?";
			next;
			mes "[Guarnien]";
			mes "ในเมื่อเลเวลทักษะ Increase Weight";
			mes "Limit ของคุณสูงพอแล้ว คุณก็น่าจะเห็นทักษะใหม่ในหน้าต่าง Skill: ^871F78Discount^000000!";
			next;
			mes "[Guarnien]";
			mes "แต่เราจะมาคุยเรื่องทักษะ Discount ทีหลัง ยังมีทักษะพื้นฐานอื่นๆ อีกสองสามอย่างที่คุณสามารถเรียนได้ทันทีซึ่งฉันจะพูดถึงก่อน อย่ารีบร้อนไปเลย~";
			next;
			mes "[Guarnien]";
			mes "อย่างแรก คือทักษะ ^871F78Item Appraisal^000000 ความสามารถนี้เมื่อเรียนแล้ว จะช่วยให้คุณ ^871F78ระบุไอเทมที่ไม่รู้จัก^000000 ได้";
			next;
			mes "[Guarnien]";
			mes "ทักษะนี้ทำงานเหมือนกับไอเทม ^871F78Magnifier^000000 ทุกประการ ดังนั้นคุณจึงไม่ต้องเสีย Zeny ซื้อพวกมัน และทักษะนี้จะสูงสุดที่ ^871F78เลเวล 1^000000";
			next;
			mes "[Guarnien]";
			if(getskilllv("MC_IDENTIFY")){
				mes "ฉันเห็นในแววตาของคุณว่าคุณเรียนรู้วิธีการประเมินไอเทมมาแล้ว ฮ่าฮ่า ถึงแม้การมีความรู้นั้นจะเป็นรางวัลในตัวมันเองอยู่แล้ว แต่ขอให้ฉันมอบอะไรพิเศษให้เพิ่มนะ!";
				tu_merchant = 2;
				erasequest 8229;
				setquest 8230;
				getexp 58,11;
				getitem 1351,1; //Battle_Axe
				next;
				mes "[Guarnien]";
				mes "เอาล่ะ ครั้งหน้า";
				mes "ฉันจะพูดถึงทักษะ Mammonite ตอนนี้ขอฉันพักจากการบรรยายอันเหน็ดเหนื่อยนี้สักครู่นะ ^666666*เฮ้อ!*^000000";
				close;
			}
			mes "เอาล่ะ ครั้งหน้า";
			mes "ฉันจะพูดถึงทักษะ Mammonite ตอนนี้ขอฉันพักจากการศึกษาที่ยากลำบากสักครู่";
			next;
			mes "[Guarnien]";
			mes "โอ้...";
			mes "และฉันจะเล่าให้ฟัง";
			mes "เพิ่มเติมเรื่องทักษะ Discount";
			mes "ทีหลังนะ ฉันมักจะชอบ";
			mes "ลืมเรื่องนั้นอยู่เรื่อยเลย!";
			tu_merchant = 2;
			erasequest 8229;
			setquest 8230;
			close;
		}
	} else {
		mes "[Guarnien]";
		mes "ด้วยตัวเองเหรอ?";
		mes "ฮ่าฮ่าฮ่า จิตวิญญาณดีนี่!";
		mes "แต่ถึงอย่างนั้น คำแนะนำที่ฉันให้ก็ฟรีนะ มันคงไม่เสียหายอะไรถ้าจะเก็บสิ่งที่ฉันพูดไปคิดสักนิด";
		next;
		mes "[Guarnien]";
		mes "เอาเป็นว่า ถ้าคุณเปลี่ยนใจเมื่อไหร่ ก็แวะมาคุยกับฉันได้ทุกเมื่อ และในฐานะ Merchant คุณควรรู้ไว้นะว่าคำแนะนำฟรีๆ น่ะดีที่สุดแล้ว!";
		close;
	}
}

// Sagle
//============================================================
prontera,66,111,3	script	Sagle	4_KID01,{
	mes "[Sagle]";
	if (tu_merchant == 6)
		callsub L_Potions, 390,9;// <Zeny cost>, <next 'tu_merchant' value>
	else if (tu_merchant == 7)
		callsub L_Potions, 410,10;
	else if (tu_merchant == 8)
		callsub L_Potions, 420,10;
	mes "มันติดอยู่ที่ปลาย";
	mes "ลิ้นของฉันนี่เอง แต่";
	mes "ฉันนึกมันไม่ออกสักที...";
	next;
	mes "[Sagle]";
	mes "คุณรู้ไหม ชื่อบริษัทที่มีสาวสวยมากมายทำงานอยู่ด้วยน่ะ คา... คา-อะไรสักอย่างนี่แหละ ยังไงก็เถอะ ฉันได้ยินมาว่าพวกเขากำลังกังวลเกี่ยวกับการแข่งขันที่เพิ่มขึ้น ";
	next;
	mes "[Sagle]";
	mes "มันไม่ใช่ธุระกงการอะไรของฉันหรอก แต่มันก็ดีที่จะรู้ว่ามีอะไรเกิดขึ้นใน";
	mes "โลกบ้าง โดยเฉพาะอย่างยิ่งถ้ามันส่งผลกระทบต่อตลาด";
	close;

L_Potions:
	.@cost = getarg(0);
	mes "สวัสดี สวัสดี~";
	mes "ทำไมไม่ลองซื้อ";
	mes "Red Potion ไปหน่อยล่ะ?";
	mes "พวกมันจำเป็นต่อการเดินทางมาก และ Red Potion พวกนี้ก็ดีที่สุดสำหรับนักผจญภัยหน้าใหม่เลยนะ~";
	next;
	mes "[Sagle]";
	mes "ฉันจะขาย 10 Red Potion ให้คุณในราคา ^871F78"+ .@cost +" zeny^000000 รีบซื้อตอนนี้เลยก่อนที่มันจะหมดล่ะ!";
	next;
	switch(select("ซื้อ:ยกเลิก")){
	case 1:
		mes "[Sagle]";
		mes "ยอดเยี่ยม!";
		mes "คุณตาถึงเรื่องของราคาถูกจริงๆ เอ้า นี่เลย 10 Red Potion สดๆ ใหม่ๆ จาก... คนปรุงยา...";
		if(Zeny < .@cost){
			next;
			mes "[Sagle]";
			mes "ว้าว เดี๋ยวก่อน!";
			mes "คุณไม่มีเงินพอจะ";
			mes "ซื้อโพชั่นพวกนี้...";
			mes "โปรดกลับมาใหม่";
			mes "เมื่อคุณมี Zeny พอ";
			mes "ตกลงไหม?";
			close;
		}
		if(MaxWeight - Weight < 71){
			next;
			mes "[Sagle]";
			mes "ว้าว เดี๋ยวก่อน!";
			mes "ไม่มีทางที่คุณจะแบกทั้งหมดนี้ไหวหรอก! ทำไมไม่ลองเอาของบางอย่างไปเก็บในคลัง Kafra ก่อนจะกลับมาล่ะ?";
		} else {
			Zeny = Zeny - .@cost;
			tu_merchant = getarg(1);
			getitem 501,10; //Red_Potion
		}
		close;
	case 2:
		mes "[Sagle]";
		mes "ตกลง...";
		mes "แต่อย่ามา";
		mes "บ่นทีหลังนะถ้า";
		mes "โพชั่นพวกนี้";
		mes "ขายหมดเกลี้ยง!";
		close;
	}
}

// Kellion
//============================================================
prontera,93,330,3	script	Kellion	4W_M_01,{
	mes "[Kellion]";
	if (tu_merchant == 6)
		callsub L_Potions, 400,10;// <Zeny cost>, <next 'tu_merchant' value>
	else if (tu_merchant == 7)
		callsub L_Potions, 390,9;
	else if (tu_merchant == 8)
		callsub L_Potions, 340,10;
	mes "เมื่อเร็วๆ นี้ ฉันได้ยินมาว่ามีบางอย่าง";
	mes "เกิดขึ้นกับราชวงศ์";
	next;
	mes "[Kellion]";
	mes "มันอาจจะไม่ใช่ข่าวลือที่เชื่อถือได้นัก แต่ฉันได้ยินมาว่าสมาชิกในราชวงศ์ล้มตายด้วยสาเหตุลึกลับบางอย่าง";
	next;
	mes "[Kellion]";
	mes "หึ";
	mes "นั่นเป็นข่าวที่ค่อนข้างน่าสยดสยองทีเดียว!";
	mes "เอาเถอะ หวังว่ามันจะไม่ใช่ความจริงแม้แต่นิดเดียวนะ ไม่เลยสักนิด...";
	close;

L_Potions:
	.@cost = getarg(0);
	mes "ฉันกำลังขายเซ็ต";
	mes "10 Red Potion ในราคา "+ .@cost +" Zeny";
	mes "คุณอยากได้สักหน่อยไหม?";
	next;
	switch(select("ซื้อ:ยกเลิก")){
	case 1:
		mes "[Kellion]";
		mes "เลือกได้ดีนี่~";
		mes "ขอบคุณที่";
		mes "มาใช้บริการร้านฉัน";
		if(Zeny < .@cost){
			next;
			mes "[Kellion]";
			mes "อุ๊ย!";
			mes "ฉันเสียใจด้วย แต่คุณมี Zeny ไม่พอ ฉันเองก็ต้องทำธุรกิจให้อยู่รอดเหมือนกันนะ รู้ไหม?";
			close;
		}
		if(MaxWeight - Weight < 71){
			next;
			mes "[Kellion]";
			mes "...หือ?";
			mes "คุณควรเอาของบางอย่างไปเก็บในคลัง Kafra นะ คุณแบกอะไรเพิ่มไม่ได้อีกแล้ว!";
		} else {
			Zeny = Zeny - .@cost;
			tu_merchant = getarg(1);
			getitem 501,10; //Red_Potion
		}
		close;
	case 2:
		mes "[Kellion]";
		mes "อืม ฉันเสียใจที่";
		mes "ได้ยินแบบนั้น แต่";
		mes "ฉันคิดว่าคงได้";
		mes "พบกันใหม่ภายหลัง";
		close;
	}
}

// Aigie
//============================================================
prt_in,169,11,3	script	Aigie	4_F_03,{
	mes "[Aigie]";
	if (tu_merchant == 6)
		callsub L_Potions, 340,10;// <Zeny cost>, <next 'tu_merchant' value>
	else if (tu_merchant == 7)
		callsub L_Potions, 420,10;
	else if (tu_merchant == 8)
		callsub L_Potions, 390,9;
	mes "มันเป็นความจริงที่ว่าเงินไม่ใช่ทุกอย่าง ฉันมั่นใจว่าสิ่งอื่นๆ ก็สำคัญในการทำให้มีความสุขเช่นกัน";
	next;
	mes "[Aigie]";
	mes "อย่างไรก็ตาม เงินยังพึ่งพาได้มากกว่าบางคนที่ฉันรู้จักเสียอีก คุณสามารถเชื่อใจเงินได้มากกว่าสิ่งอื่นใดในโลกใบนี้";
	close;

L_Potions:
	.@cost = getarg(0);
	mes "ยินดีต้อนรับสู่ร้านของฉัน";
	mes "ฉันเป็นเพียงเด็กสาวที่เป็นเหยื่อของสถานการณ์ ถูกบังคับให้มาขายโพชั่นริมถนนในราคาถูกอย่างกับได้เปล่า";
	next;
	mes "[Aigie]";
	mes "คุณอยากจะช่วย";
	mes "ซื้อ 10 Red Potion";
	mes "ในราคา "+ .@cost +" Zeny ไหมคะ ท่านนักผจญภัยผู้ใจดี?";
	next;
	switch(select("ซื้อ:ยกเลิก")){
	case 1:
		mes "[Aigie]";
		mes "ขอบคุณมากค่ะ";
		mes "ทีนี้ฉันก็พอจะ";
		mes "มีเงินซื้ออาหารแล้ว...";
		if(Zeny < .@cost){
			next;
			mes "[Aigie]";
			mes "รอเดี๋ยว รอเดี๋ยว!";
			mes "ฉันเสียใจด้วยนะคะ แต่คุณมีเงินไม่พอ ได้โปรดไปหา Zeny เพิ่มแล้วค่อยกลับมาใหม่ได้ไหมคะ?";
			close;
		}
		if(MaxWeight - Weight < 71){
			next;
			mes "[Aigie]";
			mes "รอเดี๋ยว รอเดี๋ยว!";
			mes "คุณไม่สามารถแบกของเพิ่มได้แล้วล่ะค่ะ ทำไมไม่ลองเอาของไปเก็บที่คลัง Kafra ก่อนล่ะคะ?";
		} else {
			Zeny = Zeny - .@cost;
			tu_merchant = getarg(1);
			getitem 501,10; //Red_Potion
		}
		close;
	case 2:
		mes "[Aigie]";
		mes "ฉันเข้าใจค่ะ";
		mes "แต่ฉันหวังว่าคุณ";
		mes "จะเห็นว่าฉันเสนอ";
		mes "ราคาที่ถูกจริงๆ นะคะ";
		mes "^666666*ซิก ซิก*^000000";
		close;
	}
}

// Jayon
//============================================================
prontera,247,129,3	script	Jayon	4_M_03,{
	mes "[Jayon]";
	if (tu_merchant == 6)
		callsub L_Potions, 410,10;// <Zeny cost>, <next 'tu_merchant' value>
	else if (tu_merchant == 7)
		callsub L_Potions, 400,10;
	else if (tu_merchant == 8)
		callsub L_Potions, 410,10;
	mes "สาธารณรัฐ Schwarzwald...";
	mes "มันเป็นประเทศที่น่าสนใจจริงๆ มี Juno และสถานที่อื่นๆ อีกสองสามแห่งที่คุ้มค่ากับการไปสำรวจดู ";
	next;
	mes "[Jayon]";
	mes "พวกมันอาจจะไม่ใช่สถานที่ที่ดีที่สุด";
	mes "สำหรับการเที่ยวชม แต่ฉันมั่นใจว่าพวกมันจะมีสิ่งที่น่าสนใจให้กับเหล่านักผจญภัยอย่างพวกคุณแน่นอน";
	close;

L_Potions:
	.@cost = getarg(0);
	mes "ถ้าคุณได้ตรวจสอบราคาตลาดสำหรับ Red Potion แล้ว คุณจะรู้ว่าราคาของฉันดีที่สุด ฉันเสนอขาย 10 Red Potion เพียง "+ .@cost +" Zeny เท่านั้น!";
	next;
	switch(select("ซื้อ:ยกเลิก")){
	case 1:
		mes "[Jayon]";
		mes "เหะ เหะ...!";
		mes "ดูเหมือนว่าคุณ";
		mes "จะรู้เรื่องตลาดดีนะ";
		mes "คุณเองก็เป็น";
		mes "Merchant เหมือนกัน";
		mes "ใช่ไหมล่ะ?";
		if(Zeny < .@cost){
			next;
			mes "[Jayon]";
			mes "แต่เสียใจด้วยนะพวก";
			mes "กฎก็ต้องเป็นกฎ";
			mes "คุณต้องจ่ายตามราคา";
			mes "ถ้าคุณอยากได้โพชั่นพวกนี้";
			close;
		}
		if(MaxWeight - Weight < 71){
			next;
			mes "[Jayon]";
			mes "แต่เสียใจด้วยนะพวก ฉันปล่อยให้คุณแบกของเกินกว่าที่จะรับไหวไม่ได้";
			mes "คุณควรไปเพิ่มพื้นที่ว่างในกระเป๋าเมื่อมีโอกาสนะ";
		} else {
			Zeny = Zeny - .@cost;
			tu_merchant = getarg(1);
			getitem 501,10; //Red_Potion
		}
		close;
	case 2:
		mes "[Jayon]";
		mes "แค่ดูผ่านๆ";
		mes "เฉยๆ สินะ?";
		mes "ฉันเข้าใจ";
		mes "แต่คุณควรจะรีบ";
		mes "คว้าข้อเสนอดีๆ";
		mes "เมื่อเจอเข้าล่ะ!";
		close;
	}
}

// Maos
//============================================================
prt_in,251,129,3	script	Maos	4_M_SEAMAN,{
	mes "[Maos]";
	if (tu_merchant == 6)
		callsub L_Potions, 400,10;// <Zeny cost>, <next 'tu_merchant' value>
	else if (tu_merchant == 7)
		callsub L_Potions, 340,10;
	else if (tu_merchant == 8)
		callsub L_Potions, 420,10;
	mes "ฉันเห็นผู้คนมากมายดิ้นรนที่จะเก็บออมทุกบาททุกสตางค์และเอามันทั้งหมดไปฝากไว้ในบัญชีออมทรัพย์...";
	next;
	mes "[Maos]";
	mes "อย่าเข้าใจฉันผิดนะ มันเป็นความคิดที่ดีที่จะลงทุนเพื่ออนาคต แต่เงินน่ะมีไว้เพื่อใช้นะ! สนุกกับทุกช่วงเวลา นั่นคือสิ่งที่ฉันพูดเสมอ~";
	next;
	mes "[Maos]";
	mes "นอกจากนี้...";
	mes "คุณไม่สามารถพก";
	mes "เงินติดตัวไปได้";
	mes "เมื่อเวลาของคุณหมดลง";
	mes "จริงไหมล่ะ?";
	close;

L_Potions:
	.@cost = getarg(0);
	mes "ไม่ต้องพูดอะไรทั้งนั้น";
	mes "ฉันรู้ว่าคุณต้องการอะไร";
	mes "10 Red Potion ใช่ไหมล่ะ?";
	mes "ฉันจะขายพวกมันให้";
	mes "ในราคาถูกๆ เพียง "+ .@cost +" Zeny~";
	next;
	switch(select("ซื้อ:ยกเลิก")){
	case 1:
		mes "[Maos]";
		mes "เลือกได้ยอดเยี่ยม!";
		mes "สินค้าคุณภาพในราคาที่เอื้อมถึง! นั่นคือจริยธรรมของฉันในฐานะพ่อค้า";
		if(Zeny < .@cost){
			next;
			mes "[Maos]";
			mes "โอ้ แต่คุณไม่มี Zeny พอจะซื้อโพชั่นพวกนี้ด้วยซ้ำ ฉันเสียใจนะ แต่ฉันขายให้แบบขาดทุนไม่ได้จริงๆ รู้ไหม?";
			close;
		}
		if(MaxWeight - Weight < 71){
			next;
			mes "[Maos]";
			mes "โอ้ แต่ดูเหมือนคุณจะไม่มีพื้นที่ในกระเป๋าพอที่จะแบกไอเทมเพิ่มได้แล้วนะ";
		} else {
			Zeny = Zeny - .@cost;
			tu_merchant = getarg(1);
			getitem 501,10; //Red_Potion
		}
		close;
	case 2:
		mes "[Maos]";
		mes "อืม ฉันบังคับให้คุณซื้อไม่ได้หรอก แต่ฉันขอบอกเลยว่าคุณกำลังพลาดข้อเสนอที่ถูกสุดๆ ไปจริงๆ!";
		close;
	}
}
