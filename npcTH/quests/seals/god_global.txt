//===== rAthena Script ======================================= 
//= God Item Quest - Variable Management
//===== By: ================================================== 
//= MasterOfMuppets
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Use this NPC if the seals quest variables get messed up.
//===== Additional Comments: =================================
//= 1.0 First version. [MasterOfMuppets]
//= 1.1 Updated several aspects of the script. [L0ne_W0lf]
//= 1.2 Renewal script update. [Euphy]
//=    Added GM management function.
//============================================================

sec_in02,15,170,0	script	Golbal var	817,{
	callfunc "F_GM_NPC";
	mes "[ตรวจสอบ]";
	mes "กรุณาใส่รหัสผ่าน";
	next;
	if (callfunc("F_GM_NPC",68392411,0) == 1) {
		mes "[ตรวจสอบ]";
		mes "กรุณาเลือกเมนู";
		next;
		switch(select("ตรวจสอบค่าปัจจุบัน:ลำดับที่ 1:ลำดับที่ 2:ลำดับที่ 3:ลำดับที่ 4:รีเซ็ตค่าทั้งหมด:god_sl_1:god_eremes:god_brising:god_mjo_0:god_mjo_1:god_mjo_2:god_mjo_3:god_mjo_4")) {
		case 1:
			mes "^0000FF$God1^000000 = ^FF0000" + $God1 + "^000000.";
			mes "^0000FF$God2^000000 = ^FF0000" + $God2 + "^000000.";
			mes "^0000FF$God3^000000 = ^FF0000" + $God3 + "^000000.";
			mes "^0000FF$God4^000000 = ^FF0000" + $God4 + "^000000.";
			close;
		case 2: callsub L_Number,"$God1"; break;
		case 3: callsub L_Number,"$God2"; break;
		case 4: callsub L_Number,"$God3"; break;
		case 5: callsub L_Number,"$God4"; break;
		case 6:
			mes "[ตรวจสอบ]";
			mes "คุณแน่ใจหรือไม่ว่าต้องการรีเซ็ตรายการ God Globalvar ทั้งหมด?";
			mes "กรุณาใส่รหัสผ่าน";
			next;
			if (callfunc("F_GM_NPC",68392411,0) == 1) {
				mes "[ตรวจสอบ]";
				mes "ขณะนี้รายการ God Globalvar ทั้งหมดกำลังถูกรีเซ็ต";
				next;
				set $God1,0;
				set $God2,0;
				set $God3,0;
				set $God4,0;
				mes "^0000FF$God1^000000 = ^FF0000" + $God1 + "^000000.";
				mes "^0000FF$God2^000000 = ^FF0000" + $God2 + "^000000.";
				mes "^0000FF$God3^000000 = ^FF0000" + $God3 + "^000000.";
				mes "^0000FF$God4^000000 = ^FF0000" + $God4 + "^000000.";
				close;
			} else {
				mes "[ตรวจสอบ]";
				mes "คำสั่งถูกยกเลิก";
				close;
			}
		case 7: callsub L_Var,"god_sl_1"; break;
		case 8: callsub L_Var,"god_eremes"; break;
		case 9: callsub L_Var,"god_brising"; break;
		case 10: callsub L_Var,"god_mjo_0"; break;
		case 11: callsub L_Var,"god_mjo_1"; break;
		case 12: callsub L_Var,"god_mjo_2"; break;
		case 13: callsub L_Var,"god_mjo_3"; break;
		case 14: callsub L_Var,"god_mjo_4"; break;
		}
	} else {
		mes "[ตรวจสอบ]";
		mes "รหัสผ่านไม่ถูกต้อง";
		close;
	}

L_Number:
	input .@input,0,$@god_check2;
	mes "[ตรวจสอบ]";
	mes "คุณต้องการเปลี่ยนค่าเป็น "+.@input+" ใช่หรือไม่?";
	mes "กรุณาใส่รหัสผ่าน";
	next;
	if (callfunc("F_GM_NPC",68392411,0) == 1) {
		mes "[ตรวจสอบ]";
		mes "ยืนยันคำสั่งเปลี่ยนค่าเป็น "+.@input+" เรียบร้อยแล้ว";
		next;
		setd getarg(0),.@input;
		mes getarg(0)+" "+.@input;
		close;
	} else {
		mes "[ตรวจสอบ]";
		mes "คำสั่งถูกยกเลิก";
		close;
	}

L_Var:
	input .@input;
	setd getarg(0),.@input;
	next;
	mes "[ตรวจสอบ]";
	mes "ดำเนินการเสร็จสิ้น";
	close;

OnInit:
	// Seals roll at 25/50 in Renewal and 50/100 in Pre-Renewal.
	// (การปลดผนึกจะอยู่ที่ 25/50 ในคลาส Renewal และ 50/100 ในคลาส Pre-Renewal)
	if (checkre(0)) {
		set $@god_check1,25;
		set $@god_check2,50;
	} else {
		set $@god_check1,50;
		set $@god_check2,100;
	}
	end;
}
