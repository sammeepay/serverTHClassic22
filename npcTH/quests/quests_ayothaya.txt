//===== rAthena Script =======================================
//= Quest NPCs related to Ayothaya
//===== By ===================================================
//= Fredzilla, MasterOfMuppets
//===== Version ==============================================
//= 2.6
//===== Compatible With ======================================
//= rAthena Project
//===== Description ==========================================
//= [Official Conversion]
//= Ayothaya Ring Quest
//= Ayothaya Dungeon Quest
//= Tom Yum Goong Quest
//===== Comments =============================================
//= 1.0 First Release, made with infomation from RagnaInfo
//= 1.1 Added anti-novice protection [Lupus]
//= 1.3 Fixed exploit [Lupus]
//= 1.4 Updated, more close to the official. Thanks to birkiczd
//=     Also optimized it again, remove unused global variable [Lupus]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Moved quests from cities/ to here:
//=     Shrimp, Dungeon, Tom Yum Goong [Evera]
//= 1.7 Updated the Ayothaya Ring quest to the official one [MasterOfMuppets]
//= 1.8 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.9 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.0 Rescripted Tom Yum Goong and Dungeon quest to Aegis [L0ne_W0lf]
//=     10.3 standards.
//= 2.0a Corrected a Typo error ";;". [Samuray22]
//= 2.1 Added missing checkweights. [L0ne_W0lf]
//= 2.2 Replaced effect numerics with constants. [L0ne_W0lf]
//= 2.3 Replaced effect numerics with constants. [Samuray22]
//= 2.4 Updated RE/Pre-RE EXP. [Euphy]
//= 2.5 Added questlog support. [Euphy]
//= 2.6 Added Renewal quest items. [Euphy]
//============================================================

// Ayothaya Ring Quest :: ayo_ring
//============================================================
ayothaya,233,105,3	script	Powerful-Looking Woman	838,2,2,{
	if (thai_find == 14) {
		mes "[Shuda]";
		mes "เจ้า";
		mes "เจอ Annon หรือยัง?";
		next;
		mes "[Shuda]";
		mes "เจ้ายัง";
		mes "ไม่เจอเขาอีกเหรอ?";
		mes "แล้วเขาหายหัว";
		mes "ไปอยู่ที่ไหนกันล่ะหะ?!";
		next;
		mes "[Shuda]";
		mes "ช่างเถอะ...";
		mes "ข้าไม่แน่ใจว่าจะเชื่อทุกอย่างที่เจ้าพูดได้ไหม แต่ในเมื่อเจ้าหาแหวนมาให้ข้าได้ นั่นก็ดีพอแล้ว ขอบใจที่ช่วยนะ";
		next;
		mes "[Shuda]";
		mes "ถ้าเจ้าบังเอิญเจอ Annon ระหว่างการเดินทาง บอกเขาให้รีบกลับมาทันทีเลยนะ เจ้าไปผจญภัยต่อได้แล้วล่ะ~";
		next;
		mes "[Shuda]";
		mes "ถ้าข้า";
		mes "เจอเขาอีกครั้งล่ะก็...";
		mes "เขาจะต้องถูก ^660000ลงทัณฑ์^000000...";
		mes "^666666*กัดฟันกรอด*^000000";
		close;
	} else if (thai_find > 2 && thai_find < 14) {
		mes "[Shuda]";
		mes "^666666*เฮ้อ...*^000000";
		mes "เจ้ายัง";
		mes "ไม่เจอ Annon อีกเหรอ?";
		mes "งั้นเหรอ ไม่เป็นไรหรอก";
		mes "เวลาไม่ใช่ปัญหาสำหรับเรา";
		next;
		mes "[Shuda]";
		mes "และที่ข้าพูดว่า 'เรา' ข้าหมายถึง '^660000เจ้า^000000' ข้าไม่สนหรอกว่าเจ้าจะมีแผนการหรือนัดหมายอะไร เพราะเจ้าจะต้องสละ";
		mes "เวลามาให้ข้า! โอ๊ะ โฮะ โฮะ โฮะ โฮะ!";
		close;
	} else if (thai_find == 2) {
		if (countitem(7288)) {
			mes "[Shuda]";
			mes "อา... นี่มัน...?!";
			mes "ใช่เลย สิ่งนี้แหละ!";
			mes "แหวนหมั้นของข้าที่ถูกไอ้คนโง่";
			mes "Annon นั่นขว้างทิ้งลงน้ำไป!";
			next;
			mes "[Shuda]";
			mes "ขอบคุณมาก! เพื่อเป็นการให้เกียรติเจ้า ลูกเจ็ดคนแรกของเราจะตั้งชื่อตามเจ้าเลยนะ! โอ๊ะ โฮะ โฮะ โฮะ โฮะ โฮะ โฮะ~";
			next;
			mes "[Shuda]";
			mes "เอาล่ะ...";
			mes "ที่ข้าต้องทำ";
			mes "ก็แค่ตามหา Annon";
			mes "ไอ้บ้าตัวแสบนั่น";
			mes "หายไปอยู่ไหนกันแน่เนี่ย?!";
			next;
			mes "[Shuda]";
			mes "^666666*อะแฮ่ม*^000000";
			mes "ขะ-ข้าไม่ได้ตั้งใจ";
			mes "จะทำให้เขากลัวหรอกนะ ข้าก็แค่ต้องการ";
			mes "แหวนหมั้นของข้าคืนเท่านั้นเอง";
			next;
			mes "[Shuda]";
			mes "^666666*เฮ้อ*^000000";
			mes "ข้าว่าวันเวลา";
			mes "ของการเป็นเจ้าสาวที่มีความสุข";
			mes "ยังคงอยู่ไกลห่างจากข้าเหลือเกิน...";
			next;
			mes "^3355FFShuda จ้องมองมาที่คุณ";
			mes "ด้วยสายตาออดอ้อนเหมือนลูกหมา^000000";
			next;
			switch(select("อยะ-อย่ามองข้าแบบนั้นสิ!:ก็ได้ๆ ตกลง")) {
			case 1:
				mes "[Shuda]";
				mes "โธ่เอ๋ย เจ้าก็รู้เรื่องที่เกิดขึ้นทั้งหมดแล้ว และข้าก็รู้ว่าเจ้ายังต้องผจญภัยต่อ";
				mes "ไปรอบๆ แถวนี้อยู่ดี";
				next;
				mes "[Shuda]";
				mes "ข้าก็แค่ขอให้เจ้าช่วยหา Annon ไปด้วยในระหว่างนั้นเอง ไม่ใช่เรื่องใหญ่โตอะไรเลยใช่ไหมล่ะ? ขอบใจนะที่ช่วยข้า~";
				next;
				mes "[Shuda]";
				mes "ในระหว่างนี้ ข้าจะลองคิดดูว่าจะตอบแทน Annon ยังไงดีที่ทิ้งให้ข้าต้องทนทุกข์แบบนี้! เพราะงั้นรีบไปหา Annon ซะ! โฮะ โฮะ โฮะ โฮะ โฮะ!";
				emotion ET_SMILE;
				next;
				emotion ET_THINK, playerattached();
				mes "["+strcharinfo(0)+"]";
				mes "(ยัยนี่น่ากลัวชะมัด!";
				mes "หลอกใช้ให้ฉันทำโน่นทำนี่";
				mes "ให้อีกแล้ว...)";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "(ทำไมฉันต้องคอย";
				mes "ทำงานให้คนอื่น";
				mes "อยู่เรื่อยเลยนะ?!)";
				break;
			case 2:
				mes "[Shuda]";
				mes "เย้ ข้ารู้ว่าข้าพึ่งพาเจ้าได้! งั้นไปตามหา Annon ซะ! มันเป็นโชคชะตาของข้าที่จะต้องทำให้เขาเป็นของ ^660000ข้า^000000 โฮะ โฮะ โฮะ โฮะ โฮะ!";
				next;
				mes "[Shuda]";
				mes "ข้าไม่เห็นเขาแถวๆ หมู่บ้านมาพักใหญ่แล้ว และข้าไม่คิดว่าเขาจะกระโดดลงน้ำเพื่อหาแหวนข้าหรอก เพราะเขามันคนขี้ขลาด เขาคงไม่กล้าสู้หน้าข้าถ้าไม่มีแหวนวงนั้น";
				next;
				mes "[Shuda]";
				mes "เจ้าช่วยบอก Annon ทีว่า";
				mes "ข้าได้แหวนคืนแล้ว เขาจะได้กลับมาหาข้าสักที หวังว่าเจ้าจะจำเรื่องนี้ให้ข้านะ นักเดินทาง";
				break;
			}
			delitem 7288,1; //Thai_Ring
			changequest 12030,12031;
			set thai_find,3;
			close;
		} else {
			mes "[Shuda]";
			mes "หือ...?";
			mes "เจอแหวนของข้าแล้วเหรอ?";
			mes "เอาออกมาสิ เอามาให้ข้าดูหน่อย!";
			next;
			mes "[Shuda]";
			mes "อะไรกันเนี่ย...";
			mes "เจ้าไม่มีอะไรเลยนี่!";
			mes "อย่ามาโกหกข้านะ";
			mes "เอาแหวนมาให้ข้าเดี๋ยวนี้!";
			next;
			mes "[Shuda]";
			mes "รีบๆ ไปหาแหวน";
			mes "ของข้ามาซะ ข้าจะรอ";
			mes "ฟังข่าวดีอยู่ที่นี่";
			mes "โอ๊ะ โฮะ โฮะ โฮะ โฮะ~!";
			if (checkquest(12029) == -1)
				setquest 12029;
			close;
		}
	} else if (thai_find == 1) {
		mes "[Shuda]";
		mes "ข้ารู้สึกผิดนิดหน่อยที่ต้องขอร้องเจ้าแบบนี้ โดยเฉพาะเมื่อเจ้าเป็น";
		mes "คนนอก";
		next;
		mes "[Shuda]";
		mes "แต่แหวนวงนั้นสำคัญกับข้ามากจริงๆ ถ้าเจ้าหามันมาให้ข้าได้ ข้าคงจะเป็นผู้หญิงที่มีความสุขที่สุดในโลก";
		next;
		mes "[Shuda]";
		mes "เพราะงั้นได้โปรดเถอะ ข้าขอร้อง";
		mes "ช่วยเอาแหวนกลับคืนมาให้ข้าที";
		mes "ข้าอยากใช้ชีวิตคู่กับ Annon อย่างมีความสุขตลอดไป เจ้าไม่เข้าใจความรู้สึกข้าหรือไงกัน?!";
		close;
	} else {
		mes "[Powerful-Looking Woman]";
		mes "อืม...?";
		mes "อ๋อ เจ้าคงจะเป็นนักท่องเที่ยวสินะ ข้าอยากจะพาเจ้าเดินชมรอบๆ และฟังเรื่องราวเกี่ยวกับโลกภายนอกนะ แต่ว่า...";
		next;
		if (rand(0,1)) {
			mes "[Powerful-Looking Woman]";
			mes "ตอนนี้ข้ายุ่ง";
			mes "ม๊ากกกกกก";
			mes "ยุ่งสุดๆ เลยล่ะ";
			next;
			mes "[Powerful-Looking Woman]";
			mes "หือ...?";
			mes "เจ้าคิดว่าข้าไม่ยุ่งเพียงเพราะข้ายืนอยู่เฉยๆ ไม่ขยับไปไหนงั้นเหรอ?! โอ้ คุณ พระ ช่วย เจ้าช่างหยาบคายเสียจริง!";
			next;
			mes "[Powerful-Looking Woman]";
			mes "ตอนนี้ข้าต้องหาแหวน";
			mes "หมั้นของข้าให้เจอ ถ้าเพียงแต่";
			mes "คู่หมั้นของข้าไม่ได้";
			mes "เขวี้ยงมันทิ้งไป...";
			next;
			switch(select("อะไรนะ...?:โกหกน่า~!")) {
			case 1:
				mes "[Powerful-Looking Woman]";
				mes "'อะไรนะ' อะไรของเจ้า?";
				mes "เจ้ายังคิดว่าข้ามายืน";
				mes "อยู่แถวนี้เพื่อความ";
				mes "เพลิดเพลินส่วนตัวงั้นเหรอ?!";
				next;
				emotion ET_KIK, playerattached();
				mes "["+strcharinfo(0)+"]";
				mes "เปล่าๆ ไม่ใช่แบบนั้น...";
				mes "ข้าหมายถึง ข้าไม่อยากจะ";
				mes "เชื่อเลยว่าเจ้าจะมี";
				mes "คู่หมั้นกับเขาด้วย!";
				next;
				mes "[Powerful-Looking Woman]";
				mes "แว๊รรรรรรรรรร!";
				mes "จะ-เจ้าเป็นใครกันถึงมาพูด";
				mes "จาหยาบคายกับสุภาพสตรีผู้น่ารักอย่างข้า ไปเลยนะ ข้าไม่อยากคุยกับเจ้าแล้ว!";
				close;
			case 2:
				mes "[Powerful-Looking Woman]";
				mes "โกหกอะไร?";
				mes "ข้าจะไม่ทนฟังเรื่องไร้สาระนะ";
				mes "ถ้าเจ้าจะพูดอะไรประมาณว่า 'ฉันไม่อยากเชื่อเลยว่าคนอย่างเธอจะมีคู่หมั้น!'";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "โว้วๆๆๆ~";
				mes "ใจเย็นก่อนแม่คุณ";
				mes "ฟังข้าก่อน";
				mes "สิ่งที่ข้าสงสัยก็คือ...";
				next;
				mes "[Powerful-Looking Woman]";
				mes "ข้ารู้ ข้ารู้";
				mes "เจ้าสงสัยว่าทำไมข้าถึง";
				mes "มายืนอยู่ใกล้ๆ น้ำใช่ไหมล่ะ? ก็เพราะว่าแหวนข้าถูกเขวี้ยงลงไปในนั้น และข้าต้อง";
				mes "เอามาคืนให้ได้";
				next;
				mes "[Powerful-Looking Woman]";
				mes "ข้าทำอะไรอยู่เนี่ย มายืนคุยกับนักท่องเที่ยวแบบนี้? ^666666*เฮ้อ...*^000000 เอาเถอะ เจ้าก็ได้รู้ในสิ่งที่อยากรู้แล้ว หวังว่าเจ้าคงพอใจนะ";
				next;
				mes "[Powerful-Looking Woman]";
				mes "...";
				next;
				mes "[Powerful-Looking Woman]";
				mes "...";
				mes "......";
				next;
				mes "[Powerful-Looking Woman]";
				mes "...";
				mes "......";
				mes "........นี่";
				next;
				mes "[Powerful-Looking Woman]";
				mes "จ้องมองข้าทำไม?";
				mes "ไม่มีอะไรทำหรือไง?";
				next;
				switch(select("ไม่มี.:ไม่มี~ และเจ้าก็ดูเป็นผู้หญิงที่น่าสนใจดีนะ")) {
				case 1:
					mes "[Powerful-Looking Woman]";
					mes "งั้นเหรอ อืมมม...";
					mes "เจ้าก็เป็นสเปกของข้าอยู่นะ";
					if (Sex == SEX_MALE) {
						mes "เปล่าๆ ไม่ได้แปลว่าข้าสนใจเจ้านะ อีกอย่างข้า";
						mes "ก็มีคู่หมั้นอยู่แล้ว";
					} else
						mes "ก่อนที่เจ้าจะคิดอะไรแปลกๆ ข้าหมายความว่าข้าคิดว่าเจ้ากับข้าดูมีอะไรหลายๆ อย่างคล้ายกันนะ";
					next;
					mes "[Powerful-Looking Woman]";
					mes "ขอแนะนำตัวก่อนนะ";
					mes "ข้าชื่อ Shuda อาศัยอยู่ใน Ayothaya มาตลอดชีวิต ในเมื่อเจ้าไม่มีอะไรทำ ข้าก็มี";
					mes "เรื่องอยากจะรบกวนเจ้าหน่อย";
					next;
					mes "[Shuda]";
					mes "Annon คู่หมั้นของข้ามักจะสนใจเรื่องอื่นมากกว่าข้า ข้าไม่เข้าใจเลย และข้าไม่คิดว่าข้าควรจะถูกละเลยแบบนี้!";
					next;
					mes "[Shuda]";
					mes "^666666*เฮ้อ...*^000000";
					mes "อย่างไรก็ตาม เราหมั้นกันแล้ว";
					mes "และเราก็ได้แลกแหวนกัน...";
					next;
					mes "[Shuda]";
					mes "ข้าคิดว่าการหมั้นจะทำให้ความสัมพันธ์ของเราดีขึ้น แต่เขากลับเอาแหวนของข้าไปเขวี้ยงทิ้งลงน้ำซะงั้น";
					next;
					mes "[Shuda]";
					mes "ข้าเสียใจมากเลยตะโกนด่าเขาให้ไปเก็บแหวนมาคืนข้า และหลังจากนั้น";
					mes "ข้าก็ไม่เห็นเขาอีกเลย";
					mes "ดังนั้นสิ่งที่ข้าอยากจะขอคือ...";
					next;
					mes "[" +strcharinfo(0)+ "]";
					mes "เจ้าอยากให้ข้า";
					mes "ไปหาแหวน";
					mes "ให้ใช่ไหมล่ะ?";
					next;
					mes "[Shuda]";
					mes "อา...";
					mes "เจ้ารู้ใจข้า";
					mes "ซะด้วย";
					mes "งั้นเรื่องก็";
					mes "ง่ายขึ้นเยอะ";
					next;
					mes "[Shuda]";
					mes "และเมื่อข้าได้";
					mes "แหวนคืนแล้ว ข้าจะ";
					mes "บังคับให้เขาพาข้าไป";
					mes "เที่ยวพักผ่อนที่ Jawaii ให้สาสม...";
					next;
					mes "[Shuda]";
					mes "...ที่นั่นแหละข้าจะ";
					mes "^660000จองจำ^000000 เขาซะ";
					mes "^666666*กัดฟันกรอด*^000000";
					next;
					emotion ET_SURPRISE, playerattached();
					mes "[" +strcharinfo(0)+ "]";
					mes "(...ส-สยองขวัญชะมัด!)";
					next;
					mes "[Shuda]";
					mes "สรุปก็คือ ข้าอยากให้เจ้าออกไปหาแหวนหมั้นของข้า เนื่องจากน้ำแถวนี้ค่อนข้างนิ่ง แหวนน่าจะยังอยู่ใต้น้ำและยังไม่ถูกพัดพาไปไหนไกล";
					next;
					mes "[Shuda]";
					mes "และนั่นคือเควสสำหรับเจ้า มันน่าจะทำให้เจ้าเพลิดเพลินได้สักพักนะ ลงไปในน้ำซะ เดี๋ยวข้าจะคอยคุมอยู่ตรงนี้เอง";
					mes "โอ๊ะ โฮะ โฮะ โฮะ โฮะ!";
					emotion ET_SMILE;
					set thai_find,1;
					close;
				case 2:
					mes "[Powerful-Looking Woman]";
					mes "^666666*เฮ้อ...*^000000";
					mes "เจ้าจะเอาแต่จ้องมองแล้ว";
					mes "ไม่ช่วยอะไรเลยเหรอ? ข้านึกว่าคำว่านักเดินทางจะมีความหมายเดียวกับ";
					mes "คำว่า 'ฮีโร่' ซะอีก ไม่ใช่ 'ไอ้ขี้เกียจ'";
					next;
					mes "[Powerful-Looking Woman]";
					mes "อ้อ ข้าเข้าใจแล้ว...";
					mes "ไม่ชอบช่วยคนแปลกหน้าสินะ?";
					mes "เอาล่ะ ข้าชื่อ Shuda เป็นสาวงามผู้น่าสงสารที่กำลังรอให้แหวนหมั้นลอยขึ้นมาจากน้ำ";
					next;
					mes "[Powerful-Looking Woman]";
					mes "นี่ไง...!";
					mes "เราไม่ใช่";
					mes "คนแปลกหน้ากันแล้วนะ!";
					mes "โอ๊ะ โฮะ โฮะ โฮะ โฮะ!";
					next;
					mes "[Shuda]";
					mes "เจ้าไม่สงสารข้าและสถานการณ์ที่น่าสลดใจนี้บ้างเหรอ? ถ้ามีใครช่วยหญิงสาวผู้น่ารักคนนี้หาแหวนเจอ เธอคงจะเป็นเจ้าสาวที่มีความสุขที่สุดในโลก";
					next;
					mes "[Shuda]";
					mes "และผู้ช่วยชีวิตเธอ";
					mes "ก็จะได้รับการขนานนามว่า";
					mes "ฮีโร่แห่ง Ayothaya";
					next;
					mes "[" +strcharinfo(0)+ "]";
					mes " ...ว่าไงนะ?!";
					next;
					mes "[Shuda]";
					mes "สรุปง่ายๆ คือ เจ้าจะไปหาแหวนหมั้นมาให้ข้า แล้วข้าจะกลายเป็นเจ้าสาวที่มีความสุขที่สุดในโลกและใช้ชีวิตอย่างสุขสบายตลอดไป มีอะไรเข้าใจยาก";
					mes "ตรงไหนกัน?";
					next;
					mes "[Shuda]";
					mes "แน่นอนว่า Annon คู่หมั้นข้าน่ะมันคนไม่มีกระดูกสันหลัง ชอบหายตัวไปทุกครั้ง";
					mes "ที่ข้าพูดจาแรงนิดหน่อย! ทำตัวเหมือนเด็กผู้หญิงไปได้! เขาไม่เห็นเหรอว่าข้ากำลังพยายามทำให้เขาเป็นผู้ชายที่ดีขึ้นน่ะ?!";
					next;
					mes "[" +strcharinfo(0)+ "]";
					mes "เขาวิ่งหนี";
					mes "เจ้าไปแล้วใช่ไหม?";
					next;
					mes "[Shuda]";
					mes "เอ๊ะ...?";
					mes "เมื่อกี้เจ้าพูดว่าอะไรนะ?";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "^666666*อะแฮ่ม*^000000";
					mes "เจ้าดี";
					mes "เกินไปสำหรับเขาต่างหากล่ะ";
					mes "(ถ้าเป็นฉัน ฉันก็หนีเหมือนกันแหละ!)";
					next;
					mes "[Shuda]";
					mes "ใช่ๆ ข้ารู้";
					mes "แต่สิ่งที่สำคัญคือ";
					mes "เมื่อข้าได้แหวนคืนแล้ว Annon จะต้องเป็นของข้าตลอดกาล จนกว่า ^660000ความตายจะพรากเราจากกัน^000000";
					next;
					mes "[Shuda]";
					mes "สรุปแล้ว เจ้าจะ";
					mes "ช่วยข้าไหมหะ?!";
					mes "ถ้าเจ้าช่วย ข้ากับ Annon จะตั้งชื่อลูกเจ็ดคนแรกตามชื่อเจ้าเลยล่ะ";
					next;
					mes "[Shuda]";
					mes "เพราะงั้นไปหาใต้น้ำซะ! แถวนี้ไม่มีกระแสน้ำ ข้าเชื่อว่ามันยังอยู่ตรงนั้นแหละ! รอน้องตัดริบบิ้นอยู่เหรอ มันควรจะเป็นงานว่ายน้ำง่ายๆ สำหรับเจ้านะ! โอ๊ะ โฮะ โฮะ โฮะ โฮะ!";
					if (checkquest(12029) == -1)
						setquest 12029;
					set thai_find,1;
					close;
				}
			break;
			}
		} else {
			mes "[Powerful-Looking Woman]";
			mes "^666666*เฮ้อ...*^000000";
			mes "ทำไมมันไม่ลอยขึ้นมาสักทีนะ? หรือถ้าฉันลองเอาไม้กวนน้ำดูหน่อยไหมนะ...?";
			next;
			mes "[Powerful-Looking Woman]";
			mes "...?!";
			mes "เจ้าจ้องมอง";
			mes "อะไรของเจ้าน่ะ?!";
			mes "ข้ายุ่งอยู่ ถ้าเจ้าต้องการ";
			mes "ความช่วยเหลืออะไร";
			mes "ข้าช่วยไม่ได้หรอกนะ!";
			next;
			mes "[Powerful-Looking Woman]";
			mes "^666666พวกนักท่องเที่ยวบ้าๆ วิ่ง";
			mes "พล่านไปทั่ว Ayothaya คอย";
			mes "เซ้าซี้และรบกวนสาวสวย";
			mes "อย่างข้าอยู่ได้...^000000";
			close;
		}
	}

OnTouch_:
	if (thai_find == 0) {
		mes "[Powerful-Looking Woman]";
		mes "^666666*เฮ้อ...*^000000";
		mes "มันหายหัว";
		mes "ไปอยู่ที่ไหนกันนะ!?";
		next;
		mes "[Powerful-Looking Woman]";
		mes "เจ้าจ้อง";
		mes "อะไรของเจ้า?";
		mes "ไม่เคยเห็นหญิงสาวผู้อ่อนหวานถอนหายใจหรือไง? ไปให้พ้นหน้าข้าเลยไป!";
		close;
	}
}

ayo_dun02,91,263,0	script	#Annonblood	111,2,3,{
if (!$@annonactive) {
	if (thai_find == 13) {
		mes "["+strcharinfo(0)+"]";
		mes "คราบเลือดงั้นเหรอ?";
		mes "มันเกิดอะไรขึ้นเนี่ย?";
		mes "ข้าควรจะลองดูหน่อย...";
		next;
		mes "^3355FFภายใต้ต้นไม้ใหญ่";
		mes "คุณพบพุ่มไม้บางพุ่มที่มี";
		mes "รอยเลือดเปื้อนอยู่บน";
		mes "ใบไม้บางส่วน^000000";
		next;
		mes "^3355FFภายในพุ่มไม้ คุณเห็น";
		mes "รอยเลือดเป็นทางยาวลึกเข้าไป เหมือนมีสัตว์ที่บาดเจ็บพยายามจะหาที่กำบังภายใน";
		mes "พุ่มไม้^000000";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "อืมมมม....";
		next;
		switch(select("แหวกพุ่มไม้:ขุดพุ่มไม้ออก")) {
		case 1:
			if (!rand(0,2)) {
				mes "[" +strcharinfo(0)+ "]";
				mes "^666666*แค่ก แค่ก*^000000";
				mes "ฝุ่นเยอะชะมัด!";
				next;
				mes "[?]";
				mes "...โอ๊ยยยย";
				mes "โอ้... พระเจ้าช่วย...!";
				next;
				emotion ET_SURPRISE, playerattached();
				mes "[" +strcharinfo(0)+ "]";
				mes "หือ...?";
				mes "ข้าได้ยินเสียงอะไรบางอย่าง";
				mes "จากข้างล่าง! มาขุด";
				mes "มันขึ้นมากันเถอะ!";
				next;
				mes "^3355FFคุณดึงพุ่มไม้ออก";
				mes "^3355FFและภายใต้ฝุ่นและ";
				mes "^3355FFกิ่งไม้ คุณพบ";
				mes "โพรงขนาดเล็ก^000000";
				next;
				set $@annonactive,1;
				enablenpc "Haggard Man";
				mes "[Haggard Man]";
				mes "^666666*แค่ก แค่ก!*^000000";
				mes "คะ...ใครอยู่ตรงนั้นน่ะ?";
				mes "Shuda ใช่ไหม?! ถ้าเป็นเจ้า";
				mes "ถอยไปนะ ไม่งั้นข้าจะฆ่าตัวตายจริงๆ ด้วย!";
				emotion ET_HUK, playerattached();
				next;
				switch(select("เจ้ามาทำอะไรที่นี่?:ข้าไม่ใช่ Shuda!")) {
				case 1:
					mes "["+strcharinfo(0)+"]";
					mes "นี่...";
					mes "เจ้ากำลังทำอะ--";
					next;
					set $@annonactive,0;
					disablenpc "Haggard Man";
					mes "^3355FFเขาหายวับไปแล้ว!^000000";
					close;
				case 2:
					mes "[Haggard Man]";
					mes "หืมมม...?";
					mes "ข้าสัมผัสได้ถึงความใจดี ความจริงใจ และความรักเพื่อนมนุษย์ในน้ำเสียงของเจ้า เจ้าไม่ใช่ Shuda จริงๆ ด้วย";
					next;
					mes "[Haggard Man]";
					mes "เจ้าดูไม่คุ้นหน้าเลย คนนอก ข้าไม่แน่ใจว่าเจ้าหาที่ซ่อนข้าเจอได้ยังไง แต่ถ้าเจ้ามีอะไรจะพูด ก็รีบพูดมาสั้นๆ!";
					close;
				}
			} else {
				mes "^3355FFคุณขุดพุ่มไม้ขึ้นมา ทำให้";
				mes "มีกลุ่มฝุ่นขนาดใหญ่ลอยฟุ้งขึ้นมาจนสำลัก^000000";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "^666666*แค่ก แค่ก!*^000000";
				mes "ฝุ่นมันฟุ้ง";
				mes "จนหายใจ";
				mes "ไม่ออกเลย!";
				close;
			}
		case 2:
			mes "^3355FFขณะที่เจ้าขุดพุ่มไม้ขึ้นมา พื้นดินใต้พุ่มไม้พุ่มหนึ่งก็ถล่มลง และเจ้าได้ยินเสียงกรีดร้อง";
			mes "จากด้านล่าง^000000";
			next;
			if (!rand(0,2)) {
				set $@annonactive,1;
				enablenpc "Haggard Man";
				mes "[?]";
				mes "^666666*แค่ก แค่ก*^000000";
				mes "...ขะ-ขาข้า!";
				close;
			} else {
				mes "[" +strcharinfo(0)+ "]";
				mes "หืมมม...?";
				mes "เสียงอะไรน่ะ?";
				mes "อึ๋ย ฝุ่นเยอะน่าดูเลย";
				mes "ข้าหยุดขุดดีกว่า";
				mes "^666666*แค่ก แค่ก แค่ก*^000000";
				close;
			}
		}
	} else if (thai_find == 14) {
		mes "^3355FFคุณพบว่าโพรงนั้นว่างเปล่า ดูเหมือนว่า Annon จะหาที่ซ่อนใหม่ได้แล้ว^000000";
		close;
	} else {
		mes "^3355FFคุณพบกองพุ่มไม้แห้งตายภายใต้ต้นไม้ใหญ่ พื้นที่รอบพุ่มไม้ดูมีฝุ่นเยอะมาก^000000";
		close;
	}

OnTouch_:
	if (thai_find == 12) {
		if (!rand(0,2)) {
			emotion ET_SURPRISE, playerattached();
			mes "[" +strcharinfo(0)+ "]";
			mes "หือ...?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "นั่นมันดู";
			mes "เหมือนเลือดเลย!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "หืม...";
			mes "ใช่...";
			mes "นั่นเลือดแน่นอน";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "แปลกจังที่มาเจอ";
			mes "คราบเลือดใน";
			mes "ที่แบบนี้...";
			set thai_find,13;
			changequest 12033,12034;
			close;
		}
	}
}

}

ayo_dun02,91,264,3	script	Haggard Man	841,{
	//completequest 12034;
	if (thai_find == 13) {
		mes "[Haggard Man]";
		mes "^666666*...แค่ก แค่ก*^000000";
		mes "ไม่นะ...! ที่ซ่อนของข้า!";
		next;
		mes "[Haggard Man]";
		mes "หืมมม...?";
		mes "ข้าเลือดออกงั้นเหรอ?!";
		mes "โอ้ มันเกิดขึ้นได้ยังไงกัน?";
		mes "การฝึกฝนทางวิญญาณของข้าไม่ควรจะเป็นแบบนี้สิ...!";
		next;
		switch(select("ทำแผลให้เขาก่อน:เจ้าคือ Annon ใช่ไหม?")) {
		case 1:
			mes "[" +strcharinfo(0)+ "]";
			mes "อย่างไรก็ตาม ข้าว่าเจ้าต้องได้รับการรักษา ให้ข้าดูหน่อยว่าข้ามีโพชั่นติดตัวมาบ้างไหม";
			next;
			if (countitem(504)) {
				mes "["+strcharinfo(0)+"]";
				mes "อา...";
				mes "อยู่นี่ไง";
				mes "White Potion";
				delitem 504,1; //White_Potion
				next;
				mes "[Haggard Man]";
				mes "เจ้าหมายถึง White Potion";
				mes "สุดยอดเครื่องดื่มเพื่อการรักษาเนี่ยนะ?";
				mes "ข้าซึ้งใจจริงๆ ที่เจ้าช่วยข้า ^666666*ฟู่ว*^000000 ตอนนี้ข้ารู้สึกดีขึ้นมากเลย";
				next;
				mes "[Annon]";
				mes "อา ข้าต้องขออภัยที่ไม่ได้แนะนำตัวก่อนหน้านี้";
				mes "ข้าชื่อว่า Annon เจ้าคงสงสัยว่าข้ามาทำอะไร";
				mes "ที่นี่ใต้ผืนดินแบบนี้";
				next;
				mes "[Annon]";
				mes "คือว่า ข้ากำลังฝึกปฏิบัติการพิเศษเพื่อฝึกจิตวิญญาณของข้าอยู่น่ะ ถึงมันอาจจะเชื่อยากไปหน่อยก็เถอะ";
				next;
				mes "[Annon]";
				mes "อะไรนะ...?";
				mes "Shuda ส่งเจ้ามางั้นเหรอ?! เอาเถอะ...";
				mes "ข้าขอบใจที่เจ้าช่วยรักษาข้า ตอนนี้ได้เวลาที่ข้าต้องฝึกต่อแล้ว";
				next;
				mes "[" +strcharinfo(0)+ "]";
				specialeffect EF_CLOAKING;
				mes "เดี๋ยวสิ ข้าปล่อยเจ้าไปไม่ได้! เจ้าไม่รู้หรอกว่ามันยากแค่ไหนกว่าข้าจะหาเจ้าเจอ! เจ้าจะหนีไปเฉยๆ แบบนี้ไม่ได้นะ!";
				next;
				mes "[Annon]";
				mes "หืม? แล้วเจ้าต้องการอะไรล่ะ? ข้าขอร้องล่ะ อย่าบอก Shuda ว่าเจ้าเจอข้า ไม่ว่าจะเกิดอะไรขึ้น ข้าจะไม่กลับไปหานางเด็ดขาด!";
				next;
				mes "[Annon]";
				mes "เอ๊ะ...?";
				mes "เจ้าหาแหวน";
				mes "ของนางเจอแล้วเหรอ?";
				mes "โอ้ ข้าหวังอยู่เชียวว่า";
				mes "จะไม่มีใครหามันเจอ";
				next;
				mes "[Annon]";
				mes "นางคงบอกเจ้าสินะว่าข้าหนีมาเพราะอับอายที่ทำแหวนหาย และถ้าเจ้าได้แหวนคืน ข้าจะยินดีกลับไปหาใช่ไหม?";
				next;
				mes "[Annon]";
				mes "คือว่า...";
				mes "ข้าตั้งใจ";
				mes "เขวี้ยงแหวนนั่นทิ้งเองแหละ!";
				mes "ข้าพูดจริงนะเรื่องที่จะ";
				mes "ไม่กลับไปหา Shuda น่ะ";
				next;
				mes "[Annon]";
				mes "ดูเหมือนเจ้าอยากจะรู้อะไรมากกว่านี้ ถ้าเจ้าสัญญากับข้าว่าจะไม่บอกที่อยู่ของข้า";
				mes "ให้ Shuda รู้ ข้าก็จะอธิบายให้ฟัง";
				next;
				switch(select("เจ้าคิดว่าโลกหมุนรอบตัวเองงั้นเหรอ?:เจ้าต้องการให้ข้าทำอะไร?")) {
				case 1:
					mes "[" +strcharinfo(0)+ "]";
					mes "เจ้าคิดว่าโลกมัน";
					mes "หมุนรอบตัวเจ้าหรือไง?";
					mes "เจ้าจะหนีไปเฉยๆ แบบนี้ไม่ได้นะ!";
					next;
					mes "[Annon]";
					mes "โอ้ ข้ารู้ ข้ารู้!";
					mes "แต่เจ้าไม่เข้าใจหรอก แน่นอนว่า Shuda น่ะสวย";
					next;
					mes "[Annon]";
					mes "แต่พ่อแม่ของเราคลุมถุงชนให้เราแต่งงานกันตั้งแต่ยังแบเบาะ";
					mes "การคลุมถุงชนแบบนั้นไม่ได้สำคัญกับข้าเลย แต่นางน่ะให้ความสำคัญกับมันมาก";
					next;
					mes "[Annon]";
					mes "บอกตามตรงนะ นางทำให้ข้ากลัว";
					mes "จนแทบจะบ้าตาย ทุกครั้งที่ข้าอยู่กับนาง";
					mes "ข้าอดไม่ได้ที่จะเหงื่อแตกพล่าน และเมื่อนางอยู่ใกล้ๆ ข้าก็จะฝันร้ายถึงนางเสมอ ^666666*บรื๋อ...*^000000";
					next;
					mes "[Annon]";
					mes "นางคิดว่ามันตลกที่ชอบเอาไฟฟ้ามาช็อตข้า และมัดข้าไว้ใน";
					mes "ห้องมืดๆ แล้วให้ข้ากินแต่ Jellopy เวลาที่ข้าไม่เชื่อฟังนาง ตอนนี้เจ้าเห็นหรือยังว่าทำไมข้าถึงโหยหาอิสรภาพ?";
					next;
					mes "[Annon]";
					mes "แน่นอน ถ้าเจ้าไม่เปิดเผยกับนางว่าเจ้าเจอข้า ข้าจะตอบแทนเจ้าอย่างงาม ข้าขอร้องล่ะ แค่แกล้งทำเป็นว่าหาข้าไม่เจอ ได้โปรดเถอะนะ?";
					next;
					select("...ก็ได้ๆ ตกลง");
					mes "[Annon]";
					mes "ขอบคุณมากจริงๆ!";
					mes "เพื่อเป็นการตอบแทน ให้ข้าใช้ทักษะหนึ่งที่ข้าได้เรียนรู้มาจากการฝึกจิตวิญญาณกับเจ้านะ";
					break;
				case 2:
					mes "[Annon]";
					mes "แน่นอน สิ่งที่ข้าต้องการให้เจ้าทำคือเก็บเรื่องของข้าเป็นความลับจาก Shuda แค่ลืมไปซะว่าเคยเจอข้า และบอก Shuda ไปว่าหาข้าเท่าไหร่ก็ไม่เจอ";
					next;
					mes "[Annon]";
					mes "อีกอย่าง ต่อให้เจ้าช่วยนาง สิ่งที่นางจะทำก็แค่เสนอตั้งชื่อลูกตามชื่อเจ้าเท่านั้นแหละ... เจ้าต้องการแบบนั้นจริงๆ เหรอ? ข้ามั่นใจเลยว่าข้ากับนางจะไม่มีวันมีลูกด้วยกันแน่ๆ แค่นี้โลกก็วุ่นวายพอแล้ว";
					next;
					mes "[Annon]";
					mes "เอาล่ะ เจ้าคงสงสัยว่า";
					mes "ข้าจะตอบแทนเจ้าที่ช่วยปิดเงียบยังไงดี งั้นให้ข้าโชว์หนึ่งในทักษะที่ข้าได้เรียนรู้มาจากการฝึกจิตวิญญาณให้ดูนะ";
					break;
				}
				next;
				mes "[Annon]";
				mes "ไหนดูซิ...";
				mes "ขอข้าบิดแขนเจ้า";
				mes "ไปทางนี้หน่อย และ";
				mes "กดตรงนี้...";
				next;
				mes "[" +strcharinfo(0)+ "]";
				mes "เหวอออ~!";
				mes "เจ้าทำบ้า";
				mes "อะไรของเจ้าน่ะ!";
				mes "ข้าเจ็บนะเนี่ย!";
				next;
				mes "[Annon]";
				mes "โอ้ นี่เป็นการนวด";
				mes "แบบพิเศษน่ะ ไม่ต้องห่วง มันจะ";
				mes "ช่วยคลายความเหนื่อยล้าได้ ความจริงมันเหมือนได้ขึ้นสวรรค์เลยล่ะ เดี๋ยวเจ้าก็รู้เอง~";
				next;
				mes "^3355FF*กร๊อบ กร๊อบ กร๊อบ*";
				mes "*กร๊อบ กร๊อบ กร๊อบ*";
				mes "*เปรี๊ยะ เปรี๊ยะ เปรี๊ยะ*";
				mes "*เปรี๊ยะ เปรี๊ยะ เปรี๊ยะ*^000000";
				next;
				mes "^3355FFคุณรู้สึกราวกับว่า Annon";
				mes "จัดเรียงกระดูกทุกชิ้นในร่างกายของคุณใหม่ให้อยู่ในตำแหน่งที่ถูกต้อง ที่แปลกคือคุณกลับรู้สึกผ่อนคลายและสดชื่นอย่างมาก แทนที่จะเจ็บปวดเจียนตาย";
				set thai_find,14;
				completequest 12034;
				if (checkre(3)) {
					if (BaseLevel < 56) getexp 900,0;
					else if (BaseLevel > 55 && BaseLevel < 61) getexp 1050,0;
					else if (BaseLevel > 60 && BaseLevel < 66) getexp 1868,0;
					else if (BaseLevel > 65 && BaseLevel < 71) getexp 2741,0;
					else if (BaseLevel > 70 && BaseLevel < 76) getexp 7075,0;
					else if (BaseLevel > 75 && BaseLevel < 81) getexp 13024,0;
					else if (BaseLevel > 80 && BaseLevel < 86) getexp 15034,0;
					else if (BaseLevel > 85 && BaseLevel < 91) getexp 18205,0;
					else getexp 40678,0;
				} else {
					if (BaseLevel < 56) getexp 9000,0;
					else if (BaseLevel > 55 && BaseLevel < 61) getexp 10500,0;
					else if (BaseLevel > 60 && BaseLevel < 66) getexp 18684,0;
					else if (BaseLevel > 65 && BaseLevel < 71) getexp 27411,0;
					else if (BaseLevel > 70 && BaseLevel < 76) getexp 70757,0;
					else if (BaseLevel > 75 && BaseLevel < 81) getexp 130246,0;
					else if (BaseLevel > 80 && BaseLevel < 86) getexp 150340,0;
					else if (BaseLevel > 85 && BaseLevel < 91) getexp 182052,0;
					else getexp 406786,0;
				}
				next;
				mes "[" +strcharinfo(0)+ "]";
				mes "โฮ้...?";
				mes "มันรู้สึกยอดเยี่ยมมากเลย~";
				mes "เหมือนกับว่าจิตใจของข้า";
				mes "ถูกชำระล้างไปด้วยเลย!";
				mes "ข้าตกลงตามสัญญา!";
				next;
				mes "[Annon]";
				mes "ขอบคุณ ขอบคุณมากจริงๆ";
				mes "ตอนนี้ข้าจะได้ไม่ต้องถูกผูกมัดกับ Shuda ไปอีกสักพักใหญ่";
				next;
				mes "[Annon]";
				mes "เอาล่ะ ข้าต้องขอตัว";
				mes "ข้าต้องรีบหนีไป";
				mes "ก่อนที่คนอื่นจะมาเจอ";
				mes "โชคดีนะ~!";
				close2;
				set $@annonactive,0;
				disablenpc "Haggard Man";
				end;
			} else {
				mes "[" +strcharinfo(0)+ "]";
				mes "ไม่มีทาง ไม่มีทาง";
				mes "เขาบาดเจ็บ";
				mes "หนักเกินกว่าจะรักษา";
				mes "ด้วยโพชั่นธรรมดา";
				mes "ที่ข้ามี ข้าต้องการ";
				mes "โพชั่นที่แรงกว่านี้...";
				next;
				mes "[" +strcharinfo(0)+ "]";
				mes "โอเค เจ้าอยู่ที่นี่";
				mes "สักพักนะ";
				mes "ข้าจะไปเอายา";
				mes "มาขจัดอาการบาดเจ็บ";
				mes "ของเจ้าให้เอง";
				next;
				mes "[Haggard Man]";
				mes "ไม่ ไม่ต้องหรอก...";
				mes "^666666*แค่ก แค่ก แค่ก*^000000";
				mes "เดี๋ยว อย่าผลัก";
				mes "ข้ากลับเข้าไปข้างในนะ!";
				mes "ว้ากกกกกกก~!";
				next;
				set $@annonactive,0;
				disablenpc "Haggard Man";
				mes "[" +strcharinfo(0)+ "]";
				mes "โอเค ได้เวลา";
				mes "ที่ข้าต้องสวมบทเป็นหมอแล้ว!";
				mes "ข้ามีเวลาไม่มาก!";
				mes "ภารกิจของข้าคือ: หาโพชั่น";
				mes "พลังทำลายล้างสูงมาให้";
				mes "เจ้าผู้น่าสงสารคนนี้~";
				emotion ET_BEST, playerattached();
				close;
			}
		case 2:
			mes "[Haggard Man]";
			mes "Annon...?";
			mes "ใครน่ะ?";
			mes "ข้าไม่เคยได้ยินชื่อนี้เลย!";
			mes "ข้าไม่รู้จักเขา เข้าใจไหม!";
			mes "หยุดรบกวนการฝึกของข้าได้แล้ว!";
			next;
			mes "[" +strcharinfo(0)+ "]";
			mes "เอ่อ...";
			mes "หืมม ดูเหมือนเจ้าไม่ควรจะมาตะโกนแบบนี้นะด้วยแผลแบบนั้น เจ้าแน่ใจนะว่าไม่ใช่ Annon?";
			next;
			mes "[Haggard Man]";
			mes "ข้าก็บอกแล้วไง!";
			mes "ข้าไม่เคย";
			mes "ได้ยินชื่อเขา!";
			mes "ปะ-ปล่อยข้าไว้คนเดียวเถอะ!";
			next;
			mes "[Haggard Man]";
			mes "ข้าต้อง";
			mes "หลบ Shuda ให้พ้น";
			mes "^666666*ร้องไห้*^000000";
			close2;
			set $@annonactive,0;
			disablenpc "Haggard Man";
			end;
		}
	} else {
		mes "[Haggard Man]";
		mes "หือ? เจ้าเป็นใคร?";
		mes "ที่นี่ไม่ใช่ที่ที่ปลอดภัยสำหรับนักท่องเที่ยวหรอกนะ! เจ้าควรออกไปทันที!";
		close2;
		set $@annonactive,0;
		disablenpc "Haggard Man";
		end;
	}

OnInit:
	set $@annonactive,0;
	disablenpc "Haggard Man";
	end;
}
		
ayothaya,253,99,3	script	Fisherman	843,{
	if (thai_find == 1) {
		mes "[Dannai]";
		mes "ที่นี่เป็นที่รู้กันดีว่า";
		mes "ชุกชุมไปด้วยปลา ปลาที่นี่มักจะกินทุกอย่างที่มันหาได้";
		mes "ดังนั้นมันจึงถูกจับได้ง่ายมาก";
		next;
		mes "[Dannai]";
		mes "พวกเรามีบริการให้เช่าเบ็ดตกปลา ทุกครั้งที่เจ้าตกปลา เจ้าจะต้องใช้ ^3355FF 1 Monster's Feed^000000 เพื่อใช้เป็นเหยื่อ และจ่าย";
		mes "ค่าเช่าเบ็ดจำนวน ^3355FF50 Zeny^000000";
		next;
		mes "[Dannai]";
		mes "เจ้าอยากจะ";
		mes "ลองดูไหมล่ะ?";
		next;
		switch(select("ตกลง:ไม่ล่ะ ขอบคุณ")) {
		case 1:
			if (countitem(528) && Zeny > 49) {
				mes "^3355FFเจ้าเหวี่ยงสายเบ็ด";
				mes "ของเจ้า";
				mes "ลงไปในน้ำ^000000";
				next;
				mes "...";
				next;
				mes "...";
				mes "......";
				next;
				mes "^3355FFเจ้าตกได้ Phen!^000000";
				next;
				delitem 528,1; //Monster's_Feed
				set Zeny, Zeny-50;
				set .@randfish,rand(1,100);
				if (.@randfish > 0 && .@randfish < 40) {
					mes "^3355FFในขณะที่กำลังปรุงอาหารจาก Phen เจ้า";
					mes "พบว่ามันกลืน Stone ลงไป^000000";
					close2;
					getitem 7049,1; //Stone
					end;
				} else if (.@randfish > 39 && .@randfish < 60) {
					mes "^3355FFภายในตัวของ Phen";
					mes "เจ้าพบ Red Potion^000000";
					close2;
					getitem 501,1; //Red_Potion
					end;
				} else if (.@randfish > 59 && .@randfish < 70) {
					mes "^3355FFภายในตัวของ Phen";
					mes "เจ้าพบ Yellow Potion^000000";
					close2;
					getitem 503,1; //Yellow_Potion
					end;
				} else if (.@randfish > 69 && .@randfish < 80) {
					mes "^3355FFไม่มีอะไรอยู่ข้างในเลย...";
					mes "แต่ตอนนี้เจ้าสามารถกิน Sushi ได้แล้ว!^000000";
					close2;
					getitem 551,1; //Shusi
					end;
				} else if (.@randfish > 79 && .@randfish < 90) {
					mes "^3355FFเจ้าพบ";
					mes "Flower Ring";
					mes "อยู่ข้างในตัว Phen^000000";
					close2;
					getitem 2612,1; //Flower_Ring
					end;
				} else if (.@randfish == 90) {
					mes "^3355FFเจ้าพบ";
					mes "Diamond Ring";
					mes "อยู่ข้างในตัว Phen^000000";
					close2;
					getitem 2613,1; //Diamond_Ring
					end;
				} else if (.@randfish > 90 && .@randfish < 101) {
					mes "^3355FFเจ้าพบแหวนวงหนึ่ง";
					mes "ที่มีชื่อสลักไว้";
					mes "ที่ด้านในวงแหวน^000000";
					next;
					mes "^3355FFเจ้าหยิบเอา";
					mes "แหวนหมั้นนั้นมา^000000";
					close2;
					set thai_find,2;
					changequest 12029,12030;
					getitem 7288,1; //Thai_Ring
					end;
				}
			} else {
				mes "[Dannai]";
				mes "จำไว้นะ เจ้าต้องมี ^3355FF1 Monster's Feed^000000 สำหรับใช้เป็นเหยื่อ และ ^3355FF50 zeny^000000 สำหรับเช่าเบ็ด";
				close;
			}
		case 2:
			mes "[Dannai]";
			mes "ไม่มีปัญหา";
			mes "กลับมาได้ทุกเมื่อที่";
			mes "เจ้าต้องการ การตกปลาช่วยให้";
			mes "จิตใจผ่อนคลายและทำให้เจ้า";
			mes "รู้สึกสงบ...";
			close;
		}
		close;
	} else if (thai_find < 1) {
		mes "[Dannai]";
		mes "ปลาที่นี่มักจะ";
		mes "กินทุกอย่างที่พวกมันหาเจอ";
		mes "เมื่อเจ้าผ่าท้องมัน เจ้ามักจะ";
		mes "เจอสิ่งของที่น่าสนใจอยู่ข้างใน";
		next;
		mes "[Dannai]";
		mes "ดังนั้นเมื่อเจ้ามีโอกาส";
		mes "ทำไมไม่ลองนั่งลง ผ่อนคลาย";
		mes "และเพลิดเพลินกับการตกปลาเงียบๆดูล่ะ?";
		close;
	} else {
		mes "[Dannai]";
		mes "ยินดีต้อนรับ ไม่เจอกันนานเลย!";
		mes "ดูเหมือนผู้คนจะรู้เรื่องที่เจ้าพบโชคลาภบางอย่างจากการตกปลาที่นี่ หลังจากข่าวแพร่ออกไป ข้าก็มีลูกค้า";
		mes "แวะเวียนมามากมาย";
		next;
		mes "[Dannai]";
		mes "ข้าเข้าใจว่าเจ้าต้องการ";
		mes "ปลา แต่ในตอนนี้";
		mes "ไม่มีที่ว่างสำหรับตกปลา";
		mes "ให้เจ้าเลยในขณะนี้";
		next;
		mes "[Dannai]";
		mes "ได้โปรดให้";
		mes "คนอื่นๆ ได้มี";
		mes "โอกาสค้นหาโชคลาภ";
		mes "ของพวกเขาเองบ้างเถอะ";
		close;
	}
}

ayo_dun01,255,62,0	script	AyoFootprint1	139,2,0,{
OnTouch_:
	if (thai_find == 4) {
		if (rand(1,3) < 2) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFเจ้าพบรอยเท้าที่มุ่งหน้าไปทางทิศ ^5C3317เหนือ^3355FF ดูเหมือนว่าใครบางคนกำลังรีบร้อนมาก เหมือนอย่างที่ชาวบ้านคนนั้นบอกไว้จริงๆ!^000000";
			set thai_find,5;
			changequest 12032,12033;
			close;
		}
	}
	end;
}

ayo_dun01,74,142,0	script	AyoFootprint2	139,2,0,{
OnTouch_:
	if (thai_find == 5) {
		if (rand(1,3) < 2) {
			emotion ET_THINK, playerattached();
			mes "^3355FFเจ้าพบรอยเท้าอีกชุดหนึ่ง ดูเหมือนว่าใครบางคนกำลังวิ่งหนีอะไรบางอย่าง รอยเท้าเหล่านี้มุ่งหน้าไปทางทิศ ^5C3317ตะวันออก^3355FF เจ้าควรจะตามพวกมันไปเพื่อดูว่า";
			mes "เจ้าจะพบอะไรบ้าง^000000";
			set thai_find,6;
			close;
		}
	}
	end;
}

ayo_dun01,244,256,2	script	AyoFootprint3	139,2,0,{
OnTouch_:
	if (thai_find == 6) {
		if (rand(1,3) < 2) {
			emotion ET_STARE_ABOUT, playerattached();
			mes "^3355FFรอยเท้าสิ้นสุดลงแถวๆ บริเวณนี้ ใครก็ตามที่วิ่งมาคงจะ";
			mes "ต้องเหนื่อยหอบและสะดุดเข้ากับอะไรบางอย่าง บางทีเจ้าของรอยเท้าอาจจะอยู่แถวนี้...^000000";
			set thai_find,7;
			close;
		}
	}
	end;
}

ayo_dun01,17,257,0	script	AyoFootprint4	139,2,0,{
OnTouch_:
	if (thai_find == 7) {
		if (rand(1,3) < 2) {
			emotion ET_AHA, playerattached();
			mes "^3355FFขณะที่เจ้าตามรอยเท้ามา";
			mes "เจ้ามาถึงที่ '^5C3317ทางเข้าของวิหาร^3355FF' ไม่ว่าคนๆ นี้จะเป็นใคร เขาเพิ่งจะวิ่งเข้าไปในชั้นที่สอง^000000";
			set thai_find,8;
			close;
		}
	}
	end;
}

ayo_dun02,145,120,0	script	AyoFootprint5	139,2,0,{
OnTouch_:
	if (thai_find == 8) {
		if (rand(1,3) < 2) {
			mes "^3355FFเจ้าพบรอยเท้าแบบเดียวกัน";
			mes "ในสถานที่แห่งนี้ มันดูค่อนข้าง";
			mes "อันตราย และเจ้าสงสัยว่าทำไม";
			mes "ถึงมีใครยอมมาที่นี่ รอยเท้า";
			mes "ยังคงมุ่งหน้าต่อไปทาง";
			mes "ทิศ ^5C3317เหนือ^3355FF^000000";
			set thai_find,9;
			close;
		}
	}
	end;
}

ayo_dun02,135,168,0	script	AyoFootprint6	139,0,2,{
OnTouch_:
	if (thai_find == 9) {
		if (rand(1,3) == 1) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFร่องรอยของรอยเท้า";
			mes "ยังคงมีต่อที่นี่ และดูเหมือน";
			mes "จะเป็นรอยที่เพิ่งเกิดขึ้นใหม่ๆ^000000";
			next;
			mes "^3355FFหืม...?";
			mes "มีบางอย่างอยู่ที่นี่...^000000";
			next;
			switch(select("เพิกเฉย:^5C3317ตรวจสอบ^000000")) {
			case 1:
				mes "["+strcharinfo(0)+"]";
				mes "ช่างเถอะ มันคงไม่มีอะไรหรอก แต่ตอนนี้ฉันควรไปทางไหนดีนะ?";
				set thai_find,10;
				close;
			case 2:
				set .@randayo,rand(1,10);
				if (.@randayo < 2) {
					mes "["+strcharinfo(0)+"]";
					mes "เอ๊ะ นี่มันห่อ White Potion นี่นา ใครก็ตามที่เป็นเจ้าของคงทำตกไว้โดยบังเอิญ หืม ฉันน่าจะเก็บเอาไว้ใช้ภายหลังได้นะ...";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "เดี๋ยวก่อน...";
					mes "มีรอยเท้า";
					mes "มุ่งหน้าไปทาง";
					mes "ทิศ ^5C3317ตะวันออก^000000 อีก";
					mes "ฉันควรจะเข้าไปดู";
					mes "หน่อยแล้ว";
					set thai_find,10;
					getitem 504,10; //White_Potion
					close;
				} else if (.@randayo > 2 && .@randayo < 5) {
					mes "["+strcharinfo(0)+"]";
					mes "เอ๊ะ นี่มันห่อ Yellow Potion นี่นา ใครก็ตามที่เป็นเจ้าของคงทำตกไว้โดยบังเอิญ หืม ฉันน่าจะเก็บเอาไว้ใช้ภายหลังได้นะ...";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "เดี๋ยวก่อน...";
					mes "มีรอยเท้า";
					mes "มุ่งหน้าไปทาง";
					mes "ทิศ ^5C3317ตะวันออก^000000 อีก";
					mes "ฉันควรจะเข้าไปดู";
					mes "หน่อยแล้ว";
					set thai_find,10;
					getitem 503,10; //Yellow potion
					close;
				} else {
					mes "[Rat]";
					mes "^FF0000จี๊ด!";
					mes "^FF0000จี๊ด จี๊ด!^000000";
					emotion ET_HUK, playerattached();
					next;
					mes "["+strcharinfo(0)+"]";
					mes "^8C1717ว้าว^000000!";
					mes "นั่นอะไรน่ะ?!";
					next;
					mes "^3355FFมันก็แค่หนูน่ะ";
					mes "รู้สึกตลกตัวเองชะมัดที่ดันไปกลัวหนูตัวเล็กๆ แค่ตัวเดียวว่าไหม?^000000";
					next;
					mes "^3355FFเจ้าพบร่องรอยของรอยเท้า";
					mes "อีกชุดที่มุ่งหน้าไป";
					mes "ทางทิศ ^5C3317ตะวันออก^3355FF^000000";
					set thai_find,10;
					close;
				}
			}
			close;
		}
	}
	end;
}

ayo_dun02,77,213,0	script	AyoFootprint7	139,1,1,{
OnTouch_:
	if (thai_find == 10) {
		if (rand(1,3) < 2) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFเขาอยู่ที่ไหนกันนะ...?";
			mes "ดูเหมือนเขาจะเข้าไป";
			mes "ลึกในดันเจี้ยนแล้ว";
			mes "แค่ตามหาผู้ชายคนนี้";
			mes "ก็น่าเหนื่อยชะมัด...^000000";
			set thai_find,11;
			close;
		}
	}
	end;
}

ayo_dun02,86,254,0	script	AyoFootprint8	139,1,1,{
OnTouch_:
	if (thai_find == 11) {
		if (rand(1,3) < 2) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFเจ้าพบร่องรอยของเลือด ใครก็ตามที่วิ่งไปทางนี้อาจจะได้รับบาดเจ็บ รอยเลือดมุ่งหน้าไปทางทิศ ^5C3317เหนือ^3355FF^000000";
			changequest 12032,12033;
			set thai_find,12;
			close;
		}
	}
	end;
}

ayothaya,193,171,3	script	Old Man#02	842,{
	if (thai_find == 3) {
		mes "[Tham]";
		mes "แปลกจัง...";
		mes "ทำไมผู้ชายคนนั้น";
		mes "ถึงวิ่งเข้าไปใน";
		mes "วิหารแบบนั้นล่ะ?";
		mes "มันน่ากังวล";
		mes "จริงๆ เลยนะ...";
		next;
		mes "[Tham]";
		emotion ET_SURPRISE;
		mes "โอ้ เจ้ามาจาก";
		mes "Midgard ใช่ไหม?";
		mes "สวัสดีนะ~";
		next;
		mes "[Tham]";
		mes "อา...";
		mes "เจ้าได้ยินข้าบ่นกับตัวเองเหรอ? คือข้ามองไม่ทันหรอกว่าเขาเป็นใคร แต่มีผู้ชายคนหนึ่งวิ่งเข้าไปในป่าที่มุ่งไปสู่วิหาร เขาดูหวาดกลัวอะไรบางอย่างมากเลย";
		next;
		mes "[Tham]";
		mes "เขาโผล่มาจากไหนก็ไม่รู้ ทำเอาข้าตกใจแทบตาย เจ้าคิดว่าเขาจะเป็นอาชญากรหรือเปล่า?";
		next;
		mes "[Tham]";
		emotion ET_AHA;
		mes "อา! ข้าคิดว่า ในความกลัวอย่างสุดขีดนั้น เขาตะโกนอะไรบางอย่างเกี่ยวกับ '^FF0000Shuda^000000' หรืออะไรสักอย่างนี่แหละ แต่มันจะหมายความว่าอะไรกันนะ?";
		next;
		mes "[Tham]";
		mes "ช่างเถอะ อย่าไปกังวลเลย ชาวบ้านของเราไม่ได้เป็นแบบนั้นกันทุกคนหรอกนะ ยังไงก็ขอให้เจ้าสนุกกับการเดินทางใน Ayothaya นะ";
		set thai_find,4;
		changequest 12031,12032;
		close;
	} else {
		set .@randayo,rand(1,7);
		if (.@randayo < 5) {
			mes "[Tham]";
			mes "โอ้ สวัสดี~";
			mes "ดูจากเสื้อผ้าของเจ้า ข้าบอกได้เลยว่าเจ้าต้องมาจาก Midgard แน่ๆ";
			next;
			mes "[Tham]";
			mes "ช่วงนี้ดูเหมือนว่า";
			mes "หมู่บ้านของเราจะมี";
			mes "นักท่องเที่ยวมาเยือนมากขึ้น และข้าเห็นว่า";
			mes "หมู่บ้านของเราได้รับอิทธิพล";
			mes "ในทางที่ดีขึ้นด้วยนะ";
			next;
			mes "[Tham]";
			mes "อา...";
			mes "ในเมื่อเจ้าอยู่ที่นี่แล้ว";
			mes "ทำไมไม่ลองชิม";
			mes "อาหารพื้นเมืองของ Ayothaya";
			mes "ดูล่ะ?";
			next;
			mes "[Tham]";
			mes "พวกเราขึ้นชื่อเรื่อง";
			mes "อาหารทะเลรสเผ็ดและหวาน";
			mes "และมีร้านอาหารดีๆ";
			mes "อยู่แถวนี้หลายแห่งเลยล่ะ";
			next;
			mes "[Tham]";
			mes "ยังไงข้าก็มั่นใจว่าเจ้าจะชอบมัน ขอให้เจ้ามาเยี่ยมชม Ayothaya บ่อยๆ เท่าที่เจ้าจะมาได้นะ~";
			close;
		} else if (.@randayo > 4 && .@randayo < 7) {
			mes "[Tham]";
			mes "อา~";
			mes "ข้ากำลังอยากกินอาหารฝีมือของคุณ Mali the Spicy อยู่พอดี โดยเฉพาะ 'ต้มยำกุ้ง' ของนาง";
			next;
			mes "[Tham]";
			mes "ข้าคิดว่านั่นเป็น";
			mes "อาหารที่อร่อยที่สุด";
			mes "ในโลกเลยล่ะ!";
			close;
		} else {
			mes "[Tham]";
			mes "โอ้ สวัสดี~";
			mes "ดูจากเสื้อผ้าของเจ้า ข้าบอกได้เลยว่าเจ้าต้องมาจาก Midgard แน่ๆ";
			next;
			mes "[Tham]";
			mes "ข้าหวังว่าเจ้าจะสนุกกับการพักที่นี่ และขอให้เจ้ามาเยี่ยมชม Ayothaya บ่อยๆ เท่าที่เจ้าจะมาได้นะ~";
			close;
		}
	}
}

// Ayothaya Dungeon Quest :: ayo_tiger
//============================================================
ayothaya,83,132,0	script	Dusit#thai	843,{
	if (ayodunquest == 0) {
		mes "[Dusit]";
		mes "โอ้...!";
		mes "เจ้าต้องเป็นนักเดินทางแน่ๆ";
		mes "ข้ามีเรื่องเล่าเรื่องหนึ่งที่เจ้าอาจจะอยากฟัง ถ้าเจ้าสนใจจะรับฟังนะ";
		next;
		if (select("มันคือเรื่องอะไรเหรอ?:ข้าพอจะรู้เรื่องนั้นอยู่แล้ว") == 1) {
			mes "[Dusit]";
			mes "Ayothaya มีประวัติศาสตร์อันยาวนาน บางทีตอนนี้มันอาจจะดูสงบสุข แต่ในอดีตหมู่บ้านของเราเคยถูกคุกคามโดยสัตว์ร้ายที่น่ากลัว";
			next;
			mes "[Dusit]";
			mes "สัตว์ร้ายตัวนี้เป็นที่รู้จักกันในชื่อ";
			mes "^660000เสือสมิง^000000 ซึ่งเคย";
			mes "อาศัยอยู่ลึกเข้าไปในโบราณสถานเก่าแก่";
			next;
			mes "[Dusit]";
			mes "เสือสมิงนั้นเสพติด";
			mes "รสชาติของเนื้อคน มันเป็นที่รู้กันว่ามันสามารถกลายร่างเป็นคนที่มันกินเข้าไป เพื่อล่อหลอกครอบครัวและเพื่อนของคนๆ นั้นออกมา";
			next;
			mes "[Dusit]";
			mes "คนในพื้นที่ยังคงหวาดกลัวว่าเจ้าสัตว์ร้ายนั้นอาจจะกลายร่างเป็นคนที่พวกเขารู้จัก และแอบเข้าไปในบ้านเพื่อกินพวกเขา";
			next;
			mes "[Dusit]";
			mes "อย่างไรก็ตาม มีวิธีหนึ่งที่เจ้าจะสามารถแยกแยะเสือสมิงออกจากมนุษย์ได้ เนื่องจากมันเป็นเสือ มันจึงไม่สามารถทำบางอย่างที่";
			mes "มนุษย์ได้รับการสั่งสอนมาได้";
			mes "เช่น การจุดไม้ขีดไฟ";
			next;
			mes "[Dusit]";
			mes "ดังนั้น ถ้าบังเอิญเจ้าเห็น";
			mes "เพื่อนที่มีท่าทางแปลกไป";
			mes "กะทันหัน ให้ลองถามเขาให้";
			mes "จุดไม้ขีดดู เพื่อให้แน่ใจว่าเขา";
			mes "ไม่ใช่เสือสมิง";
			next;
			mes "[Dusit]";
			mes "แต่ว่า...";
			mes "ข้าก็ไม่แน่ใจว่าวิธีนี้มันจะได้ผลจริงหรือเปล่านะ ข้าว่าทางที่ดีที่สุดคือหนีเอาชีวิตรอดถ้าเจ้าสงสัยว่าใครบางคนคือ";
			mes "เสือสมิง";
			next;
			mes "[Dusit]";
			mes "เจ้าเชื่อไหมล่ะ";
			mes "ว่ามีเสือกินคน";
			mes "อยู่จริง?";
			next;
			if (select("ก็ไม่เชิงนะ:เชื่อสิ ข้าว่ามันน่ากลัวมาก!") == 1) {
				mes "[Dusit]";
				mes "อืม...";
				mes "ช่างเถอะ ปู่ผู้ล่วงลับของข้าเคยเล่าเรื่องเสือสมิงให้ฟังตอนที่";
				mes "ข้ายังเป็นเด็ก แน่นอนว่า";
				mes "ข้าเองก็ยังไม่เคยเห็นมันหรอก";
				next;
				mes "[Dusit]";
				mes "สิ่งที่ใกล้เคียงที่สุดที่ข้าเคยเห็นคือแมวสีทองที่มีกระดิ่ง ดูเหมือนมันจะไม่กินคนหรอกนะ";
				close;
			}
			mes "[Dusit]";
			mes "จงหนีเอาชีวิตรอดซะถ้าเจ้าได้เผชิญหน้ากับเสือสมิง!";
			mes "มันจะกินเจ้าทั้งเป็น!";
			next;
			mes "[Dusit]";
			mes "ถ้าเจ้าสนใจอยากจะค้นหา";
			mes "เรื่องราวนี้เพิ่มเติม ข้าสามารถแนะนำให้เจ้ารู้จักกับคนๆ หนึ่งที่";
			mes "รู้จักสัตว์ร้ายตัวนั้นดีกว่า";
			mes "ใครๆ เลยล่ะ";
			next;
			mes "[Dusit]";
			mes "ทำไมเจ้าไม่ลองไปคุยกับ Boonthom ดูล่ะ? เขาเป็นคนค่อนข้างแปลก แต่เขามีบางอย่างที่...";
			next;
			mes "[Dusit]";
			mes "...";
			mes "......";
			next;
			mes "[Dusit]";
			mes "อา เอาเถอะ เดี๋ยวเจ้าก็เห็นเองว่าข้าพูดถึงอะไร ข้าหวังว่าเขาจะช่วยให้เจ้าได้เรียนรู้เกี่ยวกับสัตว์ร้ายตัวนั้นมากขึ้นนะ";
			set ayodunquest,1;
			setquest 12035;
			close;
		}
		mes "[Dusit]";
		mes "เจ้ารู้แล้วงั้นเหรอ?";
		mes "งั้นก็ระวังตัวด้วยล่ะ!";
		mes "มันจะกินเจ้าทั้งเป็น!";
		close;
	} else if (ayodunquest > 0) {
		mes "[Dusit]";
		mes "เจ้าเป็นอย่างไรบ้าง?";
		mes "ถ้าเจ้าสนใจ";
		mes "เรื่องเสือสมิง";
		mes "ได้โปรดไปคุยกับ Boonthom เถอะ";
		next;
		mes "[Dusit]";
		mes "แต่ระวังตัวด้วยนะ!";
		mes "เสือสมิงอาจจะอยู่";
		mes "ใกล้ตัวเจ้ามากกว่าที่คิด!";
		close;
	}
}

ayo_in01,181,193,4	script	Shaman#thai	840,{

	// For both parts of the quest:
	// - Pre-Renewal requires Needle Packet and Spool.
	// - Renewal requires Fabric and Nine Tails.
	if (checkre(0))
		setarray .@items[0],7213,7217; //Needle_Pouch, Spool
	else
		setarray .@items[0],1059,1022; //Transparent_Cloth, Fox_Tail

	if (ayodunquest == 1) {
		mes "[Boonthom]";
		mes "เจ้า...!";
		mes "นี่มัน....!";
		mes "โอ้วววววววววว!";
		next;
		mes "[Boonthom]";
		mes "โอ๊ะ...?";
		next;
		mes "[Boonthom]";
		mes "...";
		next;
		mes "[Boonthom]";
		mes "...";
		mes "......";
		next;
		mes "[Boonthom]";
		mes "...";
		mes "......";
		mes ".........";
		next;
		mes "[Boonthom]";
		mes "ช่างมันเถอะ";
		mes "เหอะ! ข้าลืมสิ่งที่";
		mes "ข้ากำลังคิดอยู่ไปซะแล้ว";
		next;
		mes "[Boonthom]";
		mes "สรุปคือเจ้ามาหาข้าเพื่อขอความช่วยเหลือสินะ? ข้าเห็นความกลัวในดวงตาของเจ้า ให้ข้าได้จ้องมองเข้าไปในจิตวิญญาณของเจ้า และดูว่าความกลัวนั้นคืออะไรด้วยตัวเองเถอะ หืมมม...";
		next;
		mes "[Boonthom]";
		mes "...";
		next;
		mes "[Boonthom]";
		mes "...";
		mes "......";
		next;
		mes "[Boonthom]";
		mes "...";
		mes "......";
		mes ".........";
		next;
		mes "[Boonthom]";
		mes "อะไรกัน...!";
		mes "ข้าเห็นว่าเจ้าหวาดกลัวเสือสมิง! ข้าไม่ได้ยินข่าวคราวของเจ้าสัตว์ร้ายตัวนั้นมานานมากแล้วนะ...";
		next;
		mes "[Boonthom]";
		mes "เมื่อนานมาแล้ว";
		mes "เสือสมิงนั้น";
		mes "เคยออกอาละวาดในหมู่บ้าน";
		mes "และกินผู้คนผู้บริสุทธิ์";
		next;
		mes "[Boonthom]";
		mes "อย่างไรก็ตาม ความสยดสยอง";
		mes "ของมันก็สิ้นสุดลงเมื่อมัน";
		mes "ถูกจับตัวได้ในที่สุด ข้าเป็นหนึ่งในผู้ที่มีส่วนเกี่ยวข้องในการจับเสือสมิงตัวนั้น";
		next;
		mes "[Boonthom]";
		mes "ข้ายืนกรานที่จะฆ่ามันซะ แต่คนอื่นๆ กลับหวาดกลัวเกินกว่าจะทำแบบนั้น แทนที่จะฆ่า เสือสมิง";
		mes "จึงถูกขังไว้ในถ้ำภายในโบราณสถานเก่าแก่";
		next;
		mes "[Boonthom]";
		mes "ข้าเชื่อว่านั่นก็น่าจะอย่างน้อย 10 ปีมาแล้ว บางทีมันอาจจะตายไปแล้วก็ได้ แต่ว่า ไม่มีใคร";
		mes "รู้ว่าเสือตัวนั้นมีพลังมาก";
		mes "แค่ไหน มันอาจจะยังขยับได้อยู่";
		next;
		mes "[Boonthom]";
		mes "ข้าคิดว่าเราไม่จำเป็นต้องกังวลเรื่องนั้นหรอก มันถูกขังอยู่และมันจะอยู่ที่นั่นตลอดไป";
		next;
		switch(select("ข้าอยากสำรวจโบราณสถานเก่าแก่:ถูกขังอยู่เหรอ! ดีเลย! ข้าไม่กลัวหรอก!")) {
		case 1:
			mes "[Boonthom]";
			mes "เจ้า...";
			mes "เจ้าช่างแปลกคน";
			mes "เจ้าอยากสำรวจโบราณสถานงั้นรึ นักเดินทางทุกคนชอบเอาชีวิตไปเสี่ยงแบบนั้นกันหมดเลยหรือไง?";
			next;
			mes "[Boonthom]";
			mes "มันเป็นการตัดสินใจของเจ้า แต่จำไว้ว่ามันอันตรายมากที่จะผ่านเส้นทางไปยังใจกลางของอาคาร โดยปกติข้าจะสั่งห้ามเข้าไป";
			next;
			mes "[Boonthom]";
			mes "แต่ถ้าเจ้าต้องการจะเข้าไปจริงๆ";
			mes "ข้ามีคำแนะนำที่มีประโยชน์จะบอก";
			mes "เจ้าอยากฟังไหมล่ะ?";
			next;
			if (select("ถ้ามันอันตรายขนาดนั้น ข้าไม่ลองดีกว่า:แน่นอน!") == 1) {
				mes "[Boonthom]";
				mes "เจ้าตัดสินใจได้ดีแล้ว";
				mes "เจ้าควรจะระวังตัวให้ดีถ้า";
				mes "เจ้ายังอยากมีชีวิตอยู่ต่อไป";
				close;
			}
			mes "[Boonthom]";
			mes "อืม โอเค";
			mes "ในเมื่อเจ้า";
			mes "สนใจขนาดนั้น...";
			next;
			mes "[Boonthom]";
			mes "หากเจ้าปรารถนาจะเข้าไปในอาคารโบราณ เจ้าต้องมีสิ่งป้องกันวิญญาณร้ายและความชั่วร้าย";
			next;
			if (Class == Job_Priest || Class == Job_Acolyte || Class == Job_Crusader) {
				mes "[Boonthom]";
				mes "แม้ว่าเจ้าจะสามารถ";
				mes "ใช้พลังแห่ง";
				mes "ความศักดิ์สิทธิ์ได้ แต่เจ้าก็ยังต้องการความปลอดภัยทางจิตวิญญาณแห่งดินแดนของเราอยู่ดี";
			} else {
				mes "[Boonthom]";
				mes "มีวิญญาณและ";
				mes "ปีศาจมากมายภายในถ้ำ และพวกมันจะโจมตีคนที่ไม่ครอบครองพลังศักดิ์สิทธิ์... อย่างเช่นเจ้า";
			}
			next;
			mes "[Boonthom]";
			mes "ข้าสามารถสร้างวัตถุ";
			mes "ที่บรรจุพลังศักดิ์สิทธิ์นี้ให้เจ้าได้ถ้าเจ้าต้องการ ในการสร้างมัน ข้าต้องการวัตถุดิบบางอย่าง";
			next;
			mes "[Boonthom]";
			mes "^3366991 "+getitemname(.@items[0])+"^000000,";
			mes "^3366991 "+getitemname(.@items[1])+"^000000,";
			mes "^3366991 Solid Husk^000000 และ";
			mes "^3366991 Holy Water^000000...";
			mes "นั่นคือทั้งหมดที่ข้าต้องการ";
			next;
			mes "[Boonthom]";
			mes "กรุณากลับมา";
			mes "เมื่อเจ้าเตรียม";
			mes "วัตถุดิบทุกอย่างครบแล้ว";
			set ayodunquest,2;
			changequest 12035,12036;
			close;
		}
		mes "[Boonthom]";
		mes "หืมมม...";
		mes "ไม่กลัวมัน";
		mes "เลยงั้นรึ?";
		next;
		mes "[Boonthom]";
		mes "............";
		mes "............";
		close;
	} else if (ayodunquest == 2) {
		if (countitem(.@items[0]) > 0 && countitem(.@items[1]) > 0 && countitem(7190) > 0 && countitem(523) > 0) {
			mes "[Boonthom]";
			mes "ยอดเยี่ยมมาก!";
			mes "ตอนนี้เจ้านำทุกอย่าง";
			mes "ที่ข้าต้องการมาครบแล้ว ให้ข้าสร้าง";
			mes "สิ่งนั้นให้เจ้าตามที่สัญญาไว้เถอะ...";
			next;
			mes "[Boonthom]";
			mes "โออออมมมมม...";
			next;
			mes "[Boonthom]";
			mes "หืมมมมม...";
			mes "หืมมมมมมมมม";
			next;
			mes "[Boonthom]";
			mes "พ้วง พ้วง...";
			next;
			mes "[Boonthom]";
			mes "นี่ไงล่ะ";
			mes "รับด้ายศักดิ์สิทธิ์นี้ไปซะ";
			mes "ด้วยสิ่งนี้ เจ้าจะสามารถเข้าไปในโบราณสถานได้โดยไม่ต้องกังวลมากนัก";
			delitem .@items[0],1;
			delitem .@items[1],1;
			delitem 7190,1; //Solid_Peeling
			delitem 523,1; //Holy_Water
			set ayodunquest,3;
			changequest 12036,12037;
			getitem 7285,1; //Thread_Skein
			next;
			mes "[Boonthom]";
			mes "แม้ว่าเจ้าจะทำมันหาย ก็ไม่ต้องกังวลไป แค่นำวัตถุดิบมาให้ข้า และ";
			mes "ข้าจะสร้างอันใหม่ให้เจ้าเอง";
			next;
			mes "[Boonthom]";
			mes "อาคารโบราณนี้ประกอบ";
			mes "ไปด้วย 2 ชั้น ก่อนที่เจ้าจะเข้าสู่ชั้นที่ 2 ใต้ดิน กรุณากลับมาหาข้าเพื่อให้ข้าได้บอกวิธีเข้าสู่สถานที่แห่งนั้นแก่เจ้า";
			close;
		}
		mes "[Boonthom]";
		mes "เจ้าลืมไปแล้วหรือว่าต้องใช้";
		mes "วัตถุดิบอะไรบ้างในการสร้างวัตถุแห่งพลังศักดิ์สิทธิ์ของข้า? คราวนี้กรุณาฟัง";
		mes "อย่างตั้งใจล่ะ";
		next;
		mes "[Boonthom]";
		mes "^3366991 "+getitemname(.@items[0])+"^000000,";
		mes "^3366991 "+getitemname(.@items[1])+"^000000,";
		mes "^3366991 Solid Husk^000000 และ";
		mes "^3366991 Holy Water^000000";
		next;
		mes "[Boonthom]";
		mes "แล้วเจอกัน";
		close;
	} else if (ayodunquest == 3) {
		if (countitem(.@items[0]) > 0 && countitem(.@items[1]) > 0 && countitem(7190) > 0 && countitem(523) > 0) {
			mes "[Boonthom]";
			mes "ยอดเยี่ยมมาก! ตอนนี้เจ้านำทุกอย่างที่ข้าต้องการมาครบแล้ว ให้ข้าสร้างสิ่งนั้นให้เจ้าตามที่สัญญาไว้เถอะ...";
			next;
			mes "[Boonthom]";
			mes "โออออมมมมม...";
			next;
			mes "[Boonthom]";
			mes "หืมมมมม...";
			mes "หืมมมมมมมมม";
			next;
			mes "[Boonthom]";
			mes "พ้วง พ้วง...";
			next;
			mes "[Boonthom]";
			mes "นี่ไงล่ะ";
			mes "รับด้ายศักดิ์สิทธิ์นี้ไปซะ";
			mes "ด้วยสิ่งนี้ เจ้าจะสามารถเข้าไปในโบราณสถานได้โดยไม่ต้องกังวลมากนัก";
			delitem .@items[0],1;
			delitem .@items[1],1;
			delitem 7190,1; //Solid_Peeling
			delitem 523,1; //Holy_Water
			getitem 7285,1; //Thread_Skein
			next;
			mes "[Boonthom]";
			mes "แม้ว่าเจ้าจะทำมันหาย ก็ไม่ต้องกังวลไป แค่นำวัตถุดิบมาให้ข้า ข้าสามารถสร้างอันใหม่ให้เจ้าได้";
			close;
		}
		mes "[Boonthom]";
		mes "ถ้าเจ้าทำด้ายศักดิ์สิทธิ์หาย ก็ไม่ต้องกังวล ข้าสร้างใหม่ให้ได้ แค่รวบรวมไอเทมต่อไปนี้มา";
		next;
		mes "[Boonthom]";
		mes "^3366991 "+getitemname(.@items[0])+"^000000,";
		mes "^3366991 "+getitemname(.@items[1])+"^000000,";
		mes "^3366991 Solid Husk^000000 และ";
		mes "^3366991 Holy Water^000000";
		next;
		mes "[Boonthom]";
		mes "แล้วเจอกัน";
		close;
	} else if (ayodunquest > 3 && ayodunquest < 9) {
		mes "[Boonthom]";
		mes "ในที่สุดเจ้าก็ได้เริ่มตามหาเจ้าเสือตัวนั้นแล้ว ข้าหวังว่าเจ้าจะปลอดภัยจากอันตรายนะ";
		next;
		mes "[Boonthom]";
		mes "อย่าได้เกรงใจที่จะขอ";
		mes "ความช่วยเหลือจากข้าได้ทุกเมื่อ";
		close;
	} else if (ayodunquest == 9) {
		mes "["+strcharinfo(0)+"]";
		mes "ข้าคัดลอกข้อความนี้มา";
		mes "ขณะที่กำลังสำรวจ";
		mes "ดันเจี้ยน ท่านช่วย";
		mes "ดูนี่หน่อยได้ไหม?";
		next;
		mes "[Stone Slate]";
		mes "^333333ห้ามเข้าสู่";
		mes "ชั้นใต้ดินที่ 2";
		mes "...อันตราย... เจ้าจะ...";
		mes "...เผชิญหน้ากับ... เสือ...^000000";
		next;
		mes "[Stone Slate]";
		mes "^333333เจ้าต้องฆ่า... เพื่อที่จะทำ... ต้อง... วิธีการไป... ชั้นใต้ดินที่ 2...";
		mes "เจ้าต้องมี... พลังศักดิ์สิทธิ์เพิ่มขึ้น... เพื่อที่จะ... มิเช่นนั้น มันจะเป็นไปไม่ได้...^000000";
		next;
		mes "[Boonthom]";
		mes "นั่นถูกเขียนโดยหนึ่งในสหายของข้าที่เข้าไปในโบราณสถาน";
		mes "เพราะเขาเสียใจที่ปล่อยให้เสือตัวนั้นมีชีวิตอยู่ เผื่อว่ามันจะกลับมาอีก";
		next;
		mes "[Boonthom]";
		mes "หากเจ้าสามารถหาโน้ตนี้พบ แสดงว่าเจ้าต้องมีความมุ่งมั่นอย่างแรงกล้าที่จะผจญภัยผ่านโบราณสถาน";
		next;
		mes "[Boonthom]";
		mes "อย่างที่เจ้าคาดเดาได้จาก";
		mes "โน้ต ชั้นใต้ดินที่ 2 นั้นอันตรายมาก เจ้าจะต้องใช้ด้ายศักดิ์สิทธิ์ที่มีการปกป้องศักดิ์สิทธิ์ที่แข็งแกร่งยิ่งขึ้น";
		next;
		mes "[Boonthom]";
		mes "เอาล่ะ ไปรวบรวมวัตถุดิบบางอย่างมา เพื่อที่ข้าจะได้สร้างด้ายศักดิ์สิทธิ์ที่ทรงพลังยิ่งขึ้นให้กับเจ้า";
		next;
		mes "[Boonthom]";
		mes "^3366992 Holy Water^000000,";
		mes "^3366991 Yggdrasil Leaf^000000,";
		mes "^3366992 "+getitemname(.@items[0])+"^000000 และ";
		mes "^3366992 "+getitemname(.@items[1])+"^000000";
		set ayodunquest,10;
		changequest 12037,12038;
		next;
		mes "[Boonthom]";
		mes "กลับมาหาข้า";
		mes "เมื่อเจ้าได้";
		mes "รวบรวมทุกอย่างครบแล้ว";
		close;
	} else if (ayodunquest == 10) {
		if (countitem(523) > 1 && countitem(610) > 0 && countitem(.@items[0]) > 1 && countitem(.@items[1]) > 1) {
			mes "[Boonthom]";
			mes "หืมม~";
			mes "เจ้านำมา";
			mes "ครบทุกอย่างแล้ว ช่างเป็น";
			mes "คนที่กระตือรือร้นจริงๆ";
			mes "ใช่ไหมล่ะ?";
			next;
			mes "[Boonthom]";
			mes "โออออมมมมม...";
			next;
			mes "[Boonthom]";
			mes "หืมมมมม...";
			mes "หืมมมมมมมมม";
			next;
			mes "[Boonthom]";
			mes "พ้วง พ้วง...";
			next;
			mes "[Boonthom]";
			mes "นี่ไงล่ะ";
			mes "รับด้ายศักดิ์สิทธิ์นี้ไปซะ";
			mes "ด้วยสิ่งนี้ เจ้าจะสามารถเข้าไปในโบราณสถานได้โดยไม่ต้องกังวลมากนัก";
			delitem .@items[0],2;
			delitem .@items[1],2;
			delitem 610,1; //Leaf_Of_Yggdrasil
			delitem 523,2; //Holy_Water
			set ayodunquest,11;
			getitem 7287,1; //Thread_Skein_
			changequest 12038,12039;
			next;
			mes "[Boonthom]";
			mes "แม้ว่าเจ้าจะทำมันหาย ก็ไม่ต้องกังวลไป แค่นำวัตถุดิบมาให้ข้า และ";
			mes "ข้าจะสร้างอันใหม่ให้เจ้าเอง";
			close;
		}
		mes "[Boonthom]";
		mes "หืมม...?";
		mes "ข้าไม่ได้บอกเจ้า";
		mes "หรือว่าข้าต้องการอะไรเพื่อสร้าง";
		mes "ด้ายศักดิ์สิทธิ์ที่";
		mes "ทรงพลังยิ่งกว่าเดิม?";
		next;
		mes "[Boonthom]";
		mes "^3366992 Holy Water^000000,";
		mes "^3366991 Yggdrasil Leaf^000000,";
		mes "^3366992 "+getitemname(.@items[0])+"^000000 และ";
		mes "^3366992 "+getitemname(.@items[1])+"^000000";
		next;
		mes "[Boonthom]";
		mes "กลับมาหาข้า";
		mes "พร้อมกับไอเทมพวกนั้น";
		mes "แล้วข้าจะคราฟต์";
		mes "ด้ายศักดิ์สิทธิ์ที่ทรงพลัง";
		mes "มากขึ้นให้แก่เจ้า";
		close;
	} else if (ayodunquest == 11) {
		if (countitem(7287) > 0) {
			mes "[Boonthom]";
			mes "ด้ายเหล่านั้น";
			mes "จะปกป้องเจ้า";
			mes "จากอันตรายทางวิญญาณ";
			mes "แต่เจ้าต้องปกป้องตัวเอง";
			mes "จากการโจมตีของมอนสเตอร์";
			mes "ด้วยนะ เข้าใจไหม?";
			close;
		} else if (countitem(523) > 1 && countitem(610) > 0 && countitem(.@items[0]) > 1 && countitem(.@items[1]) > 1 && countitem(7287) == 0) {
			mes "[Boonthom]";
			mes "โออออมมมมม...";
			next;
			mes "[Boonthom]";
			mes "หืมมมมม...";
			mes "หืมมมมมมมมม";
			next;
			mes "[Boonthom]";
			mes "พ้วง พ้วง...";
			next;
			mes "[Boonthom]";
			mes "นี่ไงล่ะ";
			mes "รับด้ายศักดิ์สิทธิ์นี้ไปซะ";
			mes "ด้วยสิ่งนี้ เจ้าจะสามารถเข้าไปในโบราณสถานได้โดยไม่ต้องกังวลมากนัก";
			delitem .@items[0],2;
			delitem .@items[1],2;
			delitem 610,1; //Leaf_Of_Yggdrasil
			delitem 523,2; //Holy_Water
			getitem 7287,1; //Thread_Skein_
			close;
		}
		mes "[Boonthom]";
		mes "หืมม...?";
		mes "ข้าไม่ได้บอกเจ้า";
		mes "หรือว่าข้าต้องการอะไรเพื่อสร้าง";
		mes "ด้ายศักดิ์สิทธิ์ที่";
		mes "ทรงพลังยิ่งกว่าเดิม?";
		next;
		mes "[Boonthom]";
		mes "^3366992 Holy Water^000000,";
		mes "^3366991 Yggdrasil Leaf^000000,";
		mes "^3366992 "+getitemname(.@items[0])+"^000000 และ";
		mes "^3366992 "+getitemname(.@items[1])+"^000000";
		close;
	} else if (ayodunquest == 12) {
		mes "[Boonthom]";
		mes "ฮะ...!";
		mes "เจ้าต้องได้เห็นอะไรบางอย่างแน่ๆ";
		mes "ข้าบอกได้จากแววตาที่หม่นหมองของเจ้า และเงาที่ทาบทับจิตวิญญาณของเจ้า";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าพบ";
		mes "จดหมายเก่าฉบับนี้";
		mes "ในโบราณสถาน";
		next;
		mes "[Old Letter]";
		mes "^333333ถึง Boonthom... ข้ามันโง่เอง... มีเสือสมิ'... ผู้ที่ ....อัฐิของข้า... โปรดนำ... ไปให้ Boonthom...";
		next;
		mes "[Boonthom]";
		mes "ข้าเข้าใจแล้ว...";
		mes "โอ้ เพื่อน";
		mes "เก่าของข้า...";
		next;
		mes "[Boonthom]";
		mes "ขอบใจที่เจ้าลำบากจัดการให้";
		mes "เพื่อนของข้าทิ้งกล่องนั้นไว้ให้ข้า";
		mes "แต่ข้าเดาว่าเขาคงอยากจะมอบกล่องนั้นให้แก่คนที่พบอัฐิของเขามากกว่า";
		next;
		mes "[Boonthom]";
		mes "ถ้าเจ้าอยากจะสำรวจอาคารนั้นอีกครั้ง แต่เจ้าไม่มีด้ายศักดิ์สิทธิ์แล้วล่ะก็ อย่าได้เกรงใจที่จะมาขอข้าได้ทุกเมื่อ";
		set ayodunquest,13;
		completequest 12039;
		close;
	} else if (ayodunquest > 12) {
		if (countitem(7287) > 0) {
			mes "[Boonthom]";
			mes "ขอให้โชคดีใน";
			mes "การออกสำรวจนะ";
			mes "นักผจญภัยผู้กล้าหาญ~";
			close;
		} else if (countitem(523) > 1 && countitem(610) > 0 && countitem(.@items[0]) > 1 && countitem(.@items[1]) > 1 && countitem(7287) == 0) {
			mes "[Boonthom]";
			mes "ทำด้าย";
			mes "ศักดิ์สิทธิ์หาย";
			mes "ไปแล้วงั้นรึ?";
			mes "ด้วยไอเทมที่เจ้า";
			mes "มีอยู่ ข้าจะ";
			mes "สร้างมันให้อีกอันก็แล้วกัน";
			next;
			mes "[Boonthom]";
			mes "โออออมมมมม...";
			next;
			mes "[Boonthom]";
			mes "หืมมมมม...";
			mes "หืมมมมมมมมม";
			next;
			mes "[Boonthom]";
			mes "พ้วง พ้วง...";
			next;
			mes "[Boonthom]";
			mes "นี่ไงล่ะ";
			mes "รับด้ายศักดิ์สิทธิ์นี้ไปซะ";
			mes "ด้วยสิ่งนี้ เจ้าจะสามารถเข้าไปในโบราณสถานได้โดยไม่ต้องกังวลมากนัก";
			delitem .@items[0],2;
			delitem .@items[1],2;
			delitem 610,1; //Leaf_Of_Yggdrasil
			delitem 523,2; //Holy_Water
			getitem 7287,1; //Thread_Skein_
			close;
		}
		mes "[Boonthom]";
		mes "หืมม...?";
		mes "ข้าไม่ได้บอกเจ้า";
		mes "หรือว่าข้าต้องการอะไรเพื่อสร้าง";
		mes "ด้ายศักดิ์สิทธิ์ที่";
		mes "ทรงพลังยิ่งกว่าเดิม?";
		next;
		mes "[Boonthom]";
		mes "^3366992 Holy Water^000000,";
		mes "^3366991 Yggdrasil Leaf^000000,";
		mes "^3366992 "+getitemname(.@items[0])+"^000000 และ";
		mes "^3366992 "+getitemname(.@items[1])+"^000000";
		close;
	}
	mes "[Boonthom]";
	mes "โออออมมมมม...";
	close;
}

ayo_fild01,129,197,0	script	Puraim#thai1	842,{
	if ((ayodunquest > 2 && countitem(7285) > 0) || (ayodunquest > 2 && countitem(7287) > 0)) {
		mes "[Puraim]";
		mes "หืม...?";
		mes "ข้าสัมผัสได้!";
		mes "ข้าสัมผัสได้ถึง";
		mes "พลังแห่งความศักดิ์สิทธิ์!";
		mes "มันมาจากตัวเจ้านี่เอง!";
		next;
		mes "[Puraim]";
		mes "ข้าเดาว่าเจ้าคงจะเข้าไป";
		mes "ข้างในซากปรักหักพังได้แล้วล่ะ นั่นคือ";
		mes "สิ่งที่เจ้าต้องการงั้นรึ? ที่จะสำรวจ";
		mes "พื้นที่แถวนี้น่ะ?";
		next;
		mes "[Puraim]";
		mes "ข้าอยากจะขอแนะนำว่า";
		mes "อย่าเข้าไปข้างในเลยจะดีกว่า";
		mes "แม้แต่คนในพื้นที่เอง";
		mes "ยังหวาดกลัวที่แห่งนี้";
		next;
		mes "[Puraim]";
		mes "ถ้าไม่มีพยัคฆ์สมิง (Sa-mhing Tiger) อยู่รอบๆ ที่นี่ก็ยังคงเต็มไปด้วยวิญญาณชั่วร้ายอยู่ดี แม้จะมีด้ายมงคลแล้วก็ตาม ที่นี่ก็ยังอันตรายมากอยู่ดี";
		next;
		mes "[Puraim]";
		mes "แล้วเจ้าต้องการอะไรล่ะ?";
		mes "เจ้ายังอยากที่จะ";
		mes "สำรวจพื้นที่นี้อยู่ไหม";
		mes "ท่านนักผจญภัย?";
		next;
		if (select("ไปต่อ.:ไม่ล่ะ! ข้าชักจะกลัวซากปรักหักพังนี่แล้วสิ.") == 1) {
			mes "[Puraim]";
			mes "หืม...";
			mes "ดูเหมือนพวกนักผจญภัยอย่างพวกเจ้าจะถูกล่อลวงด้วยความตื่นเต้นของอันตราย";
			mes "หรือไม่ก็พวกเจ้าคงอยากจะไปหาที่ตายกันหมด";
			next;
			mes "[Puraim]";
			mes "เอาเถอะ นั่นไม่ใช่ธุระอะไรของข้า ในเมื่อเจ้ามี";
			mes "จิตวิญญาณศักดิ์สิทธิ์อยู่แล้ว แต่อย่างไรก็ตาม จงระวังตัวด้วย เจ้าอาจจะไม่อยากลงไปที่ชั้นใต้ดินที่ 2 ของซากปรักหักพังนั่นหรอก";
			next;
			mes "[Puraim]";
			mes "อย่างที่พวกชาวมิดการ์ด";
			mes "ชอบพูดกันว่า 'ความอยากรู้อยากเห็นฆ่าแมวได้'";
			mes "เอาล่ะ ไปเถอะ!";
			next;
			mes "^3355FFเขาผลักเจ้าตกจากเนินเขา";
			mes "และเจ้าร่วงลงไปเหมือนก้อนหิน^000000";
			close2;
			percentheal -10,0;
			warp "ayo_fild02",30,135;
			end;
		}
		mes "[Puraim]";
		mes "เป็นการตัดสินใจที่ดี!";
		mes "อย่างที่ข้าคาดไว้ เจ้าฉลาดพอ";
		mes "ที่จะไม่เอาชีวิตไปเสี่ยง";
		mes "อย่างโง่เขลา";
		close;
	}
	mes "[Puraim]";
	mes "ถ้าข้าเป็นเจ้า ข้าจะไม่";
	mes "แม้แต่จะกล้าเข้าใกล้";
	mes "ซากปรักหักพังที่อยู่ข้างล่างนั่นเลย";
	next;
	mes "[Puraim]";
	mes "ใครก็ตามที่ไม่มีการปกป้องจากจิตวิญญาณศักดิ์สิทธิ์ จะกลายเป็นเหยื่ออันโอชะของเหล่าปีศาจร้ายที่อาศัยอยู่ที่นั่น เจ้าจะถูกฆ่าใน";
	mes "เวลาไม่นานเลยล่ะ!";
	next;
	mes "[Puraim]";
	mes "กลับไปซะ ท่านนักผจญภัย";
	mes "หากเจ้าปรารถนาจะสำรวจ";
	mes "พื้นที่แห่งนี้ เจ้าควรจะ";
	mes "แสดงพลังศักดิ์สิทธิ์ให้ข้าเห็นก่อน";
	close;
}

ayo_fild02,25,154,0	script	Aik#thai	843,{
	mes "[Aik]";
	mes "เป็นอย่างไรบ้างล่ะ การสำรวจของเจ้า?";
	mes "ข้าหวังว่าพวกวิญญาณชั่วร้ายจะไม่ตามเจ้าออกมานอกซากปรักหักพัง และตามหลอกหลอนเจ้าในความฝันนะ";
	next;
	mes "[Aik]";
	mes "เมื่อเจ้ากลับไป";
	mes "จงจำไว้ว่าให้ขจัด";
	mes "ความชั่วร้ายออกไปจากจิตใจ";
	mes "มิฉะนั้นเจ้าจะพบเจอกับ";
	mes "ปัญหาในภายหลัง...";
	next;
	mes "[Aik]";
	mes "เอาล่ะ เจ้าต้องการ";
	mes "ที่จะกลับไปหรือยัง?";
	next;
	if (select("ไป.:ไม่ล่ะ ข้าต้องสำรวจแถวนี้ต่ออีกหน่อย...") == 1) {
		mes "[Aik]";
		mes "ตกลงงั้นก็...";
		mes "ไปกันเลย...";
		next;
		mes "^3355FFเขาจับตัวเจ้าไว้ทันที";
		mes "และเหวี่ยงเจ้าขึ้นไปบนอากาศ!^000000";
		close2;
		warp "ayo_fild01",115,200;
		end;
	}
	mes "[Aik]";
	mes "ข้าเข้าใจแล้ว";
	mes "ดูแลตัวเองด้วย";
	close;
}

// Water Traps
ayo_dun01,273,26,0	script	 #th_dun1_1_1::AyoTrap1	111,0,4,{
	mes "^3355FFเจ้ามองลงไปและเห็นบ่อน้ำ";
	mes "อยู่ลิบๆ ดูเหมือนว่าจะเป็นการตกที่สูงมากจนถึงก้นบ่อ^000000";
	next;
	mes "^3355FFเจ้าเต็มไปด้วย";
	mes "ความหวาดวิตก,";
	mes "ท้ายที่สุดแล้ว ถ้าเจ้าตกลงไป,^000000";
	mes "^2F2F4Fเจ้าอาจจะถูกฆ่าได้เลยนะ^000000";
	close;

OnTouch_:
	mes "^3355FFเจ้ามองลงไปและเห็นบ่อน้ำ";
	mes "อยู่ลิบๆ ดูเหมือนว่าจะเป็นการตกที่สูงมากจนถึงก้นบ่อ^000000";
	next;
	mes "^3355FFเจ้าเต็มไปด้วย";
	mes "ความหวาดวิตก";
	mes "ท้ายที่สุดแล้ว ถ้าเจ้าตกลงไป,";
	mes "^2F2F4Fเจ้าอาจจะถูกฆ่าได้เลยนะ^000000";
	close;
}

ayo_dun01,273,27,0	duplicate(AyoTrap1)	 #th_dun1_1_2	111
ayo_dun01,272,27,0	duplicate(AyoTrap1)	 #th_dun1_1_3	111,0,4
ayo_dun01,272,26,0	duplicate(AyoTrap1)	 #th_dun1_1_4	111

ayo_dun01,26,27,0	script	 #th_dun1_1::AyoTrap2	-1,1,1,{
OnTouch_:
	percentheal -100,0;
	end;
}

ayo_dun01,28,27,0	duplicate(AyoTrap2)	 #th_dun1_2	-1,0,1
ayo_dun01,27,25,0	duplicate(AyoTrap2)	 #th_dun1_3	-1,1,0
ayo_dun01,25,25,0	duplicate(AyoTrap2)	 #th_dun1_4	-1
ayo_dun01,272,273,0	duplicate(AyoTrap2)	 #th_dun1_5	-1,1,1
ayo_dun01,274,273,0	duplicate(AyoTrap2)	 #th_dun1_6	-1,0,1
ayo_dun01,273,271,0	duplicate(AyoTrap2)	 #th_dun1_7	-1,1,0
ayo_dun01,271,271,0	duplicate(AyoTrap2)	 #th_dun1_8	-1
ayo_dun01,26,273,0	duplicate(AyoTrap2)	 #th_dun1_9	-1,1,1
ayo_dun01,28,273,0	duplicate(AyoTrap2)	 #th_dun1_10	-1,0,1
ayo_dun01,27,271,0	duplicate(AyoTrap2)	 #th_dun1_11	-1,1,0
ayo_dun01,25,271,0	duplicate(AyoTrap2)	 #th_dun1_12	-1

// Hints
ayo_dun01,156,148,0	script	 #hint01::AyoHint	-1,0,4,{
OnTouch_:
	if (rand(1,100) > 60 && ayodunquest < 11) {
		mes "^3355FFด้ายมงคลในกระเป๋าของเจ้าเริ่มส่องแสงขึ้นมาทันที เจ้าสัมผัสได้ว่ามีบางสิ่งอยู่ใกล้ๆ เจ้า...^000000";
		close2;
		viewpoint 1,198,164,0,0xFF0000;
		viewpoint 1,87,16,1,0xFF0000;
		viewpoint 1,268,214,2,0xFF0000;
		viewpoint 1,147,274,3,0xFF0000;
		viewpoint 1,99,118,4,0xFF0000;
		viewpoint 1,16,188,5,0xFF0000;
	}
	end;
}

ayo_dun01,259,42,0	duplicate(AyoHint)	 #hint02	-1,2,0
ayo_dun01,259,44,0	duplicate(AyoHint)	 #hint02_2	-1,2,0
ayo_dun01,259,46,0	duplicate(AyoHint)	 #hint02_3	-1,2,0

// Puzzle pieces
ayo_dun01,198,164,0	script	 #hun_thai_1	111,{
	if (ayodunquest == 3) {
		set ayodunquest,4;
		mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 1 ของแผ่นหิน^000000";
		next;
		mes "^3355FFมันเขียนว่า '^0000FFอย่าลงไปยังชั้นใต้ดินที่ 2^3355FF' เจ้าไม่สามารถอ่านข้อความส่วนที่เหลือได้^000000";
		close;
	}
	mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 1 ของแผ่นหิน^000000";
	close;
}

ayo_dun01,87,16,0	script	 #hun_thai_2	111,{
	if (ayodunquest == 4) {
		set ayodunquest,5;
		mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 2 ของแผ่นหิน^000000";
		next;
		mes "^3355FFมันเขียนว่า '^0000FF...คืออันตราย... เจ้าจะ...^3355FF' เจ้าไม่สามารถอ่านข้อความส่วนที่เหลือได้^000000";
		close;
	}
	mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 2 ของแผ่นหิน^000000";
	close;
}

ayo_dun01,268,214,0	script	 #hun_thai_3	111,{
	if (ayodunquest == 5) {
		set ayodunquest,6;
		mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 3 ของแผ่นหิน^000000";
		next;
		mes "^3355FFมันเขียนว่า '^0000FF...เผชิญหน้า... เสือ...^3355FF' เจ้าไม่สามารถอ่านข้อความส่วนที่เหลือได้^000000";
		close;
	}
	mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 3 ของแผ่นหิน^000000";
	close;
}

ayo_dun01,147,274,0	script	 #hun_thai_4	111,{
	if (ayodunquest == 6) {
		set ayodunquest,7;
		mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 4 ของแผ่นหิน^000000";
		next;
		mes "^3355FFมันเขียนว่า '^0000FFเจ้าต้องฆ่า... เพื่อที่จะทำ... ต้อง... วิธีการไป... ชั้นใต้ดินที่ 2...^3355FF' เจ้าไม่สามารถอ่านข้อความส่วนที่เหลือได้^000000";
		close;
	}
	mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 4 ของแผ่นหิน^000000";
	close;
}

ayo_dun01,99,118,0	script	 #hun_thai_5	111,{
	if (ayodunquest == 7) {
		set ayodunquest,8;
		mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 5 ของแผ่นหิน^000000";
		next;
		mes "^3355FFมันเขียนว่า '^0000FFเจ้าต้องการมากกว่า... พลังศักดิ์สิทธิ์... เพื่อที่จะ...^3355FF' เจ้าไม่สามารถอ่านข้อความส่วนที่เหลือได้^000000";
		close;
	}
	mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 5 ของแผ่นหิน^000000";
	close;
}

ayo_dun01,16,188,0	script	 #hun_thai_6	111,{
	if (ayodunquest == 8) {
		set ayodunquest,9;
		// setquest 12036;
		mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนสุดท้ายของแผ่นหิน^000000";
		next;
		mes "^3355FFมันเขียนว่า '^0000FFมิฉะนั้น มันเป็นไปไม่ได้...^3355FF' เจ้าไม่สามารถอ่านข้อความส่วนที่เหลือได้^000000";
		close;
	}
	mes "^3355FFเจ้าพบชิ้นส่วนของแผ่นหินสลักที่มีตัวอักษรเขียนอยู่ ดูเหมือนว่าจะเป็นส่วนสุดท้ายของแผ่นหิน^000000";
	close;
}

ayo_dun01,24,283,0	script	 #2_dun_in	111,{
	if (ayodunquest > 10) {
		if (countitem(7287) > 0) {
			mes "^3355FFด้ายมงคล";
			mes "แผ่ซ่านไปด้วยความรู้สึก";
			mes "แห่งความปลอดภัยและมั่นคง";
			mes "ด้วยการปกป้องจาก";
			mes "ความศักดิ์สิทธิ์ เจ้าเริ่มที่จะ";
			mes "ลงไปยังชั้นที่ 2^000000.";
			close2;
			if (rand(1,10) > 5) {
				warp "ayo_dun02",23,26;
			} else {
				warp "ayo_dun02",275,26;
			}
			end;
		}
		mes "^3355FFเจ้าพบสิ่งที่ดูเหมือนจะเป็นทางเข้าไปยังชั้นที่ 2 อย่างไรก็ตาม เจ้ากลับเต็มไปด้วยความรู้สึกหวาดกลัวอย่างท่วมท้น อย่างไรก็ตาม เจ้าไม่สามารถฝืนใจตัวเองให้ลงไปได้...^000000";
		close;
	}
	mes "^3355FFเจ้าพบสิ่งที่ดูเหมือนจะเป็นทางเข้าไปยังชั้นที่ 2 อย่างไรก็ตาม เจ้ากลับเต็มไปด้วยความรู้สึกหวาดกลัวอย่างท่วมท้น อย่างไรก็ตาม เจ้าไม่สามารถฝืนใจตัวเองให้ลงไปได้...^000000";
	close;
}

ayo_dun02,268,108,0	script	 #reward_tiger	111,{
	if (ayodunquest == 11 && countitem(7287) > 0) {
		mes "^3355FFเจ้าพบเศษซากกระดูกครึ่งตัวของใครบางคนโผล่ออกมาจากพื้นดิน ดูเหมือนว่าจะเป็นร่างของสหายของ Boonthom^000000";
		next;
		mes "^3355FFเจ้าพบแผ่นกระดาษวางอยู่ข้างๆ โครงกระดูก พร้อมกับกล่องใบหนึ่ง^000000";
		next;
		mes "^3355FFเจ้าอ่านสิ่งที่เขียนอยู่บนแผ่นกระดาษเก่านั่น ตัวอักษรซีดจางไปตามกาลเวลา และมันอ่านยากมาก^000000";
		next;
		mes "[จดหมายเก่า]";
		mes "^333333ถึง, Boonthom,";
		mes "ข้ามันโง่เอง มันมี 'พยัคฆ์สมิ'...ใครก็ตามที่ ....ซากของข้า... โปรดนำ...ไปให้ Boonthom...";
		next;
		mes "^3355FFเจ้าหยิบ";
		mes "กล่องขึ้นมา^000000";
		set ayodunquest,12;
		// completequest 12039;
		getitem 603,1; //Old_Blue_Box
		close;
	}
}

// Tom Tung Goong Quest :: ayo_tom
//============================================================
ayothaya,196,265,3	script	Einon#ayo	842,{
	if (tomyumgoong == 0) {
		mes "[Einon]";
		mes "เจ้าพอมันจะรู้ไหมว่า";
		mes "อาหารยอดนิยมที่สุด";
		mes "ของหมู่บ้านนี้คืออะไร?";
		next;
		mes "[Einon]";
		mes "อโยธยามีชื่อเสียงระดับโลก";
		mes "ในด้านอาหารประจำถิ่น แต่";
		mes "ในบรรดาอาหารอโยธยาทั้งหมด";
		mes "'^3131FFต้มยำกุ้ง^000000' คือที่สุด";
		next;
		mes "[Einon]";
		mes "ต้มยำกุ้งเป็นแกงประเภทหนึ่ง";
		mes "ที่มีรสชาติเผ็ดมาก ลองชิมสักครั้ง แล้วเจ้าจะทึ่งกับรสชาติที่ล้ำลึกของมัน ลองชิมครั้งที่สอง แล้วเจ้าจะหลงใหลในกลิ่นหอมที่เย้ายวนใจ";
		next;
		mes "[Einon]";
		mes "มันเป็นหนึ่งในอาหารที่อร่อยที่สุดในโลกจริงๆ!";
		mes "ทุกคนในโลกต่างก็";
		mes "อยากลองชิมสิ่งนี้อย่างน้อยสักครั้งในชีวิต แล้วเจ้าล่ะ อยากจะ";
		mes "ลองชิมดูไหม?";
		next;
		if (select("แน่นอน.:ไม่ล่ะ ขอบใจ ข้าเกลียดอาหารเผ็ด") == 1) {
			mes "[Einon]";
			mes "ตกลง...!";
			mes "แล้วใครล่ะที่";
			mes "จะเหมาะที่สุดในการเตรียม";
			mes "ต้มยำกุ้งให้เจ้า?";
			mes "หืมมมม...";
			next;
			mes "[Einon]";
			mes "ทุกคนในหมู่บ้าน";
			mes "ต่างรู้วิธีทำทั้งนั้น แต่";
			mes "ข้าขอแนะนำให้เจ้าไป";
			mes "พบกับ ^3131FFMali the Spicy^000000";
			next;
			emotion ET_BEST;
			set tomyumgoong,1;
			setquest 8123;
			mes "[Einon]";
			mes "นางคือแม่ครัวที่เก่งที่สุด";
			mes "เมื่อพูดถึงต้มยำกุ้ง!";
			mes "ทำไมเจ้าไม่ลองไปขอให้นาง";
			mes "ปรุงให้เจ้าดูล่ะ?";
			close;
		}
		mes "[Einon]";
		mes "โอ้ ข้าเข้าใจแล้ว...";
		mes "อย่างไรก็ตาม เมื่อเจ้า";
		mes "เปลี่ยนใจ";
		mes "ก็กลับมาหาข้าได้นะ";
		close;
	} else if (tomyumgoong == 1) {
		mes "[Einon]";
		mes "อา เจ้าอยากรู้ว่า";
		mes "^3131FFMali the Spicy^000000 อยู่ที่ไหนงั้นรึ?";
		mes "ก็นะ คงไม่มีที่ไหนที่";
		mes "คนปรุงอาหารจะอยู่นอกจากใน";
		mes "ร้านอาหารหรอก จริงไหม? ฮ่าๆๆ~";
		close;
	}
	mes "[Einon]";
	mes "อโยธยาเมืองทอง...";
	mes "แม้ว่าข้าจะเติบโต";
	mes "ในหมู่บ้านนี้";
	mes "ข้าก็ยังคิดว่าหมู่บ้านของข้า";
	mes "ช่างสง่างามและงดงามเหลือเกิน";
	next;
	mes "[Einon]";
	mes "เจ้าคิดว่าอย่างไรล่ะ?";
	mes "เจ้าไม่เห็นด้วยกับข้าหรือ?";
	close;
}

ayo_in01,145,163,3	script	Cook#ayo	839,{
	if (tomyumgoong < 2) {
		mes "[Mali the Spicy]";
		mes "สวัสดีจ้ะ!";
		mes "ข้าชื่อ Mali the Spicy";
		mes "อาหารทุกจานของข้าทำให้";
		mes "ลูกค้าตื่นตาตื่นใจเสมอด้วยกลิ่นที่เย้ายวนและรสชาติที่ล้ำลึก";
		next;
		mes "[Mali the Spicy]";
		mes "ข้าภูมิใจเป็นพิเศษกับ";
		mes "^3131FFต้มยำกุ้ง^000000 ของข้า ซึ่ง";
		mes "ดีที่สุดในหมู่บ้าน ข้าคิดว่า";
		mes "มันคือโชคชะตาของข้าที่จะปรุงต้มยำกุ้งที่ยอดเยี่ยมที่สุดเท่าที่เคยมีมา";
		if (tomyumgoong == 0) {
			close;
		} else {
			next;
			mes "[Mali the Spicy]";
			mes "โอ้ ข้าเข้าใจแล้ว...";
			mes "เจ้าต้องการให้ข้าปรุง";
			mes "ต้มยำกุ้งให้เจ้า";
			mes "ใช่ไหมล่ะ?";
			next;
			mes "[Mali the Spicy]";
			mes "บอกตามตรง ข้า";
			mes "ยุ่งมากตั้งแต่เปิดร้านนี้มา ถ้าเจ้าอยากจะลิ้มรสต้มยำกุ้งของข้าจริงๆ เจ้าจะ";
			mes "ช่วยอะไรข้าหน่อยได้ไหม?";
			next;
			if (select("แน่นอน!:เอ่อ... ข้าไม่แน่ใจแฮะ") == 1) {
				mes "[Mali the Spicy]";
				mes "ขั้นแรก เจ้าช่วยไปหา";
				mes "วัตถุดิบในการทำ";
				mes "ต้มยำกุ้งมาให้ข้าได้ไหม? ข้าต้องการ ^CE0000กุ้ง (Shrimps)^000000, ^CE0000พริก (Chiles)^000000 และ ^CE0000มะนาว (Lemons)^000000";
				next;
				mes "[Mali the Spicy]";
				mes "จำไว้ว่า กุ้งเป็นวัตถุดิบหลักของจานนี้ ดังนั้น ยิ่งกุ้งสดเท่าไหร่ อาหารของเจ้าก็จะยิ่งอร่อยมากขึ้นเท่านั้น";
				next;
				select("ข้าจะหาพวกมันได้ที่ไหน?");
				mes "[Mali the Spicy]";
				mes "ข้ากะแล้วว่า";
				mes "เจ้าต้องถามแบบนี้";
				mes "สำหรับกุ้ง ลองดูแถว";
				mes "ชายหาดนะ เจ้าควรจะ";
				mes "เห็นพวกมันมากมายที่นั่น";
				next;
				mes "[Mali the Spicy]";
				mes "อย่างไรก็ตาม เจ้าอาจจะพบพวกมันได้ยากเพราะเจ้าไม่เคยทำมาก่อน ดังนั้นแค่ไปซื้อกุ้งจากชายที่ขาย";
				mes "กุ้งที่สดที่สุดแถวนี้ก็ได้นะ";
				next;
				mes "[Mali the Spicy]";
				mes "หืมมม...";
				mes "ถ้าเจ้าบอก";
				mes "ชื่อของข้า ชายคนนั้น";
				mes "อาจจะลดราคาให้เจ้าก็ได้";
				set tomyumgoong,2;
				changequest 8123,8124;
				next;
				mes "[Mali the Spicy]";
				mes "เขาชื่อ ^3131FFThongpool^000000";
				mes "เขาจะอยู่แถวๆ ในหมู่บ้านนี่แหละ คอยขายกุ้งอยู่ ไปหาเขาแล้วขอกุ้ง 20 ตัวเพื่อมาทำต้มยำกุ้งนะ";
				close;
			}
			mes "[Mali the Spicy]";
			mes "ข้าเข้าใจว่านี่อาจจะเป็นเรื่องยาก แต่มันก็คุ้ม";
			mes "ที่จะลองดูนะ ถ้าเจ้าอยากลิ้มรสต้มยำกุ้งที่ยอดเยี่ยมที่สุดจริงๆ";
			close;
		}
	} else if (tomyumgoong > 1 && tomyumgoong < 4) {
		if (tomyumgoong == 2 && countitem(567) > 19) {
			mes "[Mali the Spicy]";
			mes "เอ่อ...";
			mes "กุ้งพวกนี้มันเป็นอะไรไปเนี่ย?";
			mes "พวกมันมีแต่รอยขีดข่วนและรอยฉีกขาดเต็มไปหมด! ข้าไม่อยากจะเชื่อเลยว่า Thongpool จะขายกุ้งที่แย่ขนาดนี้";
			mes "ให้กับลูกค้าของเขา!";
			next;
			mes "[Mali the Spicy]";
			mes "ข้าเสียใจจริงๆ แต่";
			mes "ข้าไม่สามารถปรุงต้มยำกุ้ง";
			mes "ที่ยอดเยี่ยมที่สุดด้วย";
			mes "กุ้งพวกนี้ได้";
			next;
			mes "[Mali the Spicy]";
			mes "ข้ารู้ว่ามันไม่สะดวก แต่เจ้าช่วยกลับไปหา Thongpool";
			mes "แล้วขอให้เขาให้กุ้งที่สดที่สุดเพื่อมาปรุงต้มยำกุ้ง";
			mes "ที่ยอดเยี่ยมที่สุดได้ไหม?";
			close;
		} else if (tomyumgoong == 3) {
			if (countitem(567) > 19) {
				mes "[Mali the Spicy]";
				mes "อา เจ้ากลับมาแล้ว!";
				mes "ไหน ขอดูสิว่า";
				mes "เจ้านำอะไรมา หืม...";
				mes "นี่มัน...";
				next;
				emotion ET_BEST;
				mes "[Mali the Spicy]";
				mes "กุ้งที่สดที่สุดที่";
				mes "ข้าตามหาอยู่เลย! ทำได้ดีมาก~";
				mes "ด้วยกุ้งที่ตัวใหญ่และสดแบบนี้";
				mes "ข้าสามารถทำต้มยำกุ้งที่อร่อยที่สุดให้เจ้าได้แล้ว! ขอบใจเจ้ามากนะ!";
				delitem 567,20; //Prawn
				next;
				mes "[Mali the Spicy]";
				mes "มันจะเป็นอาหารที่สมบูรณ์แบบด้วยความหวานตามธรรมชาติจากกุ้ง ความเปรี้ยวจากมะนาว และน้ำปลานิดหน่อย!";
				next;
				mes "[Mali the Spicy]";
				mes "โอ้... ใช่";
				mes "แต่เรามีแค่กุ้ง";
				mes "ตอนนี้ ข้าอยากให้เจ้าไปหา";
				mes "^CE0000มะนาว (Lemons)^000000 มาให้ข้า";
				next;
				if (select("ข้าจะหามะนาวได้จากที่ไหน?:ข้าเกลียดอาหารเปรี้ยว เลิกทำดีกว่า!") == 1) {
					set tomyumgoong,4;
					changequest 8124,8125;
					mes "[Mali the Spicy]";
					mes "เมื่อไม่กี่วันก่อน ข้าเห็นต้น";
					mes "มะนาวสวยๆ ตอนออกไปเดินเล่น";
					mes "แต่ต้นไม้ต้นนี้เป็นของชายธรรมดาคนหนึ่งที่ไม่ใช่พ่อค้าผลไม้";
					next;
					mes "[Mali the Spicy]";
					mes "ข้าไม่แน่ใจว่า";
					mes "เขาจะขายมะนาว";
					mes "ให้เจ้าหรือไม่ แต่เจ้าก็ลองดู";
					mes "ได้นะ ข้าว่า";
					close;
				}
				emotion ET_ANGER;
				mes "[Mali the Spicy]";
				mes "เจ้าคนโง่...!";
				mes "ถ้าเจ้าจะเลิกเพียงเพราะเจ้าเกลียดรสเปรี้ยว เจ้าคงไม่อยากกินต้มยำกุ้งของข้าขนาดนั้นสินะ!";
				next;
				mes "[Mali the Spicy]";
				mes "เมื่อเจ้าได้ลิ้มรส";
				mes "ต้มยำกุ้งของข้าแล้ว เจ้าจะ";
				mes "เสียใจกับสิ่งที่เจ้าเพิ่งพูดออกมา!";
				close;
			}
			mes "[Mali the Spicy]";
			mes "หืม...?";
			mes "เจ้าทำอะไร";
			mes "กับพวกกุ้งน่ะ?";
			mes "เจ้าไม่ได้กินพวกมัน";
			mes "ระหว่างทางมาที่นี่ใช่ไหม?";
			close;
		}
		mes "[Mali the Spicy]";
		mes "อย่าลืมขอให้";
		mes "คุณ Thongpool ให้";
		mes "กุ้ง 20 ตัวแก่เจ้า";
		mes "ทีนี้รีบไปเถอะ~";
		close;
	} else if (tomyumgoong > 3 && tomyumgoong < 6) {
		if (tomyumgoong == 4 && countitem(568) > 9) {
			mes "[Mali the Spicy]";
			mes "เจ้าคิดว่านี่คือ";
			mes "มะนาวคุณภาพดีที่สุดแล้วงั้นรึ?";
			mes "ดูให้ใกล้กว่านี้สิ!";
			next;
			mes "[Mali the Spicy]";
			mes "ผิวของมะนาวพวกนี้";
			mes "มันดูไม่เงางาม";
			mes "และพวกมันยังไม่มีกลิ่น";
			mes "ของความสดเลย! มันก็แค่";
			mes "มะนาวธรรมดาๆ!";
			close;
		} else if (tomyumgoong == 5) {
			if (countitem(568) > 9) {
				emotion ET_SURPRISE;
				mes "[Mali the Spicy]";
				mes "ยินดีต้อนรับกลับมา!";
				mes "ว้าว เจ้ากลับมา";
				mes "เร็วจริงๆ! เอาล่ะ ไหนขอดู";
				mes "มะนาวที่";
				mes "เจ้านำมาหน่อยสิ?";
				next;
				mes "[Mali the Spicy]";
				mes "ว้าว ยอดเยี่ยม!";
				mes "นี่คือมะนาวที่สดจริงๆ!";
				mes "เจ้านำพวกมันทั้งหมดมาด้วยตัวเองได้อย่างไรกัน? ขอบใจที่ลำบากนะ~";
				next;
				mes "[Mali the Spicy]";
				mes "ทีนี้ เจ้าควรจะไปซื้อ";
				mes "^CE0000พริก (Chiles)^000000 และวัตถุดิบ";
				mes "อื่นๆ ในตลาด";
				next;
				mes "[Mali the Spicy]";
				mes "เมื่อเจ้าไปคุย";
				mes "กับเหล่าพ่อค้า";
				mes "ในตลาด พวกเขา";
				mes "จะรู้เองว่าเจ้าต้องการอะไร";
				next;
				if (select("ตกลง ข้าจะรีบกลับมา.:อา ข้าเหนื่อยแล้ว ขอข้าพักก่อนเถอะนะ...") == 1) {
					delitem 568,10; //Lemon
					set tomyumgoong,6;
					changequest 8125,8126;
					mes "[Mali the Spicy]";
					mes "แล้วพบกันจ้ะ~";
					close;
				}
				mes "[Mali the Spicy]";
				mes "แล้วแต่เจ้าเลย";
				mes "แต่จำไว้ว่า...";
				mes "กุ้งพวกนั้นอาจจะเน่าเสีย";
				mes "ในขณะที่เจ้ากำลังพักผ่อนอยู่นะ";
				close;
			}
			mes "[Mali the Spicy]";
			mes "หืม...?";
			mes "เจ้าทำอะไร";
			mes "กับพวกมะนาวน่ะ?";
			mes "เจ้าไม่ได้กินมัน";
			mes "หมดระหว่างทางมาที่นี่ใช่ไหม?";
			close;
		}
		mes "[Mali the Spicy]";
		mes "เอาล่ะ...";
		mes "ข้าหวังพึ่งเจ้า";
		mes "ที่จะหามะนาวที่ดีที่สุด";
		mes "จากชายเจ้าของต้นไม้นั่นนะ";
		next;
		mes "[Mali the Spicy]";
		mes "ไม่ว่าจะอย่างไร เจ้าต้อง";
		mes "หามะนาวคุณภาพสูงพวกนั้น";
		mes "มาให้ได้ ข้าจะได้ทำ";
		mes "ต้มยำกุ้งสักที!";
		close;
	} else if (tomyumgoong == 6) {
		if (countitem(7286) > 29) {
			mes "[Mali the Spicy]";
			mes "ข้าไม่เข้าใจเลย...";
			mes "เจ้าไปเอาวัตถุดิบ";
			mes "แย่ๆ พวกนี้มาจากไหนกัน?! ข้า";
			mes "ไม่ได้บอกเจ้าหรือว่า";
			mes "ต้มยำกุ้งของข้า";
			mes "ต้องการสิ่งที่ดีที่สุดน่ะ?!";
			close;
		}
		mes "[Mali the Spicy]";
		mes "ข้ารู้ว่าเจ้าทำงานหนักเพื่อช่วยข้าในเรื่องนี้ ข้าหวังว่าเจ้าจะพยายามในเฮือกสุดท้ายเพื่อทำมันให้สำเร็จนะ";
		next;
		mes "[Malli the Spicy]";
		mes "โปรดไปลองดูในตลาด ข้าไม่สงสัยเลยว่ามันจะเป็นประสบการณ์ที่ดี ท้ายที่สุดแล้ว ต้มยำกุ้งที่แสนวิเศษของข้ากำลังรอเจ้าอยู่!";
		close;
	} else if (tomyumgoong == 7) {
		if (MaxWeight - Weight > 1499) {
			if (countitem(7286) > 29) {
				delitem 7286,30; //Chilli
				emotion ET_SURPRISE;
				mes "[Mali the Spicy]";
				mes "ทีนี้...";
				mes "ทุกอย่างพร้อมแล้ว!";
				mes "ก่อนที่เราจะเริ่ม ข้าขอ";
				mes "ตรวจสอบวัตถุดิบก่อนนะ";
				next;
				mes "[Mali the Spicy]";
				mes "^CE0000กุ้ง^000000, ^CE0000พริก^000000, ^CE0000มะนาว^000000...";
				mes "น้ำปลาและสิ่งเล็กๆ น้อยๆ อื่นๆ...";
				next;
				mes "[Mali the Spicy]";
				mes "สมบูรณ์แบบ!";
				mes "เราจะเริ่มกันเลยไหม?";
				next;
				mes "^FF0000*ตุ๊ก*^000000";
				mes "^9C009C*ตุ๊บ*^000000";
				mes "^009C00*ปุ๊ก*^000000";
				specialeffect EF_ENCHANTPOISON;
				next;
				mes "^FF0031*สับ สับ*^000000";
				mes "^FF9C00*ตุ๊บ ตุ๊บ ตุ๊บ*^000000";
				mes "^639CFF*ปู๊ ปู๊^000000 -";
				specialeffect EF_VENOMDUST;
				next;
				mes "[Mali the Spicy]";
				mes "- ^9C6300ตุ๊บ ตุ๊บ^000000 ^006363ฉ่า^000000";
				mes "^316363สับ สับ^000000";
				mes "^FF0000ตุ๊บ ตุ๊บ ตุ๊บ^000000 ^009C00ปู๊ ปู๊^000000 -";
				specialeffect EF_STONECURSE;
				specialeffect EF_HASTEUP;
				specialeffect EF_BEGINSPELL;
				next;
				mes "[Mali the Spicy]";
				mes "ว้า^00009Cอา^0000FFอาาาาา^0063FFอาาาาห์^000000!";
				emotion ET_HUK;
				specialeffect EF_FLASHER;
				specialeffect EF_LORD;
				next;
				mes "[Mali the Spicy]";
				mes " ...";
				mes " ......";
				mes " .........";
				next;
				set tomyumgoong,8;
				completequest 8126;
				getitem 566,10; //Tomyumkung
				emotion ET_BEST;
				mes "[Mali the Spicy]";
				mes "นี่จ้ะ";
				mes "ต้มยำกุ้งของเจ้า!";
				mes "อา~ รสชาตินี้";
				mes "กลิ่นนี้... ใช่เลย";
				mes "ข้ารู้ว่าข้าทำออกมาได้ดี";
				next;
				mes "[Mali the Spicy]";
				mes "เจ้ารู้ไหมว่าความลับของต้มยำกุ้งคืออะไร? มันไม่ใช่แค่เพียงอาหารธรรมดา แต่มันมีพลังที่จะรักษาเจ้า และช่วยให้เจ้ามีสุขภาพที่ดีขึ้น";
				next;
				mes "[Mali the Spicy]";
				mes "เพราะว่า...";
				mes "มันมี";
				mes "พลังแห่งพระเจ้าอยู่";
				next;
				mes "[Mali the Spicy]";
				mes "ตกลง...!";
				mes "ข้าจะให้ต้มยำกุ้ง";
				mes "แก่เจ้า 10 ถ้วย";
				mes "แบบไม่คิดเงินเลยนะ~";
				next;
				mes "[Mali the Spicy]";
				mes "ขอบคุณสำหรับความลำบากทั้งหมดที่เจ้าผ่านมาเพื่อหาวัตถุดิบพวกนี้ มันจะทำให้ข้ามีความสุขมากถ้าเจ้าแบ่งปันต้มยำกุ้งนี้กับเพื่อนๆ ของเจ้าด้วย";
				next;
				mes "[Mali the Spicy]";
				mes "แบ่งปันรสชาติของอโยธยา และให้พวกเขาได้รับรู้ว่าแผ่นดินของเรางดงามเพียงใด ดีใจที่ได้พบเจ้านะ ดูแลตัวเองด้วยจ้ะ~";
				close;
			}
			mes "[Mali the Spicy]";
			mes "หืม? เจ้าทำอะไรกับพวกพริกน่ะ? เจ้าไม่ได้กินมันหมดระหว่างทางมาที่นี่ใช่ไหม?";
			close;
		}
		mes "[Mali the Spicy]";
		mes "หืม...";
		mes "ดูเหมือนเจ้าจะแบกของมากเกินไปนะ ทำไมเจ้าไม่ลองไปเคลียร์ที่ว่างในกระเป๋าก่อน แล้วค่อยกลับมาล่ะ?";
		close;
	}
	emotion ET_KEK;
	mes "[Mali the Spicy]";
	mes "อา...!";
	mes "ยุ่งมาก ยุ่งมากจริงๆ!";
	mes "ลูกค้าของข้าไม่เคยเบื่อ";
	mes "รสชาติต้มยำกุ้งของข้าเลย!";
	mes "เฮะ เฮะ เฮะ~!";
	close;
}

ayothaya,153,86,5	script	Thongpool#ayo	843,{
	if ((MaxWeight-Weight) < 2000 || checkweight(1201,1) == 0) {
		mes "- รอก่อน! -";
		mes "- ตอนนี้เจ้าแบก -";
		mes "- สิ่งของมากเกินไป -";
		mes "- โปรดลดน้ำหนักของเจ้าลง -";
		mes "- แล้วลองใหม่อีกครั้ง -";
		close;
	}
	if (tomyumgoong == 2) {
		mes "[Thongpool]";
		mes "มาเลยเหล่านักผจญภัย";
		mes "มาดูนี่สิ! ข้ามี";
		mes "กุ้งมากมายเลยนะ!";
		next;
		mes "[Thongpool]";
		mes "เจ้ากำลังมองหากุ้งอยู่ใช่ไหม? เจ้าต้องการ";
		mes "กุ้งกี่ตัวล่ะ? ยี่สิบ? สี่สิบ?";
		next;
		if (select("ข้าต้องการกุ้งสำหรับทำต้มยำกุ้ง.:เอ่อ ดูเหมือนมันจะราคาแพงนะ") == 1) {
			mes "[Thongpool]";
			mes "อา! ข้าเดาว่าคุณ Mali the Spicy แนะนำข้าให้กับเจ้า ข้าเดา";
			mes "ว่านั่นหมายความว่าเจ้าอยากจะซื้อพวกมันในปริมาณมากสินะ?";
			next;
			mes "[Thongpool]";
			mes "ก็นะ";
			mes "เจ้าต้องใช้";
			mes "กุ้งปริมาณมากเพื่อ";
			mes "ปรุงต้มยำกุ้งน่ะ";
			next;
			mes "[Thongpool]";
			mes "โอ้ ใช่แล้ว เจ้าจะต้องใช้กุ้ง 20 ตัวสำหรับต้มยำกุ้งของคุณ Mali";
			mes "ข้าจะลดราคาให้เจ้าด้วยนะ:";
			mes "11,000 zeny สำหรับกุ้ง 20 ตัว";
			mes "เจ้าว่าอย่างไรล่ะ?";
			next;
			if (select("ข้าจะเอาพวกมันเอง.:ข้าเอาเงินนั่นไปซื้อ Jellopy ได้ตั้ง 100 อันแนะ!") == 1) {
				if (Zeny > 10999) {
					if (MaxWeight - Weight > 3999) {
						set Zeny, Zeny-11000;
						set tomyumgoong,3;
						getitem 567,20; //Prawn
						emotion ET_BEST;
						mes "[Thongpool]";
						mes "ดีมาก ตอนนี้เจ้ามี";
						mes "กุ้งที่สดที่สุดในหมู่บ้านนี้แล้ว! ถึงเวลาที่เจ้าจะต้องกลับไปหาคุณ Mali เพื่อให้นางปรุงมันให้เจ้าแล้วล่ะ";
						close;
					}
					mes "[Thongpool]";
					mes "โธ่เอ๊ย...";
					mes "เจ้าแบกของมากเกินไปแล้ว ข้าไม่คิดว่าเจ้าจะแบกอะไรเพิ่มไปได้มากกว่าที่เจ้ามีอยู่ตอนนี้หรอกนะ";
					close;
				}
				mes "[Thongpool]";
				mes "โธ่เอ๊ย...";
				mes "เจ้ามีเงิน";
				mes "ไม่พอนี่นา";
				close;
			}
			mes "[Thongpool]";
			mes "เอาเถอะๆ...";
			mes "เจ้าอาจจะเติมท้องที่หิวโหยด้วย Jellopy มากมายขนาดนั้นได้ แต่มันคงรสชาติไม่ดีเท่ากุ้งของข้าหรอก!";
			close;
		}
		mes "[Thongpool]";
		mes "อะไรนะ...?";
		mes "เจ้าคิดว่า";
		mes "มันแพงงั้นรึ?";
		mes "แต่รสชาติที่ยอดเยี่ยม";
		mes "ของต้มยำกุ้งน่ะ";
		mes "ประเมินค่าไม่ได้เลยนะ!";
		close;
	} else if (tomyumgoong == 3) {
		mes "[Thongpool]";
		mes "อืม...?";
		mes "เจ้าต้องการ";
		mes "กุ้งเพิ่มอีกรึเปล่า?";
		mes "เจ้าอยากจะ";
		mes "ซื้อเพิ่มไหม?";
		next;
		if (select("ใช่!:ไม่ล่ะ ขอบใจ") == 1) {
			if (Zeny > 10999) {
				if (MaxWeight - Weight > 3999) {
					set Zeny, Zeny-11000;
					getitem 567,20; //Prawn
					mes "[Thongpool]";
					mes "นี่เลยจ้ะ~";
					mes "กุ้งที่สดที่สุดเท่าที่เจ้าจะหาได้ในหมู่บ้านนี้ และอาจจะที่สุดในโลกเลยก็ได้นะ!";
					close;
				}
				mes "[Thongpool]";
				mes "โธ่เอ๊ย...";
				mes "เจ้าแบกของมากเกินไปแล้ว ข้าไม่คิดว่าเจ้าจะแบกอะไรเพิ่มไปได้มากกว่าที่เจ้ามีอยู่ตอนนี้หรอกนะ";
				close;
			}
			mes "[Thongpool]";
			mes "โธ่เอ๊ย...";
			mes "เจ้ามีเงิน";
			mes "ไม่พอนี่นา";
			close;
		}
		mes "[Thongpool]";
		mes "ดีมาก ตอนนี้เจ้าได้กุ้งที่สดที่สุดในหมู่บ้านนี้ไปแล้ว!";
		mes "ถึงเวลาที่เจ้าจะกลับไปขอให้คุณ Mali ปรุงอาหารให้เจ้าแล้วล่ะ";
		close;
	}
	mes "[Thongpool]";
	mes "ยินดีต้อนรับ ยินดีต้อนรับ!";
	mes "ข้ามีกุ้งที่ดีที่สุดที่จับได้จากน่านน้ำที่สะอาดที่สุดในโลกเลยนะ~!";
	close;
}

ayothaya,121,240,7	script	Mr. Jun#ayo	842,1,1,{
	if (MaxWeight - Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน! -";
		mes "- ตอนนี้เจ้าแบกไอเทม -";
		mes "- ติดตัวมากเกินไป -";
		mes "- กรุณาลดน้ำหนักของเจ้าลง -";
		mes "- แล้วลองใหม่อีกครั้ง -";
		close;
	}
	if (tomyumgoong == 5) {
		emotion ET_SWEAT;
		mes "[Mr. Jun]";
		mes "อืม...?";
		mes "เจ้าคงไม่ได้ต้องการ";
		mes "มะนาวเพิ่ม";
		mes "อีกแล้วใช่ไหม?";
		next;
		switch(select("เปล่าครับ/ค่ะ.:เอามานาวมาให้ข้า ตาแก่!:ขอมะนาวเพิ่มอีกหน่อยได้ไหมครับ/ค่ะ?")) {
		case 1:
			emotion ET_DELIGHT;
			mes "[Mr. Jun]";
			mes "งั้นทำไมเจ้าไม่ลองไปชิม";
			mes "มะนาวที่ข้าให้ไปคราวก่อนล่ะ? มันเปรี้ยวมากเลยนะ แต่ก็อร่อยมากด้วย!";
			close;
		case 2:
			emotion ET_FRET;
			mes "[Mr. Jun]";
			mes "อะไรนะ...!?";
			mes "เจ้าเป็นอะไรไปน่ะไอ้หนู?";
			mes "เจ้าเอาพวกมะนาวที่ข้า";
			mes "ให้ไปคราวก่อนไปทำอะไรหมด?";
			close;
		case 3:
			mes "[Mr. Jun]";
			mes "โอ้ ข้าเสียใจด้วยนะ.... เมื่อเร็วๆ นี้คลังเก็บมะนาวของข้า";
			mes "เพิ่งโดนพวก ^3131FF'Leaf Cats'^000000 ที่ป้วนเปี้ยนอยู่รอบๆ ศาลเจ้าโจมตีเข้าน่ะ";
			next;
			mes "[Mr. Jun]";
			mes "ข้าก็อยากจะช่วยเจ้าอยู่หรอกนะ";
			mes "แต่ตอนนี้ข้าเองก็มีมะนาวไม่พอสำหรับทำธุรกิจของข้าเหมือนกัน";
			mes "ถ้าเจ้าต้องการมะนาว ทำไมไม่ลองไปล่าพวกมันดูล่ะ?";
			close;
		}
	}
	mes "[Mr. Jun]";
	mes "...";
	close;

OnTouch_:
	if (MaxWeight - Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน! -";
		mes "- ตอนนี้เจ้าแบกไอเทม -";
		mes "- ติดตัวมากเกินไป -";
		mes "- กรุณาลดน้ำหนักของเจ้าลง -";
		mes "- แล้วลองใหม่อีกครั้ง -";
		close;
	}
	if (tomyumgoong == 4) {
		mes "[Mr. Jun]";
		mes "สวัสดี มีอะไรหรือเปล่า?";
		mes "เจ้าต้องการ";
		mes "ความช่วยเหลืออะไรไหม...?";
		emotion ET_SCRATCH;
		next;
		if (select("ข้าต้องการมะนาวนิดหน่อยครับ/ค่ะ.:ไม่เป็นไรครับ/ค่ะ ขอบคุณ") == 1) {
			mes "[Mr. Jun]";
			mes "อืม...";
			mes "มะนาวงั้นรึ?";
			mes "อย่างที่เจ้าเห็น ข้ามีมะนาว";
			mes "อยู่เต็มต้นไปหมด";
			mes "เจ้าต้องการมันจริงๆ หรือ?";
			next;
			if (select("ใช่ครับ/ค่ะ ข้าจะจ่ายเงินให้ท่าน.:ไม่เป็นไรครับ/ค่ะ ขอบคุณ") == 1) {
				mes "[Mr. Jun]";
				mes "ข้าเริ่มปลูกต้นมะนาวนี่เป็นงานอดิเรกน่ะ แต่ไม่คิดเลยว่าจะได้ผลผลิตที่หวาน";
				mes "และฉ่ำน้ำขนาดนี้";
				next;
				mes "[Mr. Jun]";
				mes "เมื่อกี้เจ้าบอกว่า";
				mes "จะจ่ายเงินค่ามะนาวให้ข้าอย่างนั้นรึ?";
				mes "โอ้ เจ้าเข้าใจผิดแล้ว ข้าไม่ได้ตั้งใจจะขายพวกมันหรอก";
				next;
				mes "[Mr. Jun]";
				mes "แต่ว่า...";
				mes "ข้ายินดีอย่างยิ่ง";
				mes "ที่จะแบ่งปันพวกมันบางส่วน";
				mes "ให้กับเจ้า แต่มีเงื่อนไขว่าเจ้าต้อง...";
				next;
				mes "[Mr. Jun]";
				mes "มาเล่นเกมเล็กๆ กับข้าหน่อย!";
				mes "ว่าไงล่ะ? ข้าเบื่อจะตายอยู่แล้ว แม้แต่คนแก่อย่างข้าก็อยากจะสนุกบ้างเหมือนกันนะ!";
				next;
				mes "[Mr. Jun]";
				mes "ฮ่าๆๆๆ~!";
				mes "เจ้าว่ายังไง?";
				mes "อยากจะลองดูไหมล่ะ?";
				next;
				if (select("แน่นอน ทำไมจะไม่ได้ล่ะ!:ไม่ล่ะครับ/ค่ะ ขอบคุณ") == 1) {
					mes "[Mr. Jun]";
					mes "ดีมาก!";
					mes "เกมที่ข้าอยากให้เรา";
					mes "เล่นด้วยกันน่ะ มันเป็น";
					mes "เกมเด็กเล่นง่ายๆ";
					next;
					mes "[Mr. Jun]";
					mes "^660000เป่ายิ้ง";
					mes "ฉุบ!^000000";
					mes "เจ้าอาจจะเคยได้ยิน";
					mes "มันมาจากดินแดนของเจ้าบ้าง";
					emotion ET_SCISSOR;
					next;
					mes "[Mr. Jun]";
					mes "เอาน่า อย่ามองข้าแบบนั้นสิ ข้ามันคนแก่ และข้าแค่อยากเล่นเกมนี้เพื่อย้อนรำลึกความหลัง";
					mes "ตอนเป็นหนุ่มนิดหน่อยน่ะ เดี๋ยวเจ้าถึงอายุเท่าข้าแล้วเจ้าจะเข้าใจเอง";
					next;
					mes "[Mr. Jun]";
					mes "เอาล่ะ ถ้าเจ้าชนะ";
					mes "^3131FF3^000000 ใน ^3131FF5^000000 เกม";
					mes "มะนาวพวกนี้ก็เป็นของเจ้า";
					mes "พร้อมหรือยัง?";
					next;
					mes "[Mr. Jun]";
					mes "เอาละนะ...";
					mes "อย่างที่พวกเด็กๆ ชอบพูดกัน";
					mes "^990000มาลุยกันให้สุดเหวี่ยงไปเลย!^000000";
					next;
					set @user_score,0;
					set @pc_score,0;
					mes "[Mr. Jun]";
					mes "ค้อน!";
					mes "กระดาษ!";
					mes "กรรไกร!";
					next;
					while(1) {
						if (@pc_score == 5 || @user_score == 3) {
							break;
						}
						else {
							set .@jun_mark,rand(1,3);
							switch(select("กรรไกร:ค้อน:กระดาษ")) {
							case 1:
								if (.@jun_mark == 1) {
									emotion ET_SCISSOR;
									emotion ET_SCISSOR, playerattached();
									mes "[Mr. Jun]";
									mes "หึ.";
									mes "เสมอซะได้";
									mes "เอาใหม่อีกรอบ!";
									mes "ค้อน! กระดาษ!";
									mes "กรรไกร!";
									next;
								}
								else if (.@jun_mark == 2) {
									emotion ET_ROCK;
									emotion ET_SCISSOR, playerattached();
									set @pc_score,@pc_score + 1;
									mes "[Mr. Jun]";
									mes "ใช่เลย...!";
									mes "โอ้พระเจ้า ช่วยด้วย ใช่เลย!";
									next;
								}
								else {
									emotion ET_WRAP;
									emotion ET_SCISSOR, playerattached();
									set @pc_score,@pc_score + 1;
									set @user_score,@user_score + 1;
									mes "[Mr. Jun]";
									mes "อะไรนะ...?";
									mes "ข้าไม่เชื่อเลย!";
									mes "บัดซบจริง.. !";
									next;
								}
								break;
							case 2:
								if (.@jun_mark == 1) {
									emotion ET_SCISSOR;
									emotion ET_ROCK, playerattached();
									set @pc_score,@pc_score + 1;
									set @user_score,@user_score + 1;
									mes "[Mr. Jun]";
									mes "ไม่นะ...!";
									mes "เสียลางหมด!";
									next;
								}
								else if (.@jun_mark == 2) {
									emotion ET_ROCK;
									emotion ET_ROCK, playerattached();
									mes "[Mr. Jun]";
									mes "หึ.";
									mes "เสมอซะได้";
									mes "เอาใหม่อีกรอบ!";
									mes "ค้อน! กระดาษ!";
									mes "กรรไกร!";
									next;
								}
								else {
									emotion ET_WRAP;
									emotion ET_ROCK, playerattached();
									set @pc_score,@pc_score + 1;
									mes "[Mr. Jun]";
									mes "ว้าฮ่าฮ่าฮ่า!";
									mes "เจ้าแพ้แล้วไอ้หนู!";
									next;
								}
								break;
							case 3:
								if (.@jun_mark == 1) {
									emotion ET_SCISSOR;
									emotion ET_WRAP, playerattached();
									set @pc_score,@pc_score + 1;
									mes "[Mr. Jun]";
									mes "เหะ เหะ~";
									mes "เจ้าเด็กน้อยผู้อ่อนหัด!";
									next;
								}
								else if (.@jun_mark == 2) {
									emotion ET_ROCK;
									emotion ET_WRAP, playerattached();
									set @user_score,@user_score + 1;
									set @pc_score,@pc_score + 1;
									mes "[Mr. Jun]";
									mes "ไม่นะ...!";
									mes "นี่มันเป็นไปไม่ได้!";
									next;
								}
								else {
									emotion ET_WRAP;
									emotion ET_WRAP, playerattached();
									mes "[Mr. Jun]";
									mes "หึ.";
									mes "เสมอซะได้";
									mes "เอาใหม่อีกรอบ!";
									mes "ค้อน! กระดาษ!";
									mes "กรรไกร!";
									next;
								}
								break;
							}
						}
					}
					if (@user_score == 3) {
						emotion ET_HNG;
						mes "[Mr. Jun]";
						mes "เจ้า... ชนะแล้ว";
						mes "ข้าหมดแรงเลย";
						mes "ข้าเดาว่าข้าคงสู้กับ";
						mes "พวกคนหนุ่มสาวอย่างเจ้า";
						mes "ไม่ไหวแล้วล่ะ ฮ่า ฮ่า ฮ่า!";
						next;
						mes "[Mr. Jun]";
						mes "ตามที่สัญญาไว้";
						mes "ข้าจะแบ่งปันมะนาว";
						mes "ของข้าให้กับเจ้า";
						mes "หยิบไปเท่าที่เจ้าต้องการเลย!";
						mes "^3355FFเจ้าเด็ดมะนาว";
						mes "10 ผลออกมาจากต้น^000000";
						set tomyumgoong,5;
						getitem 568,10; //Lemon
						next;
						emotion ET_THINK;
						mes "[Mr. Jun]";
						mes "อืม...";
						mes "ดูเหมือนเจ้าจะต้องการพวกมันเยอะนะ เอาเถอะ ไม่เป็นไรหรอก ข้าก็สนุกมากเหมือนกันที่ได้เล่นกับเจ้า";
						next;
						mes "[Mr. Jun]";
						mes "แต่อย่าลืมสิ่งนี้นะ:";
						mes "อย่าเห็นแก่ตัว และจงแบ่งปันสิ่งที่มีให้แก่ผู้อื่นเสมอ นั่นคือหนึ่งในคุณค่าที่สำคัญที่สุดของมนุษย์เรา";
						close;
					}
					emotion ET_SCISSOR;
					mes "[Mr. Jun]";
					mes "มูฮ่าฮ่าฮ่าฮ่า!";
					mes "ชัยชนะเป็นของข้า!";
					mes "ดูเหมือนว่าตอนนี้เจ้าจะยังไม่ได้";
					mes "มะนาวไปนะ! แต่เจ้าสามารถกลับมาท้าข้าได้ทุกเมื่อเลย!";
					close;
				}
				mes "[Mr. Jun]";
				mes "กลัวอย่างนั้นรึ?";
				mes "หลอกเจ้าไม่ได้จริงๆ:";
				mes "ข้านี่แหละคือหนึ่งในจ้าวแห่งเกมเป่ายิ้งฉุบ!";
				close;
			}
			mes "[Mr. Jun]";
			mes "อืม...";
			mes "ทีนี้ข้าจะทำ";
			mes "ยังไงดีนะ";
			mes "กับมะนาว";
			mes "ตั้งมากมายพวกนี้?";
			close;
		}
		mes "[Mr. Jun]";
		mes "อย่าเกรงใจที่จะ";
		mes "ขอความช่วยเหลือจากข้า";
		mes "ได้ทุกเมื่อเลยนะ ตกลงไหม?";
		close;
	}
	mes "[Mr. Jun]";
	mes "...";
	close;
}

ayothaya,213,94,7	script	Merchant#ayo	841,{
	if (tomyumgoong == 6) {
		mes "[Merchant Thongdum]";
		mes "สวัสดีจ้ะ~";
		mes "เจ้ากำลังมองหา";
		mes "วัตถุดิบสำหรับทำ";
		mes "ต้มยำกุ้งอยู่ใช่ไหม?";
		next;
		mes "[Merchant Thongdum]";
		mes "ไหนดูซิ...";
		mes "ข้ามีพริกขายนะ";
		mes "มีน้ำปลา แล้วก็พวก";
		mes "เครื่องเทศกับเครื่องปรุง";
		mes "ที่เจ้าต้องใช้ ข้าจะขาย";
		mes "ทั้งหมดนี่ให้เจ้าในราคา 2,000 zeny";
		next;
		if (select("ขอบคุณครับ/ค่ะ ข้าจะรับไว้เอง.:นี่มันขูดเลือดขูดเนื้อกันชัดๆ!") == 1) {
			emotion ET_THANKS;
			if (Zeny > 1999) {
				if (MaxWeight - Weight > 1199) {
					set Zeny, Zeny-2000;
					set tomyumgoong,7;
					getitem 7286,30; //Chilli
					mes "[Merchant Thongdum]";
					mes "ขอบคุณนะ";
					mes "หวังว่าเจ้าจะอร่อยกับ";
					mes "ต้มยำกุ้งของเจ้านะ~";
					close;
				}
				mes "[Merchant Thongdum]";
				mes "โอ๊ะ โอ...";
				mes "ดูเหมือนว่า";
				mes "เจ้าจะแบกของ";
				mes "เยอะเกินไป";
				mes "ในตอนนี้";
				next;
				mes "[Merchant Thongdum]";
				mes "ข้าให้ของเจ้าไม่ได้";
				mes "ถ้าเจ้าไม่มีที่ว่างพอจะเก็บมัน!";
				close;
			}
			mes "[Merchant Thongdum]";
			mes "หืม...?";
			mes "ตอนนี้ ดูเหมือน";
			mes "เจ้าจะไม่มีเงินพอ";
			mes "ที่จะซื้อสิ่งที่เจ้า";
			mes "ต้องการจากข้านะ";
			close;
		}
		emotion ET_STARE_ABOUT;
		mes "[Merchant Thongdum]";
		mes "อย่าพูดอย่างนั้นสิ";
		mes "ราคาของข้าน่ะ";
		mes "สมเหตุสมผลเสมอ ข้าอยากให้เจ้ารู้ว่า";
		mes "ข้าเป็นพ่อค้าที่น่ายกย่องคนหนึ่งนะ";
		close;
	} else if (tomyumgoong == 7) {
		mes "[Merchant Thongdum]";
		mes "ข้าคิดว่าข้าจัดเตรียม";
		mes "วัตถุดิบให้เจ้าพอแล้วนะ แต่";
		mes "พอมาคิดดูอีกที ข้าก็ไม่";
		mes "รู้ว่าเจ้าจะทำต้มยำกุ้ง";
		mes "ปริมาณมากแค่ไหน";
		next;
		mes "[Merchant Thongdum]";
		mes "เจ้าต้องการจะ";
		mes "ซื้อพริกเพิ่มอีกไหม?";
		next;
		if (select("ไม่เป็นไรครับ/ค่ะ ขอบคุณ.:ใช่ครับ/ค่ะ รบกวนด้วย") == 1) {
			mes "[Merchant Thongdum]";
			mes "ลาก่อนนะ~";
			close;
		}
		if (Zeny > 1999) {
			if (MaxWeight - Weight > 1199) {
				set Zeny, Zeny-2000;
				getitem 7286,30; //Chilli
				mes "[Merchant Thongdum]";
				mes "ขอบคุณนะ";
				mes "แล้วแวะมาใหม่ล่ะ!";
				close;
			}
			mes "[Merchant Thongdum]";
			mes "โอ๊ะ โอ...";
			mes "ดูเหมือนว่า";
			mes "เจ้าจะแบกของ";
			mes "เยอะเกินไป";
			mes "ในตอนนี้";
			next;
			mes "[Merchant Thongdum]";
			mes "ข้าให้ของเจ้าไม่ได้";
			mes "ถ้าเจ้าไม่มีที่ว่างพอจะเก็บมัน!";
			close;
		}
		mes "[Merchant Thongdum]";
		mes "หืม...?";
		mes "ตอนนี้ ดูเหมือน";
		mes "เจ้าจะไม่มีเงินพอ";
		mes "ที่จะซื้อสิ่งที่เจ้า";
		mes "ต้องการจากข้านะ";
		close;
	}
	mes "[Merchant Thongdum]";
	mes "สวัสดีจ้ะ!";
	mes "เคยลองกินพริกบ้างหรือเปล่า?";
	next;
	mes "[Merchant Thongdom]";
	mes "แม้หน้าตามันจะดูเล็กนิดเดียว";
	mes "แต่มันก็มีรสชาติที่รุนแรงมากเลยนะ ทั้งเผ็ดและร้อน เพราะฉะนั้นเจ้าควรระวังเวลาที่กินมันเข้าไปด้วย";
	next;
	mes "[Merchant Thongdum]";
	mes "ชาวอโยธยาชื่นชอบอาหารที่รสจัดจ้านและเผ็ดร้อนอย่างมีเอกลักษณ์ ในอาหารอโยธยา เราจะใช้พวก พริก, กระเทียม, ขิง และผักชี";
	next;
	mes "[Merchant Thongdum]";
	mes "ข้ามีพริกขายนะ";
	mes "ถ้าเจ้าต้องการล่ะก็";
	mes "แวะมาหาข้าได้เลย~";
	close;
}
