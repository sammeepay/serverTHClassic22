//===== rAthena Script ======================================= 
//= Quest NPCs related to Lutie
//===== By: ================================================== 
//= TonyMan & Samuray22
//===== Current Version: ===================================== 
//= 1.3b
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Blush, Cake Hat, Candle, Chef Hat, Raccoon Hat, Rainbow Eggshell,
//= Spore Hat, Wonder Nutshell Quests
//===== Additional Comments: ================================= 
//= 1.1 Optimized [Lupus]
//= 1.2 Fixed Chef Hat items [Lupus]
//= 1.3 Update NPC to the Official One. [Samuray22]
//= 1.3a Changed item names to item IDs. [Samuray22]
//= 1.3b Removed a scrap " left in a delitem. [L0ne_W0lf]
//= 1.4 Updated NPCs once again. [L0ne_W0lf]
//============================================================ 

xmas,117,295,4	script	Vending Machine Man	704,{
	mes "[Titicupe]";
	mes "หืม...?";
	mes "ฉัน Titicupe ผู้จัดการโรงงานของเล่นผู้ทรงเกียรติและนักประดิษฐ์อัจฉริยะ ทำสำเร็จอีกแล้ว! ฉันเพิ่งจะสร้างเครื่องขายของอัตโนมัติที่ล้ำสมัยที่สุดในโลกขึ้นมา!";
	next;
	switch(select("คุยกับเขาเรื่องเครื่องขายของอัตโนมัติ:ไอเทมในเครื่องขายของอัตโนมัติ:หยุดคุย")) {
	case 1:
		mes "[Titicupe]";
		mes "คุณเห็นตุ๊กตาหิมะตัวเล็กๆ ที่อยู่ทางซ้ายมือของฉันไหม? เห็นหรือเปล่า? ถ้าคุณมองดูที่ปากของเขาใกล้ๆ คุณจะเห็นว่ามันถูกสร้างมาให้ค่อนข้างใหญ่เลยล่ะ คุณอาจจะบอกว่า มันดูใหญ่เกินไปหน่อยสำหรับจุดประสงค์ปกติทั่วไป...";
		next;
		mes "[Titicupe]";
		mes "คุณเห็นไหม จริงๆ แล้วตุ๊กตาหิมะตัวนั้นคือเครื่องขายของอัตโนมัติสุดมหัศจรรย์ที่ฉันประดิษฐ์ขึ้นมาจากเลือด หยาดเหงื่อ น้ำตา และหิมะของฉันเลยนะ";
		next;
		mes "[Titicupe]";
		mes "คุณสามารถใส่ของสารพัดอย่างลงไปในปากของมันได้ และถ้าคุณใส่ไอเทมที่ถูกต้องลงไป คุณจะได้หมวกบางใบเป็นการตอบแทน...";
		next;
		mes "[Titicupe]";
		mes "หมวกแฟชั่นที่คุณใฝ่ฝันอยากจะสวมใส่มาตลอด...! บว่ะฮ่าฮ่าฮ่าฮ่า~!";
		next;
		mes "[Titicupe]";
		mes "โอ้ สิ่งประดิษฐ์ของฉันชิ้นนี้มันช่างยอดเยี่ยมเหลือเกิน!";
		emotion ET_CHUPCHUP;
		next;
		mes "[Titicupe]";
		mes "และหมวกพวกนั้น--! ฉัน... ฉันควบคุมตัวเองไม่ได้แล้ว!";
		next;
		mes "^3355FFTiticupe ผู้จัดการโรงงานของเล่นกระโดดและชักดิ้นชักงอด้วยความดีใจ เห็นได้ชัดว่าเขาเสียสติไปแล้ว แต่ก็เป็นไปได้ว่าเขาอาจจะเป็นอัจฉริยะจริงๆ^000000";
		emotion ET_SURPRISE;
		close;
	case 2:
		mes "[Titicupe]";
		mes "อ้อ ใช่แล้ว! คุณคงอยากรู้สิว่าต้องใส่ไอเทมชนิดไหนลงในเครื่องขายของอัตโนมัติเพื่อจะได้ของที่ต้องการใช่ไหม? เอาเลย... ถามฉันมาสิ~";
		emotion ET_AHA;
		next;
		switch(select("Raccoon Hat:Spore Hat:Wonder Nutshell:Ranbow Eggshell:Blush:Chef Hat:Candle:Cake Hat:จบการสนทนา")) {
		case 1:
			mes "[Titicupe]";
			mes "Raccoon Hat เหรอ?";
			mes "1 Kitty Band";
			mes "20 Dragon Scale";
			mes "200 Tough Scalelike Stem";
			mes "300 Sea-otter Fur";
			break;
		case 2:
			mes "[Titicupe]";
			mes "Spore Hat เหรอ?";
			mes "850 Poison Spore";
			mes "300 Burnt Tree";
			mes "1 Tongue";
			break;
		case 3:
			mes "[Titicupe]";
			mes "Wonder Nutshell เหรอ?";
			mes "1 Nut Shell";
			mes "500 Wing of Dragonfly";
			break;
		case 4:
			mes "[Titicupe]";
			mes "Rainbow Eggshell เหรอ?";
			mes "1 Egg Shell";
			mes "1 Cobaltblue Dyestuffs";
			mes "50 Claw of Desert Wolf";
			break;
		case 5:
			mes "[Titicupe]";
			mes "Blush?!";
			mes "คุณนี่เอง!";
			mes "คุณรู้อะไรบางอย่างเข้าแล้วสิ...";
			mes "เหะ เหะ~ เอาเป็นว่า";
			mes "100 Alice's Apron";
			break;
		case 6:
			mes "[Titicupe]";
			mes "Chef Hat เหรอ?";
			mes "120 Piece of Cake";
			mes "1 White Dyestuffs";
			mes "330 Feather";
			mes "450 Dragon Scale";
			break;
		case 7:
			mes "[Titicupe]";
			mes "Candle เหรอ?";
			mes "1 Bomb Wick";
			mes "50 Matchstick";
			mes "100 Royal Jelly";
			break;
		case 8:
			mes "[Titicupe]";
			mes "Cake Hat เหรอ?";
			mes "10 Candy";
			mes "5 Candy Cane";
			mes "15 Well-baked Cookie";
			mes "20 Piece Of Cake";
			mes "10 Steel";
			break;
		case 9:
			mes "[Titicupe]";
			mes "ถามฉันได้ตลอดเวลาเลยนะ คำถามพวกนี้ไม่มีปัญหาสำหรับอัจฉริยะอย่างฉันอยู่แล้ว!";
		}
		next;
		mes "[Titicupe]";
		mes "มีความสุขจัง!!";
		mes "ฉันมันอัจฉริยะ~!";
		mes "โฮะ โฮะ โฮะ";
		mes "โฮะ โฮะ โฮะ โฮะ!";
		close;
	case 3:
		mes "[Titicupe]";
		mes "บางครั้ง ฉันก็แทบไม่เชื่อเลยว่าสิ่งประดิษฐ์ของฉันมันจะวิเศษขนาดนี้...!";
		next;
		mes "[Titicupe]";
		mes "ฉัน...";
		mes "ฉันรักคุณนะ";
		mes "คุณตุ๊กตาหิมะ";
		mes "เครื่องขายของอัตโนมัติ";
		emotion ET_CHUPCHUP;
		next;
		mes "^3355FFTiticupe ผู้จัดการโรงงานของเล่นเริ่มกระโดดไปรอบๆ และแผดเสียงร้องด้วยความปลาบปลื้มอย่างบ้าคลั่ง ในจุดนี้ มันไม่ยากเลยที่จะสงสัยในสติสัมปชัญญะของเขาพอๆ กับความอัจฉริยะของเขา";
		close;
	}
}

xmas,115,297,4	script	Vending Machine	111,{
	mes "[Audi]";
	mes "^555555เครื่องขายไอเทมอัตโนมัติสุดตื่นเต้น ประดิษฐ์โดย Titicupe ผู้ยิ่งใหญ่ โปรดเลือกไอเทมที่ต้องการและวางไอเทมที่กำหนดลงในช่องรับของที่ปากตุ๊กตาหิมะ^000000";
	next;
	switch(select("^FF3355ประกาศ (ต้องอ่าน)^000000:Raccoon Hat:Spore Hat:Wonder Nutshell:Rainbow Eggshell:Blush:Chef Hat:Candle:Cake Hat:ยกเลิก")) {
	case 1:
		mes "[Audi]";
		mes "สวัสดี~ !";
		mes "ฉันมีเรื่องสำคัญอย่างหนึ่งที่ต้องบอกคุณ เครื่องขายของอัตโนมัตินี้ไม่ได้ถูกตั้งค่ามาให้แยกแยะระหว่างไอเทมพิเศษกับไอเทมธรรมดา";
		next;
		mes "[Audi]";
		mes "ดังนั้นหากคุณใช้ไอเทมที่ได้รับการ ^FF3355อัปเกรด (ตีบวก) หรือมีการใส่การ์ดไว้^000000 การ์ดหรือการอัปเกรดทั้งหมดจะสูญหายไปในระหว่างกระบวนการสร้างไอเทมใหม่";
		next;
		mes "[Audi]";
		mes "ดังนั้นเมื่อคุณพยายามจะแลกเปลี่ยนและสร้างไอเทม โปรดนำไอเทมมีค่าที่คุณไม่ต้องการเสียไปเก็บไว้ในคลังของ Kafra ก่อน";
		next;
		mes "[Audi]";
		mes "โปรดจำข้อนี้ไว้ให้ดี เพราะเครื่องขายของอัตโนมัตินี้ไม่มีการคืนของ คืนเงิน หรือแลกเปลี่ยนใดๆ ทั้งสิ้น";
		next;
		mes "[Audi]";
		mes "ขอให้มีความสุขนะ!";
		mes "ขอบคุณ!";
		close;
	case 2:
		if ((countitem(2213) > 0) && (countitem(1036) > 19) && (countitem(7012) > 199) && (countitem(7065) > 299)) {
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 2213,1; //Cat_Hairband
			delitem 1036,20; //Dragon_Scale
			delitem 7012,200; //Tough_Scalelike_Stem
			delitem 7065,300; //Sea_Otter_Leather
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*^000000";
			getitem 5033,1; // Smokie_Hat
			next;
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 3:
		if ((countitem(7033) > 849) && (countitem(7068) > 299) && (countitem(1015) > 0)) {
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 7033,850; //Poison_Spore
			delitem 7068,300; //Burn_Tree
			delitem 1015,1; //Thin_N'_Long_Tongue
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*";
			mes "*แว๊ก แว๊ก*^000000";
			getitem 5029,1; // Spore_Hat
			next;
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 4:
		if ((countitem(5037) > 0) && (countitem(7064) > 499)) {
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 5037,1; //Fruit_Shell
			delitem 7064,500; //Dragon_Fly_Wing
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*";
			mes "*บี๊บบู๊บบี๊บบู๊บ*";
			mes "*บี๊บบบบบบบบ*^000000";
			emotion ET_O;
			getitem 5050,1; // Mystery_Fruit_Shell
			next;
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 5:
		if ((countitem(5015) > 0) && (countitem(978) > 0) && (countitem(7030) > 49)){
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 5015,1; //Egg_Shell
			delitem 978,1; //Cobaltblue_Dyestuffs
			delitem 7030,50; //Claw_Of_Desert_Wolf
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*อู้วววววว อี้เย้อออออ~*^000000";
			getitem 5039,1; // Mottled_Egg_Shell
			next;
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 6:
		if (countitem(7047) > 99) {
			mes "^3355FFคุณค่อยๆ วางผ้ากันเปื้อนทั้ง 100 ชิ้นลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 7047,100; //Alice's_Apron
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*";
			mes "*จ๊วบจ๊วบ*";
			mes "*ชว้าาาาาา!*^000000";
			getitem 5040,1; // Blush
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			next;
			mes "[Titicupe]";
			mes "คุณได้ Blush ไปแล้วเหรอ?! เหอะ ฉันเดาว่าคุณคงชอบดูดีมีเสน่ห์จริงๆ สินะ~! ฉันพนันได้เลยว่าตอนนี้คุณคงเห็นค่าในความเป็นอัจฉริยะของฉันแล้วล่ะสิ หืม?";
			close;
		}
		break;
	case 7:
		if ((countitem(539) > 119) && (countitem(982) > 0) && (countitem(949) > 329) && (countitem(1036) > 449)) {
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 539,120; //Piece_Of_Cake
			delitem 982,1; //White_Dyestuffs
			delitem 949,330; //Feather
			delitem 1036,450; //Dragon_Scale
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*";
			mes "*มูววววววว กี้ๆๆๆๆๆ*^000000";
			getitem 5026,1; // Hat_Of_Cook
			next;
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 8:
		if ((countitem(2279) > 0) && (countitem(7035) > 49) && (countitem(526) > 99)) {
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 2279,1; //Bomb_Wick
			delitem 7035,50; //Matchstick
			delitem 526,100; //Royal_Jelly
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*^000000";
			getitem 5028,1; // Candle
			next;
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 9:
		if ((countitem(529) > 9) && (countitem(530) > 4) && (countitem(538) > 14) && (countitem(539) > 19) && (countitem(999) > 9)) {
			mes "^3355FFคุณค่อยๆ วางไอเทมลงในปากของตุ๊กตาหิมะอย่างระมัดระวัง^000000";
			next;
			delitem 529,10; //Candy
			delitem 530,5; //Candy_Striper
			delitem 538,15; //Well_Baked_Cookie
			delitem 539,20; //Piece_Of_Cake
			delitem 999,10; //Steel
			emotion ET_O;
			mes "^3355FF*บรื๊นนนนนน~~*";
			mes "*บึซซซซซซซ*";
			mes "*ฉึกฉักๆๆๆ*";
			mes "*กะปัง!*^000000";
			getitem 5024,1; // Hat_Of_Cake
			next;
			mes "[Audi]";
			mes "^555555ขอบคุณที่แวะมา";
			mes "โปรดมาใช้บริการใหม่นะ!";
			mes "โอ้ แล้วอย่าลืมบอกเพื่อนด้วยล่ะ!^000000";
			close;
		}
		break;
	case 10:
		mes "[Audi]";
		mes "^555555ได้โปรด...";
		mes "ใส่...";
		mes "ไอเทมลงไป^000000";
		close;
	}
	emotion ET_X;
	mes "[Audi]";
	mes "^555555Error Error!";
	mes "ไอเทมไม่ถูกต้อง!^000000";
	next;
	mes "[Titicupe]";
	mes "ฉัน...";
	mes "ฉันว่าคนจำนวนมากยอมทำทุกอย่างเพื่อให้ได้เป็นเจ้าของ Blush นั่นสินะ";
	close;
}
