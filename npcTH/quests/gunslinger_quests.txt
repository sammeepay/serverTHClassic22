//===== rAthena Script =======================================
//= Gunslinger quests
//===== By: ==================================================
//= Playtester
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Gunslinger guns and bullet quests
//===== Additional Comments: =================================
//= 1.0 Only Garrison Gun done yet [Playtester]
//= 1.1 Added Garrison[2] [Playtester]
//= 1.2 Added Drifter, Destroyer and Inferno [Playtester]
//= 1.3 Added official Gatling quests [Playtester]
//= 1.4 Updated Destroyer Quest - Credits to Sinoco for first
//= official version. [SinSloth]
//= 1.5 Updated Inferno Quest to official version [SinSloth]
//= 1.6 Updated Garrison Quest - Credits to Lazarus^ for
//= making the first official version. [SinSloth]
//= 1.6a Removed .GATs [Lupus]
//= 1.6b Changed item names to item IDs on some scripts. [Samuray22]
//= 1.6c Corrected a bad delitem. (bugreport:763) [Samuray22]
//= 1.7 Removed all 'menu' and 'goto'. [Euphy]
//============================================================

que_ng,182,85,3	script	Garrison	109,{

	if(BaseJob != Job_Gunslinger)
	{
		mes "[Garrison]";
		mes "คุณไม่ใช่ Gunslinger";
		mes "คุณกำลังขัดขวางการทำงานของข้า ไปซะ";
		close;
	}
	else if(BaseLevel < 55)
	{
		mes "[Garrison]";
		mes "ข้าชื่อ Garrison เป็นปรมาจารย์ในการสร้างปืน";
		mes "ปืนที่ข้าสร้างก็ถูกเรียกว่า Garrison เช่นกัน ข้าตั้งชื่อมันแบบนั้นเพราะข้าคิดว่า Garrison นั้นเท่และสมบูรณ์แบบเหมือนตัวข้าเอง";
		next;
		mes "[Garrison]";
		mes "ทำไมเจ้าไม่ลองสั่งทำ Garrison จากข้าดูล่ะ?";
		next;
		callsub SubGarrison;
	}
	switch(gun_gs)
	{
		case 0:
			if(countitem(13104) < 1)
			{
				mes "[Garrison]";
				mes "ข้าชื่อ Garrison เป็นปรมาจารย์ในการสร้างปืน";
				mes "ปืนที่ข้าสร้างก็ถูกเรียกว่า Garrison เช่นกัน ข้าตั้งชื่อมันแบบนั้นเพราะข้าคิดว่า Garrison นั้นเท่และสมบูรณ์แบบเหมือนตัวข้าเอง";
				next;
				mes "[Garrison]";
				mes "เจ้ามาที่นี่เพื่อสั่งทำ Garrison จากข้าอย่างนั้นหรือ?";
				next;
				callsub SubGarrison;
			}
			mes "[Garrison]";
			mes "หืม~ เกิดอะไรขึ้น?";
			mes "เจ้ามาที่นี่เพราะต้องการอาวุธงั้นหรือ?";
			next;
			switch( select( "ข้าต้องการ Garrison.","ไม่เชิงนะ.","ยกเลิก") )
			{
				case 1:
					mes "[Garrison]";
					mes "โอ้~ ยินดีต้อนรับ";
					mes "ใครๆ ก็ต้องการข้าทั้งนั้น~!";
					mes "มันเป็นเครื่องพิสูจน์ว่าปืน Garrison ของข้าน่ะ ดีที่สุดในบรรดาสิ่งที่เยี่ยมยอดที่สุดแล้ว";
					mes "เอาล่ะ เจ้าจะให้ข้าสร้างให้ตอนนี้เลยไหม?";
					next;
					callsub SubGarrison;

				case 2:
					mes "[Garrison]";
					mes "หืม~ ดูเหมือนเจ้ากำลังเดินเตร่ไปมาโดยไม่มีจุดหมายนะ";
					mes "ถ้าเจ้ามีเวลา เจ้าจะช่วยอะไรข้าหน่อยได้ไหม?";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "มันคืออะไรล่ะ?";
					next;
					mes "[Garrison]";
					mes "อืม~ คือว่า จริงๆ แล้วข้าเพิ่งได้รับจดหมายระบุว่ามีปืนที่ข้าสร้างที่ชื่อว่า Six Shooter จำนวนมากที่มีตำหนิ";
					next;
					mes "[Garrison]";
					mes "ข้าเป็นพวกนิยมความสมบูรณ์แบบ แต่ด้วยอายุที่มากขึ้น ตาของข้าเริ่มพร่ามัว และข้าคิดว่าข้าอาจจะขาย Six Shooter ที่มีตำหนิออกไป";
					next;
					mes "[Garrison]";
					mes "ดังนั้นสิ่งที่ข้ากำลังจะบอกคือ ข้าหวังว่าจะมีใครสักคนหาชิ้นส่วนอะไหล่มาให้ข้า เพื่อที่ข้าจะใช้เปลี่ยนชิ้นส่วนที่เสียใน Six Shooter พวกนั้น";
					next;
					mes "[Garrison]";
					mes "ถ้าเจ้าช่วยข้า ข้าจะเจาะรู (slot) ให้กับ Garrison ที่เจ้าถืออยู่ตอนนี้";
					mes "เจ้าว่ายังไงล่ะ?";
					next;
					mes "[Garrison]";
					mes "บอกไว้ก่อนนะ ถ้าข้าเจาะรูให้ Garrison ของเจ้า ค่าตีบวกหรือการ์ดที่ใส่อยู่เดิมจะหายไปทั้งหมด ตัดสินใจให้ดีล่ะ";
					next;
					switch( select( "ข้าไม่ค่อยชอบเงื่อนไขนี้เลย.","ตกลง ข้าจะลองดู.") )
					{
						case 1:
							mes "[Garrison]";
							mes "หืม ข้าเข้าใจแล้ว";
							mes "ข้าเข้าใจ";
							mes "ข้าคงต้องไปหาคนอื่นแทน";
							close;

						case 2:
							mes "[Garrison]";
							mes "โอ้ เจ้าจะช่วยงั้นหรือ?";
							mes "ข้าขอบใจมาก";
							mes "นี่คือวัตถุดิบที่ข้าต้องการเพื่อทำชิ้นส่วนอะไหล่...";
							next;
							mes "[Garrison]";
							mes "10 Steel, 1 Elunium, 10 Emveretarcon, 30 Coal, และ 10 Rusty Screws";
							mes "ข้าต้องการจำนวนที่เป๊ะตามนี้ อย่าลืมล่ะ";
							set gun_gs,1;
							close;
					}

				case 3:
					mes "[Garrison]";
					mes "หืม... แล้วค่อยมาพบข้าใหม่ล่ะกัน";
					close;
			}

		case 1:
			if((countitem(999) < 10) || (countitem(7317) < 10) || (countitem(1011) < 10) || (countitem(1003) < 30) || (countitem(985) < 1))
			{
				mes "[Garrison]";
				mes "10 Steel, 1 Elunium, 10 Emveretarcon, 30 Coal, และ 10 Rusty Screws";
				mes "อย่าลืมล่ะ -- จำนวนต้องเป๊ะตามนี้";
				close;
			}
			mes "[Garrison]";
			mes "โอ้~ เจ้ากลับมาพร้อมสิ่งที่ข้าต้องการแล้ว~";
			mes "ข้าซาบซึ้งมาก";
			mes "โอ้.. ยังมีอีกเรื่องที่ข้าอยากให้เจ้าช่วยหน่อย เอ่อ...";
			next;
			mes "[Garrison]";
			mes "ข้าต้องส่งชิ้นส่วนอะไหล่พวกนี้ไปให้คนอื่นทันทีที่สร้างเสร็จ แต่ข้ามีงานล้นมือเลยที่นี่";
			next;
			mes "[Garrison]";
			mes "ขอโทษที่ต้องขอแรงนะ แต่ข้าหวังว่าเจ้าจะสามารถนำของที่ข้าสร้างจากวัตถุดิบพวกนี้ไปส่งให้คนต่อไปที";
			next;
			mes "[Garrison]";
			mes "ในขณะที่เจ้าไปส่งของ ข้าจะเตรียมการเจาะรู Garrison ของเจ้าตามที่สัญญาไว้";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "-หืม ข้าไม่อยากทำเลย แต่ก็ต้องทำอยู่ดี-";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ตกลง";
			next;
			mes "[Garrison]";
			mes "โอ้ ขอบใจมาก";
			mes "ถ้างั้นจงนำสิ่งนี้ไปส่งให้คนที่ชื่อ Ravey เขาอาศัยอยู่ในย่านสลัมของ Lighthalzen";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "คุณได้รับสินค้าที่จะนำไปส่งจาก Garrison";
			mes "มานำสิ่งนี้ไปส่งให้คนที่ชื่อ Ravey ซึ่งอาศัยอยู่ในย่านสลัมของ Lighthalzen กันเถอะ";
			delitem 999,10;
			delitem 1011,10;
			delitem 7317,10;
			delitem 985,1;
			delitem 1003,30;
			set gun_gs,2;
			close;

		case 2:
			mes "["+strcharinfo(0)+"]";
			mes "คุณได้รับสินค้าที่จะนำไปส่งจาก Garrison";
			mes "มานำสิ่งนี้ไปส่งให้คนที่ชื่อ Ravey ซึ่งอาศัยอยู่ในย่านสลัมของ Lighthalzen กันเถอะ";
			close;

		case 3:
			mes "[Garrison]";
			mes "โอ้~ การเดินทางเป็นอย่างไรบ้าง?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "อ๊ากกก~";
			mes "ข้าเกือบตายแหน่ะ!!!";
			mes "ชายคนนั้นพยายามโจมตีข้าทันทีที่เห็นข้า ข้าหนีความตายมาได้อย่างหวุดหวิดเลยนะ!";
			next;
			mes "[Garrison]";
			mes "อ่า~ โอ้ ไม่นะ";
			mes "ข้าไม่รู้เลยว่าคนที่ส่งจดหมายมาจริงๆ แล้ววางแผนจะลอบสังหารข้า เขาคงถูกส่งมาจากกิลด์ศัตรูของเราแน่ๆ...";
			next;
			mes "[Garrison]";
			mes "ดูเหมือนข้าจะส่งเจ้าไปทำภารกิจที่อันตรายมากโดยไม่ได้ตั้งใจสินะ";
			next;
			mes "[Garrison]";
			mes "ข้าขอโทษจริงๆ";
			mes "เจ้าอาจจะรู้อยู่แล้ว แต่มีพวกคนเลวที่คอยวางแผนโจมตี Professor และ Gunslinger Guild อยู่ทุกที่";
			next;
			mes "[Garrison]";
			mes "ถือซะว่านี่เป็นส่วนหนึ่งของการฝึกฝนเพื่อเป็น Gunslinger ของเจ้าก็แล้วกัน";
			next;
			mes "[Garrison]";
			mes "ข้าเสียใจกับสิ่งที่เกิดขึ้น";
			mes "แต่ตามที่สัญญาไว้ ข้าจะเจาะรู Garrison ให้เจ้า...";
			next;
			if(countitem(13104) < 1)
			{
				mes "[Garrison]";
				mes "หือ? เฮ้อ";
				mes "เจ้าขายปืนทิ้งไปตอนที่ไม่อยู่หรือเปล่าเนี่ย?";
				mes "ข้าบอกว่าจะเจาะรูให้ปืนเจ้า แต่ข้าไม่เคยบอกว่าจะเจาะรูให้ปืนที่ไม่ได้อยู่ที่นี่นะ";
				mes "ไปเอา Garrison ของเจ้ามาแล้วค่อยกลับมาหาข้า";
				set gun_gs,4;
				close;
			}
			mes "[Garrison]";
			mes "ไหนดูซิ~";
			mes "*ต๊อก* *ต๊อก* *ตุ้บ* *ตั้บ*";
			mes "นี่คือ Garrison ของเจ้า";
			mes "ที่ได้รับการปรับปรุงแล้ว";
			mes "โปรดใช้มันให้ดีล่ะ";
			delitem 13104,1;
			getitem 13105,1;
			set gun_gs, 5;
			close;
		case 4:
			if(countitem(13104) < 1)
			{
				mes "[Garrison]";
				mes "ไปเอา Garrison ของเจ้ามาแล้วค่อยกลับมาหาข้า";
				close;
			}
			mes "[Garrison]";
			mes "อา อยู่นี่เอง";
			mes "นี่คือ Garrison ของเจ้า";
			mes "ที่ได้รับการปรับปรุงแล้ว";
			mes "ใช้มันให้ดีล่ะ";
			delitem 13104, 1;
			getitem 13105, 1;
			set gun_gs,5;
			close;
		case 5:
			mes "[Garrison]";
			mes "หืม~ เจ้ากลับมาอีกแล้วรึ~";
			mes "มีเรื่องอะไรล่ะ?";
			mes "กลับมาเพราะต้องการอาวุธงั้นหรือ?";
			mes "เจ้าต้องการอะไร?";
			next;
			switch( select( "Garrison","เจาะรู Garrison (Slot)","ยกเลิก"))
			{
				case 1:
					callsub SubGarrison;
				case 2:
					callsub SubGarrison_;
				case 3:
					mes "[Garrison]";
					mes "แล้วเจอกันเมื่อถึงเวลา~";
					close;
			}
	}

SubGarrison:
	mes "[Garrison]";
	mes "ในการสร้าง Garrison ข้าต้องการ...";
	mes "50 Steel, 3 Eluniums,";
	mes "1 Oridecon, 50 Coal,";
	mes "20 Rusty Screws, และ";
	mes "มีค่าธรรมเนียม 30,000 Zeny";
	mes "เอาล่ะ เจ้าต้องการสักอันไหม?";
	next;
	switch( select( "ไว้วันหลังล่ะกัน.","ใช่ สร้างให้ข้าเดี๋ยวนี้เลย.","ยกเลิก") )
	{
		case 1:
			mes "[Garrison]";
			mes "หืม~ ข้าตื่นเต้นฟรีเลยนะเนี่ย~";
			mes "ลองกลับไปคิดดู ถ้าตัดสินใจได้แล้วก็มาหาข้า";
			close;

		case 2:
			if((countitem(999) < 50) || (countitem(7317) < 20) || (countitem(984) < 1) || (countitem(1003) < 50) ||(countitem(985) < 3) )
			{
				mes "[Garrison]";
				mes "เจ้าเอาวัตถุดิบมาไม่พอ";
				mes "ในการสร้าง Garrison ข้าต้องการ...";
				mes "50 Steel, 3 Eluniums,";
				mes "1 Oridecon, 50 Coal,";
				mes "50 Rusty Screws,";
				mes "และมีค่าธรรมเนียม 30,000 Zeny";
				mes "อย่าลืมล่ะ";
				close;
			}
			else if(Zeny < 30000)
			{
				mes "[Garrison]";
				mes "เจ้าต้องมี Zeny มากกว่านี้~!";
				mes "Zeny มากกว่านี้!!";
				close;
			}
			else if(checkweight(13104,1) == 0)
			{
				mes "[Garrison]";
				mes "ไม่มีพื้นที่ในช่องเก็บของ";
				mes "สำหรับสิ่งที่ข้าสร้าง";
				mes "ไปจัดที่ว่างในกระเป๋าของเจ้า";
				mes "แล้วค่อยกลับมา";
				mes "พบข้าใหม่";
				close;
			}
			mes "[Garrison]";
			mes "หืม ดูเหมือนจำนวนวัตถุดิบและ Zeny จะถูกต้องนะ";
			mes "นี่คือ Garrison~";
			mes "ถ้าเจ้าต้องการอีก ก็มาหาข้าได้เสมอ~";
			delitem 984,1; //Oridecon
			delitem 985,3; //Elunium
			delitem 999,50; //Steel
			delitem 1003,50; //Coal
			delitem 7317,20; //Rusty Screw
			set Zeny,Zeny-30000;
			getitem 13104,1; //Garrison
			close;

		case 3:
			mes "[Garrison]";
			mes "แล้วเจอกันเมื่อถึงเวลา~";
			close;
	}

SubGarrison_:
	mes "[Garrison]";
	mes "ในการเจาะรู Garrison ข้าต้องการ";
	mes "10 Steel, 1 Elunium,";
	mes "10 Emveretarcon, 30 Coal,";
	mes "10 Rusty Screws, และ";
	mes "1 Garrison";
	mes "เอาล่ะ เจ้าต้องการทำไหม?";
	next;
	switch( select( "ไว้วันหลังล่ะกัน.","ใช่ ทำให้ข้าเดี๋ยวนี้เลย.","ยกเลิก" ))
	{
		case 1:
			mes "[Garrison]";
			mes "เจ้าทำข้าตื่นเต้นฟรีอีกแล้วนะ~";
			mes "กลับมาหาข้าเมื่อเจ้าตัดสินใจได้แล้วล่ะกัน";
			close;

		case 2:
			if((countitem(999) < 10) || (countitem(7317) < 10) || (countitem(1011) < 10) || (countitem(1003) < 30) || (countitem(985) < 1) || (countitem(13104) < 1))
			{
				mes "[Garrison]";
				mes "เจ้าเอาวัตถุดิบมาไม่พอ";
				mes "ในการเจาะรู Garrison ข้าต้องการ";
				mes "10 Steel, 1 Elunium,";
				mes "10 Emveretarcon, 30 Coal,";
				mes "10 Rusty Screws, และ";
				mes "1 Garrison";
				mes "อย่าลืมล่ะ";
				close;
			}
			else if(checkweight(13105,1) != 1)
			{
				mes "[Garrison]";
				mes "ไม่มีพื้นที่ในช่องเก็บของ";
				mes "สำหรับสิ่งที่ข้าสร้าง";
				mes "ไปจัดที่ว่างในกระเป๋าของเจ้า";
				mes "แล้วค่อยกลับมา";
				mes "พบข้าใหม่";
				close;
			}
			mes "[Garrison]";
			mes "หืม ดูเหมือนจำนวนวัตถุดิบจะถูกต้องนะ";
			mes "นี่คือ Garrison ที่เจาะรูและปรับปรุงแล้ว";
			mes "ถ้าเจ้าต้องการอีก ก็มาหาข้าได้เสมอ~";
			delitem 999, 10;
			delitem 1011, 10;
			delitem 7317, 10;
			delitem 985, 1;
			delitem 1003, 30;
			delitem 13104, 1;
			getitem 13105, 1;
			close;
		
		case 3:
			mes "[Garrison]";
			mes "แล้วเจอกันเมื่อถึงเวลา~";
			close;
	}
}

lighthalzen,322,247,6	script	Ravey	86,{

	if(gun_gs == 2)
	{
		mes "["+strcharinfo(0)+"]";
		mes "ขอประทานโทษครับ สวัสดีครับ";
		mes "ข้ามาเพื่อนำชิ้นส่วนอะไหล่สำหรับปืน Gunsling ที่บกพร่องมาให้...";
		next;
		mes "[Ravey]";
		mes "แก!!!";
		mes "หึ้ยยยย~!!";
		mes "ตายซะ!!!";
		set gun_gs,3;
		percentheal 100,0;
		percentheal -90,0;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "- ชายที่ชื่อ Ravey จู่โจมทันที ราวกับว่าเขารอคำว่า Gunslinger อยู่... -";
		mes "คุณเกือบตาย -";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "คุณวิ่งหนีโดยไม่หันกลับไปมอง";
		mes "รีบกลับไปหา Garrison กันเถอะ -";
		close;
	}
	if(gun_gs == 3)
	{
		mes "["+strcharinfo(0)+"]";
		mes "- ชายที่ชื่อ Ravey จู่โจมทันที ราวกับว่าเขารอคำว่า Gunslinger อยู่... -";
		mes "คุณเกือบตาย -";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "รีบกลับไปหา Garrison กันเถอะ -";
		close;
	}
	mes "[Ravey]";
	mes "อึก...";
	mes "เอื้ออ.. เอื้ออ..";
	mes "......";
	close;
}


que_ng,187,163,3	script	Ingrid	744,{

	if(BaseJob != Job_Gunslinger)
	{
		mes "[Ingrid]";
		mes "สวัสดีค่ะ!";
		mes "ข้าได้รับการแต่งตั้งให้เป็นผู้สร้างอาวุธ Gunslinger คนใหม่ ชื่อของข้าคือ Ingrid ค่ะ";
		next;
		mes "[Ingrid]";
		mes "อย่างไรก็ตาม ดูเหมือนเจ้าจะไม่ใช่สมาชิกของ Gunslinger Guild ดังนั้นข้าจึงช่วยอะไรเจ้าไม่ได้ ขอโทษด้วยนะ";
		close;
	}
	switch(gun_inf)
	{
		case 0:
			mes "[Ingrid]";
			mes "สวัสดีค่ะ!";
			mes "ข้าได้รับการแต่งตั้งให้เป็นผู้สร้างอาวุธ Gunslinger";
			mes "คนใหม่ ชื่อของข้าคือ Ingrid ค่ะ";
			mes "ยินดีที่ได้ให้บริการนะคะ!";
			next;
			mes "[Ingrid]";
			mes "มันเพิ่งผ่านไปเพียงครู่เดียว";
			mes "ตั้งแต่ข้าเริ่มทำงาน";
			mes "แต่ข้าจะพยายามอย่างเต็มที่เพื่อ";
			mes "ช่วยเหลือเจ้านะคะ";
			next;
			mes "[Ingrid]";
			mes "อ๊า ... ข้ารู้สึกประหม่าจัง";
			mes "ในตอนนี้ ข้ากำลังทำหน้าที่สร้างอาวุธที่เรียกว่า Inferno ให้กับลูกค้าอยู่ค่ะ";
			next;
			mes "[Ingrid]";
			mes "Inferno คืออาวุธที่ล้ำสมัยที่สุดที่สร้างขึ้นในห้องแล็บของกิลด์เรา";
			next;
			mes "[Ingrid]";
			mes "มันโดดเด่นด้วยพลังและระยะการยิงที่เหลือเชื่อ และการออกแบบที่ถูกหลักสรีรศาสตร์เพื่อให้ถือได้กระชับมือและช่วยลดแรงถีบกลับเมื่อยิงได้อย่างมาก";
			mes "มันเป็นอาวุธที่คู่ควรกับการถูกเรียกว่าสุดยอดอาวุธสำหรับ Gunslinger ทุกคนเลยค่ะ";
			mes "แฮ่ก แฮ่ก...";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "เจ้ารู้เรื่องนี้ดีทีเดียวนะ";
			mes ".....";
			next;
			mes "[Ingrid]";
			mes "อา แน่นอนค่ะ~";
			mes "ข้านี่แหละเป็นคนออกแบบอาวุธชิ้นนี้เอง..";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ว้าววว~ สุดยอดไปเลยค่ะ~";
			next;
			mes "[Ingrid]";
			mes "อา อุ๊ย...";
			mes "ข้าเผลอโม้ไปหรือเปล่านะ?";
			mes "ข้ายังเทียบไม่ได้เลยกับ Professor Serena ค่ะ";
			next;
			mes "[Ingrid]";
			mes "จริงๆ แล้วข้าก็อยากเป็น Gunslinger เหมือนกันนะคะ แต่เพราะข้อบกพร่องทางกายภาพและความมุ่งมั่นที่อ่อนแอ ข้าเลยต้องถอดใจไปค่ะ";
			next;
			mes "[Ingrid]";
			mes "ในขณะที่ข้าสมัครเป็น Gunslinger ข้าโชคดีมากที่ Professor Serena มองเห็นความสามารถของข้า และด้วยความช่วยเหลือของท่าน ข้าเลยได้มาอยู่จุดนี้เพื่อช่วยในการผลิตและจำหน่ายอาวุธ Gunslinger ค่ะ";
			next;
			mes "[Ingrid]";
			mes "ข้ายังรู้สึกเสียดายที่ไม่ได้เป็น Gunslinger แต่อย่างน้อย พี่ชายของข้าที่เข้าทดสอบ Gunslinger พร้อมกับข้าก็ผ่านและได้เป็นค่ะ";
			next;
			mes "[Ingrid]";
			mes "ข้าดีใจที่ถึงแม้ข้าจะตกรอบ แต่พี่ชายก็สามารถสานฝันของเราได้สำเร็จค่ะ";
			next;
			mes "[Ingrid]";
			mes "ฮ่าๆ~ ดูข้าสิ เล่าเรื่องพวกนี้ให้เจ้าฟังเฉยเลยทั้งที่เจ้าไม่ได้ถาม";
			mes "ขอโทษนะคะ";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ไม่เป็นไรหรอก";
			mes "เจ้าอาจจะไม่ได้เป็น Gunslinger แต่ข้าคิดว่าเจ้าได้กลายเป็นบุคคลที่ยอดเยี่ยมแล้วล่ะ";
			next;
			mes "[Ingrid]";
			mes "จริงหรือคะ? ขอบคุณค่ะ~";
			mes "ข้าจะพยายามอย่างเต็มที่ค่ะ";
			next;
			mes "[Ingrid]";
			mes "ในการผลิต Inferno";
			mes "ข้าต้องการ 100 Used Iron Plates, 10 Oridecons, 50 Old Rusty Screws, 100 Burning Hearts, และ 200,000 Zeny ค่ะ";
			mes "จะให้ข้าผลิตให้เลยไหมคะ?";
			next;
			switch( select( "ขอข้าคิดดูก่อนนะ", "ได้ โปรดทำให้ข้าที" ) )
			{
				case 1:
					mes "[Ingrid]";
					mes "อา~ ข้าเข้าใจแล้วค่ะ";
					mes "ข้าเข้าใจ";
					mes "โอเค เอาเป็นว่าลองกลับไปคิดดูนะคะ";
					mes "กลับมาเมื่อเจ้าตัดสินใจได้แล้ว";
					close;

				case 2:
					mes "[Ingrid]";
					mes "ข้าเข้าใจแล้วค่ะ";
					mes "นี่คือวัตถุดิบที่ต้องใช้อีกครั้งนะคะ";
					next;
					mes "[Inferno]";
					mes "ในการผลิต Inferno";
					mes "ข้าต้องการ 100 Used Iron Plates, 10 Oridecons, 50 Rusty Old Screws, 100 Burning Hearts, และ 200,000 Zeny ค่ะ";
					mes "เจ้าต้องนำวัตถุดิบมาให้ครบตามจำนวนที่กำหนดนะคะ";
					mes "ได้โปรดอย่าลืมเรื่องนั้นล่ะ";
					set gun_inf,1;
					close;
			}

		case 1:
			callsub SubInferno;

		case 2:
			mes "[Ingrid]";
			mes "สวัสดีค่ะ~ กลับมาแล้วหรือคะ~";
			mes "กลับมาเพราะต้องการ Inferno ใช่ไหมคะ?";
			next;
			switch( select( "เปล่าหรอก ข้าแค่แวะมา..","ใช่ ทำ Inferno ให้ข้าที" ) )
			{
				case 1:
					mes "[Ingrid]";
					mes "อา~ ข้าเข้าใจแล้วค่ะ";
					mes "ข้าเข้าใจ";
					mes "โอเค เอาเป็นว่าลองกลับไปคิดดูนะคะ";
					mes "กลับมาเมื่อเจ้าตัดสินใจได้แล้ว";
					close;

				case 2:
					callsub SubInferno;
			}
	}

SubInferno:
	if ((countitem(7319) < 100) || (countitem(7317) < 50) || (countitem(984) < 10) || countitem(7097) < 100)
	{
		mes "[Ingrid]";
		mes "ใช่ค่ะ วัตถุดิบที่ต้องการคือ 100 Used Iron Plates, 10 Oridecons, 50 Rusty Old Screws, 100 Burning Hearts, และ 200,000 Zeny ค่ะ";
		mes "เจ้าต้องนำวัตถุดิบมาให้ครบตามจำนวนที่กำหนดนะคะ";
		mes "ได้โปรดอย่าลืมเรื่องนั้นล่ะ";
		close;
	}
	else if(Zeny < 200000)
	{
		mes "[Ingrid]";
		mes "เจ้านำวัตถุดิบมาครบแล้ว";
		mes "แต่เจ้านำ Zeny มาพอหรือเปล่าคะ?";
		mes "ราคาในการสร้าง Inferno คือ 200,000 Zeny ค่ะ";
		mes "ได้โปรดอย่าลืมเรื่องนั้นนะคะ";
		close;
	}
	else if(checkweight(13162,1) != 1)
	{
		mes "[Ingrid]";
		mes "ดูเหมือนว่าเจ้าจะมีไอเทมมากเกินไปที่จะแบกรับสิ่งที่ข้าสร้าง ดังนั้นข้าจึงมอบให้เจ้าไม่ได้ค่ะ";
		mes "ทำไมไม่ลองกลับมาหาข้าใหม่เมื่อเจ้าพร้อมจะรับมันล่ะคะ?";
		close;
	}
	else
	{
		mes "[Ingrid]";
		mes "ข้าเห็นว่าเจ้ามีวัตถุดิบและ Zeny ครบถ้วนแล้วนะคะ";
		mes "นี่คือ Inferno ที่ข้าสร้างให้เจ้าค่ะ";
		mes "ใช้มันให้ดีนะคะ";
		set Zeny,Zeny-200000;
		delitem 7319,100;
		delitem 984,10;
		delitem 7317,50;
		delitem 7097,100;
		getitem 13162,1;
		if(gun_inf == 1) set gun_inf,2;
		close;
	}
}

que_ng,185,180,3	script	Vanessa	726,{

	if(BaseJob != Job_Gunslinger)
	{
		mes "[Vanessa]";
		mes "ฮ้า !ฮ้า !!";
		mes "Ballitude! Commander Sambo!";
		mes "Wrestling!, Muye Tai!";
		mes "Pancracion!, Lucharibre!";
		mes "ฉันจะฝึกฝนอาวุธมือเปล่าทุกชนิดในโลกนี้ให้ช่ำชองเลย!!";
		next;
		mes "[Vanessa]";
		mes "หืม?!";
		mes "แกเป็นใคร? แกกำลังขัดขวางการฝึกของฉันนะ!";
		mes "ไสหัวไปซะ!";
		close;
	}
	switch(gunst)
	{
		case 0:
			mes "[Vanessa]";
			mes "ฮ้า! ย้า!!";
			mes "Vale Tudo! Commando Sambo!";
			mes "Wrestling!, Muay Thai!";
			mes "Pankration!, Mucho Libre!";
			mes "ฉันจะฝึกฝนรูปแบบการต่อสู้ทั้งหมด";
			mes "ในโลกนี้ให้ช่ำชอง!!";
			next;
			mes "[Vanessa]";
			mes "หืม?";
			mes "แกเป็น Gunslinger งั้นเหรอ?";
			mes "ทำไมแกถึงมาขัดขวาง";
			mes "การออกกำลังกายของฉัน แถมยังมายืน";
			mes "เซ่อข้างๆ ฉันอีกล่ะ?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ก็แค่ดูการออกกำลังกาย";
			mes "มันดูเข้าท่าดีน่ะ...";
			mes ".............";
			next;
			mes "[Vanessa]";
			mes "โอ้! แกชอบศิลปะการต่อสู้เหรอ?";
			mes "มานี่สิ~ เดี๋ยวฉันจะจัด";
			mes "ท่า arm-bar ให้แกเอง~";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ม~ ไม่ล่ะ ขอบคุณ~";
			mes "ข้าไม่เป็นไร~";
			next;
			mes "[Vanessa]";
			mes "ไม่เป็นไรได้ไงเล่า~";
			mes "มานี่เลย~!";
			mes "-ปั้ก! ปั้ก!-";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "อ๊ายย~ อั้ก~ อย่าทำแบบนั้นนะ~";
			mes "อ๊า..อ๊างงงง~~!!";
			mes "อ๊ากกก~";
			mes "-เสียงอะไรบางอย่างหัก-";
			mes "โอ๊ยยยย~~";
			mes "*สะอึกสะอื้น*~~";
			next;
			mes "[Vanessa]";
			mes "หืม... ฉันทำแรงไปหน่อยเหรอเนี่ย";
			mes "อืมม~";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ก็เออสิ!";
			mes "ยัยบ้า! ยัยบ้า!!";
			mes "*สะอื้น*~";
			next;
			mes "[Vanessa]";
			mes "อืมม~ อืมม~";
			mes "ขอโทษนะ~";
			mes "โอเคๆ";
			mes "อย่าร้องไห้ไปเลย";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "*สะอื้น*~";
			next;
			mes "[Vanessa]";
			mes "โอเค~ โอเค..";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "*สะอื้น*~";
			next;
			mes "[Vanessa]";
			mes "หยุดนะ! อ๊ากก!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "........";
			next;
			mes "[Vanessa]";
			mes "ก็ได้ โอเค";
			mes "ฉันขอโทษสำหรับสิ่งที่เกิดขึ้น";
			mes "ฉันจะสร้างอาวุธให้แกแล้วกัน";
			mes "ถ้าแกไปรวบรวม";
			mes "วัตถุดิบมาให้ฉัน...";
			next;
			mes "[Vanessa]";
			mes "ฉันชื่อ Vanessa Louise";
			mes "เดิมทีเป็นผู้สมัครนักศิลปะการต่อสู้";
			mes "แต่ดันเลี้ยวผิดทาง";
			mes "เลยต้องมาติดแหง็กสร้างอาวุธอยู่นี่";
			mes "โอ้พระเจ้า~ โอ้~ ชีวิตที่แสนรันทดของฉัน~";
			next;
			mes "[Vanessa]";
			mes "เอาล่ะ เข้าเรื่องเลย";
			mes "อาวุธที่ฉันสร้างได้น่ะ";
			mes "เรียกว่า Destroyer";
			next;
			mes "[Vanessa]";
			mes "ฉันตั้งชื่อมันเป็นพิเศษ";
			mes "ตามเทคนิคที่ฉันใช้ในสนามประลองเลยนะ";
			mes "ฮิฮิ~";
			mes "รับไปซะนี่แน่ะ! Destroyer~";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "*สะอื้น*~ ได้โปรด~ หยุดเถอะ~";
			next;
			mes "[Vanessa]";
			mes "อา อะ..ขอโทษที...";
			mes "ก่อนอื่น Destroyer ต้องมี";
			mes "วัตถุดิบก่อนถึงจะสร้างได้";
			next;
			mes "[Vanessa]";
			mes "ไอเทมที่ต้องใช้น่ะมีจำนวนไม่ใช่น้อยๆ เลยนะ";
			mes "ดังนั้นคิดดูให้ดีล่ะ";
			next;
			mes "[Vanessa]";
			mes "ว่ายังไงล่ะ? โอกาสแบบนี้";
			mes "ไม่ได้มีบ่อยๆ นะ";
			mes "อยากจะจ้างฉันไหม?";
			next;
			switch( select( "ไว้วันหลังแล้วกัน.","ตกลง") )
			{
				case 1:
					mes "[Vanessa]";
					mes "หืม~ งั้นเหรอ~";
					mes "มันเป็นโอกาสของแกเลยนะ";
					mes "เดี๋ยวแกจะเสียใจทีหลัง~";
					mes "งั้นไว้เจอกันคราวหน้า~";
					mes "บ๊ายบาย~";
					close;

				case 2:
					mes "[Vanessa]";
					mes "อืม ตกลง";
					mes "ในการสร้าง Destroyer";
					mes "แกต้องมี 50 Used Iron Plates";
					mes "5 Oridecons, 70 Rusty Old Screws";
					mes "และค่าธรรมเนียม 100,000 zeny";
					mes "แกต้องนำวัตถุดิบมาให้เป๊ะๆ ตามนี้ เข้าใจไหม?";
					set gunst,1;
					close;
			}

		case 1:
			callsub SubDestroyer;
			
		case 2:
			mes "[Vanessa]";
			mes "โอ้~ แกกลับมาแล้วเหรอ?";
			mes "ช่วงนี้เป็นยังไงบ้าง?";
			mes "มีไอเทมใหม่ในร้านด้วยนะ";
			mes "เชิญเลือกได้เลย";
			next;
			switch(select( "Destroyer","เจาะรู Destroyer (Slot)","ยกเลิก") )
			{
				case 1:
					mes "[Vanessa]";
					mes "เออ ดีเหมือนกัน";
					mes "นั่นคือ Destroyer รุ่นปกติ";
					mes "ต้องใช้ 50 Used Iron Plates,";
					mes "5 Oridecons, 70 Rusty Old Screws,";
					mes "และ 100,000 Zeny ในการสร้าง";
					mes "แกต้องนำวัตถุดิบมาให้ครบตามจำนวน";
					mes "แกต้องการมันไหม?";
					next;
					switch( select( "ไว้วันหลังแล้วกัน.","ตกลง") )
					{
       					case 1:
							mes "[Vanessa]";
							mes "อะไรกัน~ โถ่เอ๊ย";
							mes "มีอะไรต้องคิดอีกเนี่ย?";
							mes "โอเคๆ ลองไปคิดดูแล้วกัน";
							mes "แล้วค่อยกลับมา...";
							close;
							case 2:
							callsub SubDestroyer;
					}
							
				case 2:
					mes "[Vanessa]";
					mes "โอ้~ Slotted Destroyer สินะ~";
					mes "ไม่เหมือนกับ Destroyer รุ่นปกติหรอกนะ";
					mes "ฉันอยากให้แกหาไอเทมหายากที่ฉันกำลังสะสมอยู่มาให้ฉัน";
					mes "แล้วฉันจะมอบมันให้แก";
					next;
					mes "[Vanessa]";
					mes "ฉันกำลังมองหา Finger[2]";
					mes "แล้วก็ต้องการ 5 Oridecons ด้วย";
					mes "แกต้องนำวัตถุดิบมาให้ครบตามจำนวนนะ";
					mes "แกยังอยากได้ Slotted Destroyer อยู่ไหม?";
					next;
					switch( select( "ขอฉันคิดดูก่อน.","ตกลง! ฉันเอา!") )
					{
       					case 1:
							mes "[Vanessa]";
							mes "อะไรกัน~ โถ่เอ๊ย";
							mes "มีอะไรต้องคิดอีกเนี่ย?";
							mes "โอเคๆ ลองไปคิดดูแล้วกัน";
							mes "แล้วค่อยกลับมา...";
							close;
			
						case 2:
							callsub SubDestroyer_;
			
					}
     
				case 3:
					mes "[Vanessa]";
					mes "หืม~ เออ~";
					mes "ตามสบายนะ";
					mes "หรืออยากจะลองมาประลองกับฉันดูไหมล่ะ?";
					close;
			}
	}

SubDestroyer:

	if ((countitem(7319) < 50) || (countitem(7317) < 70) || (countitem(984) < 5))
	{
		mes "[Vanessa]";
		mes "เออ วัตถุดิบที่ต้องใช้คือ 50 Used Iron Plates,";
		mes "5 Oridecons, 70 Rusty Old Screws, และ 100,000 Zeny";
		mes "จำไว้นะว่าแกต้องนำมาให้ครบตามจำนวน";
		close;
	}
	else if(Zeny < 100000)
	{
		mes "[Vanessa]";
		mes "แกนำวัตถุดิบมาเพียบเลย";
		mes "แต่ค่าธรรมเนียมคือ 100,000 นะ";
		mes "จำเรื่องนั้นไว้ด้วย";
		close;
	}
	else if(checkweight(13160,1) != 1)
	{
		mes "[Vanessa]";
		mes "ดูเหมือนแกจะแบกสิ่งที่ฉันสร้างไม่ไหวหรอก ต่อให้ฉันสร้างให้ตอนนี้ก็เถอะ";
		mes "ไปเคลียร์ของในกระเป๋าออกหน่อยไป";
		close;
	}
	else
	{
		mes "[Vanessa]";
		mes "โอเค~ ดีมาก~";
		mes "ตรวจสอบเรียบร้อย~";
		mes "แกมีวัตถุดิบและ Zeny ครบถ้วนพอดี~";
		mes "นี่คือ Destroyer ที่ฉันสร้างเตรียมไว้ให้แกแล้ว";
		mes "ใช้มันให้ดีล่ะ";
		set Zeny,Zeny-100000;
		delitem 7319,50;
		delitem 984,5;
		delitem 7317,70;
		getitem 13160,1;
		if(gunst == 1) set gunst,2;
		next;
		mes "[Vanessa]";
		mes "ถ้าวันหลังแกต้องการอีก";
		mes "มาหาฉันได้ทุกเมื่อนะ~";
		mes "คราวหน้าฉันจะใช้เทคนิคการต่อสู้มือเปล่าแบบอื่นจัดการแก";
		close;
	}

SubDestroyer_:

	if ((countitem(1812) < 1) || (countitem(984) < 5))
	{
		mes "[Vanessa]";
		mes "เออ วัตถุดิบที่ต้องใช้คือ 1 Finger[2]";
		mes "และ 5 Oridecons";
		mes "จำไว้ด้วยว่าวัตถุดิบต้องเป๊ะๆ นะ";
		close;
	}
	else if(checkweight(13161,1) != 1)
	{
		mes "[Vanessa]";
		mes "ไม่มีที่ว่างในกระเป๋าของแก";
		mes "ต่อให้ฉันสร้างให้ตอนนี้";
		mes "แกก็แบกมันไปไม่ได้หรอก";
		mes "ไปเคลียร์กระเป๋ามาก่อนแล้วค่อยกลับมา";
		close;
	}
	else
	{
		mes "[Vanessa]";
		mes "โอเค~ ดีมาก~";
		mes "ตรวจสอบเรียบร้อย~";
		mes "แกมีวัตถุดิบและ Zeny ครบถ้วนพอดี~";
		mes "นี่คือ Slotted Destroyer ที่ฉันสร้างเตรียมไว้ให้แกแล้ว";
		mes "ใช้มันให้ดีล่ะ";
		delitem 1812,1;
		delitem 984,5;
		getitem 13161,1;
		next;
		mes "[Vanessa]";
		mes "ถ้าวันหลังแกต้องการอีก";
		mes "มาหาฉันได้ทุกเมื่อนะ~";
		mes "คราวหน้าฉันจะใช้เทคนิคการต่อสู้มือเปล่าแบบอื่นจัดการแก";
		close;
	}
}

que_ng,149,178,4	script	Lab Director	744,{

	if(gun_na == 1){
		if(countitem(1043) > 999 && countitem(932) > 999){
			delitem 1043,1000;
			delitem 932,1000;
			set gun_na,2;
			mes "[N. A]";
			mes "อาาา ของทั้งหมดอยู่นี่แล้ว! อาาา แล้วข้าก็";
			mes "แอบกังวลอยู่เหมือนกันว่าคงไม่มีใคร";
			mes "สามารถรับมือกับ ^ff0000Butcher^000000 ของข้าได้";
			mes "เมื่อข้าสร้างมันเสร็จสมบูรณ์!";
			next;
			mes "[N. A]";
			mes "เอาล่ะ ข้าจะมอบ ^ff0000Butcher^000000 ให้แก่เจ้า";
			mes "อย่างไรก็ตาม พวกเราไม่ทำงานให้";
			mes "ฟรีๆ ดังนั้นเราจึงต้องเก็บค่าธรรมเนียม";
			mes "จากเจ้าด้วย นอกจากนี้ เจ้ายังต้องไปขอ";
			mes "อนุญาตการใช้งาน Butcher จาก";
			mes "Lady Celena ก่อน";
			next;
			mes "[N. A]";
			mes "ทันทีที่เจ้าได้รับอนุญาต ข้าจะ";
			mes "มอบ Butcher ให้แก่เจ้า หลังจากจ่าย";
			mes "ค่าธรรมเนียมจำนวน 100,000 zeny แล้ว";
			close;
		}else{
			mes "[N. A]";
			mes "เจ้าหา ^ff00001000 Orc Claw^000000 และ ^ff00001000 Skel Bone^000000 เจอหรือยัง?";
			mes "ถ้าเจ้าคิดว่ามันยากเกินไป";
			mes "เจ้าจะเลือกยอมแพ้ก็ได้นะ";
			next;
			if(select("ข้าไม่ยอมแพ้หรอก!:ข้ายอมแพ้แล้ว...") == 1){
				mes "[N. A]";
				mes "ดีมาก ข้าเชื่อใจเจ้า";
				mes "โชคดีนะ";
				close;
			}else{
				mes "[N. A]";
				mes "เจ้ายอมแพ้งั้นรึ หืม?";
				mes "เอาเถอะ ข้ายอมรับว่า";
				mes "บททดสอบนี้มันค่อนข้างยาก แต่";
				mes "เจ้าก็คงจะถือครองอาวุธชิ้นนี้ไม่ได้";
				mes "หากเจ้าผ่านบททดสอบนี้ไปไม่ได้";
				mes "เจ้าจะกลับมาลองใหม่";
				mes "ในภายหลังก็ได้นะ";
				set gun_na,0;
				close;
			}
		}
	}
	else if(gun_na == 2){
		mes "[N. A]";
		mes "ข้าได้รับคำอนุญาตจาก Lady Celena";
		mes "ให้เจ้าใช้ Butcher ได้แล้ว";
		mes "เจ้าสามารถใช้มันได้ทันที";
		mes "เมื่อเจ้าจ่ายเงินให้ข้า 100,000 zeny";
		mes "เจ้าต้องการจ่ายตอนนี้เลยไหม?";
		next;
		if(select("ยังก่อน.:ใช่.") == 1){
			mes "[N. A]";
			mes "ตกลง ข้าจะรอเจ้ากลับมา";
			mes "พร้อมกับเงินนะ";
			close;
		}else{
			if(Zeny < 100000){
				mes "[N. A]";
				mes "หืม ข้าไม่คิดว่าเจ้าจะมี";
				mes "เงินติดตัวพอหรอกนะ";
				mes "กลับมาพร้อมกับเงิน";
				mes "เข้าใจไหม?";
				close;
			}
			if(checkweight(13158,1) == 0){
				mes "[N. A]";
				mes "น้ำหนักตัวของเจ้ามากเกินไป";
				mes "ต่อให้ข้ามอบอาวุธชิ้นนี้";
				mes "ให้เจ้าไป เจ้าก็แบกมันไม่ไหว";
				mes "กรุณาไปเคลียร์ช่องเก็บของด้วย";
				close;
			}
			set Zeny,Zeny-100000;
			set gun_na,0;
			getitem 13158,1;
			mes "[N. A]";
			mes "หนึ่ง, สอง, สาม, สี่, ห้า,";
			mes "หก... 99997, 99998, 99999...";
			mes "100,000. *ติ๊ง~!* เยี่ยมมาก!";
			mes "ค่าธรรมเนียมครบถ้วนแล้ว ตอนนี้";
			mes "เจ้ารับ ^ff0000Butcher^000000 ไปได้เลย!";
			next;
			mes "[N. A]";
			mes "คุณ F. Harrison จาก Lighthalzen";
			mes "ค่อนข้างสนใจในของเล่นชิ้นใหม่";
			mes "ของเจ้าชิ้นนั้นนะ ลองเอาไปให้";
			mes "เขาดูบ้างสิ เขาคงจะดีใจ";
			next;
			mes "[N. A]";
			mes "ใช้มันให้คุ้มค่าล่ะ!";
			mes "แล้วเจอกัน!";
			close;
		}
	}
	else if(gun_na == 10){
		if (countitem(999) > 69 && countitem(985) > 4 && countitem(984) > 2 && countitem(1003) > 69 && countitem(7317) > 49 && Zeny >= 50000){
			delitem 999,70;
			delitem 985,5;
			delitem 984,3;
			delitem 1003,70;
			delitem 7317,50;
			set Zeny,Zeny-50000;
			set gun_na,11;
			mes "[N. A]";
			mes "อาฮ่า เจ้าหาวัตถุดิบทั้งหมด";
			mes "มาให้ข้าแล้ว มานี่สิ ข้าจะ";
			mes "เริ่มลงมือทำเดี๋ยวนี้เลย! มันอาจจะ";
			mes "ใช้เวลาสักหน่อยนะ รอก่อนล่ะ...";
			close;
		}else{
			mes "[N. A]";
			mes "เอ๊ะ? เจ้ายังไม่มีวัตถุดิบ";
			mes "อยู่กับตัวงั้นรึ?";
			next;
			mes "[N. A]";
			mes "ในการสร้าง Drifter ข้าต้องการ";
			mes "70 Steel, 5 Elunium,";
			mes "3 Oridecon, 70 Coal, 50 Rusty";
			mes "Screws, และค่าธรรมเนียม";
			mes "50,000 zeny";
			mes "กลับมาหาข้าอีกครั้งเมื่อเจ้า";
			mes "เตรียมทุกอย่างพร้อมแล้ว";
			next;
			mes "[N. A]";
			mes "ถ้าเจ้าไม่ต้องการมันแล้ว";
			mes "เจ้าสามารถยกเลิกคำขอได้นะ";
			next;
			if(select("ไม่ต้องยกเลิก.:ยกเลิกคำขอ.") == 1){
				mes "[N. A]";
				mes "เอาล่ะ กรุณากลับมาพร้อมกับ";
				mes "วัตถุดิบนะ ข้าจะรอ";
				close;
			}else{
				mes "[N. A]";
				mes "ตกลง คำขอสร้าง Drifter";
				mes "ให้แก่เจ้าถูกยกเลิกแล้ว";
				mes "ข้าขอให้เจ้าโชคดี";
				mes "ในอนาคตนะ";
				set gun_na,0;
				close;
			}
		}
	}
	else if(gun_na == 11){
		if(checkweight(13157,1) == 0){
			mes "[N. A]";
			mes "น้ำหนักตัวของเจ้ามากเกินไป";
			mes "ต่อให้ข้าสร้างอาวุธชิ้นนี้";
			mes "ให้เจ้าไป เจ้าก็แบกมันไม่ไหว";
			mes "กรุณาไปเคลียร์ช่องเก็บของด้วย";
			close;
		}
		set gun_na,0;
		getitem 13157,1;
		mes "[N. A]";
		mes "อาาา นี่คือ Drifter";
		mes "ที่เสร็จสมบูรณ์สำหรับเจ้า";
		next;
		mes "[N. A]";
		mes "โปรดเรียนรู้ที่จะใช้";
		mes "Gatlings ให้ดี พลังการทำลาย";
		mes "ที่บ้าคลั่งของมันจะช่วยเยียวยา";
		mes "จิตใจของเจ้าได้อย่างแน่นอน";
		close;
	}
	else if(gun_na == 101){
		mes "[N. A]";
		mes "อา สวัสดี?";
		mes "ข้าคือผู้ประสานงานของ";
		mes "หน่วยพัฒนาอาวุธแห่ง Einbroch";
		mes "ชื่อของข้าคือ 'Lab Director'";
		mes "เจ้าต้องการอะไรหรือเปล่า?";
		next;
		select("ข้าต้องการ Special Metal Rod");
		mes "[N. A]";
		mes "หือ...? เฮ้ เจ้าไม่ใช่คนที่";
		mes "เพิ่งเดินออกไปจากที่นี่พร้อมกับ";
		mes "Butcher เมื่อไม่นานมานี้หรอกรึ? เป็นไงบ้างล่ะ";
		mes "ไอ้ Butcher นั่นน่ะ?";
		next;
		mes "[N. A]";
		mes "...... อะไรนะ!? เจ้าทำมันพังงั้นเหรอ!?";
		mes "ข้าอุตส่าห์นึกว่าเจ้าจะสามารถ";
		mes "ใช้มันได้ดีเสียอีก... เจ้าทำข้าผิดหวังมาก!!!";
		next;
		mes "[N. A]";
		mes "เจ้าทรยศต่อความเชื่อมั่นของข้า!";
		mes "ไอ้คนทรยศ! คนทรยศ!! ไอ้คนทรยศ!!!!";
		next;
		select("อธิบายทุกอย่างให้ฟัง");
		mes "[N. A]";
		mes "...... ฟิ้ววว";
		mes "สรุปคือคุณ F. Harrison เป็นคน";
		mes "ทำมันพังสินะ? ข้าขอโทษที ข้าไม่";
		mes "ควรสงสัยในตัวเจ้าเลย";
		next;
		mes "[N. A]";
		mes "Lady Celena มีแกนโลหะพิเศษ (Special metal rod)";
		mes "ที่เจ้าต้องการ แต่ตอนนี้ท่านด็อกเตอร์";
		mes "ไม่อยู่ และไม่มีใครคนอื่นเลย";
		mes "ที่จะรู้ว่ามันเก็บไว้ที่ไหน...";
		next;
		mes "[N. A]";
		mes "ตอนนี้ข้ามีงานวิจัยอื่นๆ ที่ต้องทำ";
		mes "ดังนั้นข้าจึงไม่สามารถเสียเวลาไป";
		mes "ตามหามันได้... กรร... ข้าต้องการ";
		mes "Elemental Spheres เพื่อที่จะ";
		mes "ดำเนินงานวิจัยของข้าต่อไป...";
		next;
		select("ยื่นข้อเสนอให้เขา");
		mes "[N. A]";
		mes "ข้อเสนองั้นรึ? อย่างเช่นอะไรล่ะ?";
		next;
		select("เรามาแลกเปลี่ยนความช่วยเหลือกัน");
		mes "[N. A]";
		mes "อืม... ฟังดูเข้าท่าแฮะ";
		mes "เอาล่ะ ตามข้อตกลงนะ";
		mes "ข้าจะไปตามหาแท่งเหล็กนั่นให้เจ้า";
		mes "ในขณะที่เจ้าไปตามหา";
		mes "Elemental Spheres พวกนั้นมาให้ข้า";
		next;
		mes "[N. A]";
		mes "30 Poison Sphere, 30 Flare Sphere,";
		mes "30 Lightning Sphere, 30 Blind";
		mes "Sphere, หรือ 30 Freezing Sphere";
		mes "หามาให้ข้าอย่างละ 30 ลูกต่อธาตุนะ";
		set gun_na,102;
		close;
	}
	else if(gun_na == 102){
		mes "[N. A]";
		mes "30 Poison Sphere, 30 Flare Sphere,";
		mes "30 Lightning Sphere, 30 Blind";
		mes "Sphere, หรือ 30 Freezing Sphere";
		mes "หามาให้ข้าอย่างละ 30 ลูกต่อธาตุนะ";
		mes "เจ้าหามันเจอหรือยัง?";
		next;
		switch(select("ยังเลย.:ข้าหา 30 Poison Spheres มาได้แล้ว.:ข้าหา 30 Flare Spheres มาได้แล้ว.:ข้าหา 30 Lightning Spheres มาได้แล้ว.:ข้าหา 30 Blind Spheres มาได้แล้ว.:ข้าหา 30 Freezing Spheres มาได้แล้ว.")){
		case 1:
			mes "[N. A]";
			mes "แค่นำธาตุไหนก็ได้ที่เจ้า";
			mes "พอจะหาได้มาให้ข้าก็พอ";
			close;
		case 2:
			if(countitem(13205) >= 30){
				delitem 13205,30;
				set gun_na,103;
			}
			break;
		case 3:
			if(countitem(13203) >= 30){
				delitem 13203,30;
				set gun_na,103;
			}
			break;
		case 4:
			if(countitem(13204) >= 30){
				delitem 13204,30;
				set gun_na,103;
			}
			break;
		case 5:
			if(countitem(13206) >= 30){
				delitem 13206,30;
				set gun_na,103;
			}
			break;
		case 6:
			if(countitem(13207) >= 30){
				delitem 13207,30;
				set gun_na,103;
			}
			break;
		}
		if (gun_na != 103) {
			mes "[N. A]";
			mes "เอ๊ะ? อะไรกัน? ข้าเป็นคนเดียว";
			mes "ที่มองไม่เห็นมันงั้นรึ?";
			mes "เอามาให้ข้ามากกว่านี้หน่อย!";
			close;
		}
		mes "[N. A]";
		mes "ว้าว เจ้าหามันมาให้ข้าจนครบหมดเลย!";
		mes "ข้าต้องรื้อห้องแล็บของ Lady Celena";
		mes "กระจุยเลยเพื่อหาแท่งเหล็กนี่เหมือนกัน";
		mes "งั้นเรามาแลกกันเถอะ!";
		next;
		mes "-คุณมอบ Elemental Spheres";
		mes "ให้แก่ผู้ประสานงานวิจัยและได้รับ";
		mes "แท่งโลหะ (Metal Rod) เป็นการตอบแทน-";
		next;
		mes "[N. A]";
		mes "คุณ F. Harrison น่ะเก่งมากนะ";
		mes "ข้าแน่ใจว่าเขาสามารถซ่อม";
		mes "Butcher ให้เจ้าได้แน่นอน";
		mes "เอาล่ะ แล้วเจอกัน!";
		close;
	}
	else if(gun_na == 103 || gun_na == 104){
		mes "[N. A]";
		mes "คุณ F. Harrison น่ะเก่งมากนะ";
		mes "ข้าแน่ใจว่าเขาสามารถซ่อม";
		mes "Butcher ให้เจ้าได้แน่นอน";
		mes "เอาล่ะ แล้วเจอกัน!";
		close;
	}
	mes "[N. A]";
	mes "อา สวัสดี?";
	mes "ข้าคือผู้ประสานงานของ";
	mes "หน่วยพัฒนาอาวุธแห่ง Einbroch";
	mes "ชื่อของข้าคือ 'Lab Director'";
	mes "เจ้าต้องการอะไรหรือเปล่า?";
	next;
	if (BaseJob != Job_Gunslinger){
		select("คุยกับเขา");
		mes "[N. A]";
		mes "ถ้าเจ้าเจอพวก Gunslinger แถวนี้";
		mes "ฝากบอกพวกเขาด้วยว่าข้ามี";
		mes "ข่าวสารล่าสุดเกี่ยวกับอาวุธ";
		mes "ถ้าจำเป็น ก็ส่งพวกเขามาที่";
		mes "นี่ได้เลยนะ หึหึหึหึหึ...";
		close;
	}
	if (BaseLevel < 55) {
		select("คุย");
		mes "[N. A]";
		mes "... การทำลายล้าง... ความบ้าคลั่ง...";
		mes "อืม... ความเร็วโจมตีเกิน 180...";
		next;
		mes "[N. A]";
		mes "อา ขอโทษที ข้ากำลังพัฒนา";
		mes "อาวุธใหม่ๆ อยู่น่ะ ได้โปรดอย่า";
		mes "รบกวนข้าเลย";
		close;
	}
	switch(select(((BaseLevel > 67)?"ถามเกี่ยวกับ 'Butcher'":"")+":ถามเกี่ยวกับ 'Drifter':ยกเลิก")) {
	case 1:
		mes "[N. A]";
		mes "อา เจ้าได้ยินข่าวสาร";
		mes "หลังจากที่การพัฒนาเพิ่งจะ";
		mes "เสร็จสิ้นไปได้ไม่นานสินะ หืม?";
		next;
		mes "[N. A]";
		mes "^ff0000Butcher^000000 คือการพัฒนาล่าสุด";
		mes "โดยพวกเรา ห้องแล็บอาวุธปืน Einbroch";
		mes "มันเป็น Gatling รุ่นใหม่ล่าสุด";
		mes "ที่เราพัฒนาขึ้นมา ในขณะที่";
		mes "^ff0000Drifter^000000 จะเป็นอาวุธที่ดี";
		mes "เหมือนกัน แต่ ^ff0000Butcher^000000 นั้นมี";
		mes "พลังทำลายที่รุนแรงกว่ามากแน่นอน!";
		next;
		mes "[N. A]";
		mes "ด้วยพลังทำลายที่รุนแรงของมัน";
		mes "Butcher จึงถูกขนานนามอีกชื่อหนึ่ง";
		mes "ว่า 'Murderer' (ฆาตกร)";
		next;
		mes "[N. A]";
		mes "อย่างไรก็ตาม แม้แต่เหล่า Gunslinger ก็";
		mes "อาจจะมีปัญหาในการควบคุมอาวุธ";
		mes "ที่ทรงพลังขนาดนี้ได้";
		next;
		mes "[N. A]";
		mes "เจ้าคิดว่าเจ้าจะรับมือมันไหวไหมล่ะ?";
		next;
		if(select("ข้าไม่แน่ใจเหมือนกัน...:แน่นอนว่าข้าไหว!!") == 1){
			mes "[N. A]";
			mes "^ff0000Butcher^000000 เป็นอาวุธที่เจ้า";
			mes "ไม่สามารถควบคุมได้หากปราศจากความมุ่งมั่น";
			mes "ที่แรงกล้า ข้าจะพบเจ้าอีกครั้งเมื่อ";
			mes "เจ้ามีความมั่นใจเพียงพอที่จะ";
			mes "ควบคุมเจ้าสัตว์ร้ายตัวนี้";
			close;
		}else{
			mes "[N. A]";
			mes "อาฮ่า เจ้าช่างมั่นใจในตัวเอง";
			mes "เสียจริง! อย่างไรก็ตาม ข้าเห็นคน";
			mes "ที่มั่นใจแบบนี้อยู่ทั่วไปหมด";
			mes "เจ้าจำเป็นต้องพิสูจน์มัน...";
			mes "แต่ยังไงดีล่ะ... อืม...";
			next;
			mes "[N. A]";
			mes "อืมมมมมม......";
			next;
			mes "[N. A]";
			mes "อาฮ่า! นึกออกแล้ว!";
			next;
			mes "[N. A]";
			mes "นี่ไง นำ ^ff00001000 Orc Claws^000000 และ ^ff00001000 Skel Bone^000000 มาให้ข้า";
			mes "ถ้าเจ้าสามารถนำของพวกนี้มาให้ข้าได้";
			mes "ข้าจะอนุญาตให้เจ้าใช้ Butcher";
			next;
			mes "[N. A]";
			mes "ง่ายใช่ไหมล่ะ? ไปพิสูจน์";
			mes "ความสามารถของเจ้าซะสิ! หึหึหึ...";
			set gun_na,1;
			close;
		}
	case 2:
		mes "[N. A]";
		mes "อา เจ้ามาที่นี่เพื่อเรียนรู้เกี่ยวกับ";
		mes "^ff0000Drifter^000000 สินะ หืม?";
		next;
		mes "[N. A]";
		mes "^ff0000Drifter^000000 คือหนึ่งในบรรดา";
		mes "อาวุธระดับสูงมากมายที่พัฒนาขึ้น";
		mes "โดย Lady Celena มันเป็นปืน Gatling";
		mes "ระบบอัตโนมัติที่ขึ้นชื่อเรื่องความเร็ว";
		mes "ในการรัวกระสุน ซึ่งสูงที่สุด";
		mes "ในบรรดาอาวุธของ Gunslinger ทั้งหมด";
		next;
		mes "[N. A]";
		mes "แน่นอนว่าเจ้าจำเป็นต้องมีทักษะ (Skill)";
		mes "ที่จะช่วยให้เจ้าควบคุม Gatlings ได้";
		mes "แต่ผู้ใดที่ควบคุม Gatlings ได้ดี";
		mes "จะได้รับพลังสนับสนุนอย่างเต็มที่จาก";
		mes "^ff0000Drifter^000000";
		next;
		mes "[N. A]";
		mes "เจ้าต้องการลองใช้ ^ff0000Drifter^000000 ดูไหมล่ะ?";
		next;
		if(select("เอ่อ ไม่ดีกว่า.:แน่นอน!") == 1){
			mes "[N. A]";
			mes "ถ้าเจ้าอยากจะลองใช้มัน";
			mes "ดูเมื่อไหร่ ก็กลับมาที่นี่นะ";
			close;
		}else{
			set gun_na,10;
			mes "[N. A]";
			mes "เนื่องจากมันยากมากที่จะหา";
			mes "วัตถุดิบในการสร้าง";
			mes "^ff0000Drifter^000000 พวกเราจึงสร้างมัน";
			mes "ตามคำขอเท่านั้น และเราต้องการให้";
			mes "ผู้ที่ต้องการใช้ ^ff0000Drifter^000000 นำ";
			mes "วัตถุดิบมาให้เรา";
			next;
			mes "[N. A]";
			mes "ข้าจะบอกวัตถุดิบที่";
			mes "จำเป็นให้ แค่เจ้านำมันมา";
			mes "พวกเราก็จะสร้างมันให้เจ้าเอง";
			next;
			mes "[N. A]";
			mes "ในการสร้าง Drifter ข้าต้องการ";
			mes "70 Steel, 5 Eluniums, 3";
			mes "Oridecons, 70 Coal, 50 Rusty";
			mes "Screws, และค่าธรรมเนียม";
			mes "50,000 zeny";
			mes "กลับมาหลังจากที่เจ้าหา";
			mes "พวกมันมาจนครบแล้วนะ";
			close;
		}
	case 3:
		mes "[N. A]";
		mes "ข้ามีข่าวสารเกี่ยวกับอาวุธ";
		mes "รุ่นล่าสุดอยู่แต่... ข้าเดาว่าเจ้าคง";
		mes "ยุ่งเกินกว่าจะฟังมันสินะ";
		mes "เอาไว้คราวหน้าแล้วกัน";
		close;
	}
}

lighthalzen,205,284,6	script	F. Harrison	85,{

	if (gun_na == 100){
		if(checkweight(13102,1) == 0){
			mes "[F. Harrison]";
			mes "น้ำหนักตัวของเจ้ามากเกินไป";
			mes "ต่อให้ข้ามอบอาวุธชิ้นนี้";
			mes "ให้เจ้าไป เจ้าก็แบกมันไม่ไหว";
			mes "กรุณาไปเคลียร์ช่องเก็บของด้วย";
			close;
		}
		mes "[F. Harrison]";
		mes "อืม... โอ๊ะ?";
		mes "อาาา...... เอ๊ะ?";
		next;
		mes "[F. Harrison]";
		mes "หึหึ... อืม... หือ...?";
		next;
		select("ข้าขอ Butcher ของข้าคืนตอนนี้เลยได้ไหม?");
		mes "[F. Harrison]";
		mes "หือ? โอ๊ะ เอ่อ... หึหึ...";
		next;
		mes "[F. Harrison]";
		mes "อะ-อันนั้นน่ะเหรอ หือ? เอิ่ม... หึหึ...";
		next;
		mes "[F. Harrison]";
		mes "อาฮ่าฮ่าฮ่า! ข้า เอ่อ...";
		next;
		mes "[F. Harrison]";
		mes "ไอ้นั่นน่ะ! บูม!";
		next;
		mes "[F. Harrison]";
		mes "ปัง ปัง! ข้ากะว่าจะลองยิงมันดู!";
		mes "ใช่เลย!";
		next;
		mes "[F. Harrison]";
		mes "แต่มันดันขัดขืน! ข้าก็เลย 'ปัง!' ใส่ด้วย";
		mes "มือของข้าเอง! มัวฮ่าฮ่าฮ่าฮ่า!!";
		next;
		mes "[F. Harrison]";
		mes "แล้วมันก็พัง...... *ซิก ซิก*";
		next;
		mes "[F. Harrison]";
		mes "ข้าเรียกตัวเองว่า 'ดร. สรรพสิ่ง' และ";
		mes "ข้าอยากจะซ่อมมันด้วยตัวเอง แต่ข้า";
		mes "ดูเหมือนจะไม่มีวัตถุดิบเพียงพอ";
		mes "ที่จะซ่อมมัน... และวัตถุดิบพวกนั้น";
		mes "ก็หายากสุดๆ ไปเลยด้วย... *ซิก ซิก*";
		next;
		mes "[F. Harrison]";
		mes "เอ่อ... ข้าขอโทษที่ทำมัน";
		mes "พังนะแต่... เจ้าช่วยไปหา";
		mes "วัตถุดิบที่ข้าต้องการใช้ซ่อมมาให้ทีได้ไหม?";
		mes "นำของพวกนั้นมา แล้วข้าจะ";
		mes "ซ่อมให้ แถมข้าจะปรับแต่งมันให้";
		mes "เจ้าด้วย! ข้าสัญญา!!";
		next;
		mes "ได้โปรดเถอะนะ นำวัตถุดิบมาให้ข้าที...";
		mes "*ซิก ซิก ซิก*";
		next;
		mes "[F. Harrison]";
		mes "วัตถุดิบที่ข้าต้องการคือ";
		mes "10 Steel, 2 Eluniums,";
		mes "1 Oridecon, 20 Coal...";
		mes "และแกนโลหะพิเศษ (Special Metal Rod)";
		mes "ที่ใช้ใน Butcher...";
		mes "ข้าคิดว่ามีเพียง Lady Celena เท่านั้น";
		mes "ที่จะสร้างแกนพวกนั้นได้......";
		next;
		mes "[F. Harrison]";
		mes "แต่ก่อนอื่น เจ้าจำเป็นต้องไปคุย";
		mes "กับผู้ช่วยของ Lady Celena ที่ชื่อ 'Lab Director'";
		mes "แล้วขอความช่วยเหลือจากเขา";
		mes "เด็กนั่นค่อนข้างจะขี้หงุดหงิดหน่อยนะ แต่";
		mes "เขาจะช่วยเจ้าได้มากทีเดียวถ้าเจ้าคุย";
		mes "กับเขาดีๆ...";
		next;
		mes "[F. Harrison]";
		mes "ข้าขอโทษจริงๆ... นี่ไง ข้าจะ";
		mes "ให้เจ้ายืมอาวุธสุดหวงของข้า";
		mes "'Crimson Bolt' ไปก่อน แต่ต้องแน่ใจนะ";
		mes "ว่าเจ้าจะนำวัตถุดิบทั้งหมดนั่น";
		mes "กลับมาให้ข้าเมื่อเจ้าหามันครบแล้ว!";
		mes "สัญญากับข้านะ!!!";
		set gun_na,101;
		getitem 13102,1;
		close;
	}
	else if(gun_na == 101 || gun_na == 102){
		mes "[F. Harrison]";
		mes "วัตถุดิบที่ข้าต้องการคือ";
		mes "10 Steel, 2 Eluniums,";
		mes "1 Oridecon, 20 Coal...";
		mes "และแกนโลหะพิเศษ (Special Metal Rod)";
		mes "ที่ใช้ใน Butcher...";
		mes "ไปคุยกับ 'Lab Director' เพื่อขอความ";
		mes "ช่วยเหลือเรื่องแกนโลหะนั่นซะ";
		close;
	}
	else if(gun_na == 103){
		if(countitem(999) >= 10 && countitem(985) >= 2 && countitem(984) >= 1 && countitem(1003) >= 20){
			if(countitem(13102) > 0){
				delitem 999,10;
				delitem 985,2;
				delitem 984,1;
				delitem 1003,20;
				delitem 13102,1;
				set gun_na,104;
				mes "[F. Harrison]";
				mes "อาฮ่า! เจ้าได้วัตถุดิบทั้งหมด";
				mes "มาให้ข้าแล้ว! ข้าจะเริ่มซ่อม";
				mes "เดี๋ยวนี้เลย กรุณารอสักครู่นะ...";
				close;
			}
			else{
				mes "[F. Harrison]";
				mes "อาฮ่า! เจ้าได้วัตถุดิบทั้งหมด";
				mes "มาให้ข้าแล้ว!";
				mes "... แต่ 'Crimson Bolt' สุดหวง";
				mes "ของข้าอยู่ที่ไหนล่ะ!? ข้าจะไม่ซ่อม";
				mes "Butcher ให้เจ้าหรอกนะ ถ้าเจ้า";
				mes "ไม่เอามันกลับมาคืนข้า!!";
				close;
			}
		}
		else{
			mes "[F. Harrison]";
			mes "วัตถุดิบที่ข้าต้องการคือ";
			mes "10 Steel, 2 Eluniums,";
			mes "1 Oridecon, 20 Coal...";
			mes "และแกนโลหะพิเศษ (Special Metal Rod)";
			mes "ที่ใช้ใน Butcher...";
			mes "ดีมากที่เจ้าหาแกนโลหะพิเศษ";
			mes "เจอเรียบร้อยแล้ว";
			close;
		}
	}
	else if(gun_na == 104){
		if(checkweight(13159,1) == 0){
			mes "-เจ้าน้ำหนักเกินแล้วนะ-";
			mes "-กลับมาหลังจากที่เคลียร์";
			mes "ของออกไปบ้างก่อนเถอะ-";
			close;
		}
		set gun_na,0;
		getitem 13159,1;
		mes "[F. Harrison]";
		mes "ฟู่ว... ในที่สุดมันก็ซ่อมเสร็จแล้ว ขอบคุณ";
		mes "ในความพยายามของเจ้านะ ข้าขอโทษจริงๆ";
		mes "ที่ทำมันพัง และขอบคุณมากสำหรับ";
		mes "ความช่วยเหลือของเจ้า ข้าเองก็รู้สึกผิด";
		mes "ถ้าจะแค่ซ่อมมันเฉยๆ ข้าเลยแอบเพิ่ม";
		mes "พลังพิเศษเข้าไปให้ด้วย ข้าหวังว่า";
		mes "มันจะช่วยงานเจ้าได้ดีนะ";
		mes "เอาล่ะ สนุกกับมันนะ";
		close;
	}
	else if(gun_na == 0){
		if(BaseJob != Job_Gunslinger) {
			mes "[F. Harrison]";
			mes "ให้ตายสิ ข้าเบื่อจัง...";
			mes "ข้าสงสัยจังว่าจะมีอะไร";
			mes "น่าสนใจบ้างไหมนะ...";
			next;
			mes "[F. Harrison]";
			mes "เอาเถอะ... ข้าก็แค่เล่นกับเจ้า";
			mes "สุดสวย 'Crimson Bolt' ของข้าไปวันๆ";
			close;
		}
		if(BaseLevel > 67 && countitem(13158) > 0){
			mes "[F. Harrison]";
			mes "ให้ตายสิ ข้าเบื่อจัง...";
			mes "ข้าสงสัยจังว่าจะมีอะไร";
			mes "น่าสนใจบ้างไหมนะ...";
			next;
			mes "[F. Harrison]";
			mes "เอ๊ะ? เจ้าคือ Gunslinger งั้นรึ?";
			mes "ข้าไม่เคยเห็นอาวุธในมือ";
			mes "ของเจ้าชิ้นนั้นมาก่อนเลย...";
			next;
			mes "[F. Harrison]";
			mes "ข้าขอดูหน่อยได้ไหม? ได้โปรดเถอะ?";
			mes "เอาน่า ขอดูหน่อยนะ!";
			mes "ข้าเบื่อจะตายอยู่แล้วเนี่ย!";
			mes "ได้โปรดดดดดดด~?";
			next;
			if(select("ไม่มีทาง.:นี่ครับ/ค่ะ...") == 1){
				mes "[F. Harrison]";
				mes "เหอะ! เจ้าคิดว่าเจ้าเป็นคนเดียว";
				mes "ที่มีอาวุธเจ๋งๆ หรือไง!? ข้าเองก็";
				mes "มีเจ้า Crimson Bolt สุดสวยของข้าเหมือนกัน!";
				mes "เชอะ!!";
				close;
			}
			mes "[F. Harrison]";
			mes "หึหึ... นี่คือการพัฒนา";
			mes "ล่าสุดโดยห้องแล็บอาวุธปืน Einbroch";
			mes "ที่เขาเรียกกันว่าเจ้า 'Destroyer Butcher'";
			mes "ที่ไม่มีใครควบคุมได้สินะ หืม?";
			mes "น่าสนใจ... น่าสนใจสุดๆ...";
			next;
			mes "[F. Harrison]";
			mes "เฮ้ ให้ข้าลองใช้มันดูสัก";
			mes "ประเดี๋ยวได้ไหม? ไม่ต้องห่วงนะ ข้า";
			mes "รู้วิธีจัดการกับอาวุธน่า ข้า";
			mes "ไม่ทำมันพังหรอก!";
			mes "ตามนั้นแหละ! ไปกันเถอะ~";
			delitem 13158,1;
			set gun_na,100;
			close;
		}
	}
}
