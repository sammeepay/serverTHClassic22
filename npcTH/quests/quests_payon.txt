//===== rAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project; 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//= 1.6 Updated Dialogs from "Oxygen Mask Quest" [Samuray22]
//= 1.7 Updated headgear quests. [L0ne_W0lf]
//= 1.8 Updating NPC Position. [JohnnyPlayy]
//============================================================ 

// 'Pretend Murdered' Quest
//============================================================
payon_in01,99,72,4	script	Granny	103,{
	if (checkweight(908,1) == 0) {
		mes "^3355FFรอสักครู่! ตอนนี้คุณถือไอเทมมากเกินไปแล้ว โปรดกลับมาอีกครั้งหลังจากนำของบางส่วนไปเก็บไว้ในคลังของ Kafra Storage นะ^000000";
		close;
	}
	if (countitem(1049) > 3) {
		mes "[Granny]";
		mes "ฉันอยากจะทำเสื้อผ้าให้หลานๆ ของฉันใส่ในช่วงเทศกาลเหลือเกิน...";
		next;
		mes "[Granny]";
		mes "โอ้! คุณจะยก ^3355FFSkirt of Virgin^000000 ให้ฉันได้ไหม? ฉันต้องการมันสักสี่ตัวน่ะ ได้โปรดเถอะ ฉันขอร้องล่ะ";
		next;
		switch(select("ตกลงครับ/ค่ะ:ไม่มีทาง!")) {
		case 1:
			delitem 1049,4; //Skirt_Of_Virgin
			mes "[Granny]";
			mes "โอ้! ขอบคุณมากนะหลานชาย/สาว~ ทีนี้ฉันก็สามารถทำชุดให้ Kitty Cutty ได้แล้ว~";
			next;
			mes "[Granny]";
			mes "อา เดี๋ยวสิ! ฉันเกือบลืมไปเลย สมัยฉันยังสาวๆ ฉันเคยเป็นนักแสดงที่มีชื่อเสียงมากเลยนะ เพื่อนนักแสดงของฉันมอบของขวัญแต่งงานชิ้นหนึ่งให้ตอนที่ฉันออกจากคณะละคร ฉันเก็บมันไว้เพื่อเตือนความจำถึงช่วงเวลาที่มีค่าเหล่านั้น";
			next;
			mes "[Granny]";
			mes "ถ้าคุณไม่รังเกียจ...";
			mes "ฉันอยากจะมอบสิ่งนี้ให้คุณ";
			next;
			getitem 2293,1; //Pretend_Murdered
			mes "[Granny]";
			mes "ลาก่อนนะเหล่านักเดินทาง~!";
			mes "ขอบคุณที่ทำให้หญิงชราคนนี้มีความสุขมากขนาดนี้...";
			close;
		case 2:
			mes "[Granny]";
			mes "*เฮ้อ* ยายจะไปหา ^3355FFSkirt of Virgin^000000 ด้วยตัวคนเดียวได้ยังไงกัน? ยายทั้งอ่อนแอและขี้โรค แค่โยกเก้าอี้โยกยายก็เหนื่อยแทบขาดใจแล้ว *ซิก* ยายแค่อยากทำให้หลานๆ มีความสุขเท่านั้นเอง...";
			close;
		}
	}
	else {
		mes "[Granny]";
		mes "ฉันเก็บเห็ดบนภูเขา Payon ทุกวัน เวลาผ่านไปไวเหมือนโกหก แป๊บเดียวก็ถึงช่วงเทศกาลอีกแล้ว ฉันหวังว่าจะได้ทำเสื้อผ้าให้ครอบครัวของฉันในช่วงเวลานี้ของปีบ้าง";
		next;
		mes "[Granny]";
		mes "...";
		mes "แต่สำหรับการนั้น ฉันต้องการ ^3355FF4 Skirt of Virgin^000000...";
		next;
		mes "[Granny]";
		mes "นักเดินทางตัวน้อย ได้โปรดช่วยยายเถอะ ถ้าคุณพบ ^3355FFSkirt of Virgin^000000 บ้างละก็ ช่วยนำมาให้ยายหน่อยนะ ยายจะขอบคุณมากจริงๆ...";
		close;
	}
}

// 'Ear Muffs' Quest
//============================================================
payon_in01,18,10,4	script	Mystic Lady	76,{
	mes "[Mystic Lady]";
	mes "ครอบครัวของฉันผลิตและขายสินค้าพิเศษสำหรับฤดูหนาวนั่นคือ ^3355FFEar Muffs^000000 มาหลายปีแล้ว เราเพิ่งย้ายมาที่นี่ แต่ที่นี่อากาศอบอุ่นตลอดเวลาจนพวกเราแทบจะหาเลี้ยงชีพไม่ได้เลย";
	next;
	mes "[Mystic Lady]";
	mes "หากคุณมีแผนจะเดินทางไปยังภูมิภาคที่หนาวกว่านี้ ฉันขอแนะนำให้คุณพก ^3355FFEar Muffs^000000 ติดตัวไปด้วยนะ...";
	emotion ET_THINK;
	next;
	mes "[Mystic Lady]";
	mes "ที่ครอบหูเป็นของขึ้นชื่อของครอบครัวเรา และเราจะมอบมันให้กับลูกค้าที่มี ^FF33551 Cursed Ruby^000000, ^3311AA1 Headset,^000000 ^3355FF 200 Feathers^000000 และเงินจำนวน ^DDDD005000 Zeny^000000 ค่ะ";
	emotion ET_THINK;
	next;
	switch(select("โอ้ งั้นเหรอ? ฟังดูดีนะ:ไม่ล่ะครับ/ค่ะ ขอบคุณ")) {
	case 1:
		if ((countitem(724) > 0) && (countitem(949) > 199) && (countitem(5001) > 0) && (Zeny > 4999)) {
			mes "[Mystic Lady]";
			delitem 724,1; //Cardinal_Jewel_
			delitem 949,200; //Feather
			delitem 5001,1; //Headset
			set Zeny,Zeny-5000;
			mes "นี่ค่ะ ฉันจะมอบอันที่ทำเสร็จแล้วนี้ให้คุณ";
			getitem 2283,1; //Ear_Mufs
			next;
			mes "[Mystic Lady]";
			mes "ขอบคุณที่ซื้อสินค้าของเรานะคะ คุณจะไม่เสียใจที่ซื้อ Ear Muffs ของเราแน่นอน รับประกันความพึงพอใจค่ะ~";
			emotion ET_THINK;
			close;
		}
		else {
			mes "[Mystic Lady]";
			mes "โอ้ คุณพระ...";
			mes "คุณมีเงินหรือไอเทมไม่เพียงพอ น่าเสียดายที่เราไม่สามารถให้ส่วนลดได้ โปรดเข้าใจด้วยว่าพวกเราเองก็ต้องหาเลี้ยงชีพเหมือนกัน...";
			emotion ET_THINK;
			close;
		}
	case 2:
		mes "[Mystic Lady]";
		mes "ไม่เป็นไรค่ะ";
		mes "ฉันเชื่อว่าเรา";
		mes "จะได้พบกันใหม่";
		mes "...ในเร็วๆ นี้";
		emotion ET_THINK;
		close;
	}
}

// 'Oxygen Mask' Quest
//============================================================
payon_in02,25,71,4	script	Boy	86,{
	if (countitem(701) > 4) {
		mes "[Young Man]";
		mes "อ๊ากกกกกกก!";
		mes "อา ไม่นะ ผมทนไม่ไหวแล้ว...";
		next;
		mes "[Young Man]";
		mes "*ฮึก*";
		mes "ตอนนี้แม้แต่ตาของผมก็เป็นบ้าไปแล้ว!! ทำไมจู่ๆ พวกแมลงพวกนี้ถึงดูน่ารักขนาดนี้ล่ะ?! ไม่นะะะะ~!!!";
		next;
		switch(select("โชว์ Ora Ora ให้ดู:มอบ Ora Ora ให้:ยกเลิก")) {
		case 1:
			mes "[Young Man]";
			mes "โอ้โหหหหหห!!!";
			mes "นั่น นั่นใช่";
			mes "อันนั้นหรือเปล่า?!";
			mes "นี่มัน...";
			mes "Ora Ora!!";
			next;
			mes "[Young Man]";
			mes "ผมจะพูดสั้นๆ นะ! มอบมันให้ผม แล้วผมจะมอบสมบัติของผมให้คุณ! ข้อเสนอของผมเป็นไงล่ะ? อยากแลกเปลี่ยนไหม?";
			close;
		case 2:
			if (countitem(701) > 4) {
				delitem 701,5; //Ora_Ora
			}
			mes "[Young Man]";
			mes "มูฮ่าฮ่าฮ่าฮ่า!!!";
			mes "ในที่สุดผมก็ได้มันมา!";
			mes "Ora Ora!";
			mes "ไอ้พวก Thief Bugs";
			mes "หน้าโง่และน่ารังเกียจ!!";
			mes "พวกแกต้องชดใช้!!!";
			next;
			mes "[Young Man]";
			mes "แฮก... แฮก...";
			mes "ขอโทษทีครับ พอดีผมคุมตัวเองไม่ได้ด้วยความดีใจไปหน่อย ตอนนี้ ผมจะมอบสมบัติให้ตามที่สัญญาไว้ครับ";
			next;
			getitem 5004,1; //Oxygen_Mask
			mes "[Young Man]";
			mes "เมื่อคุณสวมสิ่งนี้ครอบปากและเริ่มหายใจ มันจะช่วยกรองสิ่งสกปรกออกก่อนที่จะเข้าไปถึงปอดของคุณครับ";
			next;
			mes "[Young Man]";
			mes "คากากากากากา!";
			mes "ผมแทบรอไม่ไหวแล้ว";
			mes "ที่จะได้ใช้";
			mes "Ora Ora นี่!";
			close;
		case 3:
			mes "[Young Man]";
			mes "ฮึฮึฮึฮึฮึ...";
			mes "จะเกิดอะไรขึ้นถ้าผมกำจัดไอ้พวก Thief Bugs พวกนี้ไม่ได้? ผมอาจจะถึงขั้นตกหลุมรักพวกมันก็ได้นะ... *ฮึก*";
			close;
		}
	}
	else {
		mes "[Young Man]";
		mes "ว้าาาาาา! ผม ผม...";
		mes "ผมทนต่อไปไม่ไหวแล้ว!";
		mes "ไอ้พวกสารเลวตัวเหม็นโสโครก!";
		next;
		switch(select("ฟังต่อ:ยกเลิก")) {
		case 1:
			mes "[Young Man]";
			mes "โธ่เอ๊ย...";
			mes "มันเริ่มตั้งแต่วันที่พ่อแม่ของผมเสียชีวิตตอนที่ผมยังเป็นเด็กตัวเล็กๆ ผมต้องทำงานหาเลี้ยงชีพตั้งแต่เนิ่นๆ และผ่านช่วงเวลาที่ยากลำบากมามาก";
			next;
			mes "[Young Man]";
			mes "ผมทำงานมา 10 ปีด้วยความหวังว่าจะซื้อบ้านในฝันที่ผมจะพบความสงบและสบายใจได้อีกครั้ง ในที่สุดผมก็คิดว่าผมมีเงิน Zeny มากพอที่จะซื้อบ้านแสนหวานของตัวเองได้แล้ว";
			next;
			mes "[Young Man]";
			mes "แต่น่าเสียดายที่ผมมีเงินไม่พอซื้อบ้านสวยๆ หลังที่ผมเล็งไว้ แต่แล้วผมก็มาเจอบ้านหลังนี้ ซึ่งทั้งใหญ่กว่าและราคาถูกกว่าหลังแรก ผมเลยซื้อมันโดยไม่ทันคิด...";
			next;
			mes "[Young Man]";
			mes "คุณพระช่วย! ปรากฏว่ามันคือสวรรค์ของพวก Thief Bugs ชัดๆ!!!";
			next;
			mes "[Young Man]";
			mes "ผมพยายามทำทุกอย่างที่ทำได้แล้ว ทั้งหลอกล่อพวกมัน ฆ่าพวกมัน แต่มันก็สงบแค่ชั่วคราวเท่านั้น! ผมถึงขนาดเคยคิดจะเผาบ้านหลังนี้ทิ้งเลยด้วยซ้ำ...!";
			next;
			mes "[Young Man]";
			mes "*หอบ* ผมหมายความว่า จริงๆ นะ ผมต้องใช้ ^3355FF Ora Ora ^000000 เพื่อกำจัดพวกมันเหรอ? ผู้คนบอกว่ามันหายากมากในต่างแดน";
			next;
			mes "[Young Man]";
			mes "ถ้าผมสามารถหามาได้สัก 5 อัน ผมคงไม่ต้องมานั่งหงุดหงิดกับแมลงน่ารังเกียจพวกนี้อีกต่อไป...";
			close;
		case 2:
			mes "[Young Man]";
			mes "การต่อสู้กับแมลงที่ไม่มีวันจบสิ้นนี้มันทำให้ผมหมดแรงจริงๆ ผมยอมแพ้แล้ว อยากทำอะไรก็ทำไปเลยไอ้พวกสวะ!";
			close;
		}
	}
}

// 'Helm of Angel' and 'Deviruchi Cap' Quest
//============================================================
-	script	Young man#12	89,{
	mes "[Young man]";
	mes "...มีอะไรเหรอ?";
	next;
	switch(select("คุณสามารถทำไอเทมพิเศษให้ฉันได้ไหม?:นี่คุณ")) {
	case 1:
		mes "[Young man]";
		mes "หือ? นี่คุณรู้แล้วเหรอว่าฉันเชี่ยวชาญด้านไหนน่ะ? งั้นก็คงไม่มีความจำเป็นต้องปิดบังกันอีกต่อไป บอกมาสิว่าคุณต้องการอะไร";
		next;
		switch(select("Helm of Angel.:Deviruchi Cap.:เดี๋ยววันหลังฉันจะมาใหม่")) {
		case 1:
			mes "[Young man]";
			mes "1 Helm (แบบมีรู).";
			mes "1 Angel Wing.";
			mes "5 Fang of Garm.";
			mes "นั่นคือทั้งหมดที่ฉันต้องการ";
			next;
			mes "[Young man]";
			mes "เดี๋ยวก่อน! บอกไว้ก่อนนะ ฉันไม่สนใจหรอกว่าไอเทมในตัวคุณจะถูกตีบวกมา หรือมีการใส่การ์ดเอาไว้หรือไม่";
			next;
			mes "[Young man]";
			mes "ฉันหมายถึง การ์ดหรือการตีบวกใดๆ ในไอเทมที่ฉันใช้ทำของให้คุณจะหายไปทันทีเมื่อฉันสร้างไอเทมใหม่ขึ้นมา เพราะงั้นระวังให้ดีเวลาส่งของให้ฉันล่ะ";
			next;
			mes "[Young man]";
			mes "รวบรวมของครบแล้วใช่ไหม? งั้นก็ส่งมา";
			next;
			switch(select("ส่งไอเทมให้เขา:ไม่ส่งไอเทมให้เขา")) {
			case 1:
				if ((countitem(2229) > 0) && (countitem(2254) > 0) && (countitem(7036) > 4)) {
					mes "[Young man]";
					mes ".....อืม";
					mes "ได้เลย นี่คือ Helm of Angel ของคุณ รับไปสิ";
					delitem 2229,1; //Helm_
					delitem 2254,1; //Angelic_Chain
					delitem 7036,5; //Fang_Of_Garm
					close2;
					getitem 5025,1; //Helm_Of_Angel
					end;
				}
				else {
					mes "[Young man]";
					mes "...จำนวนไอเทมไม่ถูกต้องนะ คุณไปเช็คดูให้ดีๆ อีกรอบจะดีกว่า";
					close;
				}
			case 2:
				mes "[Young man]";
				mes "ตามใจคุณแล้วกัน...";
				close;
			}
		case 2:
			mes "[Young man]";
			mes "600 Little Evil Horn.";
			mes "40 Talon of Griffon.";
			mes "นั่นคือทั้งหมดที่ฉันต้องการ";
			next;
			mes "[Young man]";
			mes "เดี๋ยวก่อน! บอกไว้ก่อนนะ ฉันไม่สนใจหรอกว่าไอเทมในตัวคุณจะถูกตีบวกมา หรือมีการใส่การ์ดเอาไว้หรือไม่";
			next;
			mes "[Young man]";
			mes "ฉันหมายถึง การ์ดหรือการตีบวกใดๆ ในไอเทมที่ฉันใช้ทำของให้คุณจะหายไปทันทีเมื่อฉันสร้างไอเทมใหม่ขึ้นมา เพราะงั้นระวังให้ดีเวลาส่งของให้ฉันล่ะ";
			next;
			mes "[Young man]";
			mes "รวบรวมของครบแล้วใช่ไหม? งั้นก็ส่งมา";
			next;
			switch(select("ส่งไอเทมให้เขา:ไม่ส่งไอเทมให้เขา")) {
			case 1:
				if ((countitem(1038) > 599) && (countitem(7048) > 39)) {
					mes "[Young man]";
					mes ".....อืม นี่คือ Deviruchi Hat ของคุณ รับไปสิ";
					delitem 1038,600; //Petite_DiablOfs_Horn
					delitem 7048,40; //Talon_Of_Griffin
					close2;
					getitem 5038,1; //Deviruchi_Cap
					end;
				}
				else {
					mes "[Young man]";
					mes "...จำนวนไอเทมไม่ถูกต้องนะ คุณไปเช็คดูให้ดีๆ อีกรอบจะดีกว่า";
					close;
				}
			case 2:
				mes "[Young man]";
				mes "ตามใจคุณแล้วกัน...";
				close;
			}
		case 3:
			mes "[Young man]";
			mes "อืม ตกลง";
			mes "งั้นเราไว้ค่อยเจอกันใหม่";
			close;
		}
	case 2:
		mes "[Young man]";
		mes "อืม";
		mes "ดูแลตัวเองด้วย";
		close;
	}
}
