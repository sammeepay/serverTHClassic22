//===== rAthena Script ======================================= 
//= BattleGround System - Common NPCs
//===== By: ================================================== 
//= ????, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added VIP features and created a reward function. [Euphy]
//============================================================

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	419,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	419,{ end; }

bat_room,160,141,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "นักผจญภัยผู้ชาญฉลาด ท่านพอจะสละแรงกายเพื่อช่วยให้พวกเราคว้าชัยชนะในครั้งนี้ได้หรือไม่?";
	next;
	switch(select("เหตุผลของสงครามครั้งนี้คืออะไร?:ช่วยเล่าเกี่ยวกับนายพลกีโยมให้ฟังหน่อย")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "กษัตริย์ผู้ยิ่งใหญ่แห่ง Maroll พระเจ้ามาร์เซล มารอลโลที่ 7 ทรงพระประชวรหนักในช่วงนี้";
		mes "ฝ่าบาททรงประกาศว่า จะมอบอนาคตของอาณาจักร Maroll ให้ข้า หรือไม่ก็นายพลกีโยม เจ้าชายลำดับที่ 3 เป็นผู้สืบทอด";
		next;
		mes "[Prince Croix]";
		mes "นายพลกีโยมอาจจะได้เปรียบในการศึกครั้งนี้ เพราะเขาคือนายพลผู้ยิ่งใหญ่แห่ง Maroll แต่นั่นก็ไม่ได้หมายความว่าเขาจะเป็นฝ่ายชนะเสมอไป";
		mes "ข้าต้องการชนะศึกครั้งนี้ เพื่อนำพาความรุ่งเรืองมาสู่ชาว Maroll... พวกเขาบอบช้ำจากสงครามมามากพอแล้ว";
		next;
		switch(select("ตกลง ข้าต้องการเข้าร่วมกับท่าน:จบการสนทนา")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "ขอบใจเจ้ามาก ข้ารู้สึกมั่นใจว่าจะชนะแน่นอนหากมีนักผจญภัยเช่นเจ้าคอยช่วยเหลือ";
			mes "เอาล่ะ ลงไปข้างล่างเถอะ ไปรวมกลุ่มกับพวกพ้องของเจ้าแล้วเตรียมฝึกฝนทักษะเพื่อรับมือกับศัตรูซะ!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "เพื่อ Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "เจ้าชายลำดับที่ 3 กีโยม คือนายพลผู้เกรียงไกรของ Maroll";
		mes "มันเปล่าประโยชน์ที่จะอธิบายว่าเขาเป็นผู้นำหรือขุนศึกที่ยิ่งใหญ่แค่ไหน ในเมื่อเขากุมอำนาจทางการทหารส่วนใหญ่ของอาณาจักรเอาไว้ในมือ";
		next;
		mes "[Prince Croix]";
		mes "แต่น่าเสียดาย มีสิ่งหนึ่งที่เขาและเหล่าผู้ติดตามมองข้ามไป...";
		mes "ชาว Maroll ต้องการให้เราผลาญเงินมหาศาลไปกับแสนยานุภาพทางการทหารจริงๆ อย่างนั้นหรือ?";
		mes "พวกเราทุกข์ทรมานจากสงครามมามากเกินพอแล้ว";
		mes "ข้าเชื่อว่าอาวุธไม่ใช่หนทางที่ดีที่สุดในการนำพาความมั่งคั่งมาสู่ประเทศชาติ";
		next;
		mes "[Prince Croix]";
		mes "ข้าไม่ปรารถนาที่จะเสียเลือดเนื้อ แต่ข้าไม่มีทางเลือกอื่นนอกจากต้องสู้เพื่อโอกาสที่จะเกิดสันติภาพ และเพื่อความผาสุกของประชาชนของข้า";
		next;
		switch(select("ตกลง ข้าต้องการเข้าร่วมกับท่าน:จบการสนทนา")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "ขอบใจเจ้ามาก ข้ารู้สึกมั่นใจว่าจะชนะแน่นอนหากมีนักผจญภัยเช่นเจ้าคอยช่วยเหลือ";
			mes "เอาล่ะ ลงไปข้างล่างเถอะ ไปรวมกลุ่มกับพวกพ้องของเจ้าแล้วเตรียมฝึกฝนทักษะเพื่อรับมือกับศัตรูซะ!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "เพื่อ Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	415,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "นักผจญภัยผู้มีไฟอันแรงกล้า พวกเราต้องการความสามารถของเจ้าเพื่อคว้าชัยชนะในศึกครั้งนี้";
	next;
	switch(select("เหตุผลของสงครามครั้งนี้คืออะไร?:ช่วยเล่าเกี่ยวกับเจ้าชายครอยซ์ให้ฟังหน่อย")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "กษัตริย์ผู้ยิ่งใหญ่ของเรา พระเจ้ามาร์เซล มารอลโลที่ 7 ทรงพระประชวรหนักในช่วงนี้";
		mes "ฝ่าบาททรงประกาศว่า จะเลือกข้าหรือไม่ก็เจ้าชายครอยซ์ จากบรรดาโอรสทั้ง 9 พระองค์ เพื่อขึ้นเป็นกษัตริย์องค์ต่อไป";
		next;
		mes "[General Guillaume]";
		mes "แผ่นดินเดียวจะมีกษัตริย์สององค์ไม่ได้! มีเพียงผู้ชนะจากการศึกที่ฝ่าบาทกำหนดขึ้นเท่านั้นที่จะได้ครองบัลลังก์";
		next;
		mes "[General Guillaume]";
		mes "อย่างไรก็ตาม นี่ไม่ใช่แค่การต่อสู้ระหว่างเราสองคน แต่มันคือการตัดสินอนาคตของประเทศนี้";
		mes "ข้าขอเอาเกียรติเป็นเดิมพันเพื่อพิสูจน์ว่า ข้าคือเพียงผู้เดียวที่สามารถปกป้อง Maroll จากภัยคุกคามภายนอกได้";
		next;
		switch(select("ตกลง ข้าต้องการเข้าร่วมกับท่าน:จบการสนทนา")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "ยินดีต้อนรับสู่กองทัพของข้า สหาย";
			mes "แววตาของเจ้าบอกข้าว่า เจ้าคือนักรบที่ข้าสามารถไว้วางใจได้";
			next;
			mes "[General Guillaume]";
			mes "เอาล่ะ ขึ้นไปข้างบนแล้วไปลงทะเบียนร่วมรบกับพวกพ้องของเจ้าซะ";
			mes "ข้าเชื่อว่าพวกเขาจะต้อนรับเจ้าอย่างสุดหัวใจแน่นอน!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "ข้าจะเป็นคนชิงธงนั่นมาเอง!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "เจ้าชายลำดับที่ 5 ครอยซ์ ปัจจุบันดำรงตำแหน่งเป็นนายกรัฐมนตรีแห่ง Maroll";
		mes "เขาคิดว่าเรื่องสำคัญของประเทศทุกอย่างสามารถตกลงกันได้แค่บนโต๊ะทำงาน";
		mes "และเชื่อมั่นในโลกสวยที่ว่าจะอยู่ร่วมกับประเทศอื่นได้อย่างสงบสุข";
		next;
		mes "[General Guillaume]";
		mes "เขามันโง่เขลาเกินกว่าจะยอมรับความจริงที่ว่า สิ่งที่เรียกว่าสันติภาพนั้นถูกสร้างขึ้นบนกองซากศพของผู้ที่เสียสละในสงคราม... ในขณะที่พลเมืองทั่วไปและพวกชนชั้นสูงใช้ชีวิตอย่างสุขสบาย โดยไม่เคยรับรู้ถึงความโหดร้ายที่แลกมาเพื่อให้พวกเขาได้อยู่แบบนั้น";
		next;
		mes "[General Guillaume]";
		mes "เขามันอ่อนหัดเกินกว่าจะเข้าใจโลกความเป็นจริง....";
		mes "ข้าไม่อาจทิ้ง Maroll ไว้ในมือของคนที่เพ้อฝันแบบนั้นได้หรอก!";
		next;
		mes "[General Guillaume]";
		mes "ความเชื่อที่เพ้อฝันของเขาจะทำให้ประเทศนี้จมกองความยากจนและทำให้ผู้คนอ่อนแอลง ถ้าเขาได้เป็นกษัตริย์ Maroll จะไม่มีวันสงบสุขจากการรุกรานของประเทศอื่นได้เลย";
		mes "ข้าต้องการสอนให้เขาได้รู้ว่า อะไรคือสิ่งที่ทำให้ประเทศเล็กๆ แห่งนี้แข็งแกร่งและรุ่งเรืองได้... มันคือแสนยานุภาพทางการทหารยังไงล่ะ!";
		next;
		switch(select("ข้าต้องการเข้าร่วมกองทัพของท่าน!:จบการสนทนา")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "ยินดีต้อนรับสู่กองทัพของข้า สหาย";
			mes "แววตาของเจ้าบอกข้าว่า เจ้าคือนักรบที่ข้าสามารถไว้วางใจได้";
			next;
			mes "[General Guillaume]";
			mes "เอาล่ะ ขึ้นไปข้างบนแล้วไปลงทะเบียนร่วมรบกับพวกพ้องของเจ้าซะ";
			mes "ข้าเชื่อว่าพวกเขาจะต้อนรับเจ้าอย่างสุดหัวใจแน่นอน!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "ข้าจะเป็นคนชิงธงนั่นมาเอง!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	974,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	Teleporter#Battlefield	124,{
	mes "[Teleporter]";
	mes "คุณต้องการออกจากสนามรบแล้วใช่ไหม? ใช้บริการของข้าเพื่อเดินทางกลับเข้าเมืองได้เลย";
	next;
	switch(select("ออกจากที่นี่:ยังไม่ออก")) {
	case 1:
		mes "[Teleporter]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morocc Ruins","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel","rachel";
			setarray .@xy[0],115,124;
			break;
		case 8:
			setarray .@mapname$[0],"Morocc","morocc";
			setarray .@xy[0],156,46;
			break;
		}
		mes "ข้าจะส่งคุณกลับไปยังเมือง " + .@mapname$[0] + " นะ";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[Teleporter]";
		mes "ข้าจะรออยู่ที่นี่เสมอเมื่อไหร่ก็ตามที่คุณต้องการใช้บริการ";
		close;
	}
	end;
}

-	script	Maroll Battle Recruiter::BatRecruit	728,{
	mes "[Maroll Battle Recruiter]";
	mes "สวัสดี นักผจญภัย";
	mes "ข้าคืออัศวินจากอาณาจักรที่ห่างไกลที่ชื่อว่าอาณาจักร Maroll";
	next;
	mes "[Maroll Battle Recruiter]";
	mes "ในขณะนี้ เจ้าชายทั้งสองแห่งอาณาจักรเรากำลังต่อสู้เพื่อชิงบัลลังก์แห่ง Maroll และพวกเขากำลังต้องการเหล่านักรบผู้มากประสบการณ์เช่นเจ้า";
	mes "ท่านสนใจที่จะสละแรงกายเพื่อช่วยเหลือหนึ่งในเจ้าชายแห่งอาณาจักร Maroll หรือไม่?";
	next;
	switch(select("เข้าร่วม:ไม่เข้าร่วม")) {
	case 1:
		mes "[Maroll Battle Recruiter]";
		mes "ขอให้เทพเจ้าแห่งสงครามสถิตอยู่กับเจ้า";
		close2;
		getmapxy(.@mapname$,.@x,.@y,BL_NPC);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else if (.@mapname$ == "morocc")
			set bat_return,8;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Maroll Battle Recruiter]";
		mes "ข้าจะประจำการอยู่ที่นี่เพื่อมองหาเหล่านักรบเพิ่มเติมเสมอ กลับมาหาข้าได้ทุกเมื่อที่เจ้าสนใจ";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit7	728

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman
//============================================================
bat_room,138,144,4	script	Repairman#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "คำสั่งถูกยกเลิก";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "ต้องการให้ช่วยอะไรไหมครับ?";
		next;
		switch(select("ปิดสนามรบ (Close):เปิดสนามรบ (Open):รีเซ็ตสนาม a01:รีเซ็ตสนาม b01:รีเซ็ตสนาม a02:รีเซ็ตสนาม b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "ดำเนินการเสร็จสิ้น";
		close;
	}
}

// Badges Exchange
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน !! -";
		mes "- ตอนนี้คุณพกไอเทม -";
		mes "- มากเกินไป -";
		mes "- กรุณาลองใหม่ -";
		mes "- หลังจากจัดการน้ำหนักตัวแล้ว -";
		close;
	}
	mes "[Erundek]";
	mes "เจ้ามีเหรียญตราจากสนามรบมาด้วยหรือเปล่า?";
	mes "ข้าสามารถนำ Bravery Badge และ Valor Badge มาแลกเป็นไอเทมรางวัลให้เจ้าได้นะ";
	next;
	switch(select("แลกเปลี่ยนเหรียญตรา:ตรวจสอบแคตตาล็อกไอเทม")) {
	case 1:
		mes "[Erundek]";
		mes "เจ้าต้องการแลกเปลี่ยนไอเทมประเภทไหนล่ะ?";
		mes "หากต้องการข้อมูลเพิ่มเติมของไอเทมแต่ละชิ้น สามารถตรวจสอบได้ที่ ^3131FFแคตตาล็อก^000000 ของเรานะ";
		next;
		switch(select("อาวุธ:ชุดป้องกัน:เครื่องประดับ:ไอเทมใช้สิ้นเปลือง")) {
		case 1:
			mes "[Erundek]";
			mes "เจ้าเลือกหมวด ^3131FFอาวุธ^000000";
			mes "รายการอาวุธต่อไปนี้สามารถแลกได้ด้วยเหรียญตราสนามรบ";
			mes "โปรดทราบว่าไอเทมที่ใช้ ^3131FFBravery Badge จะมีสัญลักษณ์ (BB)^000000 และ ^3131FFValor Badge จะเป็น (VB)^000000";
			next;
			switch(select("มีด/ดาบมือเดียว/ดาบสองมือ/หอกสองมือ:ไม้เท้า/กระบอง/ขวานสองมือ/ดาวกระจาย:ธนู/กาตาร์/เครื่องดนตรี/แส้:หนังสือ/สนับมือ:ปืนพก/ไรเฟิล/กลไก/ลูกซอง/เครื่องยิงระเบิด")) {
			case 1:
				mes "[Erundek]";
				mes "รายการต่อไปนี้อยู่ในหมวด ^3131FFมีด, ดาบมือเดียว, ดาบสองมือ และ หอกสองมือ^000000";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[Erundek]";
				mes "รายการต่อไปนี้อยู่ในหมวด ^3131FFไม้เท้า / กระบอง / ขวานสองมือ / ดาวกระจาย Huuma^000000";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[Erundek]";
				mes "รายการต่อไปนี้อยู่ในหมวด ^3131FFธนู / กาตาร์ / เครื่องดนตรี / แส้^000000";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[Erundek]";
				mes "รายการต่อไปนี้อยู่ในหมวด ^3131FFหนังสือ / สนับมือ^000000";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[Erundek]";
				mes "รายการต่อไปนี้อยู่ในหมวด ^3131FFปืนพก / ไรเฟิล / ปืนกล / ลูกซอง / เครื่องยิงระเบิด^000000";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?" (BB)":" (VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@type$, ((.@Weapons[.@i+1]==7828)?" (BB)":" (VB)");
			mes "[Erundek]";
			mes "เจ้าเลือก ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000";
			mes "เจ้าสามารถใช้ ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 เพื่อแลกไอเทมชิ้นนี้";
			mes "ยืนยันการแลกเปลี่ยนหรือไม่?";
			next;
			switch(select("ยังไม่แลก:ตกลง แลกเปลี่ยน")) {
			case 1:
				break;
			case 2:
				mes "[Erundek]";
				mes "เจ้าต้องการใช้ ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 เพื่อรับ ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000 ใช่หรือไม่?";
				next;
				mes "[Erundek]";
				mes "โปรดจำไว้ว่า ไอเทมรางวัลจากสนามรบจะ ^FF0000ผูกมัดกับตัวละคร (Character Bound)^000000 เจ้ายังต้องการไอเทมชิ้นนี้อยู่ใช่ไหม?";
				next;
				switch(select("ใช่:ไม่")) {
				case 1:
					mes "[Erundek]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "ขอบใจมากที่มาแลกเปลี่ยน ขอให้โชคดี";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "ขออภัยด้วย แต่เจ้ามีเหรียญตราไม่เพียงพอสำหรับการแลกเปลี่ยนนะ";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "ต้องการเวลาตรวจสอบไอเทมเพิ่มอีกหน่อยไหม?";
			close;
		case 2:
			mes "[Erundek]";
			mes "เจ้าเลือกหมวด ^3131FFชุดป้องกัน^000000";
			mes "ชุดป้องกันต่อไปนี้สามารถแลกได้ด้วยเหรียญตราสนามรบ";
			next;
			switch(select("ผ้าคลุม / รองเท้า:ชุดเกราะ")) {
			case 1:
				setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "เจ้าเลือกหมวด ^3131FFเครื่องประดับ^000000";
			mes "เจ้าสามารถแลกเปลี่ยน Medal of Honor ด้วยเหรียญตราตามสายอาชีพได้ดังนี้:";
			next;
			setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[Erundek]";
			mes "เจ้าเลือกหมวด ^3131FFไอเทมใช้สิ้นเปลือง^000000";
			mes "รายการไอเทมต่อไปนี้สามารถแลกได้ด้วยเหรียญตราสนามรบ:";
			next;
			setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "เรามีไอเทมมากมาย ลองดูให้ดีก่อนตัดสินใจเลือกซื้อนะ";
		close2;
		readbook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "เจ้าเลือก ^3131FF"+getitemname(.@Items[.@i])+"^000000";
	switch(.@Items[.@i]) {
		case 2720: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Swordman และ Taekwon Master เท่านั้น"; break;
		case 2721: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Thief เท่านั้น"; break;
		case 2722: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Acolyte เท่านั้น"; break;
		case 2723: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Magician เท่านั้น"; break;
		case 2724: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Archer เท่านั้น"; break;
		case 2725: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Merchant เท่านั้น"; break;
		case 2733: mes "ไอเทมชิ้นนี้สำหรับอาชีพ Gunslinger เท่านั้น"; break;
		default: break;
	}
	mes "เจ้าสามารถใช้ ^FF0000"+.@Items[.@i+1]+" "+getitemname(7828)+" หรือ "+.@Items[.@i+1]+" "+getitemname(7829)+"^000000 เพื่อแลกไอเทมนี้";
	mes "ยืนยันการแลกเปลี่ยนหรือไม่?";
	next;
	switch(select("ยังไม่แลก:ตกลง แลกเปลี่ยน")) {
	case 1:
		mes "[Erundek]";
		mes "ต้องการเวลาตรวจสอบไอเทมเพิ่มอีกหน่อยไหม?";
		break;
	case 2:
		mes "[Erundek]";
		mes "เจ้าต้องการใช้เหรียญประเภทไหนแลกเปลี่ยนล่ะ?";
		mes "ต้องใช้เหรียญตราจำนวน ^3131FF"+.@Items[.@i+1]+" เหรียญ^000000 นะ";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "โปรดจำไว้ว่า ไอเทมรางวัลจะ ^FF0000ผูกมัดกับตัวละคร (Character Bound)^000000 เจ้ายังต้องการไอเทมชิ้นนี้อยู่ใช่ไหม?";
			next;
		}
		set .@j, select("Bravery Badge:Valor Badge:ยกเลิก");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "ยกเลิกการแลกเปลี่ยน";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@Items[.@i+1]) {
			mes "ขอบใจมากที่มาแลกเปลี่ยน ขอให้โชคดี";
			delitem .@cost, .@Items[.@i+1];
			getitem .@Items[.@i],1;
		}
		else mes "เจ้ามี " + getitemname(.@cost) + " ไม่เพียงพอ";
		break;
	}
	close;
}

// Badge Reward Function
//============================================================
function	script	F_BG_Badge	{
	.@team$ = getarg(1);
	.@bg_type$ = getarg(2);

	if (.@bg_type$ == "Tierra") {
		set .@badge,7828; // Bravery Badge
		set .@amount_win,3;
		set .@amount_lose,1;
	} else if (.@bg_type$ == "Flavius") {
		set .@badge,7829; // Valor Badge
		set .@amount_win,9;
		set .@amount_lose,3;
	} else
		return;

	if (getarg(0) == 1) { // กรณีที่ชนะ
		set .@amount, .@amount_win;
		if (.@team$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "แด่กีโยมผู้ได้รับพร!";
		} else if (.@team$ == "Croix") {
			mes "[Swandery]";
			mes "แด่ครอยซ์ผู้ได้รับพร!";
		}
		mes "พวกเรามาเฉลิมฉลองให้กับชัยชนะอันรุ่งโรจน์นี้กันเถอะ!";
		mes "คุณ " + strcharinfo(0) + " นี่คือเครื่องหมายที่สะท้อนถึงชัยชนะของท่าน";
	} else { // กรณีที่แพ้
		set .@amount, .@amount_lose;
		if (.@team$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "ถึงเจ้าจะพ่ายแพ้ แต่เจ้าก็ได้ทุ่มเทให้กับการต่อสู้ครั้งนี้อย่างเต็มที่";
			mes "นี่คือรางวัลสำหรับความทุ่มเทอันยิ่งใหญ่จากท่านกีโยม มารอลโล!";
			mes "จงใช้ความพ่ายแพ้ในครั้งนี้เป็นบทเรียน และครั้งหน้าชัยชนะจะต้องเป็นของเจ้าแน่นอน";
		} else if (.@team$ == "Croix") {
			mes "[Swandery]";
			mes "โอ้ คุณ " + strcharinfo(0) + " อย่าได้เสียใจไปเลยนะ";
			mes "แม้ว่าเราจะไม่ชนะ แต่เราก็ได้พยายามทำกันอย่างสุดความสามารถแล้ว";
			mes "นี่คือของกำนัลจากเจ้าชายครอยซ์ โปรดอย่าลืมการต่อสู้ในครั้งนี้ล่ะ ครั้งหน้าเราจะคว้าชัยชนะมาให้ได้";
		}
	}
	close2;

	// หากระบบ VIP เปิดใช้งาน ผู้เล่น VIP จะได้รับเหรียญเพิ่มอีก 2 อัน
	if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
		set .@amount, .@amount + 2;

	set .@medal_gap, 500 - countitem(.@badge);
	if (.@medal_gap >= .@amount)
		getitem .@badge, .@amount;
	else
		getitem .@badge, .@medal_gap;
	return;
}