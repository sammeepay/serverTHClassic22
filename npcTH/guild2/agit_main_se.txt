//===== rAthena Script =======================================
//= War of Emperium SE - Template File
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Like agit_main, this file is required
//= for SE castles to function.
//===== Additional Comments: =================================
//= 0.x Previous authors: L0ne_W0lf, Zephyrus, Brian.
//= 1.0 If anything breaks, blame Maki. [Euphy]
//= 1.1 Fixed an incorrect label execution. [Euphy]
//= 1.2 Hopefully fixed a processing error. [Euphy]
//= 1.3 Fixed barricade issue in schg_cas02. [Cookie]
//= 1.4 Added OnGuildBreak event and a spawn check. [Euphy]
//= 1.4a Fixed Guardian Stone respawns. [Euphy]
//============================================================

// Core, triggers all other events
//============================================================
-	script	Manager#template	-1,{
OnAgitInit2:
OnRecvCastle2:
	if (strnpcinfo(2) == "template") end;
	if (!getcastledata(strnpcinfo(2),CD_GUILD_ID)) {
		donpcevent strnpcinfo(0)+"::OnStart";
		// Monster spawns are identical for all castles.
		monster strnpcinfo(2),0,0,"Evil Druid",1117,10;
		monster strnpcinfo(2),0,0,"Khalitzburg",1132,4;
		monster strnpcinfo(2),0,0,"Abysmal Knight",1219,3;
		monster strnpcinfo(2),0,0,"Executioner",1205,1;
		monster strnpcinfo(2),0,0,"Penomena",1216,10;
		monster strnpcinfo(2),0,0,"Alarm",1193,18;
		monster strnpcinfo(2),0,0,"Clock",1269,9;
		monster strnpcinfo(2),0,0,"Raydric Archer",1276,12;
		monster strnpcinfo(2),0,0,"Wanderer",1208,3;
		monster strnpcinfo(2),0,0,"Alice",1275,1;
		monster strnpcinfo(2),0,0,"Bloody Knight",1268,2;
		monster strnpcinfo(2),0,0,"Dark Lord",1272,2;
		monster strnpcinfo(2),0,0,"Tower Keeper",1270,4;
	}
	if (getcastledata(strnpcinfo(2),CD_ENABLED_KAFRA) < 1)
		disablenpc "Kafra Employee#"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	end;

OnAgitStart2:
	if (strnpcinfo(2) == "template") end;
	if (agitcheck2()) {
		maprespawnguildid strnpcinfo(2),getcastledata(strnpcinfo(2),CD_GUILD_ID),2;
		gvgon strnpcinfo(2);
		donpcevent strnpcinfo(0)+"::OnStart";
	}
	else for(set .@i,0; .@i<4; set .@i,.@i+1)
		donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnDisable";
	end;

OnAgitEnd2:
	if (strnpcinfo(2) == "template") end;
	gvgoff strnpcinfo(2);
	if (getcastledata(strnpcinfo(2),CD_GUILD_ID)) {
		set .@str$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
		killmonster strnpcinfo(2),"Steward#"+.@str$+"::OnStartArena";
		donpcevent strnpcinfo(0)+"::OnReset";
		donpcevent "Steward#"+.@str$+"::OnStop";
	}
	end;

OnGuildBreak:
	if (strnpcinfo(2) == "template") end;
	killmonster strnpcinfo(2),"gard1#"+strnpcinfo(2)+"::OnGuardianDied";
	killmonster strnpcinfo(2),"gard2#"+strnpcinfo(2)+"::OnGuardianDied";
	disablenpc "Kafra Employee#"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setcastledata strnpcinfo(2),CD_GUILD_ID,0;
	sleep 7000;
	//announce "Guild Base ["+getcastlename(strnpcinfo(2))+"] has been abandoned.",0;
	announce "ปราสาท ["+getcastlename(strnpcinfo(2))+"] กลายเป็นพื้นที่ว่างเปล่าแล้ว",0;
	donpcevent strnpcinfo(0)+"::OnRecvCastle2";
	end;

OnStart:
	// $agit_ar0x[] - $agit_sc0x[]
	// 1st Guardian stone, 2nd Guardian stone, Barrier 1, Barrier 2, Barrier 3, Summon Guardians
	// Settings for all but Summon Guardians: 0 = Okay | 1 = Destroyed | 2 = Repairing
	// Summon Guardians: 0 = Do not Summon | 1 = Summon
	if (getcastledata(strnpcinfo(2),CD_GUILD_ID)) {
		setarray getd("$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"[0]"),0,0,0,0,0,0;
		donpcevent "df1#"+strnpcinfo(2)+"::OnEnable";
		donpcevent "df2#"+strnpcinfo(2)+"::OnEnable";
		for(set .@i,0; .@i<4; set .@i,.@i+1)
			donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnEnable";
	}

OnEmpSpawn:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	if (mobcount(strnpcinfo(2),"Steward#"+.@str$+"::OnStartArena")) end;
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") setarray .@i[0],87,219;
		else if (strnpcinfo(2) == "arug_cas02") setarray .@i[0],89,256;
		else setarray .@i[0],141,293;	// Castles 3,4,5 are identical.
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") setarray .@i[0],162,193;
		else if (strnpcinfo(2) == "schg_cas03") setarray .@i[0],338,202;
		else setarray .@i[0],120,272;	// Castles 1,4,5 are identical.
	}
	monster strnpcinfo(2),.@i[0],.@i[1],"Emperium",1288,1,"Steward#"+.@str$+"::OnStartArena";
	end;

OnReset:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	donpcevent "df1#"+strnpcinfo(2)+"::OnDisable";
	donpcevent "df2#"+strnpcinfo(2)+"::OnDisable";
	donpcevent "gard1#"+strnpcinfo(2)+"::OnReset";
	donpcevent "gard2#"+strnpcinfo(2)+"::OnReset";
	donpcevent "1st Guardian Stone#"+.@str$+"::OnDisable";
	donpcevent "2nd Guardian Stone#"+.@str$+"::OnDisable";
	for(set .@i,1; .@i<4; set .@i,.@i+1)
		donpcevent "Control Device0"+.@i+"#"+.@str$+"::OnDisable";
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnDisable";
	if (agitcheck2())
		setarray getd("$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"[0]"),0,0,1,1,1,0;
	end;

OnChange:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setarray getd("$agit_"+.@str$+"[0]"),2,2,1,1,2,0;
	donpcevent strnpcinfo(0)+"::OnEmpSpawn";
	donpcevent "Control Device03#"+.@str$+"::OnEnable";
	donpcevent "1st Guardian Stone#"+.@str$+"::OnEnable";
	donpcevent "2nd Guardian Stone#"+.@str$+"::OnEnable";
	end;

OnClock0001:
	// Spawn Treasure Chests based on castle economy.
	if (strnpcinfo(2) == "template") end;
	if (!getcastledata(strnpcinfo(2),CD_GUILD_ID)) end;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnTreasureDied";
	if (getcastledata(strnpcinfo(2),CD_INVESTED_ECONOMY)) {
		set .@Economy,getcastledata(strnpcinfo(2),CD_CURRENT_ECONOMY);
		setcastledata strnpcinfo(2),CD_CURRENT_ECONOMY,.@Economy+getcastledata(strnpcinfo(2),CD_INVESTED_ECONOMY)+(rand(2) && getgdskilllv(getcastledata(strnpcinfo(2),CD_GUILD_ID),10014));
		if (getcastledata(strnpcinfo(2),CD_CURRENT_ECONOMY) > 100) setcastledata strnpcinfo(2),CD_CURRENT_ECONOMY,100;
		setcastledata strnpcinfo(2),CD_INVESTED_ECONOMY,0;
	}
	if (getcastledata(strnpcinfo(2),CD_INVESTED_DEFENSE)) {
		set .@Defence,getcastledata(strnpcinfo(2),CD_CURRENT_DEFENSE);
		setcastledata strnpcinfo(2),CD_CURRENT_DEFENSE,.@Defence+getcastledata(strnpcinfo(2),CD_INVESTED_DEFENSE);
		if (getcastledata(strnpcinfo(2),CD_CURRENT_DEFENSE) > 100) setcastledata strnpcinfo(2),CD_CURRENT_DEFENSE,100;
		setcastledata strnpcinfo(2),CD_INVESTED_DEFENSE,0;
	}
	set .@Treasure,getcastledata(strnpcinfo(2),CD_CURRENT_ECONOMY)/5+4;
	if (!.@Treasure) end;
	freeloop(1);
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") { 
			set .@treasurebox,1943;
			setarray .@treasurex[0],251,252,253,254,255,256,257,258,251,252,253,254,255,256,257,258,251,252,253,254,255,256,257,258;
			setarray .@treasurey[0],369,369,369,369,368,368,368,368,367,367,367,367,366,366,366,366,365,365,365,365,364,364,364,364;
		}
		else if (strnpcinfo(2) == "arug_cas02") { 
			set .@treasurebox,1944;
			setarray .@treasurex[0],382,383,384,385,386,387,384,385,386,387,388,389,382,383,384,385,386,387,384,385,386,387,388,389;
			setarray .@treasurey[0],231,231,231,231,231,231,230,230,230,230,230,230,225,225,225,225,225,225,224,224,224,224,224,224;
		}
		else { 	// Castles 3,4,5 are identical, except 4's treasure.
			set .@treasurebox,(strnpcinfo(2) == "arug_cas04")?1946:1945;
			setarray .@treasurex[0],291,292,293,294,295,296,293,294,295,296,297,298,291,292,293,294,295,296,293,294,295,296,297,298;
			setarray .@treasurey[0],276,276,276,276,276,276,274,274,274,274,274,274,272,272,272,272,272,272,269,269,269,269,269,269;
		}
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") { 
			set .@treasurebox,1939;
			setarray .@treasurex[0],249,250,251,252,253,246,247,248,249,250,250,251,252,253,246,247,248,249,250,249,250,251,252,253;
			setarray .@treasurey[0],378,378,378,378,378,376,376,376,376,376,374,374,374,374,372,372,372,372,372,370,370,370,370,370;
		}
		else if (strnpcinfo(2) == "schg_cas03") { 
			set .@treasurebox,1940;
			setarray .@treasurex[0],189,190,191,192,193,194,189,190,191,192,193,194,189,190,191,192,193,194,189,190,191,192,193,194;
			setarray .@treasurey[0], 21, 21, 21, 21, 21, 21, 19, 19, 19, 19, 19, 19, 17, 17, 17, 17, 17, 17, 15, 15, 15, 15, 15, 15;
		}
		else {	// Castles 1,4,5 are identical, except treasures.
			if (strnpcinfo(2) == "schg_cas01") set .@treasurebox,1938;
			else if (strnpcinfo(2) == "schg_cas04") set .@treasurebox,1941;
			else set .@treasurebox,1942;
			setarray .@treasurex[0],388,388,388,387,386,385,384,384,384,384,384,384,385,386,387,388,389,390,390,390,389,388,387,386;
			setarray .@treasurey[0],388,389,390,390,390,390,389,388,387,386,385,384,384,384,384,384,384,384,385,386,386,386,386,386;
		}
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Treasure Chest",(.@i%2)?.@treasurebox:1324,1,strnpcinfo(0)+"::OnTreasureDied";
	for(set .@i,4; .@i<24; set .@i,.@i+1) {
		if (.@Treasure < .@i+1) break;
		monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Treasure Chest",(.@i%2)?.@treasurebox:1324,1,strnpcinfo(0)+"::OnTreasureDied";
	}
	freeloop(0);
	end;

OnTreasureDied:
	end;
}

// Guild Manager
//============================================================
-	script	Steward#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (!.@GID) {
		mes "[ ผู้ดูแลปราสาท ]";
		mes "ข้ากำลังเฝ้ารอนายเหนือหัว";
		mes "ผู้ที่โชคชะตาจะเป็นผู้เลือกสรร";
		mes "มาให้แก่ข้า... ท่านคิดว่าท่านมี";
		mes "ความกล้าและพละกำลังเพียงพอ";
		mes "ที่จะพิชิตป้อมปราการแห่งนี้หรือไม่?";
		close;
	}
	if (is_guild_leader(.@GID) == false) {
		mes "[ ผู้ดูแลปราสาท ]";
		mes "หึ! คำขู่ของเจ้าทำอะไรข้าไม่ได้หรอก!";
		mes "เหล่าผู้พิทักษ์! จงไล่พวกนอกรีตนี่";
		mes "ออกไปจากที่นี่ซะ! ข้าจะซื่อสัตย์ต่อ";
		mes "นายเหนือหัวแห่งป้อมปราการนี้แต่เพียง";
		mes "ผู้เดียวเท่านั้น นั่นคือท่าน ^FF0000"+getguildmaster(.@GID)+"^000000";
		close;
	}
	mes "[ ผู้ดูแลปราสาท ]";
	mes "โอ้ ท่านมาสเตอร์ ^FF0000"+getguildmaster(.@GID)+"^000000...";
	mes "วันนี้จะให้ข้ารับใช้อะไรดีขอรับ?";
	mes "มีเรื่องเกี่ยวกับการดูแลรักษา";
	mes "ป้อมปราการแห่งนี้ส่วนไหนที่ท่าน";
	mes "ต้องการจะหารือกับข้าหรือไม่?";
	next;
	switch(select("สรุปสถานะป้อมปราการ:ลงทุนด้านพาณิชย์ (Economy):ลงทุนด้านการป้องกัน (Defense):จ้าง/เลิกจ้าง พนักงานคลังสินค้า:ไปยังห้องลับของมาสเตอร์")) {
	case 1:
		mes "[ ผู้ดูแลปราสาท ]";
		mes "ระดับการเติบโตทางพาณิชย์ (Economy)";
		mes "ของป้อมปราการเราอยู่ที่ระดับ ^0000ff"+getcastledata(strnpcinfo(4),CD_CURRENT_ECONOMY)+"^000000";
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) > 0) {
			mes "โดยเมื่อวานนี้ ท่านได้ลงทุนด้าน";
			mes "การเติบโตไปทั้งหมด "+getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY)+" ครั้ง";
		}
		next;
		mes "[ ผู้ดูแลปราสาท ]";
		mes "ส่วนระดับพลังป้องกัน (Defense)";
		mes "ของป้อมปราการเราอยู่ที่ระดับ ^0000ff"+getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE)+"^000000";
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) > 0) {
			mes "โดยเมื่อวานนี้ ท่านได้ลงทุนด้าน";
			mes "การป้องกันไปทั้งหมด "+getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE)+" ครั้ง";
		}
		mes " ";
		mes "รายงานมีเพียงเท่านี้ครับ ท่านมาสเตอร์";
		close;
	case 2:
		set .@Economy,getcastledata(strnpcinfo(4),CD_CURRENT_ECONOMY);
		setarray .@cost[0],5000,10000,20000,35000,55000,80000,110000,145000,185000,230000,280000,335000,395000,460000,530000,605000,685000,770000,860000,955000;
		set .@j,0;
		for(set .@i,6; .@i<101; set .@i,.@i+5) {
			if (.@Economy < .@i) {
				set .@eco_invest,.@cost[.@j];
				break;
			}
			set .@j, .@j+1;
		}
		// Quadruple the cost of investing if you've already invested once.
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY)) set .@eco_invest,.@eco_invest*4;
		mes "[ ผู้ดูแลปราสาท ]";
		mes "การเพิ่มระดับการเติบโตทางพาณิชย์";
		mes "จะช่วยเพิ่มจำนวนสินค้าและสมบัติ";
		mes "ที่กิลด์จะได้รับในแต่ละวันขอรับ";
		mes "การลงทุนในส่วนนี้จะช่วยสร้าง";
		mes "รากฐานที่มั่นคงให้กับกิลด์ในอนาคต";
		next;
		mes "[ ผู้ดูแลปราสาท ]";
		mes "ท่านสามารถลงทุนได้ตามปกติวันละ 1 ครั้ง";
		mes "แต่หากท่านยอมจ่าย Zeny เพิ่มขึ้น";
		mes "ท่านจะสามารถลงทุนครั้งที่สองได้ในวันเดียวกัน";
		mes "ซึ่งจะช่วยเร่งการพัฒนาให้รวดเร็วยิ่งขึ้น";
		mes "แต่ค่าใช้จ่ายจะค่อนข้างสูงทีเดียวขอรับ";
		next;
		if (.@Economy == 100) {
			mes "[ ผู้ดูแลปราสาท ]";
			mes "อย่างไรก็ตาม ขณะนี้ระดับการเติบโต";
			mes "ทางพาณิชย์ของป้อมเราอยู่ที่ 100% แล้ว";
			mes "จึงไม่สามารถพัฒนาไปได้ไกลกว่านี้แล้วขอรับ";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) >= 2) {
			mes "[ ผู้ดูแลปราสาท ]";
			mes "วันนี้ท่านได้ทำการลงทุนไปครบ 2 ครั้งแล้ว";
			mes "ท่านต้องรอจนกว่าจะถึงวันพรุ่งนี้";
			mes "เพื่อที่จะเริ่มการลงทุนครั้งต่อไปขอรับ";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) == 0) {
			mes "[ ผู้ดูแลปราสาท ]";
			mes "ท่านต้องชำระเงินจำนวน ^FF0000"+.@eco_invest+"^000000 Zeny";
			mes "เพื่อดำเนินการลงทุนในครั้งนี้";
			mes "ท่านต้องการจะลงทุนพัฒนา";
			mes "ด้านพาณิชย์ตอนนี้เลยหรือไม่ขอรับ?";
		}
		else {
			mes "[ ผู้ดูแลปราสาท ]";
			mes "ท่านต้องชำระเงินเพิ่มอีก ^FF0000"+.@eco_invest+"^000000 Zeny";
			mes "สำหรับการลงทุนครั้งที่สองในวันนี้";
			mes "ท่านยังต้องการจะลงทุนเพิ่มอีกครั้งหรือไม่?";
		}
		next;
		switch(select("ยืนยันการลงทุน:ยกเลิก")) {
		case 1:
			if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) >= 2) {
				mes "[ ผู้ดูแลปราสาท ]";
				mes "วันนี้ท่านได้ทำการลงทุนครบ";
				mes "ทั้ง 2 ครั้งไปเรียบร้อยแล้วครับ";
				mes "ท่านต้องรอจนกว่าจะถึงวันพรุ่งนี้";
				mes "จึงจะสามารถลงทุนเพิ่มได้อีกครั้ง";
				close;
			}
			if (Zeny < .@eco_invest) {
				mes "[ ผู้ดูแลปราสาท ]";
				mes "ขออภัยด้วยครับท่านมาสเตอร์";
				mes "ดูเหมือนท่านจะมี Zeny ไม่เพียงพอ";
				mes "สำหรับการลงทุนให้กิลด์ในวันนี้ขอรับ";
				close;
			}
			set Zeny, Zeny-.@eco_invest;
			setcastledata strnpcinfo(4),CD_INVESTED_ECONOMY,getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY)+1;
			mes "[ ผู้ดูแลปราสาท ]";
			mes "เป็นการใช้ทุนของกิลด์ที่ชาญฉลาดมากครับ";
			mes "ท่านมาสเตอร์... ผลของการลงทุนในครั้งนี้";
			mes "จะปรากฏให้เห็นภายในวันพรุ่งนี้ขอรับ";
			close;
		case 2:
			mes "[ ผู้ดูแลปราสาท ]";
			mes "น้อมรับคำบัญชาครับท่านมาสเตอร์";
			close;
		}
	case 3:
		set .@Defence,getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE);
		setarray .@cost[0],10000,20000,40000,70000,110000,160000,220000,290000,370000,460000,560000,670000,790000,920000,1060000,1210000,1370000,1540000,1720000,1910000;
		set .@j,0;
		for(set .@i,6; .@i<101; set .@i,.@i+5) {
			if (.@Defence < .@i) {
				set .@def_invest,.@cost[.@j];
				break;
			}
			set .@j, .@j+1;
		}
		// Quadruple the cost of investing if you've already invested once.
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE)) set .@def_invest,.@def_invest*4;
		mes "[ ผู้ดูแลปราสาท ]";
		mes "การลงทุนในด้านการป้องกันของป้อมเรา";
		mes "จะช่วยเพิ่มความทนทานให้กับเหล่า";
		mes "ผู้พิทักษ์ (Guardians) และหิน Emperium";
		mes "เราจำเป็นต้องใช้ทุกความได้เปรียบที่มี";
		mes "เพื่อปกป้องพวกเราจากเหล่าศัตรูขอรับ";
		next;
		mes "[ ผู้ดูแลปราสาท ]";
		mes "ท่านสามารถลงทุนด้านการป้องกันได้";
		mes "วันละ 1 ครั้ง แต่หากท่านยอมจ่าย Zeny";
		mes "เพิ่มขึ้น ท่านก็จะสามารถลงทุนได้";
		mes "สูงสุดถึง 2 ครั้งต่อวันเลยครับ";
		next;
		mes "[ ผู้ดูแลปราสาท ]";
		if (getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE) == 100) {
			mes "ระดับพลังป้องกันของป้อมปราการแห่งนี้";
			mes "อยู่ที่ 100% แล้วขอรับ ไม่สามารถ";
			mes "เพิ่มพูนไปได้มากกว่านี้อีกแล้ว";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) >= 2) {
			mes "ท่านมาสเตอร์ ท่านได้ลงทุนด้านการป้องกัน";
			mes "ไปครบ 2 ครั้งสำหรับวันนี้แล้วครับ";
			mes "ท่านคงต้องรอจนถึงวันพรุ่งนี้ หากท่าน";
			mes "ต้องการจะเพิ่มความแข็งแกร่งให้ป้อมของเรา";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) == 0) {
			mes "เราจำเป็นต้องใช้เงินจำนวน ^FF0000"+.@def_invest+"^000000";
			mes "Zeny เพื่อลงทุนในระบบป้องกันของป้อมเรา";
			mes "ท่านต้องการจะลงทุนตอนนี้เลยหรือไม่?";
		}
		else {
			mes "เราจำเป็นต้องใช้เงินจำนวน ^FF0000"+.@def_invest+"^000000";
			mes "Zeny เพื่อลงทุนในระบบป้องกันเป็นครั้งที่สอง";
			mes "ท่านต้องการจะลงทุนตอนนี้เลยหรือไม่?";
		}
		next;
		switch(select("ลงทุนด้านการป้องกัน:ยกเลิก")) {
		case 1:
			if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) >= 2) {
				mes "[ ผู้ดูแลปราสาท ]";
				mes "ท่านมาสเตอร์ ท่านได้ลงทุนด้านการป้องกัน";
				mes "ไปครบ 2 ครั้งสำหรับวันนี้แล้วครับ";
				mes "ท่านคงต้องรอจนถึงวันพรุ่งนี้ขอรับ";
				close;
			}
			if (Zeny < .@def_invest) {
				mes "[ ผู้ดูแลปราสาท ]";
				mes "ขออภัยด้วยครับท่านมาสเตอร์ แต่ดูเหมือน";
				mes "ท่านจะมี Zeny ไม่เพียงพอสำหรับการ";
				mes "ลงทุนให้กิลด์ในวันนี้ขอรับ";
				close;
			}
			set Zeny, Zeny-.@def_invest;
			setcastledata strnpcinfo(4),CD_INVESTED_DEFENSE,getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE)+1;
			mes "[ ผู้ดูแลปราสาท ]";
			mes "เป็นการใช้ทุนของกิลด์ที่ชาญฉลาดมากครับ";
			mes "การเพิ่มความแข็งแกร่งให้กับปราสาท";
			mes "ย่อมส่งผลดีต่อพวกเราทุกคนแน่นอนขอรับ";
			close;
		case 2:
			mes "[ ผู้ดูแลปราสาท ]";
			mes "น้อมรับคำบัญชาครับท่านมาสเตอร์";
			close;
		}
	case 4:
		if (getcastledata(strnpcinfo(4),CD_ENABLED_KAFRA) == 1) {
			mes "[ ผู้ดูแลปราสาท ]";
			mes "ท่านต้องการจะเลิกจ้าง";
			mes "พนักงาน Kafra ที่พวกเรา";
			mes "จ้างมาดูแลกิลด์งั้นหรือขอรับ?";
			next;
			switch(select("เลิกจ้าง:ยกเลิก")) {
			case 1:
				cutin "kafra_01",2;
				mes "[ พนักงาน Kafra ]";
				mes "นายท่านคะ โปรดทบทวนอีกครั้งเถอะค่ะ!";
				mes "ดิฉันทำงานอย่างหนักเพื่อความสำเร็จ";
				mes "ของกิลด์เรามาตลอดเลยนะค๊า!";
				mes "ดิฉันสัญญาว่าจะพยายามรับใช้สมาชิก";
				mes "ในป้อมนี้ให้ดียิ่งขึ้นไปอีกค่ะ!";
				next;
				switch(select("ยืนยันการเลิกจ้าง:เปลี่ยนใจยกเลิก")) {
				case 1:
					mes "[ พนักงาน Kafra ]";
					mes "ทำไมกันคะ?! ดิฉันทำอะไรผิดไป";
					mes "ถึงต้องทำกันขนาดนี้ด้วย... แงงงงง~!";
					next;
					cutin "kafra_01",255;
					break;
				case 2:
					mes "[ พนักงาน Kafra ]";
					mes "ขอบคุณมากค่ะ นายท่าน!";
					mes "ดิฉันจะปฏิบัติตามทุกคำสั่ง";
					mes "อย่างสุดความสามารถเลยค่ะ!";
					mes "ท่านจะไม่เสียใจที่เลือกดิฉันไว้แน่นอน!";
					close;
				}
				break;
			case 2:
				mes "[ ผู้ดูแลปราสาท ]";
				mes "ในมุมมองของข้า นางทำงานหนักมากเลยนะ";
				mes "ถือเป็นเรื่องดีของพวกเราทุกคนแล้วล่ะครับ";
				mes "ที่ท่านอนุญาตให้นางอยู่รับใช้พวกเราต่อ";
				close;
			}
			disablenpc "Kafra Employee#"+strnpcinfo(2);
			setcastledata strnpcinfo(4),CD_ENABLED_KAFRA,0;
			mes "[ ผู้ดูแลปราสาท ]";
			mes "พนักงาน Kafra คนนั้นถูกเลิกจ้างแล้วครับ";
			mes "ท่านไม่พอใจในคุณภาพการบริการ";
			mes "ของนางขนาดนั้นเลยหรือขอรับ?";
			close;
		} else {
			mes "[ ผู้ดูแลปราสาท ]";
			mes "ท่านต้องการจะจ้างพนักงาน Kafra";
			mes "มาประจำที่ป้อมของเราหรือไม่ขอรับ?";
			mes "ท่านต้องจ่ายเงิน ^FF000010,000 Zeny^000000";
			mes "สำหรับค่าธรรมเนียมในการจ้างครับ";
			next;
			switch(select("ตกลงจ้าง:ยกเลิก")) {
			case 1:
				if (getgdskilllv(.@GID,10001) == 0) {
					mes "[ ผู้ดูแลปราสาท ]";
					mes "ท่านมาสเตอร์ เรายังจ้าง Kafra ไม่ได้";
					mes "เพราะท่านยังไม่ได้เรียนรู้ทักษะกิลด์";
					mes "^FF0000Contract with Kafra^000000 ครับ";
					close;
				}
				if (Zeny < 10000) {
					mes "[ ผู้ดูแลปราสาท ]";
					mes "ท่านมาสเตอร์ เรายังจ้าง Kafra ไม่ได้";
					mes "เพราะเงินในตัวท่านมีไม่เพียงพอ";
					mes "สำหรับค่าธรรมเนียมแรกเข้าครับ";
					close;
				}
				set Zeny, Zeny-10000;
				enablenpc "Kafra Employee#"+strnpcinfo(2);
				setcastledata strnpcinfo(4),CD_ENABLED_KAFRA,1;
				mes "[ ผู้ดูแลปราสาท ]";
				mes "เรียบร้อยครับ เราได้ทำสัญญากับ";
				mes "สำนักงานใหญ่ของ Kafra แล้ว";
				mes "และนี่คือพนักงานที่จะมาประจำที่ป้อมเราครับ";
				next;
				cutin "kafra_01",2;
				mes "[ พนักงาน Kafra ]";
				mes "สวัสดีค่ะ ดิฉันถูกส่งตัวมาจาก";
				mes "สำนักงานใหญ่ของ Kafra เพื่อ";
				mes "ดูแลความสะดวกให้แก่กิลด์ของท่านค่ะ";
				mes "ดิฉันจะทำหน้าที่ให้ดีที่สุดตาม";
				mes "คำสั่งของนายท่านค่ะ";
				next;
				cutin "kafra_01",255;
				mes "[ ผู้ดูแลปราสาท ]";
				mes "สัญญาของเรามีระยะเวลาหนึ่งเดือน";
				mes "ดังนั้นเราต้องจ่ายค่าธรรมเนียมเพิ่มเติม";
				mes "เพื่อรักษาการบริการของนางไว้ในอนาคตนะขอรับ";
				close;
			case 2:
				mes "[ ผู้ดูแลปราสาท ]";
				mes "น้อมรับคำบัญชาครับท่านมาสเตอร์";
				mes "อย่างไรก็ตาม ข้าแนะนำให้จ้างนางไว้";
				mes "เร็วที่สุดเท่าที่จะทำได้นะขอรับ เพราะกิลด์";
				mes "จะได้ประโยชน์อย่างมากจากบริการของนาง";
				close;
			}
		}
	case 5:
		mes "[ ผู้ดูแลปราสาท ]";
		mes "ท่านต้องการจะเข้าไปยัง";
		mes "ห้องเก็บสมบัติของกิลด์ใช่หรือไม่?";
		mes "เฉพาะท่านที่เป็นกิลด์มาสเตอร์เท่านั้น";
		mes "ที่ได้รับอนุญาตให้เข้าไปข้างในได้ขอรับ";
		next;
		mes "[ ผู้ดูแลปราสาท ]";
		mes "โปรดจำไว้ว่าท่านควรไปเปิด";
		mes "กล่องสมบัติให้ตรงเวลา มิฉะนั้น";
		mes "สมบัติเหล่านั้นอาจจะหายไปได้";
		mes "หากมีเหตุการณ์ไม่คาดฝันเกิดขึ้นนะขอรับ";
		next;
		switch(select("ไปยังห้องเก็บสมบัติ:ยกเลิก")) {
		case 1:
			mes "[ ผู้ดูแลปราสาท ]";
			mes "ขออนุญาตให้ข้านำทางท่าน";
			mes "ไปตามเส้นทางลับเพื่อเข้าสู่";
			mes "ห้องเก็บสมบัติ ณ บัดนี้ครับ";
			mes "และเมื่อท่านต้องการจะกลับมาที่นี่";
			mes "ให้กดสวิตช์ลับที่อยู่ด้านในนะขอรับ";
			close2;
			// --- ส่วนกำหนดพิกัดวาร์ปตามแผนที่ปราสาท ---
			if (compare(strnpcinfo(4),"arug")) {
				if (strnpcinfo(4) == "arug_cas01") setarray .@i[0],250,363;
				else if (strnpcinfo(4) == "arug_cas02") setarray .@i[0],382,227;
				else setarray .@i[0],292,266;	// ปราสาท 3,4,5 ใช้พิกัดเดียวกัน
			}
			else {
				if (strnpcinfo(4) == "schg_cas02") setarray .@i[0],249,373;
				else if (strnpcinfo(4) == "schg_cas03") setarray .@i[0],190,16;
				else setarray .@i[0],381,381;	// ปราสาท 1,4,5 ใช้พิกัดเดียวกัน
			}
			warp strnpcinfo(4),.@i[0],.@i[1];
			end;
		case 2:
			mes "[ ผู้ดูแลปราสาท ]";
			mes "ไอเทมในห้องเก็บสมบัตินั้น";
			mes "จะถูกผลิตขึ้นเพียงวันละหนึ่งครั้ง";
			mes "ดังนั้นท่านควรเข้าไปเก็บรวบรวม";
			mes "สมบัติเหล่านั้นในทุกๆ วันนะขอรับ";
			mes "เพื่อผลประโยชน์สูงสุดของกิลด์เรา";
			mes "โปรดให้ความสำคัญกับการเก็บสมบัติด้วยครับ!";
			close;
		}
	}

OnStop:
	awake strnpcinfo(0);
	end;

OnStartArena:
	set .@GID,getcharid(2);
	set .@region$, (compare(strnpcinfo(4),"arug"))?"Valfreyja":"Nithafjoll";
	// Lower castle Economy
	set .@Economy,getcastledata(strnpcinfo(4),CD_CURRENT_ECONOMY)-5;
	if (.@Economy < 0) set .@Economy, 0;
	setcastledata strnpcinfo(4),CD_CURRENT_ECONOMY,.@Economy;
	// Lower Castle Defence
	set .@Defence,getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE)-5;
	if (.@Defence < 0) set .@Defence, 0;
	setcastledata strnpcinfo(4),CD_CURRENT_DEFENSE,.@Defence;
	// Set new owner
	setcastledata strnpcinfo(4),CD_GUILD_ID,.@GID;
	// Clear castle's data.
	for(set .@i,CD_INVESTED_ECONOMY; .@i<CD_ENABLED_GUARDIAN00; set .@i,.@i+1)
		setcastledata strnpcinfo(4),.@i,0;
	// Disable Kafra
	disablenpc "Kafra Employee#"+strnpcinfo(2);

	//announce "The ["+getguildname(.@GID)+"] guild conquered the ["+.@region$+" "+charat(strnpcinfo(2),3)+"] stronghold of "+getcastlename(strnpcinfo(4))+"!",bc_all|bc_woe;
	announce "กิลด์ ["+getguildname(.@GID)+"] ยึดครองปราสาท ["+getcastlename(strnpcinfo(4))+"] ในเขต "+.@region$+" ได้สำเร็จ!",bc_all|bc_woe;
	//mapannounce strnpcinfo(4),"The emperium has been shattered!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	mapannounce strnpcinfo(4),"หิน Emperium ถูกทำลายแล้ว!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	donpcevent "Manager#"+strnpcinfo(4)+"::OnReset";
	maprespawnguildid strnpcinfo(4),getcastledata(strnpcinfo(4),CD_GUILD_ID),2;
	donpcevent "Manager#"+strnpcinfo(4)+"::OnRecvCastle2";
	donpcevent "::OnRecvCastle"+strnpcinfo(2);
	sleep 10000;
	if (agitcheck2()) {
		donpcevent "Manager#"+strnpcinfo(4)+"::OnChange";
		//mapannounce strnpcinfo(4),"Rebuild this stronghold's Guardian Stones and Fortress Gates to secure your guild's new aquisition!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
		mapannounce strnpcinfo(4),"โปรดสร้างหิน Guardian Stones และประตู Fortress Gates ใหม่ เพื่อเสริมการป้องกันปราสาทที่ท่านยึดครอง!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	}
	end;
}

// Castle Guardians
//============================================================
-	script	Guardian#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	set .@n$, "["+strnpcinfo(1)+"]";
	if (!.@GID) {
		mes .@n$;
		mes "ทำได้ดีมากเจ้าหนุ่ม ตอนนี้สิ่งที่";
		mes "เจ้าต้องทำก็คือทำลาย Emperium";
		mes "นี้ซะ เพื่อเข้าครอบครอง";
		mes "ป้อมปราการแห่งนี้อย่างเต็มตัว";
		close;
	}
	if (getcharid(2) == .@GID) {
		if (is_guild_leader() == false) {
			mes .@n$;
			mes "ในฐานะผู้พิทักษ์แห่งป้อมปราการ";
			mes "ข้าจะรับคำสั่งจากมาสเตอร์";
			mes "ของกิลด์ผู้ครอบครองที่นี่";
			mes "แต่เพียงผู้เดียวเท่านั้น";
			close;
		}
		else {
			if (!agitcheck2()) {
				mes .@n$;
				mes "ข้าคือ "+strnpcinfo(1)+" ผู้พิทักษ์แห่ง";
				mes "ป้อมปราการนี้ ในขณะนี้ทุกอย่าง";
				mes "ยังคงสงบเรียบร้อยดีอยู่ขอรับ";
				next;
				switch(select("สนทนา:ยกเลิก")) {
				case 1:
					mes .@n$;
					mes "ท่านมีคำถามอะไรเกี่ยวกับ";
					mes "ป้อมปราการแห่งนี้หรือไม่ขอรับ?";
					next;
					switch(select("หินป้องกัน (Guardian Stones):ประตูค่าย (Fortress Gates):ธงวาร์ป (Link Flags):กลยุทธ์การศึก:ยกเลิก")) {
					case 1:
						mes .@n$;
						mes "ป้อมปราการแต่ละแห่งจะมี Emperium 1 ก้อน";
						mes "และ Guardian Stones 2 ก้อนขอรับ";
						mes "หินเหล่านี้คือแนวป้องกันด่านแรก";
						mes "ซึ่งศัตรูจะต้องทำลายพวกมันก่อน";
						mes "ถึงจะสามารถผ่านเข้าไปข้างในได้";
						next;
						mes .@n$;
						mes "หินเหล่านี้ตั้งอยู่ใน ^4D4DFFGate Houses^000000";
						mes "ซึ่งต้องได้รับการปกป้องอย่างดีเพื่อไม่ให้";
						mes "ศัตรูเข้าถึง Emperium ได้ นอกจากนี้หิน";
						mes "ป้องกันยังสามารถ ^4D4DFFเรียก Guardian^000000";
						mes "ออกมาช่วยคุ้มกันพื้นที่ได้อีกด้วย";
						next;
						mes .@n$;
						mes "ป้อมปราการที่มีระดับการป้องกันสูง";
						mes "จะสามารถเรียก Guardian ได้มากขึ้น";
						mes "นี่คือเหตุผลว่าทำไมกิลด์จึงควรเน้น";
						mes "ลงทุนในด้านการป้องกัน (Defense Growth)";
						next;
						mes .@n$;
						mes "Guardian Stones ที่ถูกทำลายสามารถ";
						mes "กู้คืนกลับมาได้หลังจากผ่านไปชั่วระยะเวลาหนึ่ง";
						mes "แต่สมาชิกกิลด์คนหนึ่งต้องเป็นผู้สั่งการข้า";
						mes "และข้าสามารถรายงานสถานะของหินได้ด้วยครับ";
						close;
					case 2:
						mes .@n$;
						mes "^4D4DFFFortress Gates^000000 คือแนวป้องกันด่านที่สอง";
						mes "ซึ่งจะถูกคุ้มครองโดยเครื่องป้องกันพิเศษที่";
						mes "ทำงานสัมพันธ์กับ Guardian Stones ขอรับ";
						mes "ประตูเหล่านี้ตั้งอยู่ในสามจุดที่ต่างกันของป้อม";
						next;
						mes .@n$;
						mes "เครื่องป้องกันจะทำงานเมื่อหินป้องกันยังคงอยู่";
						mes "และจะฟื้นฟูเมื่อหินป้องกันถูกกู้กลับคืนมา";
						mes "อย่างไรก็ตาม การซ่อมแซม Fortress Gates";
						mes "ที่ถูกทำลายไปแล้วนั้นไม่ใช่เรื่องง่ายเลย";
						next;
						mes .@n$;
						mes "ประตูค่ายจะถูกฟื้นฟูได้ก็ต่อเมื่อมีการ ^4D4DFFเปลี่ยน";
						mes "เจ้าของปราสาท^000000 หรือมีการ ^4D4DFFร้องขอการ";
						mes "ซ่อมแซมจากตัวกิลด์มาสเตอร์เอง^000000 เท่านั้นครับ";
						close;
					case 3:
						mes .@n$;
						mes "ในป้อมปราการมี Link Flags มากมาย";
						mes "ที่จะช่วยให้ท่านเข้าถึงพื้นที่สำคัญต่างๆ";
						mes "ท่ามกลางสิ่งกีดขวางที่ขวางกั้นอยู่";
						mes "โดยปกติ ^4D4DFFธงหมายเลข 1 จะวาร์ปไป Gate House^000000";
						next;
						mes .@n$;
						mes "ธงหลายจุดจะวาร์ปตรงมายังพื้นที่ใกล้ๆ";
						mes "กับหิน Emperium โดยตรง ส่วนธงหมายเลข";
						mes "สุดท้ายจะวาร์ปไปยังจุดอำนวยความสะดวก";
						mes "ของเจ้าของปราสาท โปรดจำเรื่องนี้ไว้ให้ดี";
						close;
					case 4:
						mes .@n$;
						mes "กลยุทธ์งั้นหรือ? ข้าว่ามันจะดีกว่าหากท่าน";
						mes "วางแผนการรบโดยดึงจุดเด่นของกิลด์ท่าน";
						mes "และใช้จุดอ่อนของศัตรูให้เป็นประโยชน์";
						mes "จงใช้ Gate Houses และที่กั้นให้คุ้มค่า และซ่อมแซมพวกมันให้เร็วที่สุด!";
						close;
					case 5:
						mes .@n$;
						mes "ท่านไม่มีคำถามอื่นแล้วงั้นหรือ? เอาเถอะ";
						mes "ข้ายังคงอยู่ที่นี่เพื่อรับใช้ท่านเสมอ";
						close;
					}
				case 2:
					mes .@n$;
					mes "ข้าจะคอยอยู่ที่นี่เสมอ ท่านสามารถมา";
					mes "ร้องขอความช่วยเหลือจากข้าได้ทุกเมื่อ";
					mes "ที่มีความจำเป็นนะขอรับ";
					close;
				}
			}
			else {
				mes .@n$;
				mes "ขอรับท่าน "+strcharinfo(0)+".";
				mes "ท่านมีคำสั่งประการใดหรือไม่?";
				next;
				switch(select("เพิ่มกำลังป้องกัน (เสก Guardian):สรุปสถานการณ์ปัจจุบัน:ยกเลิก")) {
				case 1:
					if (!getd("$agit_"+strnpcinfo(2)+"[5]")) {
						if (getgdskilllv(.@GID,10002) == 0) {
							mes .@n$;
							mes "ข้าต้องขออภัยด้วยครับ พลังของ";
							mes "Guardian Stones ยังไม่เพียงพอ";
							mes "ที่จะเรียกใช้ Guardian ได้... กิลด์";
							mes "ของท่านจำเป็นต้องศึกษาทักษะ";
							mes "^FF0000Guardian Research^000000 เสียก่อน";
							close;
						}
						else {
							mes .@n$;
							mes "ข้าจะดำเนินการเรียก Guardian";
							mes "ผ่านทางหิน Guardian Stone ขอรับ";
							mes "แต่โปรดจำไว้ว่า หากหินป้องกัน";
							mes "ถูกทำลายลง ระบบนี้จะไม่ทำงาน";
							setd "$agit_"+strnpcinfo(2)+"[5]",1;
							if (!getd("$agit_"+strnpcinfo(2)+"[0]"))
								donpcevent "gard1#"+strnpcinfo(4)+"::OnEnable";
							if (!getd("$agit_"+strnpcinfo(2)+"[1]"))
								donpcevent "gard2#"+strnpcinfo(4)+"::OnEnable";
							close;
						}
					}
					else {
						mes .@n$;
						mes "ท่านได้ออกคำสั่งให้ข้าเรียก";
						mes "Guardian ออกมาป้องกัน";
						mes "ป้อมปราการเรียบร้อยแล้วขอรับ";
						close;
					}
				case 2:
					mes .@n$;
					mes "รายงานสถานะการป้องกันป้อมของเรา...";
					setarray .@status$[0],"^4D4DFFปกติ (ทำงานอยู่)","^FF0000ถูกทำลาย","^008000กำลังซ่อมแซม";
					mes "หินป้องกันก้อนที่ 1: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[0]")]+"^000000";
					mes "หินป้องกันก้อนที่ 2: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[1]")]+"^000000";
					mes "ประตูค่ายด่านที่ 1: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[2]")]+"^000000";
					mes "ประตูค่ายด่านที่ 2: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[3]")]+"^000000";
					mes "ประตูค่ายด่านที่ 3: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[4]")]+"^000000";
					close;
				case 3:
					mes .@n$;
					mes "ข้าจะประจำตำแหน่งอยู่ตรงนี้";
					mes "เพื่อรอรับคำสั่งของท่านเสมอ";
					close;
				}
			}
		}
	}
	else {
		mes .@n$;
		mes "เจ้าเป็นใครกัน!? เจ้าคนพาล!";
		mes "จงออกไปจากป้อมปราการแห่งนี้เดี๋ยวนี้!";
		close;
	}

OnInit:
	setarray getd("$agit_"+strnpcinfo(2)+"[0]"),0,0,0,0,0,0;
	end;
}

// Guild Kafras
//============================================================
-	script	Kafra#template	-1,{
	cutin "kafra_01",2;
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (getcharid(2) == .@GID && getgdskilllv(.@GID,10001)) {
		mes "[ พนักงาน Kafra ]";
		mes "ยินดีต้อนรับสมาชิกผู้ภาคภูมิใจ";
		mes "แห่งกิลด์ ^FF0000"+GetGuildName(.@GID)+"^000000 ทุกท่านค่ะ!";
		mes "Kafra Corporation พร้อมที่จะ";
		mes "สนับสนุนท่านในทุกที่ที่ท่านไปค่ะ!";
		next;
		switch(select("ใช้บริการฝากของ:ใช้บริการวาร์ป:เช่ารถเข็น (Pushcart):ยกเลิก")) {
		case 1:
			if(!callfunc("F_CanOpenStorage")){
				mes "[ พนักงาน Kafra ]";
				mes "ขออภัยด้วยค่ะ ท่านจำเป็นต้องมี";
				mes "ทักษะ Novice Skill Lv.6";
				mes "จึงจะสามารถใช้บริการฝากของได้ค่ะ";
			}
			else openstorage;
			break;
		case 2:
			mes "[ พนักงาน Kafra ]";
			mes "โปรดระบุจุดหมาย";
			mes "ที่ท่านต้องการจะไปค่ะ";
			next;
			switch(select("Rachel -> 200 z:ยกเลิก")) {
			case 1:
				if (Zeny < 200) {
					mes "[ พนักงาน Kafra ]";
					mes "ขออภัยค่ะ ดูเหมือนท่านจะมี Zeny";
					mes "ไม่เพียงพอสำหรับค่าธรรมเนียมวาร์ป";
					mes "กรุณาตรวจสอบเงินในตัวอีกครั้งนะคะ";
					close2;
					cutin "kafra_01",255;
					end;
				}
				set Zeny, Zeny-200;
				warp "rachel",115,125;
				end;
			case 2:
				cutin "kafra_01",255;
				break;
			}
			break;
		case 3:
			if (BaseClass != Job_Merchant) {
				mes "[ พนักงาน Kafra ]";
				mes "ขออภัยค่ะ บริการเช่ารถเข็นนี้";
				mes "จำกัดเฉพาะอาชีพ Merchant,";
				mes "Blacksmith และ Alchemist เท่านั้นค่ะ";
			}
			else if (checkcart() == 1) {
				mes "[ พนักงาน Kafra ]";
				mes "อ้าว? ดูเหมือนท่านจะ";
				mes "เช่ารถเข็นไปเรียบร้อยแล้วนะคะ";
			}
			else {
				mes "[ พนักงาน Kafra ]";
				mes "ค่าธรรมเนียมเช่ารถเข็นคือ 800 Zeny";
				mes "ท่านต้องการจะเช่ารถเข็น";
				mes "เลยหรือไม่คะ?";
				next;
				switch(select("ตกลงเช่ารถเข็น:ยกเลิก")) {
				case 1:
					if (Zeny < 800) {
						mes "[ พนักงาน Kafra ]";
						mes "ขออภัยค่ะ ท่านมี Zeny ไม่เพียงพอ";
						mes "สำหรับค่าธรรมเนียมเช่ารถเข็นของเราค่ะ";
						close2;
						cutin "kafra_01",255;
						end;
					}
					set Zeny, Zeny-800;
					setcart;
					break;
				case 2:
					break;
				}
			}
			break;
		case 4:
			mes "[ พนักงาน Kafra ]";
			mes "ขอบพระคุณที่ใช้บริการของ Kafra ค่ะ";
			mes "ไม่ว่าท่านจะไปที่ไหน พวกเราจะคอย";
			mes "อยู่เคียงข้างท่านเสมอค่ะ!";
			close2;
			cutin "kafra_01",255;
			end;
		}
		close2;
		cutin "kafra_01",255;
		end;
	}
	else {
		mes "[ พนักงาน Kafra ]";
		mes "ขออภัยด้วยค่ะ ดิฉันถูกจ้างมา";
		mes "เพื่อดูแลสมาชิกของกิลด์";
		mes "^FF0000"+getguildname(.@GID)+"^000000 เท่านั้นค่ะ";
		mes "ท่านอาจจะต้องไปขอความช่วยเหลือ";
		mes "จากพนักงานท่านอื่นแทนนะคะ...";
		close2;
		cutin "kafra_01",255;
		end;
	}
}

// Guardian Stones (2)
//============================================================
-	script	Guardian Stone#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	set .@num, atoi(charat(strnpcinfo(1),0));
	set .@var$,"$agit_"+strnpcinfo(2);
	if (getcharid(2) == .@GID) {
		mes "^3355FFท่านจำเป็นต้องใช้ไอเทม";
		mes "ดังต่อไปนี้ เพื่อซ่อมแซม";
		mes "Guardian Stone ที่ถูกทำลายไป^000000";
		next;
		mes "1 Oridecon";
		mes "1 Elunium";
		mes "30 Stones";
		mes "5 Blue Gemstones";
		mes "5 Yellow Gemstones";
		mes "5 Red Gemstones";
		next;
		mes "^3355FFท่านต้องการดำเนินการต่อหรือไม่?^000000";
		next;
		if(select("ไม่:ตกลง") == 1) {
			mes "^3355FFยกเลิกการทำงาน^000000";
			close;
		}
		if ((countitem(984) > 0) && (countitem(985) > 0) && (countitem(7049) > 29) && (countitem(717) > 4) && (countitem(715) > 4) && (countitem(716) > 4)) {
			mes "^3355FFขั้นตอนแรก: วาง Stone, Elunium และ";
			mes "Oridecon ตามลำดับไว้ตรงกลาง จากนั้น";
			mes "ท่านต้องวาง Gemstone ที่อัดฉีดพลังเวท";
			mes "เพื่อกู้คืนสภาพของหินป้องกันขึ้นมาใหม่^000000";
			next;
			setarray .@stone$[0],"Elunium","Oridecon","Stones";
			set .@i, select("Elunium:Oridecon:Stone")-1;
			if (.@i == 2) set .@nice,.@nice+10;
			mes "^3355FFวาง "+.@stone$[.@i]+" ไว้ที่จุดศูนย์กลางแล้ว^000000";
			next;
			set .@i, select("Elunium:Oridecon:Stone")-1;
			if (.@i == 0) set .@nice,.@nice+10;
			mes "^3355FFท่านได้วาง "+.@stone$[.@i]+" ล้อมรอบ";
			mes "จุดศูนย์กลางเรียบร้อยแล้ว^000000";
			next;
			set .@i, select("Elunium:Oridecon:Stone")-1;
			if (.@i == 1) set .@nice,.@nice+10;
			mes "^3355FFท่านได้วาง "+.@stone$[.@i]+" ทับส่วนผสม";
			mes "ที่เหลือทั้งหมดเรียบร้อยแล้ว^000000";
			next;
			mes "^3355FFตอนนี้ท่านต้องจัดเรียง Gemstone";
			mes "ที่อัดพลังเวทให้ถูกต้อง โดยท่านสามารถ";
			mes "สังเกตคุณสมบัติทางเวทมนตร์ได้จาก";
			mes "เอฟเฟกต์การร่ายเวทที่ปรากฏขึ้น^000000";
			next;
			setarray .@effect[0],EF_BEGINSPELL4,EF_BEGINSPELL2,EF_VOLCANO;
			setarray .@color$[0],"สีแดง (Red)","สีเหลือง (Yellow)","สีน้ำเงิน (Blue)";
			while(1) {
				if (.@roof0 > 7) break;
				set .@i, rand(getarraysize(.@effect));
				specialeffect .@effect[.@i];
				mes "^3355FFท่านต้องจัดเรียง Gemstone";
				mes "ให้ถูกต้องตามลำดับของคุณสมบัติ";
				mes "และพลังงานเวทมนตร์ของพวกมัน^000000";
				next;
				set .@j, select("Red Gemstone:Yellow Gemstone:Blue Gemstone")-1;
				mes "^3355FFท่านได้วาง "+.@color$[.@j]+" ลงไปแล้ว^000000";
				if (.@i == .@j) {
					mes "^3355FFอย่างไรก็ตาม ระบบซ่อมแซมหินป้องกัน";
					mes "ล้มเหลว เนื่องจากเกิดการต่อต้านกัน";
					mes "ของพลังงานเวทมนตร์อย่างรุนแรง^000000";
					close;
				}
				set .@nice,.@nice+10;
				set .@roof0,.@roof0+1;
				specialeffect EF_STEAL;
				next;
			}
			if (.@nice > 90) {
				if (!getd(.@var$+"["+(.@num-1)+"]")) {
					mes "^3355FFระบบซ่อมแซมหินป้องกัน";
					mes "เสร็จสมบูรณ์ไปเรียบร้อยแล้ว^000000";
					close;
				}
				else {
					if (!agitcheck2()) {
						mes "^3355FFไม่สามารถกู้คืนหินป้องกันได้";
						mes "เนื่องจากไม่พบหิน Emperium";
						mes "อยู่ในขณะนี้^000000";
						close;
					}
					else {
						mes "^3355FFจัดเรียง Gemstone สำเร็จ!";
						mes "หินป้องกัน (Guardian Stone)";
						mes "ถูกซ่อมแซมเรียบร้อยแล้วขอรับ^000000";
						delitem 984,1; //Oridecon
						delitem 985,1; //Elunium
						delitem 7049,30; //Stone
						delitem 717,5; //Blue_Gemstone
						delitem 715,5; //Yellow_Gemstone
						delitem 716,5; //Red_Gemstone
						close2;
						donpcevent "df"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
						specialeffect EF_ICECRASH;
						disablenpc();
						setd .@var$+"["+(.@num-1)+"]",0;
						set .@df_all,getd(.@var$+"[0]")+getd(.@var$+"[1]");
						if (!.@df_all) {
							mapannounce strnpcinfo(4),"หินป้องกันทั้งสองก้อนถูกสร้างขึ้นแล้ว! พลังป้องกันของป้อมปราการแห่งนี้แข็งแกร่งขึ้นอย่างมาก!",bc_map,"0x00ff00";
							donpcevent "RL0#"+strnpcinfo(4)+"::OnEnable";
						}
						else mapannounce strnpcinfo(4),strnpcinfo(1)+" ถูกซ่อมแซมเสร็จสมบูรณ์แล้ว",bc_map,"0x00ff00";
						if (getd(.@var$+"[5]") == 1)
							donpcevent "gard"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
						end;
					}
				}
			}
			else {
				mes "^3355FFหลังจากที่พยายามมาตั้งนาน...";
				mes "ดูเหมือนท่านจะซ่อมแซมหินป้องกัน";
				mes "ล้มเหลว และสูญเสียวัตถุดิบไปบางส่วน^000000";
				delitem 7049,10; //Stone
				delitem 717,2; //Blue_Gemstone
				delitem 715,2; //Yellow_Gemstone
				delitem 716,2; //Red_Gemstone
				close;
			}
		}
		else {
			mes "^3355FFท่านมีวัตถุดิบไม่เพียงพอ";
			mes "ที่จะใช้ซ่อมแซมหินป้องกันขอรับ^000000";
			close;
		}
	}
	end;

OnInit:
OnDisable:
	disablenpc();
	end;

OnEnable:
	enablenpc();
	specialeffect EF_MAPPILLAR2;
	end;
}

// Control Devices (3)
//============================================================
-	script	Control#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	set .@num, atoi(charat(strnpcinfo(1),15));
	set .@var$,"$agit_"+strnpcinfo(2);
	if (getcharid(2) == .@GID) {
		if (is_guild_leader() == true) {
			if (getd(.@var$+"["+(.@num+1)+"]") == 2) {
				mes "^3355FFประตูค่าย (Fortress Gate) ที่พังไป";
				mes "สามารถซ่อมแซมได้ แต่ท่านจำเป็น";
				mes "ต้องรวบรวมวัตถุดิบดังต่อไปนี้ก่อน^000000";
				next;
				mes "^4D4DFFSteel 10 อัน^000000,";
				mes "^4D4DFFTrunk 30 อัน^000000,";
				mes "^4D4DFFOridecon 5 อัน^000000, และ";
				mes "^4D4DFFEmveretarcon 10 อัน^000000";
				next;
				select("ดำเนินการต่อ");
				if ((countitem(1019) > 29) && (countitem(999) > 9) && (countitem(1011) > 9) && (countitem(984) > 4)) {
					mes "^3355FFท่านต้องใช้ Trunk เพื่อซ่อมโครงสร้าง,";
					mes "ใช้ Oridecon เพื่อเพิ่มความทนทาน,";
					mes "และใช้ Emveretarcon เพื่อยึด";
					mes "ส่วนประกอบทั้งหมดเข้าด้วยกัน^000000";
					next;
					set .@ro_of01,rand(10,15);
					while(1) {
						if (.@ro_of02 == .@ro_of01) break;
						else {
							switch(rand(1,4)) {
							case 1:
								mes "^3355FFโครงสร้างค้ำยันเสียหายอย่างหนัก:";
								mes "การซ่อมแซมส่วนนี้ถือเป็น";
								mes "ลำดับความสำคัญสูงสุด^000000";
								next;
								switch(select("Trunk:Steel:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFโครงสร้างได้รับการเสริม";
									mes "ด้วยไม้เรียบร้อยแล้ว^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 2:
									mes "^3355FFท่านพยายามใช้ Steel แต่ดูเหมือน";
									mes "มันจะไม่ได้ผลดีนัก ท่านควรจะ";
									mes "ลองใช้อย่างอื่นแทน^000000";
									close;
								case 3:
									mes "^3355FFท่านพยายามใช้ Emveretarcon";
									mes "แต่มันไม่ได้ช่วยอะไรเลย ท่านต้อง";
									mes "เริ่มกระบวนการซ่อมแซมใหม่ทั้งหมด^000000";
									close;
								case 4:
									mes "^3355FFท่านพยายามใช้ Oridecon";
									mes "แต่มันไม่ได้ผล ท่านควรจะ";
									mes "ลองใช้อย่างอื่นแทน^000000";
									close;
								}
								break;
							case 2:
								mes "^3355FFดูเหมือนความทนทานโดยรวม";
								mes "ของประตูจะต้องได้รับการเสริม";
								mes "ด้วยวัสดุบางอย่าง^000000";
								next;
								switch(select("Trunk:Steel:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFท่านลองใช้ไม้เพื่อเสริมความแข็งแรง^000000";
									set .@ro_of02,.@ro_of02+1;
									next;
									break;
								case 2:
									mes "^3355FFท่านพยายามใช้ Steel แต่มัน";
									mes "ไม่ได้ผลเลย ท่านต้องเริ่ม";
									mes "กระบวนการซ่อมแซมใหม่ทั้งหมด^000000";
									close;
								case 3:
									mes "^3355FFท่านพยายามใช้ Emveretarcon";
									mes "แต่มันไม่ได้ผลเลย ท่านต้องเริ่ม";
									mes "กระบวนการซ่อมแซมใหม่ทั้งหมด^000000";
									close;
								case 4:
									mes "^3355FFท่านตี Oridecon ลงไป:";
									mes "ดูเหมือนว่าวิธีนี้จะได้ผลแฮะ!^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								}
								break;
							case 3:
								mes "^3355FFความเสียหายบนประตูทำให้เกิด";
								mes "รอยร้าวไปทั่ว ท่านต้องเชื่อมมัน";
								mes "ให้ติดกันด้วยอะไรบางอย่าง^000000";
								next;
								switch(select("Trunk:Steel:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFท่านพยายามใช้ไม้เพื่อซ่อมรอยร้าว";
									mes "แต่ดูเหมือนมันจะทำให้แย่ลงกว่าเดิม";
									mes "ท่านต้องเริ่มใหม่ทั้งหมด^000000";
									close;
								case 2:
									mes "^3355FFท่านใช้ Steel เชื่อมรอยร้าวทั้งหมด:";
									mes "ตอนนี้ประตูดูกลับมาแข็งแกร่ง";
									mes "ขึ้นอีกครั้งแล้ว^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 3:
									mes "^3355FFท่านพยายามใช้ Emveretarcon";
									mes "แต่มันไม่ได้ผลเลย ท่านต้องเริ่ม";
									mes "กระบวนการซ่อมแซมใหม่ทั้งหมด^000000";
									close;
								case 4:
									mes "^3355FFท่านพยายามใช้ Oridecon";
									mes "แต่มันไม่เข้ากับรอยร้าวเอาเสียเลย";
									mes "ท่านต้องลองวิธีอื่น^000000";
									close;
								}
								break;
							case 4:
								mes "^3355FFตอนนี้ท่านต้องทำให้แน่ใจว่า";
								mes "ทุกส่วนประกอบถูกยึดเข้าด้วยกัน";
								mes "อย่างหนาแน่นและมั่นคง^000000";
								next;
								switch(select("Trunk:Steel:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFท่านพยายามใช้ไม้เพื่อซ่อมส่วนนี้";
									mes "แต่มันกลับทำให้แย่ลงกว่าเดิม";
									mes "ท่านต้องเริ่มใหม่ทั้งหมด^000000";
									close;
								case 2:
									mes "^3355FFท่านพยายามใช้ Steel แต่ดูเหมือน";
									mes "มันจะไม่สามารถยึดอะไรไว้ได้เลย";
									mes "ท่านควรลองใช้อย่างอื่น^000000";
									close;
								case 3:
									mes "^3355FFท่านใช้ Emveretarcon ซ่อมแซม";
									mes "จุดที่เสียหายได้สำเร็จเกือบทั้งหมด!^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 4:
									mes "^3355FFท่านพยายามใช้ Oridecon";
									mes "แต่มันไม่ได้ช่วยยึดรอยแตกเลย";
									mes "ท่านควรลองอย่างอื่น^000000";
									close;
								}
							}
						}
					}
					mes "^3355FFเอาล่ะ ดูเหมือนว่าการซ่อมแซม";
					mes "จะใกล้เสร็จสิ้นสมบูรณ์แล้ว^000000";
					next;
					if (!agitcheck2()) {
						mes "^3355FFน่าเสียดายที่ประตูค่ายไม่สามารถ";
						mes "ถูกสร้างขึ้นใหม่ได้ เนื่องจาก Emperium";
						mes "ไม่ได้อยู่ที่นี่อีกต่อไปแล้ว^000000";
						close;
					}
					else {
						if (.@rp_temp == .@ro_of01) {
							mes "^3355FFการซ่อมแซมประตูค่ายเสร็จสิ้น!";
							mes "ประตูกลับมาใช้งานได้แล้ว!^000000";
							delitem 1019,30; //Trunk
							delitem 999,10; //Steel
							delitem 1011,10; //Emveretarcon
							delitem 984,5; //Oridecon
							close2;
							donpcevent "RL"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
							disablenpc();
							if (.@num == 1) set .@str$,"ด่านที่ 1";
							else if (.@num == 2) set .@str$,"ด่านที่ 2";
							else if (.@num == 3) set .@str$,"ด่านที่ 3";
							mapannounce strnpcinfo(4),"ประตูค่าย (Fortress Gate) "+.@str$+" ถูกซ่อมแซมเรียบร้อยแล้ว!",bc_map,"0x00ff00";
							if (.@num == 1) setd .@var$+"[2]",0;
							else {
								setarray getd(.@var$+"["+.@num+"]"),2,0;
								donpcevent "Control Device0"+(.@num-1)+"#"+strnpcinfo(2)+"::OnEnable";
							}
							end;
						}
						else {
							mes "^3355FFกำแพงถูกเจาะเข้ามาเสียแล้ว!";
							mes "ความพยายามในการซ่อมแซมล้มเหลว";
							mes "และท่านได้สูญเสียวัตถุดิบไปบางส่วน...^000000";
							delitem 984,2; //Oridecon
							delitem 999,4; //Steel
							delitem 1019,14; //Trunk
							delitem 1011,3; //Emveretarcon
							close;
						}
					}
				}
				else {
					mes "^3355FFท่านไม่สามารถเริ่มการซ่อมแซมได้";
					mes "หากมีวัตถุดิบไม่ครบตามที่กำหนด^000000";
					close;
				}
			}
		}
	}
	end;

OnInit:
OnDisable:
	disablenpc();
	end;

OnEnable:
	enablenpc();
	end;
}

// Guardian Summoners (2)
//============================================================
-	script	gard#template	-1,{
OnEnable:
	// .@x[i],.@y[i]: Normal coordinates, #0-21.
	// .@w[x],.@w[y]: Special coordinates if 'defence' is under 11.
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") {
			setarray .@w[0],195,250,292,188;
			setarray .@x[0],233,252,232,201,224,196,269,252,201,224,222, 294,256,240,246,235,235,246,240,256,254,242;
			setarray .@y[0], 83, 81,108,130,168,137, 89, 81,130,168,129, 210,203,133, 92,132,132, 92,133,203, 95,151;
		}
		else if (strnpcinfo(2) == "arug_cas02") {
			setarray .@w[0],20,169,268,169;
			setarray .@x[0],104,67,67,113,122,67, 90, 91,122, 20,67, 175,204,211,209,161,186,183,150,161,209,211;
			setarray .@y[0], 32,36,85, 87,112,60,167,119,112,169,85,  31, 32, 63, 88, 91,170,121,110, 91, 88, 63;
		}
		else {	// Castles 3,4,5 are identical.
			setarray .@w[0],66,157,211,159;
			setarray .@x[0],130,128,128,128,110,91,65, 65,110,128,128, 156,172,154,156,155,187,212,211,155,156,172;
			setarray .@y[0], 60, 77, 90,100, 96,53,71,103, 96,100, 77, 101, 95, 90, 77, 60, 54, 67,105, 60, 77, 95;
		}
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") {
			setarray .@w[0],337,95,307,222;
			setarray .@x[0],326,337,334,296,285,236,285,296,334,337,334, 359,300,337,317,307,300,337,317,307,359,236;
			setarray .@y[0], 83, 95,119, 82, 40, 41, 40, 82,119, 95,119,  85,119,154,183,222,119,154,183,222, 85, 41;
		}
		else if (strnpcinfo(2) == "schg_cas03") {
			setarray .@w[0],306,325,364,305;
			setarray .@x[0],323,273,288,306,323,323,273,288,306,273,273, 338,364,365,317,338,338,364,365,317,364,329;
			setarray .@y[0],308,309,306,326,308,308,309,306,325,309,309, 309,305,261,318,310,309,305,261,318,305,314;
		}
		else {	// Castles 1,4,5 are identical.
			setarray .@w[0],108,32,128,42,187,15;	// Contains an extra pair, for whatever reason.
			setarray .@x[0],111,109,65,110,88,64,47,109,111,112,120, 130,129,151,187,128,152,187,128,130,130,151;
			setarray .@y[0], 18, 44,22, 40,20,40,43, 48, 18, 32, 37,  22, 47, 18, 15, 42, 43, 15, 42, 22, 28, 18;
		}
	}
	if (charat(strnpcinfo(1),4) == "2") set .@z,11;
	freeloop(1);
	set .@defence,getcastledata(strnpcinfo(2),CD_CURRENT_DEFENSE);
	callsub OnSummon,.@z;
	if (.@defence > 70) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),5;
	else if (.@defence > 50) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),4;
	else if (.@defence > 30) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),3;
	else if (.@defence > 10) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),2;
	if (.@w[4] && .@z)
		guardian strnpcinfo(2),.@w[4],.@w[5],"Guardian Soldier",1899,strnpcinfo(0)+"::OnGuardianDied";
	else if (.@defence < 11) {
		set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),2;
		set .@i,(.@z)?2:0;
		guardian strnpcinfo(2),.@w[.@i],.@w[.@i+1],"Guardian Soldier",1899,strnpcinfo(0)+"::OnGuardianDied";
	}
	else for(set .@i,1; .@i<getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)); set .@i,.@i+1)
		callsub OnSummon,.@i+.@z;
	freeloop(0);
	copyarray getd(".x_"+strnpcinfo(2)+"[0]"),.@x[0],22;
	copyarray getd(".y_"+strnpcinfo(2)+"[0]"),.@y[0],22;
	setd ".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2),4+.@z;
	setarray .count$[5],"1st","2nd","3rd","4th","5th";
	initnpctimer;
	end;

OnTimer300000:
OnTimer900000:
OnTimer1800000:
OnTimer2700000:
OnTimer3600000:
	if (charat(strnpcinfo(1),4) == "2") end;
	set .@var$,".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2);
	setd .@var$, getd(.@var$)+1;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
	callsub OnSummon,getd(.@var$);
	setarray .count$[5],"1st","2nd","3rd","4th","5th";
	//mapannounce strnpcinfo(2),"The "+.count$[getd(.@var$)]+" Guardian has been summoned from the Gate House.",bc_map,"0xff4500";
	mapannounce strnpcinfo(2),"Guardian ลำดับที่ "+.count$[getd(.@var$)]+" ถูกอัญเชิญออกมาจาก Gate House เรียบร้อยแล้ว!",bc_map,"0xff4500";
	if (getd(.@var$) == 9) {
		setd .@var$,0;
		stopnpctimer;
	}
	end;

OnTimer600000:
OnTimer1200000:
OnTimer2100000:
OnTimer3000000:
OnTimer3900000:
	if (!(charat(strnpcinfo(1),4) == "2")) end;
	set .@var$,".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2);
	setd .@var$, getd(.@var$)+1;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
	callsub OnSummon,getd(.@var$);
	if (getd(.@var$) == 20) {
		setd .@var$,0;
		stopnpctimer;
	}
	end;

OnSummon:
	guardian strnpcinfo(2),getd(".x_"+strnpcinfo(2)+"["+getarg(0)+"]"),getd(".y_"+strnpcinfo(2)+"["+getarg(0)+"]"),"Guardian Soldier",1899,strnpcinfo(0)+"::OnGuardianDied";
	return;

OnGuardianDied:
	if (charat(strnpcinfo(1),4) == "2") set .@z,11;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))-1;
	if (getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)) < 2) {
		set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
		callsub OnSummon,10+.@z;
	}
	end;

OnReset:
	stopnpctimer;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianDied";
	deletearray getd(".x_"+strnpcinfo(2)+"[0]"),22;
	deletearray getd(".y_"+strnpcinfo(2)+"[0]"),22;
	end;
}

// Guardian Stone Summoners (2)
//============================================================
-	script	df#template	-1,{
OnEnable:
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") setarray .@i[0],210,234,308,189;
		else if (strnpcinfo(2) == "arug_cas02") setarray .@i[0],33,168,245,168;
		else setarray .@i[0],65,171,212,149;	// Castles 3,4,5 are identical.
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") setarray .@i[0],231,58,335,230;
		else if (strnpcinfo(2) == "schg_cas03") setarray .@i[0],242,309,376,251;
		else setarray .@i[0],27,35,207,75;	// Castles 1,4,5 are identical.
	}
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@j,(.@num == 1)?0:2;
	guardian strnpcinfo(2),.@i[.@j],.@i[.@j+1],((.@num == 1)?"1st":"2nd")+" Guardian Stone",1906+.@num,strnpcinfo(0)+"::OnGuardianStoneDied";
	end;

OnDisable:
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianStoneDied";
	setd "$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"["+(atoi(charat(strnpcinfo(1),2))-1)+"]",1;
	stopnpctimer;
	end;

OnGuardianStoneDied:
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@var$,"$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setd .@var$+"["+(.@num-1)+"]",1;
	if (getd(.@var$+"[0]") == 1 || getd(.@var$+"[0]") == 2) set .@destroyed, .@destroyed+1;
	if (getd(.@var$+"[1]") == 1 || getd(.@var$+"[1]") == 2) set .@destroyed, .@destroyed+1;
	if (.@destroyed == 2) {
		mapannounce strnpcinfo(2),"หินป้องกัน (Guardian Stones) ทั้งหมดถูกทำลายลงแล้ว!",bc_map,"0x00ff00";
		donpcevent "RL0#"+strnpcinfo(2)+"::OnDisable";
	}
	else mapannounce strnpcinfo(2),"หินป้องกัน (Guardian Stone) ก้อนที่ "+((.@num == 1)?"1":"2")+" ถูกทำลายแล้ว!",bc_map,"0x00ff00";
	donpcevent "gard"+.@num+"#"+strnpcinfo(2)+"::OnReset";
	initnpctimer;
	end;

OnTimer300000:
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@str$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	donpcevent ((.@num == 1)?"1st":"2nd")+" Guardian Stone#"+.@str$+"::OnEnable";
	setd "$agit_"+.@str$+"["+(atoi(charat(strnpcinfo(1),2))-1)+"]",2;
	stopnpctimer;
	end;
}

// Barrier Summoners (4)
//============================================================
-	script	RL#template	-1,{
OnEnable:
	.@npc_name$ = strnpcinfo(0);
	.@npc_visible$ = strnpcinfo(1);
	.@npc_hidden$ = strnpcinfo(2);
	set .@num, atoi(charat(.@npc_visible$,2));
	if (.@num == 0) {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],238,74,8,6,0;
				setarray .@x[0],239,241,243,245;
				setarray .@y[0], 73, 73, 73, 73;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],136,136,8,6,0;
				setarray .@x[0],137,139,141,143;
				setarray .@y[0],137,137,137,137;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],138,110,8,6,0;
				setarray .@x[0],139,141,143,145;
				setarray .@y[0],111,111,111,111;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],290,98,8,0,0;
				setarray .@x[0],289,289,289,289;
				setarray .@y[0], 98,100,102,104;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],326,301,6,6,0;
				setarray .@x[0],326,328,330;
				setarray .@y[0],300,300,300;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],114,48,13,6,0;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0], 49, 49, 49, 49, 49, 49;
			}
		}
	}
	else if (.@num == 1) {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],239,53,8,6,1;
				setarray .@x[0],239,241,243,240,242,244;
				setarray .@y[0], 55, 55, 55, 54, 54, 54;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],150,223,12,6,1;
				setarray .@x[0],151,153,155,157,159,161;
				setarray .@y[0],222,222,222,222,222,222;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],139,158,6,6,1;
				setarray .@x[0],140,142,144,139,141,143;
				setarray .@y[0],157,157,157,156,156,156;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],279,98,8,0,1;
				setarray .@x[0],280,280,280,281,281,281;
				setarray .@y[0], 98,100,102, 99,101,103;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],325,277,8,6,1;
				setarray .@x[0],326,328,330,327,329,331;
				setarray .@y[0],278,278,278,279,279,279;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],114,51,13,6,1;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0], 50, 50, 50, 50, 50, 50;
			}
		}
	}
	else if (.@num == 2) {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],107,124,6,6,1;
				setarray .@x[0],107,109,111,108,110,112;
				setarray .@y[0],122,122,122,123,123,123;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],125,342,8,0,1;
				setarray .@x[0],126,126,126,127,127,127;
				setarray .@y[0],343,345,347,344,346,348;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],138,210,8,6,1;
				setarray .@x[0],140,142,144,139,141,143;
				setarray .@y[0],209,209,209,208,208,208;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],230,213,6,0,1;
				setarray .@x[0],231,231,231,232,232,232;
				setarray .@y[0],213,215,217,213,215,217;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],200,230,8,0,1;
				setarray .@x[0],201,201,201,202,202,202;
				setarray .@y[0],231,233,235,232,234,236;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],114,154,13,6,1;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0],153,153,153,153,153,153;
			}
		}
	}
	else {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],84,171,8,6,1;
				setarray .@x[0], 84, 86, 88, 90;
				setarray .@y[0],170,170,170,170;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],38,314,12,6,1;
				setarray .@x[0], 40, 42, 44, 46;
				setarray .@y[0],315,315,315,315;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],138,263,8,6,1;
				setarray .@x[0],139,141,143,145;
				setarray .@y[0],262,262,262,262;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],160,141,6,6,1;
				setarray .@x[0],160,162,164,166;
				setarray .@y[0],140,140,140,140;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],285,198,8,0,1;
				setarray .@x[0],284,284,284,284;
				setarray .@y[0],199,201,203,205;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],116,241,11,6,1;
				setarray .@x[0],116,118,120,122;
				setarray .@y[0],240,240,240,240;
			}
		}
	}
	setwall .@npc_hidden$,.@wall[0],.@wall[1],.@wall[2],.@wall[3],.@wall[4],substr(.@npc_hidden$,0,1)+substr(.@npc_hidden$,8,9)+"_"+.@npc_visible$;
	switch(.@num) {
	case 0:
		killmonster .@npc_hidden$, .@npc_name$ + "::OnBarrierDestroyed";
		.@j = getarraysize(.@x);
		for ( .@i = 0; .@i < .@j; ++.@i ) {
			.@unit_id = guardian( .@npc_hidden$,.@x[.@i],.@y[.@i]," ",1905, .@npc_name$ + "::OnBarrierDestroyed" );
			setunitdata .@unit_id,UMOB_DMGIMMUNE,1; // The 1st barricades are immunes to damage until both Guardian Stones are destroyed
		}
		end;
	case 3:
		setd ".MyMobCount_" + .@num + .@npc_hidden$, 4;
		break;
	default:
		setd ".MyMobCount_" + .@num + .@npc_hidden$, 6;
		break;
	}
	for ( .@i = 0; .@i < getd( ".MyMobCount_" + .@num + .@npc_hidden$ ); ++.@i )
		guardian .@npc_hidden$,.@x[.@i],.@y[.@i]," ",1905, .@npc_name$ + "::OnBarrierDestroyed";
	end;

OnBarrierDestroyed:
	set .@num, atoi(charat(strnpcinfo(1),2));
	if (!.@num) end;
	set getd(".MyMobCount_"+.@num+strnpcinfo(2)),getd(".MyMobCount_"+.@num+strnpcinfo(2))-1;
	if (getd(".MyMobCount_"+.@num+strnpcinfo(2)) == 0) {
		set .@var$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
		setd "$agit_"+.@var$+"["+(.@num+1)+"]",1;
		setarray .@count$[0],"1st","2nd","3rd";
		mapannounce strnpcinfo(2),"ประตูค่าย (Fortress Gate) ด่านที่ "+.@count$[.@num-1]+" ถูกทำลายแล้ว",bc_map,"0x00ff00";
		delwall .@var$+"_"+strnpcinfo(1);
	}
	end;

OnDisable:
	.@wall_name$ = substr(strnpcinfo(2),0,1) + substr(strnpcinfo(2),8,9) + "_" + strnpcinfo(1);
	if (checkwall(.@wall_name$) == true)
		delwall .@wall_name$;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnBarrierDestroyed";
	end;
}

// Link Flags (function)
//============================================================
function	script	LinkFlag	{
	if (!getcharid(2) || getcharid(2) != getcastledata(strnpcinfo(4),CD_GUILD_ID)) end;
	if (getarg(0) == "Convenience Facility") {
		mes "^3355FFนี่คือบริการวาร์ปภายในป้อมปราการ";
		mes "ท่านต้องการจะไปยังพื้นที่";
		mes "อำนวยความสะดวกสำหรับ";
		mes "สมาชิกกิลด์หรือไม่?^000000";
		if(select("ไปยังจุดอำนวยความสะดวก:ยกเลิก") == 1)
			warp strnpcinfo(4),getarg(1),getarg(2);
		close;
	}
	if (getarg(0) == "Emperium Center") {
		mes "^3355FFนี่คือบริการวาร์ปภายในป้อมปราการ";
		mes "ท่านต้องการจะวาร์ปไปยัง";
		mes "ห้องโถง Emperium หรือไม่?^000000";
		if(select("ตกลง:ยกเลิก") == 1)
			warp strnpcinfo(4),getarg(1),getarg(2);
		close;
	}
	mes "^3355FFนี่คือบริการวาร์ปภายในป้อมปราการ";
	mes "โปรดเลือกจุดหมายปลายทาง";
	mes "ภายในพื้นที่ของป้อมเราครับ^000000";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+3)
		set .@menu$, .@menu$+getarg(.@i)+":";
	set .@menu$, .@menu$+"ยกเลิก";
	set .@i, select(.@menu$)-1;
	if (.@i != getargcount()/3)
		warp strnpcinfo(4),getarg(.@i*3+1),getarg(.@i*3+2);
	close;
}

// Return Flags (function)
//============================================================
function	script	ReturnFlag	{
	.@map$ = getarg(0);
	set .@str$, (compare(strnpcinfo(4),"aru"))?"Arunafeltz":"Schwarzwald";
	.@GID =  getcastledata(.@map$,CD_GUILD_ID);
	if (!.@GID) {
		mes "[ ราชโองการแห่ง "+.@str$+" ]";
		mes "อาณาจักรอันศักดิ์สิทธิ์แห่ง";
		mes .@str$+" ขอประกาศว่า";
		mes "ในขณะนี้ยังไม่มีผู้ใดได้ครอบครอง";
		mes "ป้อมปราการแห่งนี้ ผู้ใดก็ตามที่";
		mes "สามารถทำลาย Emperium ได้";
		mes "จะได้รับการยอมรับว่าเป็นเจ้าของคนใหม่";
		close;
	}
	if (getcharid(2) == .@GID && getarg(1,0)) {
		mes "[ เสียงก้องกังวาน ]";
		mes "ผู้กล้าเอ๋ย...";
		mes "ท่านต้องการที่จะกลับเข้าสู่";
		mes "ป้อมปราการของท่านหรือไม่?";
		next;
		if(select("กลับเข้าสู่ป้อมปราการ:ยกเลิก") == 1 && getcharid(2) == getcastledata(.@map$,CD_GUILD_ID)) {
			if (compare(.@map$,"arug")) {
				if (.@map$ == "arug_cas01") setarray .@i[0],67,193;
				else if (.@map$ == "arug_cas02") setarray .@i[0],43,256;
				else setarray .@i[0],121,318;	// ปราสาท 3,4,5 พิกัดเดียวกัน
			}
			else {
				if (.@map$ == "schg_cas02") setarray .@i[0],136,188;
				else if (.@map$ == "schg_cas03") setarray .@i[0],308,202;
				else setarray .@i[0],120,290;	// ปราสาท 1,4,5 พิกัดเดียวกัน
			}
			warp .@map$,.@i[0],.@i[1];
		}
		close;
	}
	mes "[ ราชโองการแห่ง "+.@str$+" ]";
	mes "อาณาจักรอันศักดิ์สิทธิ์แห่ง";
	mes .@str$+" ขอประกาศว่า";
	mes "ป้อมปราการแห่งนี้อยู่ในความครอบครอง";
	mes "ของกิลด์ ^FF0000"+getguildname(.@GID)+"^000000";
	next;
	mes "[ ราชโองการแห่ง "+.@str$+" ]";
	mes "โดยมีท่าน ^FF0000"+getguildmaster(.@GID)+"^000000";
	mes "เป็นกิลด์มาสเตอร์แห่ง ^FF0000"+getguildname(.@GID)+"^000000";
	mes "ผู้ใดที่ต้องการคัดค้านสิทธิ์นี้ จงใช้";
	mes "ศาสตราและเวทมนตร์เข้าตัดสินในช่วง";
	mes "เวลาสงครามกิลด์วอร์ตามที่กำหนดไว้!";
	close;
}

// Treasure Room Switches
//============================================================
-	script	Switch#template	-1,{
	mes " ";
	mes "^3355FFท่านต้องการจะดึง";
	mes "คันโยกเล็กๆ นี้หรือไม่?^000000";
	next;
	if(select("ดึงคันโยก:ยกเลิก") == 2) close;
	
	// --- ส่วนกำหนดพิกัดวาร์ปกลับตามแผนที่ปราสาท ---
	if (compare(strnpcinfo(4),"arug")) {
		if (strnpcinfo(4) == "arug_cas01") setarray .@i[0],121,357;
		else if (strnpcinfo(4) == "arug_cas02") setarray .@i[0],387,323;
		else setarray .@i[0],321,57;	// ปราสาท 3, 4, 5 ใช้พิกัดเดียวกัน
	}
	else {
		if (strnpcinfo(4) == "schg_cas02") setarray .@i[0],339,79;
		else if (strnpcinfo(4) == "schg_cas03") setarray .@i[0],57,13;
		else setarray .@i[0],275,244;	// ปราสาท 1, 4, 5 ใช้พิกัดเดียวกัน
	}
	
	warp strnpcinfo(4),.@i[0],.@i[1];
	close;
}

// Guild Dungeon Warps
//============================================================
-	script	Sunflower#template	-1,{
	if (getcharid(2) == getcastledata(strnpcinfo(4),CD_GUILD_ID)) {
		mes "- มันเป็นดอกทานตะวันที่ใหญ่โตจนน่าตกใจ ขนาดพอกับตัวคนเลยทีเดียว! ... ท่านสัมผัสได้ถึงพลังงานลึกลับบางอย่างที่แผ่ออกมาจากดอกไม้นี้ -";
		next;
		switch(select("จับที่ลำต้น:ไม่ทำอะไรทั้งนั้น")) {
		case 1:
			if (compare(strnpcinfo(4),"arug")) {
				set .@map$,"arug_dun01";
				setarray .@mapx[0],350,350,50, 50,200;
				setarray .@mapy[0],350, 50,50,350,386;
			}
			else {
				set .@map$,"schg_dun01";
				setarray .@mapx[0],262, 94, 79,212,322;
				setarray .@mapy[0],314,284,140, 70,166;
			}
			set .@i, atoi(charat(strnpcinfo(4),9))-1;
			warp .@map$,.@mapx[.@i],.@mapy[.@i];
			close;
		case 2:
			mes "มันน่ากลัวเกินไปที่จะไปแตะต้องสิ่งที่ข้าไม่รู้จัก";
			close;
		}
	}
}
