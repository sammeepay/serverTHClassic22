//===== rAthena Script =======================================
//= Brasilis Town
//===== Description: =========================================
//= [Official Conversion]
//= Brasilis Town Script
//===== Changelog: ===========================================
//= 1.0 First version. Transportation and Basic NPCs. [L0ne_W0lf]
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================

// Brasilis Transportation :: brasilis_trans
//============================================================
-	script	::Crewman_bra2	-1,{
	mes "[ลูกเรือ]";
	mes "เฮ้! ท่านเคยได้ยินชื่อเมือง Brasilis บ้างไหม?";
	mes "มันเป็นเมืองเขตร้อนที่ร้อนระอุเหมือนทะเลทราย แต่ก็มีฝนชุกเช่นกัน เป็นสถานที่ที่ลึกลับน่าค้นหามากเลยล่ะ";
	next;
	mes "[ลูกเรือ]";
	mes "เมื่อเร็วๆ นี้พวกเราเพิ่งค้นพบเส้นทางเดินเรือใหม่ที่ทำให้ไปที่นั่นได้ง่ายขึ้นด้วยนะ";
	if (VIP_SCRIPT) {
		set .@cost, vip_status(VIP_STATUS_ACTIVE)?1000:10000;
		mes "ค่าตั๋วไป-กลับแค่ 10,000 Zeny เท่านั้น และสำหรับ VIP เพียง 1,000 Zeny! สนใจจะไปไหมล่ะ?";
	} else {
		set .@cost,10000;
		mes "ค่าตั๋วไป-กลับแค่ 10,000 Zeny เท่านั้นเอง! ท่านสนใจจะเดินทางไปตอนนี้เลยไหม?";
	}
	next;
	switch(select("พาฉันไป Brasilis เลย!:ฉันขออยู่ที่นี่ต่อดีกว่า")) {
	case 1:
		if (Zeny < .@cost) {
			mes "[ลูกเรือ]";
			mes "ข้าบอกว่าต้องใช้ "+callfunc("F_InsertComma",.@cost)+" Zeny ไงล่ะ";
			close;
		}
		else {
			mes "[ลูกเรือ]";
			mes "เยี่ยมเลย~!! ออกเดินทางกันเล้ย~!";
			set Zeny, Zeny-.@cost;
			close2;
			warp "brasilis",314,60;
			end;
		}
	case 2:
		mes "[ลูกเรือ]";
		mes "ตกลง ถ้าเมื่อไหร่ที่ท่านสนใจก็แวะมาบอกข้าแล้วกัน ข้าพร้อมจะพาท่านไปที่นั่นเสมอ";
		close;
	}
}

brasilis,316,57,3	script	Crewman#bra1	100,{
	mes "[ลูกเรือ]";
	mes "เรือของข้ากำลังจะล่องกลับไปที่ Alberta แล้วนะ ท่านจะร่วมทางไปกับเราด้วยไหม?";
	next;
	switch(select("กลับสู่ Alberta:ยังก่อน~")) {
	case 1:
		mes "[ลูกเรือ]";
		mes "ข้าเองก็เริ่มคิดถึงบ้านแล้วเหมือนกันแฮะ";
		close2;
		if (checkre(0))
			warp "alberta",245,87;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[ลูกเรือ]";
		mes "ตกลง ตามใจท่านแล้วกัน ไว้เจอกันตอนจะกลับนะ";
		close;
	}
}

// Generic Brasilis NPCs :: brazil_tre
//============================================================
brasilis,155,165,3	script	Signpost#bra1	858,{
	mes ":: พิพิธภัณฑ์ศิลปะ ::";
	close;
}

brasilis,191,239,3	script	Signpost#bra2	858,{
	mes ":: อนุสาวรีย์ Verass ::";
	close;
}

brasilis,240,247,3	script	Signpost#bra3	858,{
	mes ":: ตลาด ::";
	mes " ";
	mes "- แหล่งรวมยาสมุนไพรและอาวุธสำหรับคุณ -";
	close;
}

brasilis,303,309,3	script	Signpost#bra4	858,{
	mes ":: กระเช้าลอยฟ้าสู่ป่าลึก ::";
	mes "- ไม่เหมาะสำหรับคนขวัญอ่อน -";
	close;
}

brasilis,278,137,3	script	Signpost#bra5	858,{
	mes ":: โรงแรม Brasilis ::";
	close;
}

brasilis,137,77,5	script	Ice-Cream Maker	85,{
	mes "[คนขายไอศกรีม]";
	mes "เร่เข้ามา~ เร่เข้ามา~";
	mes "ไอศกรีมเย็นๆ เหมาะที่สุดสำหรับวันร้อนๆ แบบนี้~";
	mes "ราคาเพียงแค่ ^3355FF100 Zeny^000000 เท่านั้น~";
	mes "ไอศกรีมจ้า~";
	mes "มารับไอศกรีมไปกินกันได้เลย!";
	next;
	switch(select("เอาอันนึง!:ไอศกรีมคืออะไรเหรอ?:ยกเลิก")) {
	case 1:
		mes "[คนขายไอศกรีม]";
		mes "เนื่องจากมีคนอยากกินไอศกรีมกันเยอะมาก ข้าเลยขายให้ได้แค่ครั้งละไม่เกิน 5 อันเท่านั้นนะ";
		mes "สรุปว่าท่านอยากได้กี่อันล่ะ?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[คนขายไอศกรีม]";
				mes "ไม่เอาสักอันเลยเหรอ?";
				mes "งั้นก็หลีกทางหน่อยไป๊ ข้ายังมีลูกค้าคนอื่นต้องดูแลนะ";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[คนขายไอศกรีม]";
				mes "ว้าวๆๆ";
				mes "ท่านสั่งเยอะเกินไปแล้ว";
				mes "ถ้ากินเกิน 5 อันในทีเดียว เดี๋ยวได้ไปสู้กับสัตว์ประหลาดในท้องตัวเองหรอก ใจเย็นๆ ก่อนเพื่อนฝูง";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[คนขายไอศกรีม]";
			mes "โธ่เพื่อน~! ท่านมีเงินไม่พอนี่นา";
			mes "มันแค่ ^3355FF100 Zeny^000000 เองนะ~ สาบานได้เลย!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[คนขายไอศกรีม]";
			mes "ตัวท่านดูจะแบกของเยอะเกินไปแล้วนะ";
			mes "ไปทำตัวให้เบาๆ หน่อย แล้วค่อยมาซื้อใหม่";
			close;
		}
		set Zeny, Zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[คนขายไอศกรีม]";
		mes "ไอศกรีมก็คือ...";
		mes "เดี๋ยวนะ นี่ท่านไม่รู้จัก";
		mes "ไอศกรีมจริงๆ เหรอ?";
		mes "นี่ท่านไปมุดหัวอยู่ที่ไหนมาเนี่ย?";
		next;
		mes "[คนขายไอศกรีม]";
		mes "ข้าจะไม่เสียเวลาอธิบายหรอกว่ามันฟังดูแปลกแค่ไหน";
		mes "เอาเป็นว่า มาซื้อไอศกรีมของข้าไปกินตอนที่มันยังเย็นๆ อยู่นี่มา!";
		close;
	case 3:
		mes "[คนขายไอศกรีม]";
		mes "อย่าพลาดโอกาสที่จะได้กินไอศกรีมที่อร่อยที่สุดในปฐพีเชียวนะ~!";
		close;
	}
}
